mapscripts SunshineTown_MapScripts {
    MAP_SCRIPT_ON_TRANSITION {
        setflag(FLAG_VISITED_SUNSHINE_TOWN)
        if (flag(FLAG_SYS_QUEST_MENU_GET) == FALSE){
            setobjectxyperm(5, 19, 7)
            setobjectmovementtype(5, MOVEMENT_TYPE_FACE_UP)
            setobjectxyperm(6, 23, 3)
            setobjectmovementtype(6, MOVEMENT_TYPE_FACE_LEFT)
        }
        if(var(VAR_AKIHIKOS_INTRO_STATE) >= 16 && var(VAR_MITSURUS_INTRO_STATE)!= 19){
            setflag(FLAG_TEMP_1)
        }
        if (var(VAR_AKIHIKOS_INTRO_STATE) == 16 || var(VAR_AKIHIKOS_INTRO_STATE) == 17 || var(VAR_AKIHIKOS_INTRO_STATE) == 18) {
            playbgm(MUS_P3_CALAMITY, TRUE)
            setflag(FLAG_HIDE_HELP_BUTTON)
            setobjectxyperm(2, 14, 11)
            setobjectmovementtype (2, MOVEMENT_TYPE_WALK_RIGHT_AND_LEFT)
            setobjectxyperm(1, 3, 14)
            setobjectmovementtype (1, MOVEMENT_TYPE_LOOK_AROUND)
            }
        if (var(VAR_AKIHIKOS_INTRO_STATE) < 16 || var(VAR_AKIHIKOS_INTRO_STATE) >= 17 || var(VAR_MITSURUS_INTRO_STATE) < 20) {
            setflag(FLAG_TEMP_2)
        }
        if(var(VAR_MITSURUS_INTRO_STATE) > 16 && var(VAR_MITSURUS_INTRO_STATE) <= 50){
            setmetatile(3, 13, 0x831, TRUE)
        }
        if (var(VAR_MITSURUS_INTRO_STATE) != 20){
            setflag(FLAG_TEMP_3)
            setflag(FLAG_TEMP_5)
        }
        if(var(VAR_MITSURUS_INTRO_STATE) >= 20 && var(VAR_MITSURUS_INTRO_STATE) < 23){
            setflag(FLAG_TEMP_4)
            setflag(FLAG_TEMP_5)
        }
        if (var(VAR_MITSURUS_INTRO_STATE) >= 18 && var(VAR_MITSURUS_INTRO_STATE) <= 21 ){
            setobjectxyperm(3, 13, 6)
            setobjectmovementtype(3, MOVEMENT_TYPE_FACE_LEFT)
            setobjectxyperm(24, 14, 7)
            setobjectmovementtype(24, MOVEMENT_TYPE_FACE_LEFT)
            setobjectmovementtype(2, MOVEMENT_TYPE_WALK_RIGHT_AND_LEFT)
            setobjectmovementtype(5, MOVEMENT_TYPE_FACE_DOWN)
            setobjectxyperm(1, 8, 4)
            setobjectmovementtype(1, MOVEMENT_TYPE_FACE_DOWN)
        }
    }
    MAP_SCRIPT_ON_WARP_INTO_MAP_TABLE[
        VAR_MITSURUS_INTRO_STATE, 19: SunshineTown_Eventscript_MitsuruOnWarp19
        VAR_MITSURUS_INTRO_STATE, 24: SunshineTown_Eventscript_MitsuruOnWarp24
    ]
    MAP_SCRIPT_ON_FRAME_TABLE [
        VAR_AKIHIKOS_INTRO_STATE, 16: SunshineTown_Eventscript_OnFrame16
        VAR_AKIHIKOS_INTRO_STATE, 23: SunshineTown_Eventscript_OnFrame23
        ///////
        VAR_MITSURUS_INTRO_STATE, 18: SunshineTown_Eventscript_MitsuruOnFrame18
        VAR_MITSURUS_INTRO_STATE, 19: SunshineTown_Eventscript_MitsuruOnFrame19
        VAR_MITSURUS_INTRO_STATE, 23: SunshineTown_Eventscript_MitsuruOnFrame23
        VAR_MITSURUS_INTRO_STATE, 24: SunshineTown_Eventscript_MitsuruOnFrame24
    ]
    
}

script SunshineTown_Eventscript_MitsuruOnWarp19 {
    applymovement(1, moves(set_invisible))
    applymovement(LOCALID_PLAYER, moves(face_right))    
}

script SunshineTown_Eventscript_MitsuruOnFrame23 {
    lock
    setvar(VAR_MITSURUS_INTRO_STATE, 24)
    warpsilent(MAP_SUNSHINE_TOWN, 3, 7)
    waitstate
}

script SunshineTown_Eventscript_MitsuruOnWarp24 {
    applymovement(LOCALID_PLAYER, faceright)
}

script SunshineTown_Eventscript_MitsuruOnFrame24{
    goto(SunshineTown_Eventscript_MitsuruAskingAboutAkiShinji)
}

script SunshineTown_Eventscript_MitsuruOnFrame18 {
    call(Eventscript_CutsceneStarts)
    setflag(FLAG_DONT_TRANSITION_MUSIC)
    setvar(VAR_MITSURUS_INTRO_STATE, 19)
    warpsilent(MAP_SUNSHINE_TOWN, 4, 6)
    waitstate
}

script SunshineTown_Eventscript_MitsuruOnFrame19 {
    goto(SunshineTown_Eventscript_MitsuruSeeingPoliceRunning)
}

script SunshineTown_Eventscript_Kurosawa {
    lock
    setspeaker(SP_NAME_KRSW)
    if (flag(FLAG_TEMP_1) == TRUE) {
        faceplayer
        msgbox(format("Hm?\p
        {SPEAKER NAME_A}{CREATE MUGSHOT_A EMOTE_SURPRISED}W-we'll get going now!"))
    }
    else {
        msgbox(format("If only the higher-ups allowed us-"))
        closemessage
        playse(SE_PIN)
        applymovement(3, KurosawaNoticed)
        waitmovement(0)
        msgbox(format("{SPEAKER NAME_KRSW}Do you need some help, kids?\p
        {SPEAKER NAME_A}{CREATE MUGSHOT_A EMOTE_SURPRISED}Oh nothing! Sorry for bothering you, sir.\p
        (He looks so intimidating!)"))
        setflag(FLAG_TEMP_1)
    }
    release
    end
}

movement KurosawaNoticed {
    emote_exclamation_mark
    face_player
}
script SunshineTown_Eventscript_DontLeaveTownTrigger_Akihiko {//VAR_TEMP_F == 0
    if (var(VAR_AKIHIKOS_INTRO_STATE) == 12){
       goto(SunshineTown_Eventscript_GoGetPackage)
    }
    if (var(VAR_AKIHIKOS_INTRO_STATE) == 17){
        goto(SunshineTown_Eventscript_DontLeaveTown)
    }
    if (var(VAR_AKIHIKOS_INTRO_STATE) == 18){
        goto(SunshineTown_Eventscript_NoPointLeavingTown)
    }
    if(var(VAR_AKIHIKOS_INTRO_STATE) == 24){
        goto(SunshineTown_Eventscript_Begin_Route2_Cutscene_AkihikoVar24)
    }
}
script SunshineTown_Eventscript_GoGetPackage {
    lockall
    msgbox(format("{SPEAKER NAME_S}{CREATE MUGSHOT_S EMOTE_NORMAL}The Pokémon Center is right there, Aki."))
    closemessage
    applymovement(LOCALID_PLAYER, walkright)
    waitmovement(0)
    releaseall
    end
}

script SunshineTown_Eventscript_FatMan {
    lock
    setspeaker("Kojima")
    faceplayer
    if (var(VAR_AKIHIKOS_INTRO_STATE) == 17 || var(VAR_AKIHIKOS_INTRO_STATE) == 18){
        msgbox(format("The policeman living here ran toward the woods after hearing that explosion.\p
        {SPEAKER NAME_A}{CREATE MUGSHOT_A EMOTE_MAD}We should hurry up!"))
        setvar(VAR_AKIHIKOS_INTRO_STATE, 18)
        release
        end
    }
    else{
    msgbox(format("Technology is amazing.\p
    Now you can do so many things with decomps."))}
    release
    end
}

script SunshineTown_Eventscript_Boy {
    lock
    faceplayer
    setspeaker ("Yoshida")
    questmenu(QUEST_MENU_CHECK_ACTIVE, QUEST_FIND_STARTERS)
    if(var(VAR_RESULT))
    {
        goto (SunshineTown_Eventscript_Boy_GiveSquirtleInfo)
    }
    if (var(VAR_AKIHIKOS_INTRO_STATE) == 17 || var(VAR_AKIHIKOS_INTRO_STATE) == 18){
        msgbox(format("I think something exploded in the woods.\p
        {SPEAKER NAME_A}{CREATE MUGSHOT_A EMOTE_DARKENED}Could it be...\p
        {SPEAKER NAME_S}{CREATE MUGSHOT_S EMOTE_MAD}The orphanage!"))
        setvar(VAR_AKIHIKOS_INTRO_STATE, 18)
        release
        end
    }
    if (var(VAR_MITSURUS_INTRO_STATE) >= 18 && var(VAR_MITSURUS_INTRO_STATE) <= 21){
        msgbox(format("My brother is a fire fighter and he's at the burning orphanage right now.\p
        I'm so worried I can't stay still, I hope there's not many casualties.\p
        If only I had a Pokémon that can deal with fire to help but I only have a Poltchageist."))
    }
    else {
        msgbox(format("One of the richest families in the region lives on this island.\p
        I heard their mansion was at the south end of Route 1."))
    }
    closemessage
    release
    end
}

script SunshineTown_Eventscript_Boy_GiveSquirtleInfo {
    mcsaid(0, 0, A_AskSquirtle, M_AskSquirtle)
    setspeaker("Yoshida")
    msgbox(format("{DESTROY_MUGSHOT}Oh, that one! I saw it at the river bank one {COLOR 7}night{COLOR 2}.\p
    Tried to catch it but it wiped me out..."))
    mcsaid(EMOTE_SMILE, 0, "Thank you!", "I see.\nThank you for the information.")
    release
    end
}

text A_AskSquirtle{
    format("Hello, I'm looking for a sparkling Squirtle, have you seen it anywhere?")
}
text M_AskSquirtle{
    format("Good day, have you seen a sparkling Squirtle around here by any chance?")
}

script SunshineTown_Eventscript_DirerctionSign {
    lock
    msgbox(format("Sunshine Riverbank {DOWN_ARROW}\n
    Route 2 {LEFT_ARROW}"))
    release
    end
}

script SunshineTown_Eventscript_TownSign {
    lock
    msgbox(format("Welcome to Sunshine Town!"))
    release
    end
}

script SunshineTown_Eventscript_OnFrame16 {
    lockall
    goto (SunshineTown_Eventscript_ExitingPC)
    releaseall
}

script SunshineTown_Eventscript_ExitingPC {
    lockall
    applymovement(LOCALID_PLAYER, SunshineTown_Eventscript_AkihikoExitingPC)
    applymovement (4, SunshineTown_Eventscript_ShinjiroExitingPC)
    waitmovement(0)
    msgbox(format("{SPEAKER NAME_A}{CREATE MUGSHOT_A EMOTE_MAD}Let's ask around what happened.\p
    {SPEAKER NAME_S}{CREATE MUGSHOT_S EMOTE_MAD}Yeah."))
    closemessage
    applymovement(4, walkup)
    waitmovement(0)
    removeobject(4)
    setflag(FLAG_TEMP_2)
    setvar(VAR_AKIHIKOS_INTRO_STATE, 17)
    fwdtime(9, 35)
    releaseall
    end
}

movement SunshineTown_Eventscript_AkihikoExitingPC {
    walk_fast_down *1
    walk_fast_right *2
    face_down
}

movement SunshineTown_Eventscript_ShinjiroExitingPC {
    walk_fast_down *3
    walk_fast_right *2 
    face_up
}

script SunshineTown_Eventscript_DontLeaveTown {
    lockall
    applymovement(LOCALID_PLAYER, walkright)
    waitmovement(0)
    msgbox(format("{SPEAKER NAME_A}{CREATE MUGSHOT_A EMOTE_MAD}We should ask people in town about that explosion sound!"))
    closemessage
    releaseall
    end
}

script SunshineTown_Eventscript_NoPointEnteringPC {
    lock
    applymovement(LOCALID_PLAYER, walkdown)
    msgbox(format("{SPEAKER NAME_A}{CREATE MUGSHOT_A EMOTE_MAD}(Not the time to be dawdling around!)"))
    closemessage
    release
    end
}

script SunshineTown_Eventscript_NoPointLeavingTown {
    lock
    applymovement(LOCALID_PLAYER, walkright)
    msgbox(format("{SPEAKER NAME_A}{CREATE MUGSHOT_A EMOTE_MAD}(Not the time to be dawdling around!)"))
    closemessage
    release
    end
}

script SunshineTown_Eventscript_WarpToBurningOrphanage {//VAR_TEMP_F == 0
    if (var(VAR_AKIHIKOS_INTRO_STATE) == 17 || var(VAR_AKIHIKOS_INTRO_STATE) == 18)
    {
        lock
        playse(SE_EXIT)
        setvar(VAR_AKIHIKOS_INTRO_STATE, 18)
        setflag(FLAG_HIDE_HELP_BUTTON)
        warp(MAP_SUNSHINE_ORPHANAGE_BURNING, 16, 23)
        waitstate
    }
}

script SunshineTown_Eventscript_OW_Happiny{
    lockall
    if (var(VAR_AKIHIKOS_INTRO_STATE) < 24 || (var(VAR_MITSURUS_INTRO_STATE) >= 18 && var(VAR_MITSURUS_INTRO_STATE) <= 21)){
        msgbox(format("The Happiny is looking through the door. It won't budge no matter what."))
        closemessage
        releaseall
        end
    }
    else{
        faceplayer
        playmoncry(SPECIES_HAPPINY, CRY_MODE_NORMAL)
        startoverworldencounter(6)
        removeobject(6)
        setflag(FLAG_TEMP_A)
        releaseall
        end
    }
    
}

script SunshineTown_Eventscript_OnFrame23{
    lock
    subquestmenu(QUEST_MENU_CHECK_COMPLETE, STORY_QUESTS, MITSURUS_PROLOGUE)
    if (!var(VAR_RESULT))
    {
        call(EventScript_ShowHelpQuest)
        setflag(FLAG_SYS_QUEST_MENU_GET)
        questmenu(QUEST_MENU_SET_ACTIVE, STORY_QUESTS)
        setvar(VAR_STORY_QUESTS_STATE, 1)
        subquestmenu(QUEST_MENU_SET_ACTIVE, STORY_QUESTS, AKIHIKOS_PROLOGUE)
    }
    clearflag(FLAG_HIDE_QUEST_ICON)
    setvar(VAR_AKIHIKOS_INTRO_STATE, 24)
    release
    end
}

script SunshineTown_Eventscript_QUEST_PLAY_WITH_PKM_FOR_SOOTHE_BELL{
    lock
    setspeaker("Fumiko")
    if (flag(FLAG_SYS_QUEST_MENU_GET) == FALSE){
        goto (SunshineTown_Eventscript_ExpertF_PreQuest)
    }
    else {
        faceplayer
        questmenu(QUEST_MENU_CHECK_COMPLETE, QUEST_PLAY_WITH_PKM_FOR_SOOTHE_BELL)
        if (var(VAR_RESULT) == TRUE){
            call(SunshineTown_Eventscript_QUEST_PLAY_WITH_PKM_FOR_SOOTHE_BELL_QuestWasCompleted)
        }
        else{
            call(SunshineTown_Eventscript_QUEST_PLAY_WITH_PKM_FOR_SOOTHE_BELL_CheckIfReward)
        }
    }
    release
    end
}

script SunshineTown_Eventscript_QUEST_PLAY_WITH_PKM_FOR_SOOTHE_BELL_QuestWasCompleted {
    msgbox(format("Soothe Bell can help you befriend Pokémon faster."))
    closemessage
    release
    end
}


script SunshineTown_Eventscript_QUEST_PLAY_WITH_PKM_FOR_SOOTHE_BELL_CheckIfReward {
    questmenu(QUEST_MENU_CHECK_REWARD, QUEST_PLAY_WITH_PKM_FOR_SOOTHE_BELL)
    if (var(VAR_RESULT) == TRUE){
        setflag(FLAG_HIDE_QUEST_ICON)
        msgbox(format("My Riolu was very happy. Please take this as a thank-you for doing me a favor."))
        closemessage
        giveitem(ITEM_SOOTHE_BELL, 1)
        questmenu(QUEST_MENU_COMPLETE_QUEST, QUEST_PLAY_WITH_PKM_FOR_SOOTHE_BELL)
        release
        clearflag(FLAG_HIDE_QUEST_ICON)
        end
    }
    else {
        call(SunshineTown_Eventscript_QUEST_PLAY_WITH_PKM_FOR_SOOTHE_BELL_CheckIfActive)
    }
}

script SunshineTown_Eventscript_QUEST_PLAY_WITH_PKM_FOR_SOOTHE_BELL_CheckIfActive {
    setflag(FLAG_HIDE_QUEST_ICON)
    questmenu(QUEST_MENU_CHECK_ACTIVE, QUEST_PLAY_WITH_PKM_FOR_SOOTHE_BELL)
    if (var(VAR_RESULT) == TRUE){
        msgbox(format("You can find my poor Riolu on the second floor."))
        closemessage
        release
        clearflag(FLAG_HIDE_QUEST_ICON)
        refreshquesticon
        end
    }
    else {
        checkplayergender
        if (var(VAR_RESULT) == MALE)
        {
            msgbox(format("Excuse me, young men. Can you do me a favor?\p
            {SPEAKER NAME_A}{CREATE MUGSHOT_A EMOTE_SMILE}Sure, ma'am. What can we do for you?"))
            closemessage
            setspeaker("Fumiko")
            msgbox(format("One of the Pokémon caretakers I hired couldn't show up today.\p
            Can you go play with my poor baby Riolu? He's on the second floor.\p
            {SPEAKER NAME_A}{CREATE MUGSHOT_A EMOTE_SMILE}Leave it to us!"))
            closemessage
        }
        else{
            msgbox(format("Excuse me, young lass. Can you do me a favor?\p
            {SPEAKER NAME_M}{CREATE MUGSHOT_M EMOTE_SMILE}Certainly, ma'am. What do you need?"))
            closemessage
            setspeaker("Fumiko")
            msgbox(format("One of the Pokémon caretakers I hired couldn't show up today.\p
            Can you go play with my poor baby Riolu? He's on the second floor.\p
            {SPEAKER NAME_M}{CREATE MUGSHOT_M EMOTE_SMILE}I'll be happy to help."))
            closemessage
        }
        questmenu(QUEST_MENU_SET_ACTIVE, QUEST_PLAY_WITH_PKM_FOR_SOOTHE_BELL)
        release
        clearflag(FLAG_HIDE_QUEST_ICON)
        refreshquesticon
        end
    }
    
}

script SunshineTown_Eventscript_ExpertF_PreQuest {
    if (var(VAR_MITSURUS_INTRO_STATE) >= 18 && var(VAR_MITSURUS_INTRO_STATE) <= 21){
        faceplayer
        msgbox(format("I lent my Lucario to the police to help them with the rescue.\p
        The Riolus are still too young and I have to keep them safe here."))
        closemessage
        release
        end
    }
    if (var(VAR_AKIHIKOS_INTRO_STATE) == 17 || var(VAR_AKIHIKOS_INTRO_STATE) == 18){
        faceplayer
        msgbox(format("Did you hear a loud sound or was it because of my ears acting up again?"))
        closemessage
        release
        end
    }
    else{
        msgbox("Where did I put my key again...?")
    }
    closemessage
    release
    end
}

script SunshineTown_Eventscript_Begin_Route2_Cutscene_AkihikoVar24 {
    lock
    msgbox(format("{COLOR RED}Important scene ahead. Do you want to proceed?"), MSGBOX_YESNO)
    if (var(VAR_RESULT) == 0){
        applymovement(LOCALID_PLAYER, walkright)
        waitmovement(0)
        release
        end
    }
    else{
        fwdtime(11, 0)
        warpsilent(MAP_BLANK_MAP, 0, 0)
    }
}
    
script SunshineTown_Eventscript_HiddenGrotto {
    lock
    msgbox(Eventscript_HiddenGrotto, MSGBOX_YESNO)
    closemessage
    if (var(VAR_RESULT) == TRUE){
        warp(MAP_SUNSHINE_TOWN_HIDDEN_GROTTO, 0)
    }
    release
    end
}


script unlockcharacters
{
    msgbox(format("unlocked & made available all charas"))
    closemessage
    unlockcharacter(CHARACTER_MITSURU)
    unlockcharacter(CHARACTER_SHINJIRO)
    unlockcharacter(CHARACTER_AKIHIKO)
    clearflag(FLAG_LOCK_MITSURU)
    clearflag(FLAG_LOCK_AKIHIKO)
    clearflag(FLAG_LOCK_SHINJIRO)
    end
}

script SunshineTown_Eventscript_MitsuruSeeingPoliceRunning {
    lock
    applymovement(LOCALID_PLAYER, moves(player_run_right*3, walk_right*2, emote_exclamation_mark))
    delay(36)
    applymovement(24, moves(walk_fast_left*13))
    delay(4)
    applymovement(3, moves(walk_fast_left*13))
    delay(20)
    applymovement(LOCALID_PLAYER, moves(face_down, lock_facing_direction, walk_fast_up, unlock_facing_direction, delay_16, face_left))
    waitmovement
    delay(50)
    removeobject(3)
    removeobject(24)
    msgbox(format("{SPEAKER NAME_M}{CREATE MUGSHOT_M EMOTE_NORMAL}(The police? How did they arrive this fast?
    The explosion happened just a few minutes ago.)\p
    (Not to mention, if I remember correctly, the nearest station is in the opposite direction of Sunshine Town.)"))
    closemessage
    delay(20)
    clearflag(FLAG_DONT_TRANSITION_MUSIC)
    fadenewbgm(MUS_DUMMY)
    playse(SE_SLIDING_DOOR)
    waitse
    special(SpawnCameraObject)
    applymovement(1, moves(set_visible, delay_8, walk_fast_down*2, walk_fast_right*3))
    delay(4)
    applymovement(LOCALID_PLAYER, moves(face_down, delay_16, face_right))
    delay(4)
    applymovement(OBJ_EVENT_ID_CAMERA, moves(walk_fast_right))
    waitmovement
    msgbox(format("{SPEAKER NAME_M}{CREATE MUGSHOT_M EMOTE_NORMAL}Excuse me. Did something happen? Everyone in town looks on edge."))
    closemessage
    applymovement(1, faceleft)
    waitmovement
    setspeaker("Kojima")
    msgbox(format("There was an explosion at the orphanage east of town, didn't you hear?"))
    playbgm(MUS_P3_CALAMITY, TRUE)
    msgbox(format("Lots of people were injured and the town people are trying to help.\p
    Now excuse me, I gotta bring these medicines to the injured."))
    closemessage
    applymovement(OBJ_EVENT_ID_CAMERA, walkleft)
    waitmovement
    special(RemoveCameraObject)
    applymovement(1, moves(walk_fast_right*3, walk_fast_down*6, set_invisible))
    delay(20)
    msgbox(format("{SPEAKER NAME_M}{CREATE MUGSHOT_M EMOTE_STRUGGLED}(The orphanage was exploded?)\p
    (Damn it! How am I so naive. Of course they would do that to erase the proofs and traces.)\p
    {CREATE MUGSHOT_M EMOTE_SAD}(It was all my fault... If only I acted sooner.)\p
    ...........\p
    {CREATE MUGSHOT_M EMOTE_SERIOUS}(I should go check and help the injured as well.)"))
    closemessage
    removeobject(1)
    setvar(VAR_MITSURUS_INTRO_STATE, 20)
    setflag(FLAG_DONT_TRANSITION_MUSIC)
    call(Eventscript_CutsceneEnds)
    clearflag(FLAG_TEMP_3)
    addobject(22)
    addobject(23)
    applymovement(22, invisible)
    waitmovement
    releaseall
    end
}

script SunshineTown_Eventscript_MitsuruSeeingKGStaffAtWoodsEntranceTrigger18 {
    lock
    playse(SE_PIN)
    applymovement(LOCALID_PLAYER, moves(emote_exclamation_mark, face_right, delay_16, delay_8, player_run_left, player_run_up, face_left))
    waitmovement
    applymovement(OBJ_EVENT_ID_FOLLOWER, moves(walk_left, walk_up, face_right))
    waitmovement
    special(SpawnCameraObject)
    applymovement(OBJ_EVENT_ID_CAMERA, moves(walk_right, walk_diag_southeast))
    waitmovement(OBJ_EVENT_ID_CAMERA)
    goto(SunshineTown_Eventscript_KGStaffAtWoodsEntrance)
}

script SunshineTown_Eventscript_MitsuruSeeingKGStaffAtWoodsEntranceTrigger19 {
    lock
    playse(SE_PIN)
    applymovement(LOCALID_PLAYER, moves(emote_exclamation_mark, face_right, delay_16, delay_8, player_run_left, player_run_up*2, face_left))
    waitmovement
    applymovement(OBJ_EVENT_ID_FOLLOWER, moves(walk_left, walk_up, face_right))
    waitmovement
    special(SpawnCameraObject)
    applymovement(OBJ_EVENT_ID_CAMERA, moves(walk_right, walk_diag_southeast))
    waitmovement(OBJ_EVENT_ID_CAMERA)
    goto(SunshineTown_Eventscript_KGStaffAtWoodsEntrance)
}

script SunshineTown_Eventscript_KGStaffAtWoodsEntrance {
    delay(20)
    playse(SE_EXIT)
    clearflag(FLAG_TEMP_5)
    addobject(22, MAP_SUNSHINE_TOWN)
    delay(20)
    applymovement(22, walkinplacedown)
    waitmovement
    setspeaker("Man In Black 1")
    msgbox(format("Have you seen those boys?"))
    applymovement(23, walkinplaceup)
    waitmovement
    setspeaker("Man In Black 2")
    msgbox(format("No, I've been standing here the whole time but I didn't see them.\p
    This is the only exit to safety. Where could they be?"))
    applymovement(22, walkinplacedown)
    waitmovement
    setspeaker("Man In Black 1")
    msgbox(format("Maybe they're still in the woods?\p
    Let's go search for them."))
    closemessage
    delay(20)
    applymovement(22, moves(face_right, delay_8, set_invisible))
    waitmovement
    playse(SE_EXIT)
    delay(4)
    applymovement(23, moves(face_right, delay_8, set_invisible))
    waitmovement
    playse(SE_EXIT)
    delay(20)
    applymovement(OBJ_EVENT_ID_CAMERA, moves(walk_left, walk_diag_northwest))
    waitmovement
    special(RemoveCameraObject)
    msgbox(format("{SPEAKER NAME_M}{CREATE MUGSHOT_M EMOTE_SERIOUS}(They're searching for some boys... Ones of the orphan kids, perhaps?)\p
    (I should tail them and see what they're up to.)"))
    closemessage
    removeobject(22)
    removeobject(23)
    setflag(FLAG_TEMP_3)
    setvar(VAR_MITSURUS_INTRO_STATE, 21)
    fwdtime(9, 40)
    release
    end
}

script SunshineTown_Eventscript_MitsuruAskingAboutAkiShinji {
    lock
    applymovement(LOCALID_PLAYER, moves(player_run_right*3, walk_right*2, delay_16))
    waitmovement
    msgbox(format("{SPEAKER NAME_M}{CREATE MUGSHOT_M EMOTE_NORMAL}(This man... He was helping the orphanage so he could've known something about those boys.)"))
    closemessage
    applymovement(LOCALID_PLAYER, moves(walk_down, walk_right*2))
    waitmovement
    msgbox(format("{SPEAKER NAME_M}{CREATE MUGSHOT_M EMOTE_NORMAL}Excuse me. Have you seen these boys?"))
    setspeaker("Kojima")
    msgbox(format("{DESTROY_MUGSHOT}Oh them! I saw the policeman living in this town carried them to his house.\p
    {SPEAKER NAME_M}{CREATE MUGSHOT_M EMOTE_NORMAL}Really? When was it?"))
    setspeaker("Kojima")
    msgbox(format("{DESTROY_MUGSHOT}I think it was around an hour after the explosion at the orphanage happened?\p
    After that I've only seen the black-red beanie boy going out of the house a few times.\p
    {SPEAKER NAME_M}{CREATE MUGSHOT_M EMOTE_NORMAL}May I ask where this policeman's house is?"))
    closemessage
    applymovement(1, facedown)
    delay(4)
    applymovement(LOCALID_PLAYER, facedown)
    delay(8)
    special(SpawnCameraObject)
    applymovement(OBJ_EVENT_ID_CAMERA, moves(walk_down, walk_diag_southwest*4, walk_left*2))
    waitmovement
    setspeaker("Kojima")
    msgbox(format("It's the house over there.\p
    {SPEAKER NAME_M}{CREATE MUGSHOT_M EMOTE_NORMAL}I see. Thank you so much for your help."))
    closemessage
    applymovement(OBJ_EVENT_ID_CAMERA, moves(walk_diag_northeast*5, walk_right))
    waitmovement
    special(RemoveCameraObject)
    setvar(VAR_MITSURUS_INTRO_STATE, 25)
    fwdtime(17, 20)
    release
    end
}

script SunshineTown_Eventscript_MitsuruOverheardAkiShinji {
    lock
    fadenewbgm(MUS_DUMMY)
    playse(SE_PIN)
    applymovement(LOCALID_PLAYER, emoteexclamation)
    setspeaker("???????")
    msgbox(format("Aki! At least eat something before you go find her! You haven't eaten since yesterday!"))
    setspeaker("Aki?")
    msgbox(format("How do you expect I can eat anything when Miki is still out there missing!?\p
    What do you know anyway? It's not like she's your sister."))
    closemessage
    playse(SE_M_DOUBLE_SLAP)
    call(Eventscript_BlockShakeCamera)
    playse(SE_PIN)
    applymovement(LOCALID_PLAYER, emoteexclamation)
    waitmovement
    setspeaker("???????")
    msgbox(format("Cool your ass and sit down.\p
    Even if you find her, do you think she'll be happy seeing you in this state?\p
    Looking like a rotten walking zombie?"))
    setspeaker("Aki?")
    msgbox(format(".......... Fuck you, Shinji. Fine."))
    closemessage
    delay(20)
    setspeaker("Shinji?")
    msgbox(format("Eat first. Then we'll go find her together, okay?"))
    setspeaker("Aki?")
    msgbox(format("........Okay."))
    closemessage
    delay(20)
    applymovement(LOCALID_PLAYER, facedown)
    waitmovement
    msgbox(format("{SPEAKER NAME_M}{CREATE MUGSHOT_M EMOTE_SAD}....... (His sister's missing after the explosion, I suppose.)\p
    (I should go to the orphanage and help the searching.)"))
    closemessage
    applymovement(LOCALID_PLAYER, moves(player_run_right*9, player_run_up*2, face_down))
    delay(40)
    setvar(VAR_MITSURUS_INTRO_STATE, 26)
    warpsilent(MAP_BLANK_MAP)
}

script SunshineTown_Eventscript_NotTheWayToSunshineMeadow_Mitsuru {//VAR_TEMP_F == 0
    if (var(VAR_MITSURUS_INTRO_STATE) >= 6 && var(VAR_MITSURUS_INTRO_STATE) <= 16){
        lock
        msgbox(format("{SPEAKER NAME_M}{CREATE MUGSHOT_M EMOTE_SERIOUS}(Now is not the time to wander around in Sunshine Town.)"))
        closemessage
        applymovement(LOCALID_PLAYER, walkleft)
        waitmovement(0)
        release
        end
    }
}

script SunshineTown_Eventscript_KurosawaHouseLocked {
    if(var(VAR_MITSURUS_INTRO_STATE) > 16 && var(VAR_MITSURUS_INTRO_STATE) <= 50)
    {
        msgbox("It's locked.")
    }
    release
    end
}

script SunshineTown_Eventscript_BreederF {
    lock
    faceplayer
    setspeaker("Hahana")
    msgbox(format("They say Nickit survives by stealing other Pokémon's food.\p
    Makes me wonder if I give it enough food, will it still steal from others?"))
    release
    end
}

script SunshineTown_Eventscript_Nickit {
    lock
    playmoncry(SPECIES_NICKIT, 0)
    showmonpic(SPECIES_NICKIT, 18, 5)
    doemote(VAR_LAST_TALKED, E_HAPPY)
    msgbox(format("Nickit is enjoying its Sitrus Berry."))
    hidemonpic
    setseenmon(SPECIES_NICKIT)
    release
    end
}

script SunshineTown_Eventscript_Gentleman {
    lock
    faceplayer
    setspeaker ("Watanabe")
    msgbox(format("I enjoy the silence when the sun goes down in this town.\p
    There aren't many people living here but during the daytime,\p
    the teachers from the orphanage often bring the kids here to play.\p
    Don't get me wrong, though. I don't mind the carefree smile of those kids.\p
    I just wish their Pokémon battles weren't too rowdy."))
    release
    end
}