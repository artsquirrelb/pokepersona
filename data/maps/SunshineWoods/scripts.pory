mapscripts SunshineWoods_MapScripts {
    MAP_SCRIPT_ON_TRANSITION {
        setflag(FLAG_TEMP_8)
        if (var(VAR_AKIHIKOS_INTRO_STATE) < 22){
            setflag(FLAG_TEMP_3)
        }
        if (var(VAR_AKIHIKOS_INTRO_STATE) == 6){
            setobjectxyperm(2, 4, 3)
            setobjectxyperm(3, 5, 5)
            setobjectmovementtype(3, MOVEMENT_TYPE_FACE_RIGHT)
        }
        if(var(VAR_AKIHIKOS_INTRO_STATE) > 11) {
            setflag(FLAG_TEMP_1)
        }
        if (var(VAR_MITSURUS_INTRO_STATE) != 21){
            setflag(FLAG_TEMP_4)
            setflag(FLAG_TEMP_5)
            setflag(FLAG_TEMP_6)
            setflag(FLAG_TEMP_7)
        }
        if (var(VAR_MITSURUS_INTRO_STATE) == 21){
            clearflag(FLAG_DONT_TRANSITION_MUSIC)
            setflag(FLAG_TEMP_6)
            setflag(FLAG_TEMP_5)
            setflag(FLAG_TEMP_7)
            setobjectmovementtype(7, MOVEMENT_TYPE_FACE_UP)
            setobjectmovementtype(8, MOVEMENT_TYPE_FACE_UP)
        }
        if( var(VAR_MITSURUS_INTRO_STATE) == 22){
            setobjectxyperm(12, 12, 17)
            setobjectmovementtype(12, MOVEMENT_TYPE_FACE_LEFT_AND_RIGHT)
            setobjectxyperm(11, 20, 10)
            setobjectmovementtype(11, MOVEMENT_TYPE_WANDER_AROUND)
        }
    }
        
        
    MAP_SCRIPT_ON_WARP_INTO_MAP_TABLE [
        VAR_AKIHIKOS_INTRO_STATE, 6: SunshineWoods_Eventscript_OnWarp6
        VAR_MITSURUS_INTRO_STATE, 21: SunshineWoods_Eventscript_MitsuruOnWarp21
    ]
    MAP_SCRIPT_ON_FRAME_TABLE [
        VAR_TEMP_8, 0: SunshineWoods_CheckIfAkihikoPrologueIsDoneForMitsuru
        VAR_AKIHIKOS_INTRO_STATE, 6: SunshineWoods_Eventscript_OnFrame6
       ]
}

script SunshineWoods_CheckIfAkihikoPrologueIsDoneForMitsuru {
    subquestmenu(QUEST_MENU_CHECK_COMPLETE, STORY_QUESTS, AKIHIKOS_PROLOGUE)
    if (!var(VAR_RESULT) && var(VAR_MITSURUS_INTRO_STATE) == 100){
        clearflag(FLAG_TEMP_8)
        clearflag(FLAG_HIDE_QUEST_ICON)
    }
    setvar(VAR_TEMP_8, 1)
}

script  SunshineWoods_Eventscript_OnWarp6 {
    applymovement(LOCALID_PLAYER, faceright)
    hideobjectat (4, MAP_SUNSHINE_WOODS)
}

script SunshineWoods_Eventscript_MitsuruOnWarp21 {
}

movement playerfaceright {
    face_right
}

script SunshineWoods_Eventscript_DoNotEnterTheGrass {
    lock
    applymovement(2, facedown)
    applymovement(3, facedown)
    waitmovement(0)
    msgbox(format("{SPEAKER NAME_S}{CREATE MUGSHOT_S EMOTE_NORMAL}Alright, we're here.\p
    Don't enter the tall grass, okay?\p
    {SPEAKER NAME_MK}{DESTROY_MUGSHOT}Okay!"))
    fadescreenswapbuffers(FADE_TO_BLACK)
    release
    setvar(VAR_AKIHIKOS_INTRO_STATE, 6)
    setflag(FLAG_HIDE_HELP_BUTTON) 
    warpsilent(MAP_SUNSHINE_WOODS, 3, 5)
}

script SunshineWoods_Eventscript_OnFrame6 {
    goto(SunshineWoods_Eventscript_PlayingInTheWoods)    
}

script SunshineWoods_Eventscript_PlayingInTheWoods {
    lock 
    delay(60)
    applymovement(3, SunshineWoods_Eventscript_MikiFoundSomething)
    waitmovement(0)
    goto (SunshineWoods_Eventscript_PawmisCry)
    release
}

movement SunshineWoods_Eventscript_MikiFoundSomething {
    walk_up
    delay_16
    walk_down
    delay_16
    emote_music
    jump_in_place_right *2
    walk_fast_left
}

script SunshineWoods_Eventscript_PawmisCry {
    lock
    msgbox(format("{SPEAKER NAME_MK}Big bro! Look! I just made this flower crown! It's for you!\p
    {SPEAKER NAME_A}{CREATE MUGSHOT_A EMOTE_HAPPY}It's very pretty! Thank you, Miki!"))
    release
    goto (SunshineWoods_Eventscript_ShinjiAskingForAFlowerCrown)
}

script SunshineWoods_Eventscript_ShinjiAskingForAFlowerCrown {
    lock
    applymovement(3, faceup)
    waitmovement(0)
    msgbox(format("{SPEAKER NAME_S}{CREATE MUGSHOT_S EMOTE_SMILE}Will you make one for me too, Miki?\p
    {SPEAKER NAME_MK}{DESTROY_MUGSHOT}Of course!"))
    closemessage
    release
    goto(SunshineWoods_Eventscript_PawmiCries)
}

script SunshineWoods_Eventscript_PawmiCries {
    lock
    playmoncry(SPECIES_PAWMI, CRY_MODE_NORMAL)
    waitmoncry
    applymovement(LOCALID_PLAYER, SunshineWoods_Eventscript_AkihikoAlerted)
    applymovement(2, SunshineWoods_Eventscript_ShinjiroAlerted)
    applymovement(3, SunshineWoods_Eventscript_MikiAlerted)
    delay(30)
    waitmovement
    msgbox(format("{SPEAKER NAME_A}{CREATE MUGSHOT_A EMOTE_SURPRISED}W-What was that?\p
    {SPEAKER NAME_S}{CREATE MUGSHOT_S EMOTE_SERIOUS}Could be a wild Pokémon. Be careful!"))
    waitmessage
    release
    goto (SunshineWoods_Eventscript_PawmiAppears)
}

movement SunshineWoods_Eventscript_AkihikoAlerted {
    emote_exclamation_mark
    delay_16
    delay_16
    face_left
    delay_16
    delay_16
    face_right
}
movement SunshineWoods_Eventscript_ShinjiroAlerted {
    emote_exclamation_mark
    delay_16
    face_up
    delay_16
    face_right
    delay_16
    face_down
}
movement SunshineWoods_Eventscript_MikiAlerted {
    emote_exclamation_mark
    jump_in_place_left
}

movement jumpright {
    jump_right
}

script SunshineWoods_Eventscript_PawmiAppears {
    lock
    showobjectat(4, MAP_SUNSHINE_WOODS)
    playmoncry(SPECIES_PAWMI, CRY_MODE_NORMAL)
    waitmoncry
    applymovement(4, jumpright)
    applymovement(3, SunshineWoods_Eventscript_Stayback)
    applymovement(LOCALID_PLAYER, SunshineWoods_Eventscript_Stayback)
    applymovement(2, SunshineWoods_Eventscript_OnGuard)
    waitmovement
    release
    goto(SunshineWoods_Eventscript_PawmisInjured)
}

movement SunshineWoods_Eventscript_Stayback {
    emote_exclamation_mark
    face_left
    lock_facing_direction
    jump_right
    unlock_facing_direction
    }
movement SunshineWoods_Eventscript_OnGuard {
    emote_exclamation_mark
    walk_fast_down
    face_left   
}

script SunshineWoods_Eventscript_PawmisInjured {
    lock
    playmoncry(SPECIES_PAWMI, CRY_MODE_NORMAL)
    waitmoncry
    applymovement(4, SunshineWoods_Eventscript_PawmiIsWeak)
    waitmovement
    delay(16)
    msgbox(format("{SPEAKER NAME_A}{CREATE MUGSHOT_A EMOTE_SERIOUS}Wait...\p
    {SPEAKER NAME_S}{CREATE MUGSHOT_S EMOTE_SURPRISED}It looks heavily injured.\p
    {SPEAKER NAME_MK}{DESTROY_MUGSHOT}We should take it back to the orphanage!\p
    {SPEAKER NAME_A}{CREATE MUGSHOT_A EMOTE_SERIOUS}Yeah!"))
    release
    goto (TakePawmiToHeal)
}

movement SunshineWoods_Eventscript_PawmiIsWeak {
    emote_poisoned
    shake_vertical
    delay_16
    shake_vertical
    delay_16
}

script TakePawmiToHeal {
    lock
    applymovement(LOCALID_PLAYER, SunshineWoods_Eventscript_Akihiko_Picking_up_Pawmi)
    msgbox(format("{SPEAKER NAME_A}{CREATE MUGSHOT_A EMOTE_SMILE}Come on, lil' guy."))
    closemessage
    fadescreenswapbuffers(FADE_TO_BLACK)
    setvar(VAR_AKIHIKOS_INTRO_STATE, 7)
    removeobject(4)
    removeobject(2)
    removeobject(3)
    setflag(FLAG_TEMP_1)
    warpsilent(MAP_BLANK_MAP,0,0)
    release
    
}
//movement SunshineWoods_Eventscript_Shinjiro_joins {
 //   walk_down
//}
//movement SunshineWoods_Eventscript_Miki_joins {
//    walk_left
//}
movement SunshineWoods_Eventscript_Akihiko_Picking_up_Pawmi {
    walk_left
}

script OW_Chewtle {
    lock
    faceplayer
    startoverworldencounter(5)
    removeobject(6)
    setflag(FLAG_TEMP_2)
    release
    end
}

script SunshineWoods_Eventscript_DirectionSign {
    lock
    msgbox(format("Sunshine Orphanage {UP_ARROW}\n
    Sunshine Town {LEFT_ARROW}"))
    release
    end
}

script SunshineWoods_Eventscript_IkeBattle{
    trainerbattle_single(TRAINER_IKE, SunshineWoods_Text_IkeIntro, SunshineWoods_Text_IkeDefeated)
    setspeaker("Ike")
    msgbox(SunshineWoods_Text_IkePostBattle, MSGBOX_AUTOCLOSE)
    end
}

script SunshineWoods_Eventscript_HotaruBattle {
    trainerbattle_single(TRAINER_HOTARU, SunshineWoods_Text_HotaruIntro, SunshineWoods_Text_HotaruDefeated)
    setspeaker("Hotaru")
    msgbox(SunshineWoods_Text_HotaruPostBattle, MSGBOX_AUTOCLOSE)
    end
}

text SunshineWoods_Text_IkeIntro{
    format("
    Hey! If you have a Pokémon, let's battle!")
}

text SunshineWoods_Text_IkeDefeated {
    format("Woah! You're quite good!")
}

text SunshineWoods_Text_IkePostBattle {
    format("Your Pokémon looks... kinda different.")
}

text SunshineWoods_Text_HotaruIntro {
    format("I have cute Pokémon!")
}

text SunshineWoods_Text_HotaruDefeated {
    format("They'll look even cuter when evolved. I even have them hold Soothe Bells!")
}

text SunshineWoods_Text_HotaruPostBattle {
    format("You want to know where I got the items?\p
    A grandma in Sunshine Town gave them to me after I played with her Pokémon.")
}

script SunshineWoods_Eventscript_PolicemenBlockingOrphanage1 {
    setspeaker("Police Officer 1")
    if(var(VAR_MITSURUS_INTRO_STATE) == 22){
        msgbox(format("Who the hell would explode an orphanage? Are they even human?"))
    }
    else{
        msgbox(format("Our apologies but Sunshine Orphanage is currently off-limits."))
    }
    closemessage
    end
}

script SunshineWoods_Eventscript_PolicemenBlockingOrphanage2 {
    setspeaker("Police Officer 2")
    if(var(VAR_MITSURUS_INTRO_STATE) == 22){
        msgbox(format("Maybe this is what Kurosawa-san was talking about... If only the higher-ups listened to him."))
    }
    else{
        msgbox(format("Our apologies but Sunshine Orphanage is currently off-limits."))
    }
    closemessage
    end
}

script SunshineWoods_Eventscript_MitsuruTailingKGStaffTrigger2 {
    lock
    applymovement(LOCALID_PLAYER, moves(player_run_up, face_left))
    waitmovement
    applymovement(OBJ_EVENT_ID_FOLLOWER, moves(walk_left, walk_up, face_right))
    waitmovement
    goto(SunshineWoods_Eventscript_KGStaffTalkingAboutAkiShinji)
}
script SunshineWoods_Eventscript_MitsuruTailingKGStaffTrigger3 {
    lock
    applymovement(LOCALID_PLAYER, moves(player_run_up*2, face_left))
    waitmovement
    applymovement(OBJ_EVENT_ID_FOLLOWER, moves(walk_left, walk_up, face_right))
    waitmovement
    goto(SunshineWoods_Eventscript_KGStaffTalkingAboutAkiShinji)
}

script SunshineWoods_Eventscript_KGStaffTalkingAboutAkiShinji {
    special(SpawnCameraObject)
    applymovement(OBJ_EVENT_ID_CAMERA, moves(walk_right, walk_diag_southeast))
    waitmovement
    delay(20)
    applymovement(11, walkinplacedown)
    waitmovement
    setspeaker("Man In Black 1")
    msgbox(format("Keep your eyes peeled. One has silver hair and the other wears a black-red beanie."))
    applymovement(12, walkinplaceup)
    waitmovement
    setspeaker("Man In Black 2")
    msgbox(format("You should cover the east. I'll search the south."))
    closemessage
    delay(20)
    applymovement(12, moves(walk_fast_down*6))
    delay(4)
    applymovement(11, moves(walk_fast_right*8))
    delay(30)
    applymovement(OBJ_EVENT_ID_CAMERA, moves(walk_left, walk_diag_northwest))
    waitmovement(OBJ_EVENT_ID_CAMERA)
    delay(20)
    special(RemoveCameraObject)
    setobjectxyperm(12, 12, 17)
    setobjectxy(12, 12, 17)
    setobjectnewmovementtype(12, MOVEMENT_TYPE_WANDER_LEFT_AND_RIGHT)
    setobjectxyperm(11, 20, 10)
    setobjectxy(11, 20, 10)
    setobjectnewmovementtype(11, MOVEMENT_TYPE_WANDER_AROUND)
    release
    setvar(VAR_MITSURUS_INTRO_STATE, 22)
    fwdtime(9, 45)
    end
}

script SunshineWoods_Eventscript_KGStaff1_Battle {
    lock
    faceplayer
    getplayerxy(VAR_0x8000, VAR_0x8001)
    setobjectxy(INTELEON_T_SW, VAR_0x8000, VAR_0x8001)
    setobjectxyperm(INTELEON_T_SW, VAR_0x8000, VAR_0x8001)
    msgbox(format("{SPEAKER NAME_MIB}Wait, you're...!"))
    trainerbattle_no_intro(TRAINER_KG_STAFF_SUNSHINE_WOODS_1, format("This is not good. I must alert the manager."))
    clearflag(FLAG_TEMP_6)
    addobject(INTELEON_T_SW)
    playse(SE_LEDGE)
    specialvar(VAR_RESULT, GetPlayerFacingDirection)
    if (var(VAR_RESULT) == DIR_SOUTH){
        applymovement(INTELEON_T_SW, moves(jump_down))
        waitmovement
    }
    elif (var(VAR_RESULT) == DIR_EAST){
        applymovement(INTELEON_T_SW, moves(jump_right))
        waitmovement
    }
    else{
        applymovement(INTELEON_T_SW, moves(jump_up))
        waitmovement
    }
    fadescreenswapbuffers(FADE_TO_BLACK)
    playse(SE_M_TAKE_DOWN)
    waitse
    playse(SE_M_COMET_PUNCH)
    waitse
    removeobject(12)
    clearflag(FLAG_TEMP_5)
    addobject(14, MAP_SUNSHINE_WOODS)
    specialvar(VAR_RESULT, GetPlayerFacingDirection)
    if (var(VAR_RESULT) == DIR_SOUTH){
        applymovement(INTELEON_T_SW, moves(walk_down, face_up))
    }
    else{
        applymovement(INTELEON_T_SW, moves(walk_up, face_down))
    }
    waitmovement
    fadescreenswapbuffers(FADE_FROM_BLACK)
    delay(20)
    msgbox(format("{SPEAKER NAME_M}{CREATE MUGSHOT_M EMOTE_CONFIDENT}Great teamwork, Inteleon.\p
    Now let's check if he has any useful information."))
    closemessage
    playse(SE_RG_CARD_FLIPPING)
    specialvar(VAR_RESULT, GetPlayerFacingDirection)
    if (var(VAR_RESULT) == DIR_SOUTH){
        applymovement(LOCALID_PLAYER, moves(walk_in_place_down))
    }
    elif (var(VAR_RESULT) == DIR_EAST){
        applymovement(LOCALID_PLAYER, moves(walk_in_place_right))
    }
    else{
        applymovement(LOCALID_PLAYER, moves(walk_in_place_up))
    }
    waitmovement
    giveitem(ITEM_EXP_CANDY_XS, 10)
    playse(SE_M_SCRATCH)
    setvar(VAR_0x8000, PIC_AKIHIKO)
    callnative(DrawMugshot)
    msgbox(format("Besides the Exp candies, there's also a picture of a silver-haired boy."))
    closemessage
    msgbox(format("{SPEAKER NAME_M}{CREATE MUGSHOT_M EMOTE_NORMAL}So this is one of the boys they're searching for?"))
    closemessage
    callnative(ClearMugshot)
    playse(SE_RG_CARD_FLIPPING)
    applymovement(LOCALID_PLAYER, emotepensive)
    waitmovement
    msgbox(format("{SPEAKER NAME_M}{CREATE MUGSHOT_M EMOTE_NORMAL}There's nothing else here, let's go, Inteleon."))
    closemessage
    specialvar(VAR_RESULT, GetPlayerFacingDirection)
    if (var(VAR_RESULT) == DIR_SOUTH){
        applymovement(INTELEON_T_SW, moves(walk_left, walk_up*2, walk_right, set_invisible))
    }
    elif (var(VAR_RESULT) == DIR_EAST){
        applymovement(INTELEON_T_SW, moves(walk_left, walk_down, set_invisible))
    }
    else{
        applymovement(INTELEON_T_SW, moves(walk_left, walk_down*2, walk_right, set_invisible))
    }
    waitmovement
    removeobject(INTELEON_T_SW)
    setflag(FLAG_TEMP_6)
    setvar(VAR_RESULT, 0)
    setvar(VAR_0x8001, 0)
    addvar(VAR_TEMP_1, 1)
    release //10:05 or 10:25
    end
}

script SunshineWoods_Eventscript_KGStaff1_PostBattle {
    msgbox(format("{SPEAKER NAME_M}{CREATE MUGSHOT_M EMOTE_NORMAL}(He's out cold. There's no further information I can get out of him.)"))
    closemessage
}

script SunshineWoods_Eventscript_KGStaff2_Battle {
    lock
    faceplayer
    msgbox(format("{SPEAKER NAME_MIB}The Kirijo Young Lady?"))
    closemessage
    getplayerxy(VAR_0x8000, VAR_0x8001)
    setobjectxy(INTELEON_T_SW, VAR_0x8000, VAR_0x8001)
    setobjectxyperm(INTELEON_T_SW, VAR_0x8000, VAR_0x8001)
    trainerbattle_no_intro(TRAINER_KG_STAFF_SUNSHINE_WOODS_2, format("What are you doing here?"))
    clearflag(FLAG_TEMP_6)
    addobject(INTELEON_T_SW)
    playse(SE_LEDGE)
    specialvar(VAR_RESULT, GetPlayerFacingDirection)
    if (var(VAR_RESULT) == DIR_SOUTH){
        applymovement(INTELEON_T_SW, moves(jump_down))
    }
    elif (var(VAR_RESULT) == DIR_WEST){
        applymovement(INTELEON_T_SW, moves(jump_left))
    }
    elif (var(VAR_RESULT) == DIR_EAST){
        applymovement(INTELEON_T_SW, moves(jump_right))
    }
    else{
        applymovement(INTELEON_T_SW, moves(jump_up))
    }
    waitmovement
    fadescreenswapbuffers(FADE_TO_BLACK)
    playse(SE_M_TAKE_DOWN)
    waitse
    playse(SE_M_COMET_PUNCH)
    waitse
    removeobject(11)
    clearflag(FLAG_TEMP_7)
    addobject(15, MAP_SUNSHINE_WOODS)
    if (var(VAR_TEMP_1) == 0){
        removeobject(14)
    }
    specialvar(VAR_RESULT, GetPlayerFacingDirection)
    if (var(VAR_RESULT) == DIR_SOUTH){
        applymovement(INTELEON_T_SW, moves(walk_down, face_up))
        waitmovement
    }
    elif (var(VAR_RESULT) == DIR_WEST){
        applymovement(INTELEON_T_SW, moves(walk_left, face_right))
        waitmovement
    }
    elif (var(VAR_RESULT) == DIR_EAST){
        applymovement(INTELEON_T_SW, moves(walk_right, face_left))
        waitmovement
    }
    else{
        applymovement(INTELEON_T_SW, moves(walk_up, face_down))
        waitmovement
    }
    
    fadescreenswapbuffers(FADE_FROM_BLACK)
    delay(20)
    msgbox(format("{SPEAKER NAME_M}{CREATE MUGSHOT_M EMOTE_CONFIDENT}Great job, Inteleon.\p
    Now let's see..."))
    closemessage
    playse(SE_RG_CARD_FLIPPING)
    specialvar(VAR_RESULT, GetPlayerFacingDirection)
    if (var(VAR_RESULT) == DIR_SOUTH){
        applymovement(LOCALID_PLAYER, moves(walk_in_place_down))
    }
    elif (var(VAR_RESULT) == DIR_EAST){
        applymovement(LOCALID_PLAYER, moves(walk_in_place_right))
    }
    elif (var(VAR_RESULT) == DIR_WEST){
        applymovement(LOCALID_PLAYER, moves(walk_in_place_left))
    }
    else{
        applymovement(LOCALID_PLAYER, moves(walk_in_place_up))
    }
    waitmovement
    giveitem(ITEM_EXP_CANDY_XS, 10)
    playse(SE_M_SCRATCH)
    setvar(VAR_0x8000, PIC_SHINJIRO)
    callnative(DrawMugshot)
    msgbox(format("In another pocket, there's also a picture of a boy wearing black-red beanie."))
    closemessage
    msgbox(format("{SPEAKER NAME_M}{CREATE MUGSHOT_M EMOTE_CONFIDENT}Magnifique. Now I know how one of their targets looks like."))
    closemessage
    callnative(ClearMugshot)
    playse(SE_RG_CARD_FLIPPING)
    applymovement(LOCALID_PLAYER, emotepensive)
    waitmovement
    msgbox(format("{SPEAKER NAME_M}{CREATE MUGSHOT_M EMOTE_NORMAL}There's nothing else here, time to go."))
    closemessage
    specialvar(VAR_RESULT, GetPlayerFacingDirection)
    if (var(VAR_RESULT) == DIR_SOUTH){
        applymovement(INTELEON_T_SW, moves(walk_left, walk_up*2, walk_right, set_invisible))
    }
    elif (var(VAR_RESULT) == DIR_EAST){
        applymovement(INTELEON_T_SW, moves(walk_up, walk_left*2, walk_down, set_invisible))
    }
    elif (var(VAR_RESULT) == DIR_WEST){
        applymovement(INTELEON_T_SW, moves(walk_up, walk_right*2, walk_down, set_invisible))
    }
    else{
        applymovement(INTELEON_T_SW, moves(walk_left, walk_down*2, walk_right, set_invisible))
    }
    waitmovement
    removeobject(INTELEON_T_SW)
    setflag(FLAG_TEMP_6)
    setvar(VAR_0x8000, 0)
    setvar(VAR_0x8001, 0)
    addvar(VAR_TEMP_1, 1)
    release //10:05 or 10:25
    end
}

script SunshineWoods_Eventscript_KGStaff2_PostBattle {
    msgbox(format("{SPEAKER NAME_M}{CREATE MUGSHOT_M EMOTE_NORMAL}(He's fainted. Inteleon hit him a little too hard but he should be fine.)"))
    closemessage
}

script SunshineWoods_Eventscript_CheckIfBothKGStaffAreDown {
    if (var(VAR_TEMP_1) <2){
        lock
        applymovement(LOCALID_PLAYER, walkright)
        waitmovement
        msgbox(format("{SPEAKER NAME_M}{CREATE MUGSHOT_M EMOTE_NORMAL}(Let's check the men in black for information.)"))
        closemessage
        release
        end
    }
    if (var(VAR_TEMP_1) == 2){
        lock
        applymovement(LOCALID_PLAYER, faceright)
        waitmovement
        hidefollower
        getplayerxy(VAR_0x8000, VAR_0x8001)
        setobjectxy(INTELEON_T_SW, VAR_0x8000, VAR_0x8001)
        setobjectxyperm(INTELEON_T_SW, VAR_0x8000, VAR_0x8001)
        clearflag(FLAG_TEMP_6)
        addobject(INTELEON_T_SW)
        applymovement(INTELEON_T_SW, moves(walk_right, face_left))
        waitmovement
        msgbox(format("{SPEAKER NAME_M}{CREATE MUGSHOT_M EMOTE_NORMAL}Can you go help the rescue at the orphanage, Inteleon?\p
        Don't worry about me, I'll just go back to the house in Route 2 to do some research about the boys they're so interested in."))
        playmoncry(SPECIES_INTELEON, CRY_MODE_NORMAL)
        applymovement(INTELEON_T_SW, shakehorizontal)
        waitmovement
        msgbox(format("{SPEAKER NAME_ITLN}{CREATE MUGSHOT_INTELEON EMOTE_SERIOUS}(Inteleon shaked its head.)\p
        {SPEAKER NAME_M}{CREATE MUGSHOT_M EMOTE_SAD}Sigh... Then how about after I get to the house safely?\p
        I promise I'll stay there until you return."))
        playmoncry(SPECIES_INTELEON, CRY_MODE_NORMAL)
        applymovement(INTELEON_T_SW, walkinplaceleft)
        waitmovement
        msgbox(format("{SPEAKER NAME_ITLN}{CREATE MUGSHOT_INTELEON EMOTE_SERIOUS}(Inteleon reluctantly agreed.)\p
        {SPEAKER NAME_M}{CREATE MUGSHOT_M EMOTE_SMILE}C'est magnifique. Let's move out."))
        closemessage
        setvar(VAR_MITSURUS_INTRO_STATE, 22)
        setvar(VAR_0x8000, 0)
        setvar(VAR_0x8001, 0)
        fwdtime(15, 05)
        warp(MAP_ROUTE2_HOUSE, 3, 9)
    }
}

script SunshineWoods_Eventscript_AkihikoPrologueStart {
    hidefollower
    hidefollowernpc
    lock
    if (!flag(FLAG_HELP_MAKE_CHOICE)){
        call(EventScript_ShowHelpChoice)
        setflag(FLAG_HELP_MAKE_CHOICE)
    }
    faceplayer
    setflag(FLAG_HIDE_QUEST_ICON)
    if (var(VAR_TEMP_0) == 1){
        msgbox(format("{SPEAKER NAME_A}{CREATE MUGSHOT_A EMOTE_SAD}.......{PAUSE 60}
        Sorry, I want to be alone for a while. Can we talk later?"))
        closemessage
        release
        end
    }
    else{
        fadenewbgm(MUS_XC2_GARFONT_MERCERNARIES)
        msgbox(format("{SPEAKER NAME_M}{CREATE MUGSHOT_M EMOTE_SMILE}So this is where you are.\p
        {SPEAKER NAME_A}{CREATE MUGSHOT_A EMOTE_SMILE}Hey, Kirijo. Have a seat."))
        closemessage
        specialvar(VAR_RESULT, GetPlayerFacingDirection)
        if(var(VAR_RESULT) == DIR_NORTH){
            applymovement(LOCALID_PLAYER, moves(walk_left*2, walk_up*4, face_down))
            applymovement(16, moves(walk_left, walk_up*3, face_down))
        }
        elif(var(VAR_RESULT) == DIR_EAST){
            applymovement(LOCALID_PLAYER, moves(walk_left, walk_up*3, face_down))
            applymovement(16, moves(walk_left, walk_up*3, face_down))
        }
        elif(var(VAR_RESULT) == DIR_WEST){
            applymovement(16, moves(walk_left, walk_up*3, face_down))
            delay(8)
            applymovement(LOCALID_PLAYER, moves(walk_left*3, walk_up*3, face_down))
        }
        else{
            applymovement(LOCALID_PLAYER, moves(walk_left*2, walk_up*2, face_down))
            delay(8)
            applymovement(16, moves(walk_left, walk_up*3, face_down))
        }
        waitmovement
        delay(60)
        msgbox(format("{SPEAKER NAME_M}{CREATE MUGSHOT_M EMOTE_SMILE}What are you doing here, if I may ask?\p
        {SPEAKER NAME_A}{CREATE MUGSHOT_A EMOTE_SMILE}Just... {PAUSE 60}reminiscing,{PAUSE 60} I guess.\p
        This was where I, my sis and Shinji used to hang out.\p
        {SPEAKER NAME_M}{CREATE MUGSHOT_M EMOTE_SMILE}That sounded like good times."))
        closemessage
        delay(30)
        applymovement(16, emotepensive)
        waitmovement
        msgbox(format("{SPEAKER NAME_A}{CREATE MUGSHOT_A EMOTE_SAD}Kirjo, uh...{PAUSE 60}
        This is kinda sudden but I guess I just want to talk about this to someone."))
        msgbox(format("{SPEAKER NAME_A}{CREATE MUGSHOT_A EMOTE_NORMAL}Would you listen?
        {COLOR GREEN}(Begin Akihiko's prologue quest?)"), MSGBOX_YESNO)
        if (var(VAR_RESULT) == FALSE){
            msgbox(format("{SPEAKER NAME_A}{CREATE MUGSHOT_A EMOTE_SAD}Oh, okay......"))
            closemessage
            playse(SE_M_STAT_DECREASE)
            applymovement(16, pointdown3)
            waitmovement
            subvar(VAR_AKIHIKO_RELATIONSHIP_POINTS, 3)
            setvar(VAR_TEMP_0, 1)
            release
            end
        }
        else
        {
            closemessage
            playse(SE_SHINY)
            applymovement(16, pointup3)
            waitmovement
            msgbox(format("{SPEAKER NAME_A}{CREATE MUGSHOT_A EMOTE_SMILE}Thanks!"))
            closemessage
            fadescreenswapbuffers(FADE_TO_BLACK)
            switchmaincharacter
            setvar(VAR_AKIHIKOS_INTRO_STATE, 0)
            setvar(VAR_MITSURUS_INTRO_STATE, 50)
            unlockcharacter(CHARACTER_AKIHIKO)
            clearflag(FLAG_LOCK_AKIHIKO)
            setflag(FLAG_LOCK_MITSURU)
            setflag(FLAG_LOCK_INTELEON_T)
            setflag(FLAG_LOCK_SHINJIRO)
            clearflag(FLAG_SYS_B_DASH)
            clearflag(FLAG_SYS_POKEDEX_GET)
            clearflag(FLAG_SYS_POKEMON_GET)
            removeitem(ITEM_EXP_SHARE)
            removeitem(ITEM_CAMP_GEARS)
            removeitem(ITEM_ESCAPE_ROPE)
            removeitem(ITEM_MACH_BIKE)
            removeallitem(ITEM_POKE_BALL, VAR_0x800A)
            removeallitem(ITEM_GREAT_BALL, VAR_0x800B)
            destroyfollowernpc
            manuallysettime(WEEKDAY_WED, 16, 30)
            subquestmenu(QUEST_MENU_SET_ACTIVE, STORY_QUESTS, AKIHIKOS_PROLOGUE)
            setvar(VAR_STORY_QUESTS_STATE, 1)
            warpsilent(MAP_SUNSHINE_ORPHANAGE, 13, 18)
            special(DrawWholeMapView)
            waitstate
        }
    }
}

movement pointup3 {
    point_up_3
}