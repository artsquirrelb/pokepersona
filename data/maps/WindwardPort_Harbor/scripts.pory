mapscripts WindwardPort_Harbor_MapScripts {
    MAP_SCRIPT_ON_TRANSITION: WindwardPort_Harbor_MapScripts_OnTransition
    MAP_SCRIPT_ON_FRAME_TABLE[
        VAR_CHAPTER_1_STATE, 4: WindwardPort_Harbor_MapScripts_OnFrame4
    ]
    MAP_SCRIPT_ON_RESUME: WindwardPort_Harbor_MapScripts_UpdatingNPCTime
    
}
script WindwardPort_Harbor_MapScripts_OnTransition{
    setflag(FLAG_VISITED_WINDWARD_PORT)
    setrespawn(HEAL_LOCATION_WINDWARD_PORT)
    setflag(FLAG_TEMP_1)
    setflag(FLAG_TEMP_2)
    setflag(FLAG_TEMP_3)
    if(var(VAR_CHAPTER_1_STATE) == 4){
        clearflag(FLAG_TEMP_2)
        setflag(FLAG_SPAWN_INVISIBLE)
    }
    gettime
    if ((var(VAR_WINDWARD_HARBOR_GHOST_BOY) >= 0 && var(VAR_WINDWARD_HARBOR_GHOST_BOY) <= 20) || var(VAR_WINDWARD_HARBOR_GHOST_BOY) == 100)
    {
        if (var(VAR_0x8000) < 6 || var(VAR_0x8000) >= 21){
            fadenewbgm(MUS_DP_LAKE_CAVERNS)
            savebgm(MUS_DP_LAKE_CAVERNS)
            clearflag(FLAG_TEMP_3)
            setflag(FLAG_TEMP_5)
        }
    }
    if(var(VAR_WINDWARD_HARBOR_GHOST_BOY) == 1){
        setobjectxyperm(GHOSTBOY, 17, 29)
        setobjectmovementtype(GHOSTBOY, MOVEMENT_TYPE_FACE_RIGHT)
    }
}
script WindwardPort_Harbor_MapScripts_OnFrame4 {
    goto(WindwardPort_Harbor_Eventscript_MitsuruRemindsToVisitPersonalLab)
}

script WindwardPort_Harbor_MapScripts_UpdatingNPCTime {
    gettime
    if (var(VAR_0x8000) < 7 || var(VAR_0x8000) >= 17){
        clearflag(FLAG_TEMP_1)
    }
    else{
        setflag(FLAG_TEMP_1)
    }
    if (var(VAR_0x8000) < 7 || var(VAR_0x8000) >= 21){
        setflag(FLAG_TEMP_4)
    }
    else{
        clearflag(FLAG_TEMP_4)
    }
}

script WindwardPort_Harbor_Eventscript_SailorOnGuard {
    lock
    faceplayer
    setspeaker("Anderson")
    gettime
    if(var(VAR_0x8000) < 7 || var(VAR_0x8000) >= 17){
        showmoneybox(0, 0)
        msgbox(format("This ship is departing for Coralport.\p
        It will arrive at the destination in approximately {COLOR RED}3 hours{COLOR 2}.\p
        It costs {COLOR GREEN}¥100 {COLOR 2}per ticket. Would you like to board the ship?"), MSGBOX_YESNO)
        closemessage
        if (var(VAR_RESULT)){
            checkmoney(300)
            if (var(VAR_RESULT)){
                playse(SE_SHOP)
                removemoney(300)
                updatemoneybox
                setspeaker("Anderson")
                msgbox(format("This way, please."))
                closemessage
                hidemoneybox
                applymovement(1, walkinplacedown)
                waitmovement
                delay(30)
                addtime(0, 3, 0)
                warp(MAP_CORALPORT, 4, 20)
                waitstate
            }
            else{
                setspeaker("Anderson")
                msgbox(format("I'm sorry but you don't have enough money for 3 tickets."))
                closemessage
                hidemoneybox
                release
                end
            }
        }
        else{
            hidemoneybox
        }
    }
    else{
        call(WindwardPort_Harbor_Eventscript_ShipOutOfCommission)
    }
    closemessage
    release
    end
}

script WindwardPort_Harbor_Eventscript_ShipOutOfCommission {
    msgbox(format("The ship to Coralport is currently out of commission.\p
    Please come back between 5PM and 7AM."))
}

script WindwardPort_Harbor_Eventscript_VeteranF {
    lock
    faceplayer
    setspeaker("Jura")
    msgbox(format("Windward Port is in the center of the region so you would think it's the biggest.\p
    In fact, the biggest port is the one next to Faeruma City, my hometown.\p
    Faeruma Pharmacy factory produces most of the region's medicine supply and that's where Faeruma Port comes in."))
    closemessage
    release
    end
}

script WindwardPort_Harbor_Eventscript_Pelipper {
    moncryscript(SPECIES_PELIPPER)
    release
    end
}

script WindwardPort_Harbor_Eventscript_Gurdurr {
    moncryscript(SPECIES_GURDURR)
    release
    end
}

script WindwardPort_Harbor_Eventscript_Bewear {
    moncryscript(SPECIES_BEWEAR)
    release
    end
}

script WindwardPort_Harbor_Eventscript_Worker {
    lock
    faceplayer
    setspeaker("Earu")
    msgbox(format("I'm so glad my shift is in the daytime.\p
    My co-workers said they kept hearing a weeping sound at night and honestly, I don't want to find out where that sound comes from."))
    closemessage
    release
    end
}

script WindwardPort_Harbor_Eventscript_Sign {
    lock
    msgbox(format("Please stay clear of the entrance."))
    release
    end
}

script WindwardPort_Harbor_Eventscript_MitsuruRemindsToVisitPersonalLab {
    lock
    delay(20)
    applymovement(A_WP, faceleft)
    applymovement(M_WP, moves(walk_left, face_right))
    delay(4)
    applymove(S_WP, moves(walk_down, face_left))
    delay(20)
    msgbox(format("{SPEAKER NAME_M}{CREATE MUGSHOT_M EMOTE_SMILE}We're in Windward Port at last.\p
    {SPEAKER NAME_M}{CREATE MUGSHOT_M EMOTE_NORMAL}Let's visit my personal lab to pick up our package.\p
    It's the house with the tall red roof at the south of the city.\p
    {SPEAKER NAME_A}{CREATE MUGSHOT_A EMOTE_NORMAL}Sure."))
    closemessage
    delay(20)
    clearflag(FLAG_SPAWN_INVISIBLE)
    applymoveplayer(moves(face_left, set_visible), 1)
    applymovement(M_WP, moves(walk_right, set_invisible))
    applymovement(S_WP, moves(walk_up, set_invisible))
    applymove(A_WP, invisible)
    delay(30)
    removeobject(A_WP)
    delay(1)
    removeobject(S_WP)
    delay(1)
    removeobject(M_WP)
    delay(1)
    setflag(FLAG_TEMP_2)
    setvar(VAR_CHAPTER_1_STATE, 5)
    release
    end
}

script WindwardPort_Harbor_Eventscript_GhostBoyEventTrigger { //VAR_TEMP_F == 0
    gettime
    if (var(VAR_0x8000) < 6 || var(VAR_0x8000) >= 21){
        if (var(VAR_WINDWARD_HARBOR_GHOST_BOY) == 0 || var(VAR_WINDWARD_HARBOR_GHOST_BOY) == 2
        || var(VAR_WINDWARD_HARBOR_GHOST_BOY) == 4 || var(VAR_WINDWARD_HARBOR_GHOST_BOY) == 6
        || var(VAR_WINDWARD_HARBOR_GHOST_BOY) == 8 || var(VAR_WINDWARD_HARBOR_GHOST_BOY) == 10
        || var(VAR_WINDWARD_HARBOR_GHOST_BOY) == 12)
        {
            specialvar(VAR_RESULT, GetPlayerFacingDirection)
            if(var(VAR_RESULT) == DIR_EAST){
                lock
                playse(SE_M_SWEET_SCENT)
                waitse
                msgbox(format("The wind is getting chilly."))
                closemessage
            }
            release
            end
        }
        if(var(VAR_WINDWARD_HARBOR_GHOST_BOY) == 1 || var(VAR_WINDWARD_HARBOR_GHOST_BOY) == 3
        || var(VAR_WINDWARD_HARBOR_GHOST_BOY) == 5 || var(VAR_WINDWARD_HARBOR_GHOST_BOY) == 7
        || var(VAR_WINDWARD_HARBOR_GHOST_BOY) == 9 || var(VAR_WINDWARD_HARBOR_GHOST_BOY) == 11
        || var(VAR_WINDWARD_HARBOR_GHOST_BOY) == 13)
        {
            lock
            msgbox(format("Leave the harbor and ignore the kid?"), MSGBOX_YESNO)
            if (var(VAR_RESULT)){
                playse(SE_M_SWEET_SCENT)
                waitse
                msgbox(format("The wind is getting stronger and chillier."))
                closemessage
                setvar(VAR_WINDWARD_HARBOR_GHOST_BOY, 44)
            }
            else{
                closemessage
                applymoveplayer(walkright)
            }
        release
        end
        }
    }
    release
    end
}

script WindwardPort_Harbor_Eventscript_GhostBoyMad1{
    lock
    clearflag(FLAG_TEMP_3)
    setobjectxyperm(GHOSTBOY, 0, 20)
    setobjectxy(GHOSTBOY, 0, 20)
    setobjectmovementtype(GHOSTBOY, MOVEMENT_TYPE_FACE_RIGHT)
    addobject(GHOSTBOY)
    goto(WindwardPort_Harbor_Eventscript_GhostBoyMad)
}

script WindwardPort_Harbor_Eventscript_GhostBoyMad2 {
    lock
    clearflag(FLAG_TEMP_3)
    setobjectxyperm(GHOSTBOY, 0, 21)
    setobjectxy(GHOSTBOY, 0, 21)
    setobjectmovementtype(GHOSTBOY, MOVEMENT_TYPE_FACE_RIGHT)
    addobject(GHOSTBOY)
    goto(WindwardPort_Harbor_Eventscript_GhostBoyMad)
}

script WindwardPort_Harbor_Eventscript_GhostBoyMad {
    special(StopMapMusic)
    setspeaker("Yamaki?")
    msgbox(format("Leaving a kid alone at night while he's waiting for you?\p
    Do you know what people like you are called?\p
    Yes, I'm talking to {COLOR RED}YOU{COLOR 2}. Not the characters {COLOR RED}YOU{COLOR 2}'re controlling.\p
    They aren't scumbags like {COLOR RED}YOU{COLOR 2}."))
    closemessage
    playse(SE_M_SWEET_SCENT)
    delay(30)
    setspeaker("Yamaki?")
    msgbox("Die.")
    setflag(FLAG_NO_WHITEOUT)
    trainerbattle_no_intro(TRAINER_GHOSTBOY, WindwardPort_Harbor_GhostBoy_Defeated)
    msgbox(format("The air is getting colder and colder.\p
    It's hard to open your eyes."))
    closemessage
    setspeaker("Yamaki?")
    msgbox("Leave. And don't your dare come back.")
    closemessage
    clearflag(FLAG_NO_WHITEOUT)
    clearflag(FLAG_HIDE_QUEST_ICON)
    questmenu(QUEST_MENU_COMPLETE_QUEST, QUEST_GHOST_BOY)
    warpwhitefade(MAP_ROUTE2_HOUSE, 4, 4)
    waitstate
}

text WindwardPort_Harbor_GhostBoy_Defeated {
    format("...")
}

script WindwardPort_Harbor_Eventscript_PreschoolerM {
    setflag(FLAG_HIDE_QUEST_ICON)
    lock
    faceplayer
    setspeaker("Yamaki")
    if(var(VAR_WINDWARD_HARBOR_GHOST_BOY) == 0){
        goto(WindwardPort_Harbor_Eventscript_StartQuestGhosBoy)
    }
    if(var(VAR_WINDWARD_HARBOR_GHOST_BOY) == 1){
        goto(WindwardPort_Harbor_Eventscript_FoundGhostBoy1)
    }
    if(var(VAR_WINDWARD_HARBOR_GHOST_BOY) == 2){
        goto(WindwardPort_Harbor_Eventscript_StartTreasureHunt2)
    }
    if(var(VAR_WINDWARD_HARBOR_GHOST_BOY) == 3){
        goto(WindwardPort_Harbor_Eventscript_TreasureHint)
    }
    if(var(VAR_WINDWARD_HARBOR_GHOST_BOY) == 4){
        subquestmenu(QUEST_MENU_CHECK_COMPLETE,QUEST_GHOST_BOY, SUB_QUEST_GHOST_BOY_2)
        if (!var(VAR_RESULT)){
            goto(WindwardPort_Harbor_Eventscript_FoundTreasure)
        }
        else{
            goto(WindwardPort_Harbor_Eventscript_StartQuiz3)
        }
    }
}

script WindwardPort_Harbor_Eventscript_StartQuestGhosBoy {
    playse(SE_M_BUBBLE)
    applymovement(GHOSTBOY, emotecry)
    msgbox(format("-sob- -sob-"))
    closemessage
    checkplayergender
    if (var(VAR_RESULT) == MALE){
        msgbox(format("{SPEAKER NAME_A}{CREATE MUGSHOT_A EMOTE_NORMAL}What's wrong, little boy?\nWhy are you here alone?"))
        setspeaker("Yamaki")
        msgbox(format("{DESTROY_MUGSHOT}No one wants to play with me...\p
        Will you play with me, mister?"), MSGBOX_YESNO)
        if(var(VAR_RESULT)){
            msgbox(format("{SPEAKER NAME_A}{CREATE MUGSHOT_A EMOTE_SMILE}Sure! Just for a little while, though.\p
            After that you gotta go home, alright?"))
            closemessage
            goto(WindwardPort_Harbor_Eventscript_StartHideAndSeek)
        }
        else{
            goto(WindwardPort_Harbor_Eventscript_HideAndSeekRejected)
        }
    }
    else{
        msgbox(format("{SPEAKER NAME_M}{CREATE MUGSHOT_M EMOTE_NORMAL}Why are you crying? Are you lost?"))
        setspeaker("Yamaki")
        msgbox(format("{DESTROY_MUGSHOT}No one wants to play with me...\p
        Will you play with me, miss?"), MSGBOX_YESNO)
        if(var(VAR_RESULT)){
            msgbox(format("{SPEAKER NAME_M}{CREATE MUGSHOT_M EMOTE_SMILE}I don't see why not.\p
            But promise me you'll go home after we're done playing?"))
            setspeaker("Yamaki")
            msgbox(format("{DESTROY_MUGSHOT}Promise!"))
            closemessage
            goto(WindwardPort_Harbor_Eventscript_StartHideAndSeek)
        }
        else{
            goto(WindwardPort_Harbor_Eventscript_HideAndSeekRejected)
        }
    }
}

script WindwardPort_Harbor_Eventscript_HideAndSeekRejected {
    setspeaker("Yamaki")
    msgbox(format("Why does no one want to play with me?"))
    closemessage
    clearflag(FLAG_HIDE_QUEST_ICON)
    refreshquesticon
    release
    end
}

script WindwardPort_Harbor_Eventscript_StartHideAndSeek {
    playse(SE_SHINY)
    applymove(GHOSTBOY, moves(emote_music))
    setspeaker("Yamaki")
    msgbox(format("Alright! Let's play a round of hide-and-seek!\p
    I'll go hide! Don't worry, I won't leave this harbor!"))
    closemessage
    setvar(VAR_WINDWARD_HARBOR_GHOST_BOY, 1)
    getobjectfacingdirection(GHOSTBOY, VAR_0x8002)
    if(var(VAR_0x8002) == DIR_WEST){
        applymove(GHOSTBOY, moves(walk_fast_down, walk_fast_left*4, walk_fast_down*6))
    }
    else{
        applymove(GHOSTBOY, moves(walk_fast_left*4, walk_fast_down*7))
    }
    release
    setobjectxy(GHOSTBOY, 17, 29)
    setobjectxyperm(GHOSTBOY, 17, 29)
    addobject(GHOSTBOY)
    lock
    setobjectnewmovementtype(GHOSTBOY, MOVEMENT_TYPE_FACE_RIGHT)
    applymove(GHOSTBOY, faceright)
    release
    end
}

script WindwardPort_Harbor_Eventscript_FoundGhostBoy1 {
    playse(SE_PIN)
    applymove(GHOSTBOY, emoteexclamation)
    msgbox(format("Oh! You found me!\p
    Hehehe, that makes me really happy. I thought you'd leave me alone.\p
    I'll go home now. Let's play again some times!"))
    closemessage
    setvar(VAR_WINDWARD_HARBOR_GHOST_BOY, 2)
    subquestmenu(QUEST_MENU_COMPLETE_QUEST, QUEST_GHOST_BOY, SUB_QUEST_GHOST_BOY_1)
    fadescreenswapbuffers(FADE_TO_BLACK)
    removeobject(GHOSTBOY)
    setflag(FLAG_TEMP_3)
    delay(30)
    fwdtime(7, 0)
    fadescreenswapbuffers(FADE_FROM_BLACK)
    release
    playse(SE_PIN)
    applymoveplayer(emoteexclamation)
    checkplayergender
    if(var(VAR_RESULT) == MALE){
        msgbox(format("{SPEAKER NAME_A}{CREATE MUGSHOT_A EMOTE_SURPRISED}(Did he just vanish??)\p
        {SPEAKER NAME_A}{CREATE MUGSHOT_A EMOTE_STRUGGLED}(Maybe I'm getting tired. It's pretty late-)\p
        {SPEAKER NAME_A}{CREATE MUGSHOT_A EMOTE_SURPRISED}(Wait! Since when the sun's out??)"))
        closemessage
    }
    else{
        msgbox(format("{SPEAKER NAME_M}{CREATE MUGSHOT_M EMOTE_SURPRISED}(Did my eyes play tricks on me? He just vanished.)\p
        {SPEAKER NAME_M}{CREATE MUGSHOT_M EMOTE_STRUGGLED}(Perhaps I do need to go rest. It's late after all-)\p
        {SPEAKER NAME_M}{CREATE MUGSHOT_M EMOTE_SURPRISED}(Wait, it's already morning?)"))
        closemessage
    }
    clearflag(FLAG_HIDE_QUEST_ICON)
    call(EventScript_CheckDaysLeft)
    special(SetCB2LoadMap)
    release
    end
}

script WindwardPort_Harbor_Eventscript_StartTreasureHunt2 {
    msgbox(format("Hello! We meet again!\p
    Will you play with me today?"), MSGBOX_YESNO)
    if (!var(VAR_RESULT)){
        setspeaker("Yamaki")
        msgbox(format("Understood. I'll play by myself then..."))
        closemessage
        clearflag(FLAG_HIDE_QUEST_ICON)
        refreshquesticon
        release
        end
    }
    else{
        playse(SE_SHINY)
        applymove(GHOSTBOY, emotemusic)
        setspeaker("Yamaki")
        msgbox(format("Awesome!\p
        So... Let's play treasure hunt today!\p
        I've hidden a Revive within one of the cargo boxes in this harbor.\p
        If you can find it and bring it back to me, I'll give you a gift!\p
        Now off you go! I'll be waiting for you here until you find it."))
        closemessage
        setvar(VAR_WINDWARD_HARBOR_GHOST_BOY, 3)
        clearflag(FLAG_HIDE_QUEST_ICON)
        refreshquesticon
        release
        end
    }
}

script WindwardPort_Harbor_Eventscript_TreasureInBarrel {
    if (var(VAR_WINDWARD_HARBOR_GHOST_BOY) == 3)
    {
        lock
        playse(SE_PIN)
        applymoveplayer(emoteexclamation)
        msgbox(format("There's something inside the barrel!\p
        It's a Revive. It must be the hidden treasure."))
        closemessage
        setvar(VAR_WINDWARD_HARBOR_GHOST_BOY, 4)
        release
        end
    }    
}

script WindwardPort_Harbor_Eventscript_TreasureHint {
    msgbox(format("You still haven't found it? Let me give you a hint then!\p
    It's inside a wooden barrel!"))
    closemessage
    release
    end  
}

script WindwardPort_Harbor_Eventscript_FoundTreasure {
    checkplayergender
    if(var(VAR_RESULT) == MALE){
        msgbox(format("{SPEAKER NAME_A}{CREATE MUGSHOT_A EMOTE_SMILE}It's this one, right?"))
        setspeaker("Yamaki")
        msgbox(format("{DESTROY_MUGSHOT}Woah! You found it!\p
        You're really good at games, mister!"))
    }
    else{
        msgbox(format("{SPEAKER NAME_M}{CREATE MUGSHOT_M EMOTE_SMILE}Is this the right one?"))
        msgbox(format("{DESTROY_MUGSHOT}Woah! You found it!\p
        You're really good at games, miss!"))
    }
    msgbox(format("I'll give you a gift as promised!"))
    closemessage
    giveitem(ITEM_CARBOS)
    //setvar(VAR_WINDWARD_HARBOR_GHOST_BOY, 5)
    setspeaker("Yamaki")
    msgbox(format("I have to go now.\p
    I had fun today! Let's play again next time!"))
    closemessage
    fadescreenswapbuffers(FADE_TO_BLACK)
    removeobject(GHOSTBOY)
    setflag(FLAG_TEMP_3)
    delay(30)
    fwdtime(7, 0)
    fadescreenswapbuffers(FADE_FROM_BLACK)
    release
    playse(SE_PIN)
    applymoveplayer(emoteexclamation)
    checkplayergender
    if(var(VAR_RESULT) == MALE){
        msgbox(format("{SPEAKER NAME_A}{CREATE MUGSHOT_A EMOTE_SURPRISED}(I swear he vanished into thin air again.)\p
        {SPEAKER NAME_A}{CREATE MUGSHOT_A EMOTE_NORMAL}(What a weird boy.)"))
        closemessage
    }
    else{
        msgbox(format("{SPEAKER NAME_M}{CREATE MUGSHOT_M EMOTE_SURPRISED}(He just vanished again.)\p
        {SPEAKER NAME_M}{CREATE MUGSHOT_M EMOTE_NORMAL}(That's... quite weird.)"))
        closemessage
    }
    clearflag(FLAG_HIDE_QUEST_ICON)
    subquestmenu(QUEST_MENU_COMPLETE_QUEST, QUEST_GHOST_BOY, SUB_QUEST_GHOST_BOY_2)
    call(EventScript_CheckDaysLeft)
    special(SetCB2LoadMap)
    release
    end
}

script WindwardPort_Harbor_Eventscript_StartQuiz3 {
    msgbox(format("Oh! We meet again!\p
    How about another game tonight?"), MSGBOX_YESNO)
    if (var(VAR_RESULT)){
        closemessage
        setvar(VAR_WINDWARD_HARBOR_GHOST_BOY, 5)
        applymove(GHOSTBOY, moves(emote_happy))
        setspeaker("Yamaki")
        msgbox(format("Awesome! It's a simple quiz of 5 questions this time!\n
        I'll give you a small prize depending on how many questions you answer correctly!\p
        You may want to take a look around the harbor before attempting.\p
        When you're ready for the quiz, talk to me again!"))
    }
    else{
        closemessage
        applymove(GHOSTBOY, moves(emote_sad))
        setspeaker("Yamaki")
        msgbox(format("Oh, I guess you're busy then..."))
        
    }
    closemessage
    clearflag(FLAG_HIDE_QUEST_ICON)
    refreshquesticon
    release
    end
}

script WindwardPort_Harbor_Eventscript_QuizQuestions {
    msgbox(format("Alright! Here we go!\p
    Question 1: Not counting you, your friends, people in the ships and the Pokémon,\p
    how many people are there when it's daytime in this harbor?"))
    dynmultipush("5", 0)
    dynmultipush("4", 1)
    dynmultipush("3", 2)//
    dynmultistack(18, 4, TRUE, 3, FALSE, 0, NULL)
    if (var(VAR_RESULT) == 2){
        playse(SE_SUCCESS)
        waitse
        addvar(VAR_TEMP_F, 1)
    }
    delay(20)
    setspeaker("Yamaki")
    msgbox(format("Question 2: How long does it take to travel from Windward Port to Coralport by ship?"))
    dynmultipush("3 hours", 0)//
    dynmultipush("4 hours", 1)
    dynmultipush("5 hours", 2)
    dynmultistack(18, 4, TRUE, 3, FALSE, 0, NULL)
    if(var(VAR_RESULT) == 0){
        playse(SE_SUCCESS)
        waitse
        addvar(VAR_TEMP_F, 1)
    }
    delay(20)
    setspeaker("Yamaki")
    msgbox(format("Question 3: How many white barrels are there in this harbor?"))
    dynmultipush("6", 0)
    dynmultipush("7", 1)
    dynmultipush("8", 2)//
    dynmultipush("9", 3)
    dynmultistack(18, 4, TRUE, 3, FALSE, 0, NULL)
    if (var(VAR_RESULT) == 2){
        playse(SE_SUCCESS)
        waitse
        addvar(VAR_TEMP_F, 1)
    }
    setspeaker("Yamaki")
    msgbox(format("Those questions were quite hard, right? I'll give you an easier one this time.\p
    Question 4: What type is Wynaut?"))
    dynmultipush("Water", 0)
    dynmultipush("Psychic", 1)//
    dynmultipush("Normal", 2)
    dynmultistack(18, 4, TRUE, 3, FALSE, 0, NULL)
    if (var(VAR_RESULT) == 1){
        playse(SE_SUCCESS)
        waitse
        addvar(VAR_TEMP_F, 1)
    }
    delay(20)
    setspeaker("Yamaki")
    msgbox(format("Alright, last question!\p
    Question 5: Which one of these abilities can a Gimmighoul normally have?"))
    dynmultipush("Run Away", 0)//
    dynmultipush("Rattled", 1)//
    dynmultipush("Cursed Body", 2)
    dynmultipush("Good As Gold", 3)
    if (var(VAR_RESULT) == 0 || var(VAR_RESULT) == 1){
        playse(SE_SUCCESS)
        waitse
        addvar(VAR_TEMP_F, 1)
    }
    delay(30)
    setspeaker("Yamaki")
    msgbox(format("Whew, that was all of the questions!\p
    Let's see how many correct answers you got{PAUSE 30}.{PAUSE 30}.{PAUSE 30}."))
    if (var(VAR_TEMP_F) == 0){
        msgbox(format("Oof... Too bad, you got none...\p
        I'll still give you a small prize for spending time with me, though!"))
        closemessage
        giveitem(ITEM_EXP_CANDY_S, 3)
        goto(WindwardPort_Harbor_Eventscript_QuizFinished)
    }
    if (var(VAR_TEMP_F) == 1 || var(VAR_TEMP_F) == 2){
        buffernumberstring(STR_VAR_1, VAR_TEMP_F)
        msgbox(format("You got {COLOR BLUE}{STR_VAR_1}{COLOR 2} correct answer(s)!\p
        Here's the prize for that!"))
        closemessage
        giveitem(ITEM_EXP_CANDY_S, 3)
        giveitem(ITEM_CAREFUL_MINT, 1)
        goto(WindwardPort_Harbor_Eventscript_QuizFinished)
    }
    if (var(VAR_TEMP_F) == 3 || var(VAR_TEMP_F) == 4){
        buffernumberstring(STR_VAR_1, VAR_TEMP_F)
        msgbox(format("You got {COLOR BLUE}{STR_VAR_1}{COLOR 2} correct answers!\p
        Here's the prize for that!"))
        closemessage
        giveitem(ITEM_EXP_CANDY_S, 3)
        giveitem(ITEM_CAREFUL_MINT, 1)
        giveitem(ITEM_ABILITY_CHANGER, 1)
        goto(WindwardPort_Harbor_Eventscript_QuizFinished)
    }
    if(var(VAR_TEMP_F) == 5){
        msgbox(format("You got everything right! That's awesome!\p
        Here, you can have all of these!"))
        closemessage
        giveitem(ITEM_EXP_CANDY_S, 3)
        giveitem(ITEM_CAREFUL_MINT, 1)
        giveitem(ITEM_ABILITY_CHANGER, 1)
        giveitem(ITEM_LUCKY_EGG, 1)
        goto(WindwardPort_Harbor_Eventscript_QuizFinished)
    }
}

script WindwardPort_Harbor_Eventscript_QuizFinished {
    setspeaker("Yamaki")
    msgbox(format("It was fun today, too!\p
    Oh right, I haven't learned your name. If you don't mind, can I know it?"))
    checkplayergender
    if (var(VAR_RESULT) == MALE){
        msgbox(format("{SPEAKER NAME_A}{CREATE MUGSHOT_A EMOTE_SMILE}I'm Akihiko!\p
        Let's play again next time, Yamaki."))
    }
    else{
        msgbox(format("{SPEAKER NAME_M}{CREATE MUGSHOT_M EMOTE_SMILE}You can call me Mitsuru.\p
        We should play again next time, Yamaki."))
    }
    playse(SE_SHINY)
    applymove(GHOSTBOY, emotemusic)
    setspeaker("Yamaki")
    msgbox(format("{DESTROY_MUGSHOT}Of course!\p
    Gotta go now, bye-bye!"))
    closemessage
    fadescreenswapbuffers(FADE_TO_BLACK)
    removeobject(GHOSTBOY)
    setflag(FLAG_TEMP_3)
    delay(30)
    fwdtime(7, 0)
    fadescreenswapbuffers(FADE_FROM_BLACK)
    release
    playse(SE_PIN)
    applymoveplayer(emoteexclamation)
    checkplayergender
    if(var(VAR_RESULT) == MALE){
        msgbox(format("{SPEAKER NAME_A}{CREATE MUGSHOT_A EMOTE_NORMAL}(He vanished again...)\p
        (Wait a minute... Since when did I learn that his name was Yamaki?)"))
        closemessage
    }
    else{
        msgbox(format("{SPEAKER NAME_M}{CREATE MUGSHOT_M EMOTE_NORMAL}(He vanished...)\p
        (Wait... How did I know his name was Yamaki? He never told me, didn't he?)"))
        closemessage
    }
    clearflag(FLAG_HIDE_QUEST_ICON)
    subquestmenu(QUEST_MENU_COMPLETE_QUEST, QUEST_GHOST_BOY, SUB_QUEST_GHOST_BOY_3)
    setvar(VAR_WINDWARD_HARBOR_GHOST_BOY, 6)
    call(EventScript_CheckDaysLeft)
    special(SetCB2LoadMap)
    release
    end
}