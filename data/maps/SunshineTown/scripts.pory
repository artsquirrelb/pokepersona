mapscripts SunshineTown_MapScripts {
    MAP_SCRIPT_ON_TRANSITION {
        setflag(FLAG_VISITED_SUNSHINE_TOWN)
        if (flag(FLAG_SYS_QUEST_MENU_GET) == FALSE){
            setobjectxyperm(5, 19, 7)
            setobjectmovementtype(5, MOVEMENT_TYPE_FACE_UP)
            setobjectxyperm(6, 23, 3)
            setobjectmovementtype(6, MOVEMENT_TYPE_FACE_LEFT)
        }
        if(var(VAR_AKIHIKOS_INTRO_STATE) >= 16 && var(VAR_MITSURUS_INTRO_STATE)!= 19){
            setflag(FLAG_TEMP_1)
        }
        if (var(VAR_AKIHIKOS_INTRO_STATE) == 16 || var(VAR_AKIHIKOS_INTRO_STATE) == 17 || var(VAR_AKIHIKOS_INTRO_STATE) == 18) {
            setflag(FLAG_HIDE_TALK_BUTTON)
            setobjectxyperm(2, 14, 11)
            setobjectmovementtype (2, MOVEMENT_TYPE_WALK_RIGHT_AND_LEFT)
            setobjectxyperm(1, 3, 14)
            setobjectmovementtype (1, MOVEMENT_TYPE_LOOK_AROUND)
            }
        if (var(VAR_AKIHIKOS_INTRO_STATE) < 16 || var(VAR_AKIHIKOS_INTRO_STATE) >= 17 || var(VAR_MITSURUS_INTRO_STATE) < 20) {
            setflag(FLAG_TEMP_2)
        }
        if (var(VAR_MITSURUS_INTRO_STATE) >= 12 && var(VAR_MITSURUS_INTRO_STATE) <= 16){
            setobjectmovementtype(2, MOVEMENT_TYPE_LOOK_AROUND)
            setobjectmovementtype(1, MOVEMENT_TYPE_LOOK_AROUND)
            setobjectxyperm(2, 3, 6)
            setobjectxyperm(1, 3, 7)
        }
        if (var(VAR_MITSURUS_INTRO_STATE) != 20){
            setflag(FLAG_TEMP_3)
        }
        if(var(VAR_MITSURUS_INTRO_STATE) == 20){
            setflag(FLAG_TEMP_4)
        }
        if (var(VAR_MITSURUS_INTRO_STATE) == 19 || var(VAR_MITSURUS_INTRO_STATE) == 20 ){
            setobjectxyperm(3, 13, 6)
            setobjectmovementtype(3, MOVEMENT_TYPE_FACE_LEFT)
            setobjectxyperm(24, 14, 7)
            setobjectmovementtype(24, MOVEMENT_TYPE_FACE_LEFT)
            setobjectxyperm(22, 31, 16)
            setobjectmovementtype(22, MOVEMENT_TYPE_FACE_DOWN)
            setobjectxyperm(23, 31, 17)
            setobjectmovementtype(23, MOVEMENT_TYPE_FACE_UP)
            setobjectxyperm(1, 8, 4)
            setobjectmovementtype(1, MOVEMENT_TYPE_FACE_DOWN)
        }
    }
    MAP_SCRIPT_ON_WARP_INTO_MAP_TABLE[
        VAR_MITSURUS_INTRO_STATE, 19: SunshineTown_Eventscript_MitsuruOnWarp19
    ]
    MAP_SCRIPT_ON_FRAME_TABLE [
        VAR_AKIHIKOS_INTRO_STATE, 16: SunshineTown_Eventscript_OnFrame16
        VAR_AKIHIKOS_INTRO_STATE, 23: SunshineTown_Eventscript_OnFrame23
        ///////
        VAR_MITSURUS_INTRO_STATE, 18: SunshineTown_Eventscript_MitsuruOnFrame18
        VAR_MITSURUS_INTRO_STATE, 19: SunshineTown_Eventscript_MitsuruOnFrame19
    ]
    
}

script SunshineTown_Eventscript_MitsuruOnWarp19 {
    applymovement(1, moves(set_invisible))
    applymovement(LOCALID_PLAYER, moves(face_right))    
}

script SunshineTown_Eventscript_MitsuruOnFrame18 {
    call(Eventscript_CutsceneStarts)
    setflag(FLAG_DONT_TRANSITION_MUSIC)
    setvar(VAR_MITSURUS_INTRO_STATE, 19)
    warpsilent(MAP_SUNSHINE_TOWN, 4, 6)
    end
}

script SunshineTown_Eventscript_MitsuruOnFrame19 {
    goto(SunshineTown_Eventscript_MitsuruSeeingPoliceRunning)
}

script SunshineTown_Eventscript_Kurosawa {
    lock
    setspeaker(SP_NAME_KUROSAWA)
    if (flag(FLAG_TEMP_1) == TRUE) {
        faceplayer
        msgbox(format("Hm?\p
        {SPEAKER NAME_AKIHIKO}{CREATE_MUGSHOT MUGSHOT_AKIHIKO EMOTE_SURPRISED}W-we'll get going now!"))
    }
    else {
        msgbox(format("If only the higher-ups allowed us-"))
        closemessage
        playse(SE_PIN)
        applymovement(3, KurosawaNoticed)
        waitmovement(0)
        msgbox(format("{SPEAKER NAME_KUROSAWA}Do you need some help, kids?\p
        {SPEAKER NAME_AKIHIKO}{CREATE_MUGSHOT MUGSHOT_AKIHIKO EMOTE_SURPRISED}Oh nothing! Sorry for bothering you, sir.\p
        (He looks so intimidating!)"))
        setflag(FLAG_TEMP_1)
    }
    release
}

movement KurosawaNoticed {
    emote_exclamation_mark
    face_player
}

script SunshineTown_Eventscript_GoGetPackage0 {
    lockall
    msgbox(format("{SPEAKER NAME_SHINJIRO}{CREATE_MUGSHOT MUGSHOT_SHINJIRO EMOTE_NORMAL}The Pokémon Center is right there, Aki."))
    closemessage
    applymovement(LOCALID_PLAYER, walkright)
    waitmovement(0)
    releaseall
    end
}

movement SunshineTown_Eventscript_ShinjiroPreventFromLeaving0 {
    walk_left
    face_right
}
movement SunshineTown_Eventscript_ShinjiroJoinsback0 {
    walk_up
    walk_right
}

script SunshineTown_Eventscript_GoGetPackage1{
    lockall
    msgbox(format("{SPEAKER NAME_SHINJIRO}{CREATE_MUGSHOT MUGSHOT_SHINJIRO EMOTE_NORMAL}The Pokémon Center is right there, Aki."))
    closemessage
    applymovement(LOCALID_PLAYER, walkright)
    waitmovement(0)
    removeobject(4)
    releaseall
    end
}

script SunshineTown_Eventscript_FatMan {
    lock
    setspeaker("Kojima")
    faceplayer
    if (var(VAR_MITSURUS_INTRO_STATE) >= 12 && var(VAR_MITSURUS_INTRO_STATE) <= 16){
        msgbox(format("Sorry, I lost my wallet around here and Yoshida is helping me finding it."))
        closemessage
        release
        end
    }
    if (var(VAR_AKIHIKOS_INTRO_STATE) == 17 || var(VAR_AKIHIKOS_INTRO_STATE) == 18){
        msgbox(format("The policeman living here ran toward the woods after hearing that explosion.\p
        {SPEAKER NAME_AKIHIKO}{CREATE_MUGSHOT MUGSHOT_AKIHIKO EMOTE_MAD}We should hurry up!"))
        setvar(VAR_AKIHIKOS_INTRO_STATE, 18)
    }
    else{
    msgbox(format("Technology is amazing.\p
    Now you can do so many things with decomps."))}
    release
}

script SunshineTown_Eventscript_Boy {
    lock
    faceplayer
    setspeaker ("Yoshida")
    if (var(VAR_MITSURUS_INTRO_STATE) >= 12 && var(VAR_MITSURUS_INTRO_STATE) <= 16){
        msgbox(format("I'm helping Kojima find his wallet. Sigh... He's always losing important stuff."))
        closemessage
        release
        end
    }
    if (var(VAR_AKIHIKOS_INTRO_STATE) == 17 || var(VAR_AKIHIKOS_INTRO_STATE) == 18){
        msgbox(format("I think something exploded in the woods.\p
        {SPEAKER NAME_AKIHIKO}{CREATE_MUGSHOT MUGSHOT_AKIHIKO EMOTE_DARKENED}Could it be...\p
        {SPEAKER NAME_SHINJIRO}{CREATE_MUGSHOT MUGSHOT_SHINJIRO EMOTE_MAD}The orphanage!"))
        setvar(VAR_AKIHIKOS_INTRO_STATE, 18)
    }
    else {
        msgbox(format("One of the richest families in the region lived on this island.\p
    I heard their mansion was at the south end of Route 1."))
    }
    release
}

script SunshineTown_Eventscript_DirerctionSign {
    lock
    msgbox(format("Sunshine Riverbank {DOWN_ARROW}\n
    Route 2 {LEFT_ARROW}"))
    release
}

script SunshineTown_Eventscript_TownSign {
    lock
    msgbox(format("Welcome to Sunshine Town!"))
    release
}

script SunshineTown_Eventscript_OnFrame16 {
    lockall
    goto (SunshineTown_Eventscript_ExitingPC)
    releaseall
}

script SunshineTown_Eventscript_ExitingPC {
    lockall
    applymovement(LOCALID_PLAYER, SunshineTown_Eventscript_AkihikoExitingPC)
    applymovement (4, SunshineTown_Eventscript_ShinjiroExitingPC)
    waitmovement(0)
    msgbox(format("{SPEAKER NAME_AKIHIKO}{CREATE_MUGSHOT MUGSHOT_AKIHIKO EMOTE_MAD}Let's ask around what happened.\p
    {SPEAKER NAME_SHINJIRO}{CREATE_MUGSHOT MUGSHOT_SHINJIRO EMOTE_MAD}Yeah."))
    closemessage
    applymovement(4, walkup)
    waitmovement(0)
    removeobject(4)
    setflag(FLAG_TEMP_2)
    setvar(VAR_AKIHIKOS_INTRO_STATE, 17)
    clearflag(FLAG_HIDE_TALK_BUTTON)
    releaseall
}

movement SunshineTown_Eventscript_AkihikoExitingPC {
    walk_fast_down *1
    walk_fast_right *2
    face_down
}

movement SunshineTown_Eventscript_ShinjiroExitingPC {
    walk_fast_down *3
    walk_fast_right *2 
    face_up
}

script SunshineTown_Eventscript_DontLeaveTown {
    lockall
    applymovement(LOCALID_PLAYER, walkright)
    waitmovement(0)
    msgbox(format("{SPEAKER NAME_AKIHIKO}{CREATE_MUGSHOT MUGSHOT_AKIHIKO EMOTE_MAD}We should ask people in town about that explosion sound!"))
    closemessage
    releaseall
}

script SunshineTown_Eventscript_WarpToBurningOrphanage{
    lock
    playse(SE_EXIT)
    setflag(FLAG_HIDE_TALK_BUTTON)
    warp(MAP_SUNSHINE_ORPHANAGE_BURNING, 16, 23)
    delay(30)
    release
}

script SunshineTown_Eventscript_NoPointEnteringPC {
    lock
    applymovement(LOCALID_PLAYER, walkdown)
    msgbox(format("{SPEAKER NAME_AKIHIKO}{CREATE_MUGSHOT MUGSHOT_AKIHIKO EMOTE_MAD}(Not the time to be dawdling around!)"))
    closemessage
    release
}

script SunshineTown_Eventscript_NoPointLeavingTown {
    lock
    applymovement(LOCALID_PLAYER, walkright)
    msgbox(format("{SPEAKER NAME_AKIHIKO}{CREATE_MUGSHOT MUGSHOT_AKIHIKO EMOTE_MAD}(Not the time to be dawdling around!)"))
    closemessage
    release
}

script SunshineTown_Eventscript_WarpToBurningOrphanage2 {
    lock
    playse(SE_EXIT)
    setvar(VAR_AKIHIKOS_INTRO_STATE, 18)
    setflag(FLAG_HIDE_TALK_BUTTON)
    warp(MAP_SUNSHINE_ORPHANAGE_BURNING, 16, 23)
    delay(30)
    release
}

script SunshineTown_Eventscript_OW_Happiny{
    lockall
    if (var(VAR_AKIHIKOS_INTRO_STATE) < 24){
        msgbox(format("The Happiny is looking through the door. It won't budge no matter what."))
        closemessage
        releaseall
        end
    }
    else{
        faceplayer
        playmoncry(SPECIES_HAPPINY, CRY_MODE_NORMAL)
        startoverworldencounter(6)
        removeobject(6)
        setflag(FLAG_TEMP_A)
        releaseall
        end
    }
    
}

script SunshineTown_Eventscript_OnFrame23{
    lock
    msgbox(format("{COLOR GREEN}Tips: Quest System is now unlocked.\p
    Check around for quest givers with a light blue exclamation mark.\p
    You can open quest menu in\n
    {START_BUTTON} Menu {RIGHT_ARROW} {L_BUTTON}."))
    closemessage
    clearflag(FLAG_HIDE_QUEST_ICON)
    setflag(FLAG_SYS_QUEST_MENU_GET)
    setvar(VAR_AKIHIKOS_INTRO_STATE, 24)
    clearflag(FLAG_HIDE_TALK_BUTTON)
    release
    end
}

script SunshineTown_Eventscript_QUEST_1_PLAY_WITH_PKM_FOR_SOOTHE_BELL{
    lock
    setspeaker("Fumiko")
    if (flag(FLAG_SYS_QUEST_MENU_GET) == FALSE){
        goto (SunshineTown_Eventscript_ExpertF_PreQuest)
        release
        end
    }
    else {
        faceplayer
        questmenu(QUEST_MENU_CHECK_COMPLETE, QUEST_1_PLAY_WITH_PKM_FOR_SOOTHE_BELL)
        if (var(VAR_RESULT) == TRUE){
            call(SunshineTown_Eventscript_QUEST_1_PLAY_WITH_PKM_FOR_SOOTHE_BELL_QuestWasCompleted)
        }
        else{
            call(SunshineTown_Eventscript_QUEST_1_PLAY_WITH_PKM_FOR_SOOTHE_BELL_CheckIfReward)
        }
    }
    release
    end
}

script SunshineTown_Eventscript_QUEST_1_PLAY_WITH_PKM_FOR_SOOTHE_BELL_QuestWasCompleted {
    msgbox(format("Soothe Bell can help you befriend Pokémon faster."))
    closemessage
    release
    end
}


script SunshineTown_Eventscript_QUEST_1_PLAY_WITH_PKM_FOR_SOOTHE_BELL_CheckIfReward {
    questmenu(QUEST_MENU_CHECK_REWARD, QUEST_1_PLAY_WITH_PKM_FOR_SOOTHE_BELL)
    if (var(VAR_RESULT) == TRUE){
        msgbox(format("My Riolu was very happy. Please take this as a thank-you for doing me a favor."))
        closemessage
        giveitem(ITEM_SOOTHE_BELL, 1)
        questmenu(QUEST_MENU_COMPLETE_QUEST, QUEST_1_PLAY_WITH_PKM_FOR_SOOTHE_BELL)
        release
        end
    }
    else {
        call(SunshineTown_Eventscript_QUEST_1_PLAY_WITH_PKM_FOR_SOOTHE_BELL_CheckIfActive)
    }
}

script SunshineTown_Eventscript_QUEST_1_PLAY_WITH_PKM_FOR_SOOTHE_BELL_CheckIfActive {
    questmenu(QUEST_MENU_CHECK_ACTIVE, QUEST_1_PLAY_WITH_PKM_FOR_SOOTHE_BELL)
    if (var(VAR_RESULT) == TRUE){
        msgbox(format("You can find my poor Riolu on the second floor."))
        closemessage
        release
        end
    }
    else {
        msgbox(format("Excuse me, young men. Can you do me a favor?\p
        {SPEAKER NAME_AKIHIKO}{CREATE_MUGSHOT MUGSHOT_AKIHIKO EMOTE_SMILE}Sure, ma'am. What can we do for you?"))
        closemessage
        setspeaker("Fumiko")
        msgbox(format("One of the Pokémon caretakers I hired couldn't show up today.\p
        Can you go play with my poor baby Riolu? He's on the second floor.\p
        {SPEAKER NAME_AKIHIKO}{CREATE_MUGSHOT MUGSHOT_AKIHIKO EMOTE_SMILE}Leave it to us!"))
        closemessage
        questmenu(QUEST_MENU_SET_ACTIVE, QUEST_1_PLAY_WITH_PKM_FOR_SOOTHE_BELL)
        release
        end
    }
    
}

script SunshineTown_Eventscript_ExpertF_PreQuest {
    msgbox("Where did I put my key again...?")
    closemessage
    release
    end
}

script SunshineTown_Eventscript_Begin_Route2_Cutscene_AkihikoVar24 {
    lock
    msgbox(format("{COLOR RED}Important scene ahead. Do you want to proceed?"), MSGBOX_YESNO)
    if (var(VAR_RESULT) == 0){
        applymovement(LOCALID_PLAYER, walkright)
        waitmovement(0)
        release
    }
    else{
        setflag(FLAG_HIDE_TALK_BUTTON)
        warpsilent(MAP_BLANK_MAP, 0, 0)
    }
}
    
script SunshineTown_Eventscript_HiddenGrotto {
    lock
    msgbox(Eventscript_HiddenGrotto, MSGBOX_YESNO)
    closemessage
    if (var(VAR_RESULT) == TRUE){
        warp(MAP_SUNSHINE_TOWN_HIDDEN_GROTTO, 0)
    }
    release
    end
}


script unlockcharacters
{
    msgbox(format("unlocked & made available all charas"))
    closemessage
    unlockcharacter(CHARACTER_MITSURU)
    unlockcharacter(CHARACTER_SHINJIRO)
    unlockcharacter(CHARACTER_AKIHIKO)
    makecharaAvailable(CHARACTER_MITSURU)
    makecharaAvailable(CHARACTER_AKIHIKO)
    makecharaAvailable(CHARACTER_SHINJIRO)
    end
}

script makecharagrayout{
    msgbox(format("Marking prologue to completed & reset all temp vars"))
    closemessage
    setvar(VAR_AKIHIKOS_INTRO_STATE, 100)
    setvar(VAR_MITSURUS_INTRO_STATE, 100)
    setvar(VAR_TEMP_0, 0)
    setvar(VAR_TEMP_1, 0)
    setvar(VAR_TEMP_2, 0)
    setvar(VAR_TEMP_3, 0)
}

script SunshineTown_Eventscript_MitsuruSeeingPoliceRunning {
    lockall
    applymovement(LOCALID_PLAYER, moves(player_run_right*3, walk_right*2, emote_exclamation_mark))
    delay(48)
    applymovement(3, moves(walk_fast_left*13))
    delay(4)
    applymovement(24, moves(walk_fast_left*13))
    delay(20)
    applymovement(LOCALID_PLAYER, moves(face_down, lock_facing_direction, walk_fast_up, unlock_facing_direction, delay_16, face_left))
    waitmovement
    delay(50)
    removeobject(3)
    removeobject(24)
    msgbox(format("{SPEAKER NAME_MITSURU}{CREATE_MUGSHOT MUGSHOT_MITSURU EMOTE_NORMAL}(The police? How did they arrive this fast?
    The explosion happened just a few minutes ago.)\p
    (Not to mention, if I remember correctly, the nearest station is in the opposite direction of Sunshine Town.)"))
    closemessage
    delay(20)
    playse(SE_SLIDING_DOOR)
    waitse
    special(SpawnCameraObject)
    applymovement(1, moves(set_visible, delay_8, walk_fast_down*2, walk_fast_right*3))
    delay(4)
    applymovement(LOCALID_PLAYER, moves(face_down, delay_16, face_right))
    delay(4)
    applymovement(OBJ_EVENT_ID_CAMERA, moves(walk_fast_right))
    waitmovement
    msgbox(format("{SPEAKER NAME_MITSURU}{CREATE_MUGSHOT MUGSHOT_MITSURU EMOTE_NORMAL}Excuse me. Did something happen? Everyone in town looks on edge."))
    closemessage
    applymovement(1, faceleft)
    waitmovement
    clearflag(FLAG_DONT_TRANSITION_MUSIC)
    fadenewbgm(MUS_P5_RUN_RUN_RUN)
    setspeaker("Kojima")
    msgbox(format("There was an explosion at the orphanage west of town, didn't you hear?\p
    Lots of people were injured and the town people are trying to help.\p
    Now excuse me, I gotta bring these medicines to the injured."))
    closemessage
    applymovement(OBJ_EVENT_ID_CAMERA, walkleft)
    special(RemoveCameraObject)
    applymovement(1, moves(walk_fast_right*3, walk_fast_down*6, set_invisible))
    delay(20)
    msgbox(format("{SPEAKER NAME_MITSURU}{CREATE_MUGSHOT MUGSHOT_MITSURU EMOTE_STRUGGLED}(The orphanage was exploded?)\p
    (Damn it! How am I so naive. Of course they would do that to erase the proofs and traces.)\p
    {CREATE_MUGSHOT MUGSHOT_MITSURU EMOTE_SAD}(It was all my fault... If only I acted sooner.)\p
    ...........\p
    {CREATE_MUGSHOT MUGSHOT_MITSURU EMOTE_SERIOUS}(I should go check and help the injured as well.)"))
    closemessage
    setvar(VAR_MITSURUS_INTRO_STATE, 20)
    setflag(FLAG_DONT_TRANSITION_MUSIC)
    call(Eventscript_CutsceneEnds)
    releaseall
    end
}