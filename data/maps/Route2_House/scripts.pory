mapscripts Route2_House_MapScripts {
    MAP_SCRIPT_ON_WARP_INTO_MAP_TABLE[
        VAR_AKIHIKOS_INTRO_STATE, 27: Route2_House_Eventscript_AkihikoOnWarp27
        VAR_AKIHIKOS_INTRO_STATE, 32: Route2_House_Eventscript_AkihikoOnWarp32
        VAR_AKIHIKOS_INTRO_STATE, 38: Route2_House_Eventscript_AkihikoOnWarp38
        VAR_AKIHIKOS_INTRO_STATE, 39: Route2_House_Eventscript_AkihikoOnWarp39
        VAR_AKIHIKOS_INTRO_STATE, 42: Route2_House_Eventscript_AkihikoOnWarp42
        VAR_AKIHIKOS_INTRO_STATE, 43: Route2_House_Eventscript_AkihikoOnWarp43
    ]
    MAP_SCRIPT_ON_TRANSITION{
        if (var(VAR_AKIHIKOS_INTRO_STATE) == 27){
            setflag(FLAG_SPAWN_INVISIBLE)
            setobjectxyperm(6, 6, 8)
            playbgm(MUS_NONE, TRUE)
        }
        if (var(VAR_AKIHIKOS_INTRO_STATE) == 29){
            clearflag(FLAG_HIDE_TALK_BUTTON)
        }
        if (var(VAR_AKIHIKOS_INTRO_STATE) >= 29){
            setflag(FLAG_TEMP_2)
        }
        if (var(VAR_AKIHIKOS_INTRO_STATE) == 32){
            setobjectxyperm(6, 6, 7)
            setobjectxyperm(5, 1, 8)
        }
        if (var(VAR_AKIHIKOS_INTRO_STATE) > 32 && var(VAR_AKIHIKOS_INTRO_STATE) < 38){
            setflag(FLAG_TEMP_1)
        }
        if (var(VAR_AKIHIKOS_INTRO_STATE) == 38){
            setflag(FLAG_HIDE_TALK_BUTTON)
            setobjectxyperm(6, 6, 7)
            setobjectmovementtype(1, MOVEMENT_TYPE_FACE_RIGHT)
            setobjectmovementtype(5, MOVEMENT_TYPE_WALK_IN_PLACE_LEFT)
            setobjectxyperm(4, 8, 4)
            setobjectmovementtype(4, MOVEMENT_TYPE_FACE_DOWN)
            playbgm(MUS_NONE, TRUE)
        }
        if (var(VAR_AKIHIKOS_INTRO_STATE) == 39){
            setflag(FLAG_HIDE_TALK_BUTTON)
            setobjectxyperm(1, 3, 4)
            setobjectmovementtype(1, MOVEMENT_TYPE_FACE_UP)
            setobjectxyperm(5, 4, 4)
            setobjectmovementtype(5, MOVEMENT_TYPE_FACE_UP)
            setobjectxyperm(4, 4, 7)
            setobjectmovementtype(5, MOVEMENT_TYPE_FACE_LEFT)
            setobjectxyperm(6, 11, 6)
        }
        if (var(VAR_AKIHIKOS_INTRO_STATE) == 43){
            setobjectxyperm(6, 8, 8)
            setobjectmovementtype(6, MOVEMENT_TYPE_FACE_RIGHT)
        }
        if (var(VAR_AKIHIKOS_INTRO_STATE) >= 44){
            setflag(FLAG_TEMP_1)
        }
    }

    MAP_SCRIPT_ON_FRAME_TABLE[
        VAR_AKIHIKOS_INTRO_STATE, 27: Route2_House_Eventscript_AkihikoOnFrame27
        VAR_AKIHIKOS_INTRO_STATE, 32: Route2_House_Eventscript_AkihikoOnFrame32
        VAR_AKIHIKOS_INTRO_STATE, 38: Route2_House_Eventscript_AkihikoOnFrame38
        VAR_AKIHIKOS_INTRO_STATE, 39: Route2_House_Eventscript_AkihikoOnFrame39
        VAR_AKIHIKOS_INTRO_STATE, 43: Route2_House_Eventscript_AkihikoOnFrame43
    ]
}

script Route2_House_Eventscript_AkihikoOnWarp27 {
    applymovement(6, invisible)    
}

script Route2_House_Eventscript_AkihikoOnWarp32 {
    applymovement(LOCALID_PLAYER, faceup)
    applymovement(6, faceup)
    applymovement(5, faceright)
}

script Route2_House_Eventscript_AkihikoOnWarp38 {
    applymovement(6, faceup)
}
script Route2_House_Eventscript_AkihikoOnWarp39 {
    applymovement(6, invisible)
    applymovement(LOCALID_PLAYER, faceleft)
}
script Route2_House_Eventscript_AkihikoOnWarp43 {
    applymovement(LOCALID_PLAYER, faceleft)
}


script Route2_House_Eventscript_AkihikoOnFrame27 {
    goto (Route2_House_Eventscript_AkiShinjiWakingUp)
}

script Route2_House_Eventscript_AkihikoOnFrame32 {
    goto (Route2_House_Eventscript_AkiShinjiDecidingToJoinMitsuru)
}

script Route2_House_Eventscript_AkiShinjiWakingUp {
    lock
    delay(20)
    applymovement(LOCALID_PLAYER, CameraPansDownToTheTrio)
    waitmovement(0)
    delay(30)
    setspeaker("???????")
    msgbox(format("{CREATE_MUGSHOT MUGSHOT_MITSURU EMOTE_NORMAL}..."))
    closemessage
    delay(30)
    applymovement(2, shakehorizontal)
    delay(4)
    applymovement(3,shakehorizontal)
    waitmovement(0)
    delay(20)
    special(SpawnCameraObject)
    applymovement(LOCALID_PLAYER, BackToSprite)
    waitmovement(0)
    applymovement(2, invisible)
    clearflag(FLAG_SPAWN_INVISIBLE)
    playse(SE_PIN)
    applymovement(LOCALID_PLAYER, AkiShinjiWakesUp)
    waitmovement
    delay(4)
    applymovement(3, invisible)
    playse(SE_PIN)
    applymovement(6, AkiShinjiWakesUp)
    waitmovement(0)
    delay(30)
    applymovement(LOCALID_PLAYER, AkihikoLookingAround)
    delay(8)
    applymovement(6, ShinjiroLookingAround)
    waitmovement(0)
    delay(30)
    applymovement(LOCALID_PLAYER, jumpinplaceup)
    waitmovement(0)
    msgbox(format("{SPEAKER NAME_AKIHIKO}{CREATE_MUGSHOT MUGSHOT_AKIHIKO EMOTE_SURPRISED}Wh-Where are we?\p
    And who are you??"))
    setspeaker("???????")
    msgbox(format("{CREATE_MUGSHOT MUGSHOT_MITSURU EMOTE_NORMAL}Apologies for this is the best shelter I could provide in this situation.\p
    This house is of my butler's grandson, who's currently traveling the region.\p
    We're still in Route 2. Worry not, it's safe here."))
    closemessage
    goto(Route2_House_Eventscript_TriosFirstGreeting)
    release
}

script Route2_House_Eventscript_TriosFirstGreeting {
    lock
    delay(30)
    msgbox(format("{SPEAKER NAME_MITSURU}{CREATE_MUGSHOT MUGSHOT_MITSURU EMOTE_NORMAL}As for my name, I am Mitsuru Kirijo.\p
    {SPEAKER NAME_AKIHIKO}{CREATE_MUGSHOT MUGSHOT_AKIHIKO EMOTE_SURPRISED}-!\p
    {SPEAKER NAME_SHINJIRO}{CREATE_MUGSHOT MUGSHOT_SHINJIRO EMOTE_SURPRISED}Kirijo-?!"))
    closemessage
    delay(30)
    applymovement(LOCALID_PLAYER, faceright)
    waitmovement(0)
    msgbox(format("{SPEAKER NAME_AKIHIKO}{CREATE_MUGSHOT MUGSHOT_AKIHIKO EMOTE_DOUBT}Uhhh, who? The name sounds familiar."))
    closemessage
    applymovement(6, faceleft)
    waitmovement(0)
    msgbox(format("{SPEAKER NAME_SHINJIRO}{CREATE_MUGSHOT MUGSHOT_SHINJIRO EMOTE_SURPRISED}-!\p
    Dumbass! That's the billionaire group who donated us our PCs!\p
    {SPEAKER NAME_AKIHIKO}{CREATE_MUGSHOT MUGSHOT_AKIHIKO EMOTE_SURPRISED}Oooh..."))
    delay(20)
    applymovement(LOCALID_PLAYER, faceup)
    delay(8)
    applymovement(6, faceup)
    waitmovement(0)
    msgbox(format("{SPEAKER NAME_AKIHIKO}{CREATE_MUGSHOT MUGSHOT_AKIHIKO EMOTE_SERIOUS}So? What does a billionaire girl like you want with us?\p
    For whatever it is, I'm not interested. I only want to train and become a stronger trainer.\p
    I could've dealed with the Drowzee earlier if I were-\p
    {SPEAKER NAME_AKIHIKO}{CREATE_MUGSHOT MUGSHOT_AKIHIKO EMOTE_SURPRISED}Wait- Are you with the men in black who tried to kidnap us!?\p
    {SPEAKER NAME_MITSURU}{CREATE_MUGSHOT MUGSHOT_MITSURU EMOTE_SAD}...\p 
    {SPEAKER NAME_MITSURU}{CREATE_MUGSHOT MUGSHOT_MITSURU EMOTE_NORMAL}No. \p
    In fact, I managed to disrupt them and brought you two here.\p
    {SPEAKER NAME_MITSURU}{CREATE_MUGSHOT MUGSHOT_MITSURU EMOTE_SERIOUS}To be more precise, my mission is to stop them.\p
    {SPEAKER NAME_SHINJIRO}{CREATE_MUGSHOT MUGSHOT_SHINJIRO EMOTE_SERIOUS}Stop them?\p
    {SPEAKER NAME_MITSURU}{CREATE_MUGSHOT MUGSHOT_MITSURU EMOTE_SERIOUS}Both of you are from Sunshine Orphanage, correct?\p
    Right now I can say for certain that the explosion which happened a few days ago was their doing.\p
    {SPEAKER NAME_AKIHIKO}{CREATE_MUGSHOT MUGSHOT_AKIHIKO EMOTE_SURPRISED}!!\p
    {SPEAKER NAME_SHINJIRO}{CREATE_MUGSHOT MUGSHOT_SHINJIRO EMOTE_SURPRISED}-!"))
    closemessage
    delay(20)
    applymovement(1, faceleft)
    waitmovement(0)
    msgbox(format("{SPEAKER NAME_MITSURU}{CREATE_MUGSHOT MUGSHOT_MITSURU EMOTE_SAD}(Mumble) If only I were able to stop them in time... It was my fault...\p
    {SPEAKER NAME_AKIHIKO}{CREATE_MUGSHOT MUGSHOT_AKIHIKO EMOTE_SERIOUS}....."))
    closemessage
    applymovement(1, facedown)
    waitmovement(0)
    msgbox(format("{SPEAKER NAME_MITSURU}{CREATE_MUGSHOT MUGSHOT_MITSURU EMOTE_NORMAL}My request is rather simple.\p
    {SPEAKER NAME_AKIHIKO}{CREATE_MUGSHOT MUGSHOT_AKIHIKO EMOTE_SERIOUS}Request...?"))
    closemessage
    applymovement(1, MitsuruPickingAkihiko)
    waitmovement
    delay(8)
    msgbox(format("{SPEAKER NAME_MITSURU}{CREATE_MUGSHOT MUGSHOT_MITSURU EMOTE_NORMAL}I would like you to join me, Akihiko Sanada-kun."))
    closemessage
    playse(SE_PIN)
    waitse
    applymovement(LOCALID_PLAYER, emoteexclamation)
    applymovement(6, emoteexclamation)
    waitmovement(0)
    msgbox(format("{SPEAKER NAME_AKIHIKO}{CREATE_MUGSHOT MUGSHOT_AKIHIKO EMOTE_SURPRISED}Huh?\p
    {SPEAKER NAME_MITSURU}{CREATE_MUGSHOT MUGSHOT_MITSURU EMOTE_NORMAL}I believe you have a reason for wanting to get stronger.\p
    {SPEAKER NAME_AKIHIKO}{CREATE_MUGSHOT MUGSHOT_AKIHIKO EMOTE_SERIOUS}.....\p
    {SPEAKER NAME_MITSURU}{CREATE_MUGSHOT MUGSHOT_MITSURU EMOTE_NORMAL}I have clues of where to find those men in black and what they are doing.\p
    {SPEAKER NAME_MITSURU}{CREATE_MUGSHOT MUGSHOT_MITSURU EMOTE_SMILE}And earlier I saw your battle with them. You have great potential.\p
    {SPEAKER NAME_MITSURU}{CREATE_MUGSHOT MUGSHOT_MITSURU EMOTE_NORMAL}If you'll join me... I promise it'll be a great challenge.\p
    I'll also share what I know about those men with you."))
    closemessage
    applymovement(LOCALID_PLAYER, faceright)
    delay(16)
    applymovement(6, faceleft)
    waitmovement(0)
    delay(60)
    applymovement(LOCALID_PLAYER, faceup)
    delay(16)
    applymovement(6, faceup)
    waitmovement(0)
    msgbox(format("{SPEAKER NAME_AKIHIKO}{CREATE_MUGSHOT MUGSHOT_AKIHIKO EMOTE_SERIOUS}.....\p
    {SPEAKER NAME_SHINJIRO}{CREATE_MUGSHOT MUGSHOT_SHINJIRO EMOTE_SERIOUS}.....\p
    {SPEAKER NAME_AKIHIKO}{CREATE_MUGSHOT MUGSHOT_AKIHIKO EMOTE_SERIOUS}Alright, I'll join you.\p
    {SPEAKER NAME_MITSURU}{CREATE_MUGSHOT MUGSHOT_MITSURU EMOTE_SMILE}Very well."))
    closemessage
    playse(SE_PIN)
    waitse
    applymovement(6, ShinjiroSurprisedAtAkihikoChoice)
    waitmovement(0)
    msgbox(format("{SPEAKER NAME_SHINJIRO}{CREATE_MUGSHOT MUGSHOT_SHINJIRO EMOTE_SERIOUS}Wait!"))
    closemessage
    applymovement(6, ShinjiroDisruptsAkiMitsu)
    applymovement(LOCALID_PLAYER, faceright)
    waitmovement(0)
    msgbox(format("{SPEAKER NAME_SHINJIRO}{CREATE_MUGSHOT MUGSHOT_SHINJIRO EMOTE_SERIOUS}Aki, we need to talk.\p
    {SPEAKER NAME_AKIHIKO}{CREATE_MUGSHOT MUGSHOT_AKIHIKO EMOTE_NORMAL}Shinji?"))
    closemessage
    applymovement(6, faceup)
    delay(8)
    applymovement(LOCALID_PLAYER, faceup)
    waitmovement(0)
    delay(60)
    msgbox(format("{SPEAKER NAME_MITSURU}{CREATE_MUGSHOT MUGSHOT_MITSURU EMOTE_NORMAL}Go ahead, I'll be waiting right here."))
    closemessage
    applymovement(1, MitsuruGoesBackToPlace)
    waitmovement(0)
    setvar(VAR_AKIHIKOS_INTRO_STATE, 28)
    warpsilent(MAP_BLANK_MAP, 0, 0)
    special(RemoveCameraObject)
    fwdtime(13, 20)
    pausefakertc
    release
    end
}

movement jumpinplaceup {
    jump_in_place_up
}
movement CameraPansDownToTheTrio {
    walk_slow_down *4
}

movement BackToSprite {
    walk_fast_left
    face_up
}

movement AkiShinjiWakesUp {
    set_visible
    jump_in_place_up
}

movement AkihikoLookingAround {
    face_left
    delay_16
    delay_16
    emote_pensive
    delay_16
    face_right
    delay_16
    delay_16
    delay_16
    face_up
    
}

movement ShinjiroLookingAround {
    face_right
    delay_16
    delay_16
    emote_pensive
    face_up
    delay_16
    delay_16
    face_left
    delay_16
    face_up
}

movement MitsuruPickingAkihiko {
    walk_left
    face_down
}

movement MitsuruGoesBackToPlace {
    walk_right
    face_down
}

movement ShinjiroSurprisedAtAkihikoChoice {
    emote_exclamation_mark
    face_left
}

movement ShinjiroDisruptsAkiMitsu {
    walk_left
    walk_up
    face_left    
}

script Route2_House_Eventscript_Mitsuru {
    lock
    faceplayer
    if (var(VAR_AKIHIKOS_INTRO_STATE) == 29){
        msgbox(format("{SPEAKER NAME_MITSURU}{CREATE_MUGSHOT MUGSHOT_MITSURU EMOTE_NORMAL}Have you both finished talking?\p
        {SPEAKER NAME_AKIHIKO}{CREATE_MUGSHOT MUGSHOT_AKIHIKO EMOTE_NORMAL}Not yet. We still need more time.\p
        {SPEAKER NAME_MITSURU}{CREATE_MUGSHOT MUGSHOT_MITSURU EMOTE_SMILE}Be my guest."))
        closemessage
    }
    elif(var(VAR_AKIHIKOS_INTRO_STATE) == 30){
        setflag(FLAG_HIDE_TALK_BUTTON)
        msgbox(format("{SPEAKER NAME_AKIHIKO}{CREATE_MUGSHOT MUGSHOT_AKIHIKO EMOTE_NORMAL}I also have a request.\p
        {SPEAKER NAME_MITSURU}{CREATE_MUGSHOT MUGSHOT_MITSURU EMOTE_SMILE}Certainly, let's hear it."))
        closemessage
        setvar(VAR_AKIHIKOS_INTRO_STATE, 31)
        setflag(FLAG_DONT_TRANSITION_MUSIC)
        warpsilent(MAP_BLANK_MAP, 0, 0)
    }
    release
    end
}

script Route2_House_Eventscript_Shinjiro {
    lock
    faceplayer
    msgbox(format("{SPEAKER NAME_AKIHIKO}{CREATE_MUGSHOT MUGSHOT_AKIHIKO EMOTE_NORMAL}What's wrong, Shinji?\p
    {SPEAKER NAME_SHINJIRO}{CREATE_MUGSHOT MUGSHOT_SHINJIRO EMOTE_SERIOUS}I don't trust her.\p
    She's hiding lots of information and we don't even know if it's true that she's not on those men's side.\p
    {SPEAKER NAME_AKIHIKO}{CREATE_MUGSHOT MUGSHOT_AKIHIKO EMOTE_SERIOUS}But she's also the only lead we have right now. We didn't even know where to start looking for Miki.\p
    {SPEAKER NAME_AKIHIKO}{CREATE_MUGSHOT MUGSHOT_AKIHIKO EMOTE_CONFIDENT}Beside, she said it'd be a great challenge.\p
    {SPEAKER NAME_SHINJIRO}{CREATE_MUGSHOT MUGSHOT_SHINJIRO EMOTE_NORMAL}So you're tempted.\p
    {SPEAKER NAME_AKIHIKO}{CREATE_MUGSHOT MUGSHOT_AKIHIKO EMOTE_SERIOUS}Honestly, yes. \p
    If what she said was true, I may find Miki or at least her whereabouts while going after those men in black.\p
    {SPEAKER NAME_AKIHIKO}{CREATE_MUGSHOT MUGSHOT_AKIHIKO EMOTE_NORMAL}I'm joining her. Don't stop me.\p
    {SPEAKER NAME_SHINJIRO}{CREATE_MUGSHOT MUGSHOT_SHINJIRO EMOTE_SERIOUS}Told you I didn't trust her.\p
    So if you join her, I will too. Someone's gotta keep an eye on her.\p
    {SPEAKER NAME_AKIHIKO}{CREATE_MUGSHOT MUGSHOT_AKIHIKO EMOTE_CONFIDENT}Alright, let's try asking her then."))
    closemessage
    applymovement(6, ShinjiroJoinsAndVanish)
    waitmovement(0)
    setvar(VAR_AKIHIKOS_INTRO_STATE, 30)
    release
}

movement ShinjiroJoinsAndVanish {
    walk_down
    set_invisible
    walk_fast_right
    walk_fast_up *2
}

script Route2_House_Eventscript_AVulpix {
    lock
    faceplayer
    playmoncry(SPECIES_VULPIX, CRY_MODE_NORMAL)
    applymovement(5, faceleft)
    waitmovement(0)
    msgbox(format("Vulpix ignored you.\p
    {SPEAKER NAME_AKIHIKO}{CREATE_MUGSHOT MUGSHOT_AKIHIKO EMOTE_NORMAL}(Now that I have a closer look, this Vulpix looks a bit different...)"))
    closemessage
    release
}

script Route2_House_Eventscript_Inteleon {
    lock
    faceplayer
    msgbox("Inteleon's looking at you judgingly.\p
    {SPEAKER NAME_AKIHIKO}{CREATE_MUGSHOT MUGSHOT_AKIHIKO EMOTE_DOUBT}(Someone's got an attitude...)")
    closemessage
    applymovement(4, faceright)
    release
}

script Route2_House_Eventscript_MustTalkToShinjiro {
    lock
    applymovement(LOCALID_PLAYER, walkup)
    waitmovement
    msgbox(format("{SPEAKER NAME_AKIHIKO}{CREATE_MUGSHOT MUGSHOT_AKIHIKO EMOTE_NORMAL}(Shinji wants to talk with me.)"))
    closemessage
    release
}

script Route2_House_Eventscript_MustTalkToMitsuru {
    lock
    applymovement(LOCALID_PLAYER, walkup)
    waitmovement
    msgbox(format("{SPEAKER NAME_AKIHIKO}{CREATE_MUGSHOT MUGSHOT_AKIHIKO EMOTE_NORMAL}(Kirijo is still waiting for me.)"))
    closemessage
    release
}

script Route2_House_Eventscript_AkiShinjiDecidingToJoinMitsuru {
    lock
    delay(30)
    msgbox(format("{SPEAKER NAME_MITSURU}{CREATE_MUGSHOT MUGSHOT_MITSURU EMOTE_NORMAL}So what's your request?\p
    {SPEAKER NAME_SHINJIRO}{CREATE_MUGSHOT MUGSHOT_SHINJIRO EMOTE_NORMAL}Let me join too."))
    closemessage
    applymovement(1, MitsuruLookingAtShinjiro)
    waitmovement(0)
    msgbox(format("{SPEAKER NAME_MITSURU}{CREATE_MUGSHOT MUGSHOT_MITSURU EMOTE_NORMAL}Hm... Shinjiro Aragaki-kun, correct?\p
    {SPEAKER NAME_SHINJIRO}{CREATE_MUGSHOT MUGSHOT_SHINJIRO EMOTE_SERIOUS}... You sure have done your research thoroughly.\p
    {SPEAKER NAME_MITSURU}{CREATE_MUGSHOT MUGSHOT_MITSURU EMOTE_SMILE}I'll take that as a compliment.\p
    {SPEAKER NAME_SHINJIRO}{CREATE_MUGSHOT MUGSHOT_SHINJIRO EMOTE_SERIOUS}That wasn't.\p
    {SPEAKER NAME_MITSURU}{CREATE_MUGSHOT MUGSHOT_MITSURU EMOTE_SMILE}Heh...\p
    {SPEAKER NAME_MITSURU}{CREATE_MUGSHOT MUGSHOT_MITSURU EMOTE_NORMAL}I have yet to know if you're able to hold your ground when situation calls.\p
    If I'm not mistaken, you don't have a Pokémon of your own yet.\p
    {SPEAKER NAME_SHINJIRO}{CREATE_MUGSHOT MUGSHOT_SHINJIRO EMOTE_SERIOUS}I can just catch one.\p
    {SPEAKER NAME_MITSURU}{CREATE_MUGSHOT MUGSHOT_MITSURU EMOTE_SMILE}Alright. Then how about this.\p
    {SPEAKER NAME_MITSURU}{CREATE_MUGSHOT MUGSHOT_MITSURU EMOTE_CONFIDENT}Before the sun sets, if you can beat me in a Pokémon battle, you can come with us."))
    closemessage
    applymovement(1, walkdown)
    waitmessage
    msgbox(format("Mitsuru gave Shinjiro a modern device, some Pokéballs and Exp Candies."))
    applymovement(1, stepbackup)
    waitmovement(0)
    msgbox(format("{SPEAKER NAME_MITSURU}{CREATE_MUGSHOT MUGSHOT_MITSURU EMOTE_SMILE}It wouldn't be fair if you have nothing to catch a Pokémon.\p
    Oh, and that device is a Pokédex. I'm just lending it to you.\p
    It's yours if you can beat me.\p
    {SPEAKER NAME_SHINJIRO}{CREATE_MUGSHOT MUGSHOT_SHINJIRO EMOTE_CONFIDENT}Hmph. Just you wait."))
    closemessage
    applymovement(1, MitsuruGivingAkihikoPokedex)
    waitmovement(0)
    msgbox(format("{SPEAKER NAME_MITSURU}{CREATE_MUGSHOT MUGSHOT_MITSURU EMOTE_SMILE}And these are for you."))
    closemessage
    playse(MUS_OBTAIN_ITEM)
    waitse
    msgbox(format("Akihiko received a Pokédex and a wooden box.\p
    Inside was a good bunch of Pokéballs, Exp Candies and some essential Key Items."))
    closemessage
    additem(ITEM_POKE_BALL, 20)
    additem(ITEM_EXP_CANDY_XS, 20)
    additem(ITEM_EXP_ALL)
    setflag(FLAG_SYS_POKEDEX_GET)
    setflag(FLAG_SYS_NATIONAL_DEX)
    special(EnableNationalPokedex)
    setcaughtmon(SPECIES_PAWMI)
    setcaughtmon(SPECIES_PAWMI_DELTA)
    applymovement(1, stepbackup)
    waitmovement(0)
    msgbox(format("{SPEAKER NAME_MITSURU}{CREATE_MUGSHOT MUGSHOT_MITSURU EMOTE_CONFIDENT}To be honest, I'd also like to have a battle with you, Sanada-kun.\p
    It's better to personally see how you fare in battle.\p
    {SPEAKER NAME_AKIHIKO}{CREATE_MUGSHOT MUGSHOT_AKIHIKO EMOTE_CONFIDENT}Heh, sure."))
    closemessage
    applymovement(1, MitsuruGoesBackToCenter)
    waitmovement(0)
    msgbox(format("{SPEAKER NAME_MITSURU}{CREATE_MUGSHOT MUGSHOT_MITSURU EMOTE_CONFIDENT}Alright. I'll be waiting for both of you in Sunshine Meadow.\p
    We'll be having 3-vs-3 Pokémon battles so plan your party accordingly.\p
    {SPEAKER NAME_MITSURU}{CREATE_MUGSHOT MUGSHOT_MITSURU EMOTE_SMILE}Good luck, you'll need it."))
    closemessage
    special(SpawnCameraObject)
    applymovement(OBJ_EVENT_ID_CAMERA, CameraPansToLowerHalfOfRoom)
    applymovement(1, MitsuruExitsTheHouse)
    applymovement(LOCALID_PLAYER, faceright)
    waitmovement(LOCALID_PLAYER)
    applymovement(6, faceleft)
    waitmovement(6)
    delay(16)
    applymovement(5, VulpixFollowsMitsuruExit)
    delay(4)
    applymovement(4, InteleonFollowsMitsuruExit)
    applymovement(LOCALID_PLAYER, facedown)
    applymovement(6, facedown)
    waitmovement(1)
    playse(SE_EXIT)
    waitse
    waitmovement(5)
    playse(SE_EXIT)
    waitse
    waitmovement(4)
    playse(SE_EXIT)
    applymovement(OBJ_EVENT_ID_CAMERA, CameraFromLowerHalfRoomBackToAkihiko)
    waitmovement(0)
    delay(30)
    msgbox(format("{SPEAKER NAME_SHINJIRO}{CREATE_MUGSHOT MUGSHOT_SHINJIRO EMOTE_NORMAL}Bossy.\p
    {SPEAKER NAME_AKIHIKO}{CREATE_MUGSHOT MUGSHOT_AKIHIKO EMOTE_CONFIDENT}Heh. She's one weird rich girl.\p
    {SPEAKER NAME_SHINJIRO}{CREATE_MUGSHOT MUGSHOT_SHINJIRO EMOTE_NORMAL}Let's go."))
    closemessage
    applymovement(6, ShinjiroJoinsPostPokedex)
    waitmovement(0)
    setflag(FLAG_TEMP_1)
    removeobject(1)
    removeobject(4)
    removeobject(5)
    removeobject(6)
    special(RemoveCameraObject)
    setvar(VAR_AKIHIKOS_INTRO_STATE, 33)
    clearflag(FLAG_HIDE_TALK_BUTTON)
    clearflag(FLAG_DONT_TRANSITION_MUSIC)
    fwdtime(13, 40)
    release
    end
}

movement MitsuruLookingAtShinjiro {
    walk_right
    face_down
}

movement stepbackup {
    lock_facing_direction
    walk_up
    unlock_facing_direction
}
movement MitsuruGivingAkihikoPokedex {
    walk_left *2
    walk_down
}
movement MitsuruGoesBackToCenter {
    walk_right
    face_down
}
movement MitsuruExitsTheHouse {
    walk_down *5
    walk_right *2
    walk_down *3
    set_invisible
}
movement CameraPansToLowerHalfOfRoom {
    walk_down *3
    walk_right *2
}

movement VulpixFollowsMitsuruExit {
    walk_right*4
    walk_down*3
    walk_right*2
    walk_down*2
    set_invisible
}

movement InteleonFollowsMitsuruExit {
    walk_down
    walk_right*4
    walk_down*3
    walk_right*2
    walk_down*2
    set_invisible
}

movement CameraFromLowerHalfRoomBackToAkihiko {
    walk_up *3
    walk_left*2
}

movement ShinjiroJoinsPostPokedex {
    walk_left*2
    set_invisible
}

script Route2_House_Eventscript_AkihikoOnFrame38 {
    goto(Route2_House_Eventscript_MitsuruExplainingObjectives)
}

script Route2_House_Eventscript_MitsuruExplainingObjectives {
    applymovement(LOCALID_PLAYER, AkihikoWalksToListenToMitsuru)
    waitmovement(0)
    special(SpawnCameraObject)
    applymovement(OBJ_EVENT_ID_CAMERA, CameraFromAkiToCenter)
    applymovement(1, facedown)
    delay(8)
    applymovement(5, facedown)
    delay(60)
    msgbox(format("{SPEAKER NAME_MITSURU}{CREATE_MUGSHOT MUGSHOT_MITSURU EMOTE_NORMAL}Alright, everyone's here.\p
    Before I start, I'd like to ask you two a question.\p
    {SPEAKER NAME_AKIHIKO}{CREATE_MUGSHOT MUGSHOT_AKIHIKO EMOTE_NORMAL}Sure? What is it?\p
    {SPEAKER NAME_MITSURU}{CREATE_MUGSHOT MUGSHOT_MITSURU EMOTE_NORMAL}How far can a trainer synergize with their Pokémon?\p
    {SPEAKER NAME_SHINJIRO}{CREATE_MUGSHOT MUGSHOT_SHINJIRO EMOTE_DOUBT}Uhh... what?\p
    {SPEAKER NAME_AKIHIKO}{CREATE_MUGSHOT MUGSHOT_AKIHIKO EMOTE_SURPRISED}You mean their bonds?\p
    {SPEAKER NAME_AKIHIKO}{CREATE_MUGSHOT MUGSHOT_AKIHIKO EMOTE_NORMAL}I know when a trainer and a Pokémon share a deep trust, Mega Evolution can be achieved.\p
    {SPEAKER NAME_MITSURU}{CREATE_MUGSHOT MUGSHOT_MITSURU EMOTE_NORMAL}That is right, but not what I meant.\p
    Perhaps I should put it like this."))
    closemessage
    delay(50)
    playbgm(MUS_ABNORMAL_WEATHER, TRUE)
    msgbox(format("{SPEAKER NAME_MITSURU}{CREATE_MUGSHOT MUGSHOT_MITSURU EMOTE_SERIOUS}What if I say, there's a way for a trainer to become one with their Pokémon?\p
    {SPEAKER NAME_AKIHIKO}{CREATE_MUGSHOT MUGSHOT_AKIHIKO EMOTE_SURPRISED}Huh? What do you mean?\p
    {SPEAKER NAME_MITSURU}{CREATE_MUGSHOT MUGSHOT_MITSURU EMOTE_SERIOUS}In the most literal way possible.\p
    {SPEAKER NAME_SHINJIRO}{CREATE_MUGSHOT MUGSHOT_SHINJIRO EMOTE_SURPRISED}That's crazy.\p
    {SPEAKER NAME_MITSURU}{CREATE_MUGSHOT MUGSHOT_MITSURU EMOTE_SERIOUS}Those men you fought- no, the organization behind them, is researching and developping a way to do so.\p
    Lots of progress are made and there are already a few prototypes.\p
    {SPEAKER NAME_MITSURU}{CREATE_MUGSHOT MUGSHOT_MITSURU EMOTE_SAD}Of course, the method isn't perfect and there are more-\p
    {SPEAKER NAME_AKIHIKO}{CREATE_MUGSHOT MUGSHOT_AKIHIKO EMOTE_SURPRISED}Wait, wait just a minute. I'm lost.\p
    Prototypes?? Of what exactly??\p
    {SPEAKER NAME_MITSURU}{CREATE_MUGSHOT MUGSHOT_MITSURU EMOTE_SERIOUS}A new specimen of human, who is able to use Pokémon's ability, in one way or another.\p
    {SPEAKER NAME_AKIHIKO}{CREATE_MUGSHOT MUGSHOT_AKIHIKO EMOTE_SURPRISED}You're kidding??\p
    {SPEAKER NAME_SHINJIRO}{CREATE_MUGSHOT MUGSHOT_SHINJIRO EMOTE_SURPRISED}The more I listen to you, the crazier you sound.\p
    {SPEAKER NAME_MITSURU}{CREATE_MUGSHOT MUGSHOT_MITSURU EMOTE_SAD}That is to be expected. I would have thought the same."))
    closemessage
    delay(40)
    msgbox(format("{SPEAKER NAME_MITSURU}{CREATE_MUGSHOT MUGSHOT_MITSURU EMOTE_NORMAL}In any case, I already told you the goal of those men.\p
    You're free to not believe me. We simply can just part ways right here."))
    closemessage
    delay(30)
    applymovement(LOCALID_PLAYER, emotepensive)
    delay(10)
    applymovement(6, emotepensive)
    waitmovement(0)
    delay(60)
    msgbox(format("{SPEAKER NAME_SHINJIRO}{CREATE_MUGSHOT MUGSHOT_SHINJIRO EMOTE_NORMAL}Look, Princess. It's not like we don't want to believe you.\p
    It all just sounds too much like a fiction, okay.\p
    At least show us some proofs?\p
    {SPEAKER NAME_AKIHIKO}{CREATE_MUGSHOT MUGSHOT_AKIHIKO EMOTE_SERIOUS}Right, didn't you say you know where to find them?\p
    How about you leading us to them and we'll see if what you said were real.\p
    {SPEAKER NAME_MITSURU}{CREATE_MUGSHOT MUGSHOT_MITSURU EMOTE_NORMAL}.....\p
    Very well. I suppose that's a logical idea."))
    closemessage
    playbgm(MUS_NONE, TRUE)
    applymovement(1, faceup)
    applymovement(OBJ_EVENT_ID_CAMERA, CameraPansUpToClock)
    waitmovement(0)
    applymovement(1, emotepensive)
    waitmovement(0)
    delay(90)
    applymovement(1, facedown)
    applymovement(OBJ_EVENT_ID_CAMERA, CameraGoesBackToCenterOfTheRoom)
    waitmovement(0)
    playbgm(MUS_RG_SS_ANNE, TRUE)
    delay (60)
    msgbox(format("{SPEAKER NAME_MITSURU}{CREATE_MUGSHOT MUGSHOT_MITSURU EMOTE_NORMAL}For now, I suggest we rest for the night.
    It's late and we're all tired from the battles.\p
    We'll depart early tomorrow morning so you two may want to stay here."))
    closemessage
    goto(Route2_House_Eventscript_StayingTheNight)
}

movement CameraFromAkiToCenter {
    walk_up
}
movement AkihikoWalksToListenToMitsuru {
    player_run_up *4
    player_run_left *2
    walk_up *2    
}

movement CameraPansUpToClock {
    walk_up *2
}

movement CameraGoesBackToCenterOfTheRoom {
    walk_down *2
}

script Route2_House_Eventscript_StayingTheNight {
    delay(60)
    applymovement(LOCALID_PLAYER, AkihikoLookingAtTheBed)
    waitmovement(0)
    msgbox(format("{SPEAKER NAME_AKIHIKO}{CREATE_MUGSHOT MUGSHOT_AKIHIKO EMOTE_SURPRISED}Uhhh you sure about that? There's only one bed."))
    closemessage
    applymovement(1, MitsuruLookingAtTheBed)
    waitmovement(0)
    msgbox(format("{SPEAKER NAME_MITSURU}{CREATE_MUGSHOT MUGSHOT_MITSURU EMOTE_SAD}M-my apology, it slipped my mind that this house didn't have guest rooms.\p
    {SPEAKER NAME_MITSURU}{CREATE_MUGSHOT MUGSHOT_MITSURU EMOTE_NORMAL}You two can take the bed. I'm okay with the floor.\p
    {SPEAKER NAME_SHINJIRO}{CREATE_MUGSHOT MUGSHOT_SHINJIRO EMOTE_GEEZ}For Arceus's sake, Princess. Aki's kidding.\p
    Just sleep on the bed, we have our own sleeping bags."))
    closemessage
    playse(SE_PIN)
    applymovement(LOCALID_PLAYER, AKihikoRemembersCampGears)
    waitmovement(0)
    msgbox(format("{SPEAKER NAME_AKIHIKO}{CREATE_MUGSHOT MUGSHOT_AKIHIKO EMOTE_SURPRISED}Oh, right! Kurosawa-san gave us the Camping Gears."))
    closemessage
    playse(SE_M_BUBBLE3)
    applymovement(6, ShinjiroSweat)
    waitmovement(0)
    msgbox(format("{SPEAKER NAME_SHINJIRO}{CREATE_MUGSHOT MUGSHOT_SHINJIRO EMOTE_GEEZ}Dumbass, you weren't kidding!?\p
    {SPEAKER NAME_AKIHIKO}{CREATE_MUGSHOT MUGSHOT_AKIHIKO EMOTE_SERIOUS}Stop calling me dumb!"))
    closemessage
    playse(SE_FAILURE)
    applymovement(LOCALID_PLAYER, AkihikoBickering)
    delay(10)
    playse(SE_FAILURE)
    applymovement(6, ShinjiroBickering)
    waitmovement(0)
    lock
    setobjectnewmovementtype(LOCALID_PLAYER, MOVEMENT_TYPE_RUN_IN_PLACE_RIGHT)
    setobjectnewmovementtype(6, MOVEMENT_TYPE_RUN_IN_PLACE_LEFT)
    release
    delay(30)
    applymovement(LOCALID_PLAYER, jumpinplaceright)
    delay(10)
    applymovement(6, jumpinplaceleft)
    waitmovement(0)
    lock
    setobjectnewmovementtype(LOCALID_PLAYER, MOVEMENT_TYPE_RUN_IN_PLACE_RIGHT)
    setobjectnewmovementtype(6, MOVEMENT_TYPE_RUN_IN_PLACE_LEFT)
    release
    delay(30)
    applymovement(1, shakehorizontal)
    waitmovement(0)
    msgbox("Pft-")
    playse(SE_SHINY)
    applymovement(1, emotemusic)
    waitmovement(0)
    msgbox(format("{SPEAKER NAME_MITSURU}{CREATE_MUGSHOT MUGSHOT_MITSURU EMOTE_HAPPY}Hehehe."))
    closemessage
    applymovement(LOCALID_PLAYER, faceup)
    delay(10)
    applymovement(6, faceup)
    waitmovement(0)
    delay(60)
    msgbox(format("{SPEAKER NAME_AKIHIKO}{CREATE_MUGSHOT MUGSHOT_AKIHIKO EMOTE_SURPRISED}...\p
    {SPEAKER NAME_AKIHIKO}{CREATE_MUGSHOT MUGSHOT_AKIHIKO EMOTE_HAPPY}Hehe.\p
    {SPEAKER NAME_SHINJIRO}{CREATE_MUGSHOT MUGSHOT_SHINJIRO EMOTE_SMILE}Hmph."))
    closemessage
    applymovement(OBJ_EVENT_ID_CAMERA, CameraSlowlyPansUpFadeScreen)
    delay(30)
    fadescreenswapbuffers(FADE_TO_BLACK)
    delay(80)
    waitmovement(0)
    warpsilent(MAP_BLANK_MAP, 0, 0)
}

movement AkihikoLookingAtTheBed {
    face_left
    emote_pensive
    delay_16
    delay_16
    delay_16
    delay_16
    face_up
}

movement MitsuruLookingAtTheBed {
    face_left
    delay_16
    emote_pensive
    delay_16
    delay_16
    delay_16
    face_down
}

movement AKihikoRemembersCampGears {
    emote_exclamation_mark
    face_right    
}

movement ShinjiroSweat {
    emote_sweat
    face_left
}

movement jumpinplaceleft {
    jump_in_place_left
}

movement AkihikoBickering {
    jump_in_place_right
    emote_upset
    delay_16
    delay_16
    shake_horizontal
    emote_x
    delay_16
    delay_16
}

movement ShinjiroBickering {
    jump_in_place_left
    emote_x
    delay_16
    delay_16
    shake_vertical
    emote_angry
    delay_16
    delay_16
}

movement emotemusic {
    emote_music
}
movement CameraSlowlyPansUpFadeScreen {
    walk_slow_up *3
}

script Route2_House_Eventscript_AkihikoOnFrame39 {
    goto(Route2_House_Eventscript_AkihikoCantSleep)
}

script Route2_House_Eventscript_AkihikoCantSleep {
    delay(60)
    applymovement(LOCALID_PLAYER, emotepensive)
    waitmovement(0)
    delay(40)
    msgbox(format("{SPEAKER NAME_AKIHIKO}{CREATE_MUGSHOT MUGSHOT_AKIHIKO EMOTE_STRUGGLED}(Dang... I can't sleep at all.)\p
    (Gotta admit, what Kirijo said earlier does bug me.)\p
    {SPEAKER NAME_AKIHIKO}{CREATE_MUGSHOT MUGSHOT_AKIHIKO EMOTE_SERIOUS}(It didn't look like she was lying.)\p
    (If it was all true... and if Miki is already in their hands...)\p
    {SPEAKER NAME_AKIHIKO}{CREATE_MUGSHOT MUGSHOT_AKIHIKO EMOTE_STRUGGLED}(No, I must think positively. She must've still been fine and been waiting for me to come get her home.)\p
    (If I can't sleep... may as well go train a bit.)"))
    closemessage
    applymovement(LOCALID_PLAYER, faceup)
    waitmovement(0)
    special(SpawnCameraObject)
    applymovement(OBJ_EVENT_ID_CAMERA, CameraGoesUp2)
    waitmovement(0)
    delay(30)
    msgbox(format("{SPEAKER NAME_AKIHIKO}{CREATE_MUGSHOT MUGSHOT_AKIHIKO EMOTE_NORMAL}(Kirijo is already sleeping soundly. She won't know if I sneak out.)"))
    closemessage
    delay(20)
    applymovement(4, facedown)
    waitmovement(0)
    playse(SE_PIN)
    applymovement(LOCALID_PLAYER, emoteexclamation)
    waitmovement(0)
    delay(30)
    playse(SE_M_DETECT)
    applymovement(4, shakehorizontal)
    waitmovement(0)
    msgbox(format("{SPEAKER NAME_INTELEON}{CREATE_MUGSHOT MUGSHOT_INTELEON EMOTE_SERIOUS}-Hiss-"))
    closemessage
    playse(SE_M_BUBBLE3)
    applymovement(LOCALID_PLAYER, emotesweat)
    waitmovement(0)
    msgbox(format("{SPEAKER NAME_AKIHIKO}{CREATE_MUGSHOT MUGSHOT_AKIHIKO EMOTE_DOUBT}(And this Inteleon keeps looking daggers at me...)\p
    (What did I even do?)"))
    closemessage
    applymovement(LOCALID_PLAYER, faceright)
    applymovement(OBJ_EVENT_ID_CAMERA, CameraGoesRight2)
    waitmovement(0)
    delay(30)
    msgbox(format("{SPEAKER NAME_AKIHIKO}{CREATE_MUGSHOT MUGSHOT_AKIHIKO EMOTE_NORMAL}(Where's Shinji, though?)\p
    (Maybe he's outside.)"))
    closemessage
    applymovement(OBJ_EVENT_ID_CAMERA, CameraGoesLeft2)
    waitmovement(0)
    msgbox(format("{SPEAKER NAME_AKIHIKO}{CREATE_MUGSHOT MUGSHOT_AKIHIKO EMOTE_NORMAL}I uh... just gonna go outside for a bit."))
    closemessage
    delay(20)
    applymovement(OBJ_EVENT_ID_CAMERA, CameraGoesDown2)
    waitmovement(0)
    special(RemoveCameraObject)
    applymovement(LOCALID_PLAYER, AkihikoExitTheRoom)
    waitmovement(0)
    setvar(VAR_AKIHIKOS_INTRO_STATE, 40)
    fwdtime(22, 40)
    warp(MAP_ROUTE2, 8, 12)
}

movement emotesweat {
    emote_sweat
}

movement CameraGoesUp2 {
    walk_up *2
}

movement CameraGoesDown2 {
    walk_down *2
}

movement AkihikoExitTheRoom {
    player_run_right *4
    player_run_down *2
}

script TakeARest {
    msgbox(format("Let's take a quick rest."), MSGBOX_YESNO)
    if (var(VAR_RESULT) == TRUE){
        fadescreenswapbuffers(FADE_TO_BLACK)
        special(HealPlayerParty)
        playse(MUS_HEAL)
        waitse
        fadescreenswapbuffers(FADE_FROM_BLACK)
    }
    else{}
    closemessage
    end
}

script Route2_House_Eventscript_AkihikoOnWarp42 {
    goto(Route2_House_Eventscript_AkihikoGoesToSleep)
}

script Route2_House_Eventscript_AkihikoGoesToSleep {
    lock
    delay(30)
    playse(SE_M_YAWN)
    applymovement(LOCALID_PLAYER, shakehorizontal)
    waitmovement(0)
    msgbox(format("{SPEAKER NAME_AKIHIKO}{CREATE_MUGSHOT MUGSHOT_AKIHIKO EMOTE_NORMAL}-yawn- (I'm so tired. Time to sleep.)"))
    closemessage
    warpsilent(MAP_BLANK_MAP, 0 ,0)
    release
}

script Route2_House_Eventscript_Bathroom {
    lock
    if (var(VAR_TEMP_0) == 0){
        msgbox(format("Do you want to use the bathroom?"), MSGBOX_YESNO)
        closemessage
        if (var(VAR_RESULT) == TRUE){
            fadescreenswapbuffers(FADE_TO_BLACK)
            playse(SE_DOOR)
            waitse
            playse(SE_DOWNPOUR)
            delay(60)
            playse(SE_DOWNPOUR_STOP)
            waitse
            playse(SE_DOOR)
            waitse
            applymovement(LOCALID_PLAYER, faceleft)
            waitmovement(0)
            delay (30)
            fadescreenswapbuffers(FADE_FROM_BLACK)
            delay(20)
            playse(SE_SHINY)
            applymovement(LOCALID_PLAYER, emotemusic)
            waitmovement(0)
            msgbox(format("{SPEAKER NAME_AKIHIKO}{CREATE_MUGSHOT MUGSHOT_AKIHIKO EMOTE_HAPPY}(All refreshed.)"))
            closemessage
            special(HealPlayerParty)
            setvar(VAR_TEMP_0, 1)
        }
        else{
        msgbox(format("{SPEAKER NAME_AKIHIKO}{CREATE_MUGSHOT MUGSHOT_AKIHIKO EMOTE_NORMAL}(Nah, I think I'm good.)"))
        closemessage
        applymovement(LOCALID_PLAYER, faceleft)
        waitmovement(0)
        }
    }
    elif (var(VAR_TEMP_0) == 1){
        msgbox(format("{SPEAKER NAME_AKIHIKO}{CREATE_MUGSHOT MUGSHOT_AKIHIKO EMOTE_NORMAL}(I just used the bathroom.)"))
        closemessage
        applymovement(LOCALID_PLAYER, faceleft)
        waitmovement(0)
        if(var(VAR_AKIHIKOS_INTRO_STATE) >= 42)
            {
            setvar(VAR_TEMP_0, 2)
            }
    }
    elif(var(VAR_TEMP_0) == 2){
        msgbox(format("{SPEAKER NAME_AKIHIKO}{CREATE_MUGSHOT MUGSHOT_AKIHIKO EMOTE_NORMAL}(That reminds me, Kirijo took a really long bath yesterday.)\p
        {SPEAKER NAME_AKIHIKO}{CREATE_MUGSHOT MUGSHOT_AKIHIKO EMOTE_DOUBT}I don't understand. What's so fun in the bathroom that made her stay that long?\p
        {SPEAKER NAME_MITSURU}{CREATE_MUGSHOT MUGSHOT_MITSURU EMOTE_NORMAL}What are you staring at the bathroom door for? Is there anything wrong?\p
        {SPEAKER NAME_AKIHIKO}{CREATE_MUGSHOT MUGSHOT_AKIHIKO EMOTE_SURPRISED}N-No, nothing! Nothing at all.\p
        {SPEAKER NAME_MITSURU}{CREATE_MUGSHOT MUGSHOT_MITSURU EMOTE_NORMAL}?"))
        closemessage
        applymovement(LOCALID_PLAYER, faceleft)
        waitmovement(0)
        setvar(VAR_TEMP_0, 3)
    }
    elif(var(VAR_TEMP_0) == 3){
        msgbox(format("{SPEAKER NAME_AKIHIKO}{CREATE_MUGSHOT MUGSHOT_AKIHIKO EMOTE_NORMAL}(I should get going.)"))
        closemessage
        applymovement(LOCALID_PLAYER, faceleft)
        waitmovement(0)
    }
    release
    end
}

script Route2_House_Eventscript_AkihikoOnFrame43 {
    goto(Route2_House_Eventscript_TheFirstMorning)
}

script Route2_House_Eventscript_TheFirstMorning {
    lock
    delay(30)
    playse(SE_M_YAWN)
    waitse
    msgbox(format("{SPEAKER NAME_AKIHIKO}{CREATE_MUGSHOT MUGSHOT_AKIHIKO EMOTE_NORMAL}-yawn- (Gosh, I'm still feeling sleepy.)\p
    (Maybe doing night battle wasn't a good choice.)"))
    closemessage
    delay(30)
    msgbox(format("{SPEAKER NAME_MITSURU}{CREATE_MUGSHOT MUGSHOT_MITSURU EMOTE_NORMAL}Everyone, please gather around. I'll explain our plan route."))
    closemessage
    special(SpawnCameraObject)
    applymovement(OBJ_EVENT_ID_CAMERA, CameraPansToMitsuruTheFirstMorning)
    delay(60)
    applymovement(LOCALID_PLAYER, AkihikoGoesToPlaceTheFirstMorning)
    delay (4)
    applymovement(6, ShinjiroGoesToPlaceTheFirstMorning)
    waitmovement(0)
    delay(30)
    msgbox(format("{SPEAKER NAME_MITSURU}{CREATE_MUGSHOT MUGSHOT_MITSURU EMOTE_NORMAL}Please take a look at the map."))
    closemessage
    //TO DO: Create the region's map
    delay(30)
    msgbox(format("{SPEAKER NAME_MITSURU}{CREATE_MUGSHOT MUGSHOT_MITSURU EMOTE_NORMAL}I believe our destination is around the base of Goldenwings Mountain.\p
    To get there we can take a ship from Coralport, west of here\p
    and then go through Route 4 from Windward Town.\p
    Any questions?\p
    {SPEAKER NAME_AKIHIKO}{CREATE_MUGSHOT MUGSHOT_AKIHIKO EMOTE_NORMAL}Nope, the path is simple enough.\p
    {SPEAKER NAME_MITSURU}{CREATE_MUGSHOT MUGSHOT_MITSURU EMOTE_SMILE}C'est magnifique.\p
    {SPEAKER NAME_MITSURU}{CREATE_MUGSHOT MUGSHOT_MITSURU EMOTE_NORMAL}Oh, before I forget.\p
    We should make a stop at my personal researcher's lab in Windward Town. There's a package for us that I've ordered.\p
    It'll be a great help for our journey.\p
    {SPEAKER NAME_SHINJIRO}{CREATE_MUGSHOT MUGSHOT_SHINJIRO EMOTE_NORMAL}More gadgets?\p
    {SPEAKER NAME_MITSURU}{CREATE_MUGSHOT MUGSHOT_MITSURU EMOTE_SMILE}And Pokémons.\p
    {SPEAKER NAME_AKIHIKO}{CREATE_MUGSHOT MUGSHOT_AKIHIKO EMOTE_SMILE}Alright! Let's head out then!"))
    closemessage
    applymovement(5, walkleft)
    applymovement(6, walkleft)
    waitmovement(0)
    removeobject(5)
    removeobject(6)
    applymovement(1, CameraGoesDown2)
    applymovement(4, InteleonJoinsTheFirstMorning)
    waitmovement(0)
    removeobject(1)
    removeobject(4)
    applymovement(OBJ_EVENT_ID_CAMERA, walkdown)
    applymovement(LOCALID_PLAYER, facedown)
    waitmovement(0)
    special(RemoveCameraObject)
    delay(30)
    playfanfare(SE_M_SAND_ATTACK)
    waitfanfare
    msgbox(format("Akihiko's Prologue: Sunshine Memories\n
    {COLOR GREEN}{CLEAR_TO 64}COMPLETE\p
    Chapter 1: Under The Rubbles\n
    {COLOR GREEN}{CLEAR_TO 64}Level cap: 18"))
    setflag(FLAG_TEMP_1)
    setvar(VAR_AKIHIKOS_INTRO_STATE, 44)
    setvar(VAR_CURRENT_LEVEL_CAP, 18)
    clearflag(FLAG_HIDE_TALK_BUTTON)
    release
    end
}

movement CameraPansToMitsuruTheFirstMorning {
    walk_up *2
    walk_diag_northeast*2
    walk_up
}

movement AkihikoGoesToPlaceTheFirstMorning {
    walk_up *4
    walk_right *2
}
movement ShinjiroGoesToPlaceTheFirstMorning {
    walk_left *2
    walk_up
}

movement InteleonJoinsTheFirstMorning {
    walk_right *4
}
