mapscripts SunshineMeadow_MapScripts {
    MAP_SCRIPT_ON_LOAD{
        if (var(VAR_MITSURUS_INTRO_STATE) > 16){
            setmetatile(11, 5, 0x020, TRUE)
            setmetatile(12, 5, 0x01A, TRUE)
            setmetatile(8, 5, 0x01A, TRUE)
        }
    }
    MAP_SCRIPT_ON_WARP_INTO_MAP_TABLE[
        VAR_AKIHIKOS_INTRO_STATE, 34: SunshineMeadow_Eventscript_AkihikoOnWarp34
        VAR_AKIHIKOS_INTRO_STATE, 35: SunshineMeadow_Eventscript_AkihikoOnWarp35
        VAR_AKIHIKOS_INTRO_STATE, 37: SunshineMeadow_Eventscript_AkihikoOnWarp37
        /////
        VAR_MITSURUS_INTRO_STATE, 8: SunshineMeadow_Eventscript_MitsuruOnWarp8
    ]
    MAP_SCRIPT_ON_FRAME_TABLE[
        VAR_AKIHIKOS_INTRO_STATE, 35: SunshineMeadow_Eventscript_AkihikoOnFrame35
        VAR_AKIHIKOS_INTRO_STATE, 37: SunshineMeadow_Eventscript_AkihikoOnFrame37
        /////
        VAR_MITSURUS_INTRO_STATE, 6: SunshineMeadow_Eventscript_MitsuruOnFrame6
        VAR_MITSURUS_INTRO_STATE, 8: SunshineMeadow_Eventscript_MitsuruOnFrame8
        VAR_MITSURUS_INTRO_STATE, 17: SunshineMeadow_Eventscript_MitsuruOnFrame17
    ]
    MAP_SCRIPT_ON_TRANSITION{
        if(var(VAR_AKIHIKOS_INTRO_STATE) == 34){
            setflag(FLAG_TEMP_2)
        }
        if(var(VAR_AKIHIKOS_INTRO_STATE) == 35){
            setobjectxyperm(1, 12, 11)
            setobjectxyperm(2, 12, 12)
            setobjectxyperm(3, 10, 10)
        }
        if(var(VAR_AKIHIKOS_INTRO_STATE) == 37){
            setobjectxyperm(1, 12, 11)
            setobjectxyperm(2, 12, 12)
            setobjectxyperm(3, 10, 10)
            setobjectxyperm(4, 7, 11)
            setobjectxyperm(5, 9, 14)
        }
        if(var(VAR_AKIHIKOS_INTRO_STATE) >= 38 && var(VAR_MITSURUS_INTRO_STATE) != 17){
            setflag(FLAG_TEMP_1)
            setflag(FLAG_TEMP_2)
        }
        if (var(VAR_MITSURUS_INTRO_STATE) >= 6 && var(VAR_MITSURUS_INTRO_STATE) <= 8){
            setflag(FLAG_TEMP_3)
        }
        if (var(VAR_MITSURUS_INTRO_STATE) > 16){
            setobjectxyperm(12, 14, 8)
        }
        if (var(VAR_MITSURUS_INTRO_STATE) == 17){
            setflag(FLAG_SPAWN_INVISIBLE)
            setflag(FLAG_TEMP_2)
            setobjectxyperm(1, 11, 10)
            setobjectmovementtype(1, MOVEMENT_TYPE_FACE_UP)
            setobjectxyperm(2, 12, 10)
            setobjectmovementtype(2, MOVEMENT_TYPE_WALK_IN_PLACE_UP)
            setobjectxyperm(3, 10, 10)
            setobjectmovementtype(3, MOVEMENT_TYPE_FACE_UP)
        }
    }
}

script SunshineMeadow_Eventscript_AkihikoOnWarp34 {
    applymovement(4, invisible)
    applymovement(5, invisible)

}
script SunshineMeadow_Eventscript_AkihikoOnWarp35 {
    applymovement(LOCALID_PLAYER, invisible)
    applymovement(1, faceleft)
    applymovement(2, faceleft)
    applymovement(4, invisible)
}

script SunshineMeadow_Eventscript_AkihikoOnWarp37 {
    applymovement(LOCALID_PLAYER, invisible)
    applymovement(1, faceleft)
    applymovement(2, faceleft)
    applymovement(4, faceright)
    applymovement(5, faceup)
}

script SunshineMeadow_Eventscript_AkihikoOnFrame35 {
    goto (SunshineMeadow_Eventscript_StartingMitsuruFirstBattle)
}

script SunshineMeadow_Eventscript_MitsuruOnFrame6 {
    lock
    hidefollower
    setflag(FLAG_DONT_TRANSITION_MUSIC)
    call(Eventscript_CutsceneStarts)
    setvar(VAR_MITSURUS_INTRO_STATE, 7)
    warpsilent(MAP_BLANK_MAP, 0, 0)
}

script SunshineMeadow_Eventscript_MitsuruOnWarp8 {
    applymovement(LOCALID_PLAYER, faceup)    
}

script SunshineMeadow_Eventscript_MitsuruOnFrame8 {
    clearflag(FLAG_DONT_TRANSITION_MUSIC)
    goto(SunshineMeadow_Eventscript_MitsuruAproachTheFence)
}

script SunshineMeadow_Eventscript_MitsuruOnFrame17 {
    goto(SunshineMeadow_Eventscript_MitsuruLookingOverCollapsedCave)    
}

script SunshineMeadow_Eventscript_PreMitsuruFirstBattleTalk {
    faceplayer
    msgbox(format("{COLOR RED}Tips: From this point onward, the choices you made thorough the game will ultimately affect the story outcome.\p
    {SPEAKER NAME_MITSURU}{CREATE_MUGSHOT MUGSHOT_MITSURU EMOTE_SMILE}{COLOR DARK_GRAY}You've come. I suppose you're ready for our first battle?"), MSGBOX_YESNO)
    closemessage
    if (var(VAR_RESULT) == FALSE){
        msgbox(format("{SPEAKER NAME_MITSURU}{CREATE_MUGSHOT MUGSHOT_MITSURU EMOTE_SMILE}No worry. We still have time."))
        closemessage
    }
    else {
        goto (SunshineMeadow_Eventscript_CheckingPartyNumberForMitsuruFirstBattle)
    }
    end
}

script SunshineMeadow_Eventscript_CheckingPartyNumberForMitsuruFirstBattle {
    specialvar(VAR_RESULT, CountPartyNonEggMons)
    if (var(VAR_RESULT) < 3){
        msgbox(format("{SPEAKER NAME_MITSURU}{CREATE_MUGSHOT MUGSHOT_MITSURU EMOTE_NORMAL}It seems like you don't have at least 3 Pokémon in your party.\p
        I'd like us to have a fair battle. {COLOR RED}Do you still want to proceed at this state?"), MSGBOX_YESNO)
        closemessage
        if (var(VAR_RESULT) == FALSE){
            msgbox(format("{SPEAKER NAME_MITSURU}{CREATE_MUGSHOT MUGSHOT_MITSURU EMOTE_NORMAL}Well then, please come back later when you have at least 3 Pokémon in your party."))
            closemessage
            release
            end
        }
        else {
            playse(SE_M_STAT_DECREASE)
            applymovement(1, pointdown2)
            waitse
            waitmovement(0)
            delay(20)
            msgbox(format("{SPEAKER NAME_MITSURU}{CREATE_MUGSHOT MUGSHOT_MITSURU EMOTE_SERIOUS}Are you underestimating me because I'm a girl?"))
            closemessage
            subvar(VAR_MITSURU_RELATIONSHIP_POINTS, 3) //current point: 97
            fwdtime(18, 0)
            setflag(FLAG_HIDE_TALK_BUTTON)
            setflag(FLAG_DONT_TRANSITION_MUSIC)
            warpsilent(MAP_BLANK_MAP, 0, 0)
            end
            }
    }
    elif (var(VAR_RESULT) >= 3){
        playse(SE_SHINY)
        applymovement(1, pointup2)
        waitse
        waitmovement(0)
        delay(20)
        msgbox(format("{SPEAKER NAME_MITSURU}{CREATE_MUGSHOT MUGSHOT_MITSURU EMOTE_SMILE}Very well. Shall we start?"))
        closemessage
        addvar(VAR_MITSURU_RELATIONSHIP_POINTS, 3) //current point: 103
        fwdtime(18, 0)
        setflag(FLAG_HIDE_TALK_BUTTON)
        setflag(FLAG_DONT_TRANSITION_MUSIC)
        warpsilent(MAP_BLANK_MAP, 0, 0)
        end
    }
}

movement pointdown2 {
    point_down_2
}

movement pointup2 {
    point_up_2
}

script SunshineMeadow_Eventscript_StartingMitsuruFirstBattle {
    lock
    setobjectnewmovementtype(2, MOVEMENT_TYPE_WALK_IN_PLACE_LEFT)
    release
    applymovement(LOCALID_PLAYER, CameraPansDownToTheArenaMitsuruFirstBattle)
    waitmovement(0)
    delay(30)
    playse(SE_EXIT)
    waitse
    addobject(4)
    applymovement(4, ShinjiroComesInTime)
    applymovement(LOCALID_PLAYER, CameraPansDownToShinjiroMitsuruFirstFight)
    applymovement(5, AkiMitsuNoticeShinjiro)
    delay(8)
    applymovement(1,AkiMitsuNoticeShinjiro )
    waitmovement(0)
    if (var(VAR_MITSURU_RELATIONSHIP_POINTS) == 97)
    {
        msgbox(format("{SPEAKER NAME_AKIHIKO}{CREATE_MUGSHOT MUGSHOT_AKIHIKO EMOTE_CONFIDENT}Shinji! You made it!\p
        {SPEAKER NAME_SHINJIRO}{CREATE_MUGSHOT MUGSHOT_SHINJIRO EMOTE_STRUGGLED}Damn, you two are already at it, huh?\p
        {SPEAKER NAME_MITSURU}{CREATE_MUGSHOT MUGSHOT_MITSURU EMOTE_NORMAL}You're just in time. We're starting."))
    }
    else
    {
        msgbox(format("{SPEAKER NAME_AKIHIKO}{CREATE_MUGSHOT MUGSHOT_AKIHIKO EMOTE_CONFIDENT}Shinji! You made it!\p
        {SPEAKER NAME_SHINJIRO}{CREATE_MUGSHOT MUGSHOT_SHINJIRO EMOTE_STRUGGLED}Damn, you two are already at it, huh?\p
        {SPEAKER NAME_MITSURU}{CREATE_MUGSHOT MUGSHOT_MITSURU EMOTE_SMILE}Not quite, you're just in time. We're starting."))
    }
    closemessage
    applymovement(5, faceright)
    applymovement(LOCALID_PLAYER, CameraPansBackUpToArenaMitsuruFirstFight)
    delay(8)
    applymovement(1, faceleft)
    waitmovement(LOCALID_PLAYER)
    delay(60)
    applymovement(LOCALID_PLAYER, walkup)
    waitmovement(0)
    delay(20)
    if (var(VAR_MITSURU_RELATIONSHIP_POINTS) == 97){
        msgbox(format("{SPEAKER NAME_AKIHIKO}{CREATE_MUGSHOT MUGSHOT_AKIHIKO EMOTE_CONFIDENT}Not gonna use your Inteleon?\p
        {SPEAKER NAME_MITSURU}{CREATE_MUGSHOT MUGSHOT_MITSURU EMOTE_SERIOUS}I already told you I wanted a fair fight.\p
        He's not mine. He's only here to watch over me.\p
        {SPEAKER NAME_AKIHIKO}{CREATE_MUGSHOT MUGSHOT_AKIHIKO EMOTE_NORMAL}Huh, if you said so.\p
        {SPEAKER NAME_AKIHIKO}{CREATE_MUGSHOT MUGSHOT_AKIHIKO EMOTE_SERIOUS}Alright then. Let's see what you can do, Kirijo."))
    }
    else
    {
        msgbox(format("{SPEAKER NAME_AKIHIKO}{CREATE_MUGSHOT MUGSHOT_AKIHIKO EMOTE_CONFIDENT}Not gonna use your Inteleon?\p
        {SPEAKER NAME_MITSURU}{CREATE_MUGSHOT MUGSHOT_MITSURU EMOTE_SMILE}It wouldn't be a fair fight if I do.\p
        Beside, he's not mine. He's only here to watch over me.\p
        {SPEAKER NAME_AKIHIKO}{CREATE_MUGSHOT MUGSHOT_AKIHIKO EMOTE_SURPRISED}Huh, if you said so.\p
        {SPEAKER NAME_AKIHIKO}{CREATE_MUGSHOT MUGSHOT_AKIHIKO EMOTE_CONFIDENT}Alright then. Let's go, Kirijo."))
    }
    closemessage
    applymovement (LOCALID_PLAYER, walkdown)
    waitmovement(0)
    special(SpawnCameraObject)
    applymovement (LOCALID_PLAYER, InviPlayerTakingSpritesPlace)
    waitmovement(0)
    applymovement (LOCALID_PLAYER, visible)
    applymovement (5, invisible)
    waitmovement(0)
    special(RemoveCameraObject)
    clearflag(FLAG_DONT_TRANSITION_MUSIC)
    goto (SunshineMeadow_Eventscript_ChoosingPartyForMitsuruFirstBattle)
    end
}

movement CameraPansDownToTheArenaMitsuruFirstBattle {
    walk_slow_down *3
}
movement ShinjiroComesInTime {
    set_visible
    walk_fast_up *3
}
movement CameraPansDownToShinjiroMitsuruFirstFight {
    walk_fast_down *2
}
movement CameraPansBackUpToArenaMitsuruFirstFight {
    walk_up *2
}
movement AkiMitsuNoticeShinjiro {
    emote_exclamation_mark
    face_down
}
movement InviPlayerTakingSpritesPlace {
    walk_fast_left*3
    face_right
}
script SunshineMeadow_Eventscript_ChoosingPartyForMitsuruFirstBattle {
    setflag(FLAG_NO_WHITEOUT)
    setflag(FLAG_NO_BAG_USE)
    special(ChooseHalfPartyForBattle) //Choose 3 mons to fight
    waitstate()
    if (var(VAR_RESULT) == FALSE){
        msgbox(format("{SPEAKER NAME_AKIHIKO}{CREATE_MUGSHOT MUGSHOT_AKIHIKO EMOTE_NORMAL}(Let's pick 3 Pokémon to fight. Kirijo is waiting.)"))
        closemessage
        goto (SunshineMeadow_Eventscript_ChoosingPartyForMitsuruFirstBattle)
    }
    else {
        special(ReducePlayerPartyToSelectedMons)
        setvar (VAR_0x8004, FRONTIER_UTIL_FUNC_SET_DATA)
	    setvar (VAR_0x8005, FRONTIER_DATA_SELECTED_MON_ORDER)
        special (CallFrontierUtilFunc)
        setvar (VAR_0x8004, SPECIAL_BATTLE_MULTI)
        setvar (VAR_0x8005, MULTI_BATTLE_CHOOSE_MONS)
        trainerbattle_no_intro(TRAINER_MITSURU_SUNSHINE_MEADOW, MitsuruFirstBattleDefeated)
        fwdtime(18, 30)
        setvar (VAR_0x8004, FRONTIER_UTIL_FUNC_SAVE_PARTY)
        special (CallFrontierUtilFunc)
	    special (LoadPlayerParty)
        goto (SunshineMeadow_Eventscript_MitsuruFirstBattleResult)       
    }
}

script SunshineMeadow_Eventscript_MitsuruFirstBattleResult {
    specialvar(VAR_RESULT, GetBattleOutcome)
        if (var(VAR_RESULT) == B_OUTCOME_WON && var(VAR_MITSURU_RELATIONSHIP_POINTS) >100){
            msgbox (SunshineMeadow_Eventscript_WonInMitsuruFirstBattlePointUp)
            closemessage
        }
        elif(var(VAR_RESULT) == B_OUTCOME_WON && var(VAR_MITSURU_RELATIONSHIP_POINTS) <100){
            msgbox (SunshineMeadow_Eventscript_WonInMitsuruFirstBattlePointDown)
            closemessage
        }
        elif(var(VAR_RESULT) != B_OUTCOME_WON && var(VAR_MITSURU_RELATIONSHIP_POINTS) >100){
            msgbox (SunshineMeadow_Eventscript_LostInMitsuruFirstBattlePointUp)
            closemessage
        }
        else{
            delay(20)
            playse(SE_M_STAT_DECREASE)
            applymovement(1, pointdown3)
            waitse
            waitmovement(0)
            delay(20)
            setvar(VAR_MITSURU_RELATIONSHIP_POINTS, 0)
            msgbox(SunshineMeadow_Eventscript_LostInMitsuruFirstBattlePointDown) //Game Over
            closemessage
            goto (SunshineMeadow_Eventscript_PostMitsuruFirstBattleGameOver)
            end
        }
        goto (SunshineMeadow_Eventscript_PostMitsuruFirstBattle)
}

movement pointdown3 {
    point_down_3
}

text SunshineMeadow_Eventscript_WonInMitsuruFirstBattlePointUp {
    format("{SPEAKER NAME_MITSURU}{CREATE_MUGSHOT MUGSHOT_MITSURU EMOTE_SMILE}To be honest, althought I didn't use my full team,
    it's still very impressive that you managed to beat me, Sanada-kun.\p
    {SPEAKER NAME_AKIHIKO}{CREATE_MUGSHOT MUGSHOT_AKIHIKO EMOTE_SERIOUS}(I only managed to win by sheer luck...\p
    The way she ordered her Pokémons was so smart.\p
    It's... amazing.)")
}

text SunshineMeadow_Eventscript_WonInMitsuruFirstBattlePointDown {
    format("{SPEAKER NAME_MITSURU}{CREATE_MUGSHOT MUGSHOT_MITSURU EMOTE_SERIOUS}... Looks like my observation about your fighting prowess is correct.\p
    {SPEAKER NAME_MITSURU}{CREATE_MUGSHOT MUGSHOT_MITSURU EMOTE_NORMAL}Very well then, I hope we get along, Sanada-kun.\p
    {SPEAKER NAME_AKIHIKO}{CREATE_MUGSHOT MUGSHOT_AKIHIKO EMOTE_CONFIDENT}Same here.")
}

text SunshineMeadow_Eventscript_LostInMitsuruFirstBattlePointUp {
    format("{SPEAKER NAME_MITSURU}{CREATE_MUGSHOT MUGSHOT_MITSURU EMOTE_SMILE}That was a very good fight. I've learned better of how you battle.\p
    {SPEAKER NAME_MITSURU}{CREATE_MUGSHOT MUGSHOT_MITSURU EMOTE_SMILE}Don't be discouraged. It's proof that you can still get so much more stronger.\p
    {SPEAKER NAME_AKIHIKO}{CREATE_MUGSHOT MUGSHOT_AKIHIKO EMOTE_SERIOUS}(Damn... The way she ordered her Pokémons was so smart.\p
    It's... amazing.)")
}

text SunshineMeadow_Eventscript_LostInMitsuruFirstBattlePointDown {
    format("{SPEAKER NAME_MITSURU}{CREATE_MUGSHOT MUGSHOT_MITSURU EMOTE_SERIOUS}...\p
    Do you understand now that you shouldn't have underestimated me?\p
    {SPEAKER NAME_MITSURU}{CREATE_MUGSHOT MUGSHOT_MITSURU EMOTE_NORMAL}Now please step down, I still have a battle to do with Aragaki-kun.\p
    {SPEAKER NAME_AKIHIKO}{CREATE_MUGSHOT MUGSHOT_AKIHIKO EMOTE_SERIOUS}(Damn it... She was confident for a reason.\p
    Her skill is... amazing.)")
}

text MitsuruFirstBattleDefeated {
    format("C'est magnifique.")
}

script SunshineMeadow_Eventscript_PostMitsuruFirstBattle {
    delay(20)
    setflag(FLAG_DONT_TRANSITION_MUSIC)
    special(SpawnCameraObject)
    applymovement(OBJ_EVENT_ID_CAMERA, CameraGoesLeft2)
    applymovement(1, MitsuruApproachesToHeal)
    waitmovement (0)
    special(HealPlayerParty)
    playse(MUS_HEAL)
    waitse
    msgbox(format("{SPEAKER NAME_MITSURU}{CREATE_MUGSHOT MUGSHOT_MITSURU EMOTE_SMILE}Alright, I've healed your party."))
    closemessage
    applymovement(1, MitsuruGoesBackAfterHealing)
    waitmovement(0)
    msgbox(format("{SPEAKER NAME_MITSURU}{CREATE_MUGSHOT MUGSHOT_MITSURU EMOTE_CONFIDENT}It's your turn now, Aragaki-kun.\p
    {SPEAKER NAME_SHINJIRO}{CREATE_MUGSHOT MUGSHOT_SHINJIRO EMOTE_SERIOUS}Aight. I'll show you what I've got."))
    closemessage
    applymovement(1, MitsuruGoesBackToStartBattle)
    applymovement(OBJ_EVENT_ID_CAMERA, CameraGoesRight2)
    waitmovement(0)
    delay(8)
    applymovement(LOCALID_PLAYER, AkihikoGetsOutOfBattleSpace)
    delay(8)
    applymovement(4, ShinjiroGoesToStartBattle)
    waitmovement(0)
    delay(30)
    msgbox(format("{SPEAKER NAME_MITSURU}{CREATE_MUGSHOT MUGSHOT_MITSURU EMOTE_CONFIDENT}Are you ready?\p
    {SPEAKER NAME_SHINJIRO}{CREATE_MUGSHOT MUGSHOT_SHINJIRO EMOTE_SERIOUS}Whenever you are.\p
    {SPEAKER NAME_AKIHIKO}{CREATE_MUGSHOT MUGSHOT_AKIHIKO EMOTE_SERIOUS}(I should also study more of how they fight.)"))
    closemessage
    warpsilent(MAP_BLANK_MAP, 0, 0)
    fwdtime(19, 10)
    setvar(VAR_AKIHIKOS_INTRO_STATE, 36)
}
movement CameraGoesLeft2 {
    walk_left*2
}

movement CameraGoesRight2 {
    walk_right*2
}

script SunshineMeadow_Eventscript_PostMitsuruFirstBattleGameOver {
    delay(20)
    special(SpawnCameraObject)
    applymovement(1, facedown)
    waitmovement(0)
    msgbox(format("{SPEAKER NAME_MITSURU}{CREATE_MUGSHOT MUGSHOT_MITSURU EMOTE_NORMAL}Please take your place, Aragaki-kun.\p
    {SPEAKER NAME_SHINJIRO}{CREATE_MUGSHOT MUGSHOT_SHINJIRO EMOTE_SERIOUS}Aight. I'll show you what I've got."))
    closemessage
    applymovement(1, faceleft)
    applymovement(OBJ_EVENT_ID_CAMERA, walkinplacedown)
    waitmovement(0)
    delay(8)
    applymovement(LOCALID_PLAYER, AkihikoGetsOutOfBattleSpace)
    delay(8)
    applymovement(4, ShinjiroGoesToStartBattle)
    waitmovement(0)
    delay(30)
    msgbox(format("{SPEAKER NAME_MITSURU}{CREATE_MUGSHOT MUGSHOT_MITSURU EMOTE_SERIOUS}Are you ready?\p
    {SPEAKER NAME_SHINJIRO}{CREATE_MUGSHOT MUGSHOT_SHINJIRO EMOTE_SERIOUS}Whenever you are.\p
    {SPEAKER NAME_AKIHIKO}{CREATE_MUGSHOT MUGSHOT_AKIHIKO EMOTE_SERIOUS}(Let's see how Shinji fare against her.)"))
    closemessage
    warpsilent(MAP_BLANK_MAP, 0, 0)
    fwdtime(19, 10)
    setvar(VAR_AKIHIKOS_INTRO_STATE, 36)
}

script SunshineMeadow_Eventscript_AkihikoOnFrame37 {
    if (var(VAR_MITSURU_RELATIONSHIP_POINTS) == 0){
        goto(SunshineMeadow_Eventscript_PostShinjiMitsuFirstBattleGameOver)
    }
    else{
        goto(SunshineMeadow_Eventscript_PostShinjiMitsuFirstBattle)
    }
}

script SunshineMeadow_Eventscript_PostShinjiMitsuFirstBattle{
    delay(30)
    msgbox(format("{SPEAKER NAME_SHINJIRO}{CREATE_MUGSHOT MUGSHOT_SHINJIRO EMOTE_STRUGGLED}Damn, you're tough.\p
    {SPEAKER NAME_MITSURU}{CREATE_MUGSHOT MUGSHOT_MITSURU EMOTE_SMILE}I appreciate the compliment."))
    closemessage
    applymovement(4, ShinjiroGoesBackToPlacePostShinjiMitsuFirstBattle)
    applymovement(1, MitsuruGoesToTheCenterPostShinjiMitsuFirstBattle)
    applymovement(2, VulpixFollowsMitsuruPostShinjiMitsuFirstBattle)
    applymovement(LOCALID_PLAYER, CameraPansDownPostShinjiMitsuFirstBattle)
    waitmovement
    msgbox(format("{SPEAKER NAME_MITSURU}{CREATE_MUGSHOT MUGSHOT_MITSURU EMOTE_SMILE}Now that I get a clearer vision of how both of you fare in battle,\p
    I'll be counting on you, Sanada-kun and Aragaki-kun.\p
    {SPEAKER NAME_SHINJIRO}{CREATE_MUGSHOT MUGSHOT_SHINJIRO EMOTE_SURPRISED}Huh?\p
    {SPEAKER NAME_AKIHIKO}{CREATE_MUGSHOT MUGSHOT_AKIHIKO EMOTE_SURPRISED}You mean...\p
    {SPEAKER NAME_MITSURU}{CREATE_MUGSHOT MUGSHOT_MITSURU EMOTE_SMILE}Yes, both of you are coming with me. You two have great potential.\p
    {SPEAKER NAME_AKIHIKO}{CREATE_MUGSHOT MUGSHOT_AKIHIKO EMOTE_HAPPY}YESSS!!\p
    {SPEAKER NAME_SHINJIRO}{CREATE_MUGSHOT MUGSHOT_SHINJIRO EMOTE_CONFIDENT}Heh. Just you wait. I'll win against you in no time.\p
    {SPEAKER NAME_AKIHIKO}{CREATE_MUGSHOT MUGSHOT_AKIHIKO EMOTE_CONFIDENT}Same here!\p
    {SPEAKER NAME_MITSURU}{CREATE_MUGSHOT MUGSHOT_MITSURU EMOTE_SMILE}I'm looking forward to that."))
    closemessage
    delay(30)
    msgbox(format("{SPEAKER NAME_MITSURU}{CREATE_MUGSHOT MUGSHOT_MITSURU EMOTE_NORMAL}Anyway, back to business.\p
    I suppose you both want to know further about those men.\p
    Let's go back to the house on Route 2, I'll explain our objective."))
    closemessage
    setflag(FLAG_TEMP_1)
    setvar(VAR_AKIHIKOS_INTRO_STATE, 38)
    clearflag(FLAG_HIDE_TALK_BUTTON)
    clearflag(FLAG_NO_WHITEOUT)
    clearflag(FLAG_NO_BAG_USE)
    clearflag(FLAG_DONT_TRANSITION_MUSIC)
    fwdtime(19, 20)
    warpsilent(MAP_ROUTE2, 10, 13)
    end
}

movement MitsuruApproachesToHeal {
    walk_left *4
}

movement MitsuruGoesBackAfterHealing {
    walk_right *2
    face_down
}

movement MitsuruGoesBackToStartBattle {
    walk_right *2
    face_left
}

movement AkihikoGetsOutOfBattleSpace {
    walk_down*3
    walk_right*2
    face_up
}

movement ShinjiroGoesToStartBattle {
    walk_up *3
    walk_left *3
    face_right
}

movement ShinjiroGoesBackToPlacePostShinjiMitsuFirstBattle {
    walk_down *2
    walk_right*3
    walk_down
    face_up
}

movement MitsuruGoesToTheCenterPostShinjiMitsuFirstBattle {
    walk_left*3
    walk_down
    face_down
}
movement VulpixFollowsMitsuruPostShinjiMitsuFirstBattle {
    walk_left*2
    face_down
}
movement CameraPansDownPostShinjiMitsuFirstBattle {
    walk_down*2
}

script SunshineMeadow_Eventscript_PostShinjiMitsuFirstBattleGameOver {
    playbgm(MUS_CREDITS, TRUE)
    delay(30)
    msgbox(format("{SPEAKER NAME_SHINJIRO}{CREATE_MUGSHOT MUGSHOT_SHINJIRO EMOTE_STRUGGLED}Damn, how are you so strong?\p
    {SPEAKER NAME_MITSURU}{CREATE_MUGSHOT MUGSHOT_MITSURU EMOTE_SERIOUS}...\p
    {SPEAKER NAME_MITSURU}{CREATE_MUGSHOT MUGSHOT_MITSURU EMOTE_SAD}It seems like neither of you are whom I can put my trust on."))
    closemessage
    applymovement(1, faceright)
    waitmovement(0)
    delay(30)
    msgbox(format("{SPEAKER NAME_MITSURU}{CREATE_MUGSHOT MUGSHOT_MITSURU EMOTE_SAD}I'd hate to go back on my word\p
    but I'd like for us to go on our own ways.\p
    My apology for wasting your time.\p
    Farewell."))
    closemessage
    setvar(VAR_AKIHIKOS_INTRO_STATE, 38)
    clearflag(FLAG_NO_WHITEOUT)
    warpsilent(MAP_BLANK_MAP, 0, 0)
    end
}


script SunshineMeadow_Eventscript_MeadowSign {
    msgbox(format("Sunshine Meadow"))
    closemessage
    end
}

script SunshineMeadow_Eventscript_DirectionSign {
    msgbox(format("Route 2 {DOWN_ARROW}"))
    closemessage
    end
}

script SunshineMeadow_Eventscript_OW_Flabebe_White {
    lock
    faceplayer
    playmoncry(SPECIES_FLABEBE, CRY_MODE_NORMAL)
    waitmoncry
    startoverworldencounter(7)
    removeobject(5)
    setflag(FLAG_TEMP_3)
    release
    end
}

script SunshineMeadow_Eventscript_VulpixPreBattle {
    msgbox(format("Vulpix looks aloof and completedly ignores you."))
    closemessage
    end
}

script SunshineMeadow_Eventscript_InteleonPreBattle {
    msgbox(format("Inteleon stares down at you but says nothing."))
    closemessage
    end
}

script SunshineMeadow_Eventscript_DoSparkleFieldEffect {
    playse(SE_SHINY)
    dofieldeffectsparkle(12, 9, 2)
    waitfieldeffect(FLDEFF_SPARKLE)
    setvar(VAR_TEMP_0, 1)
}

script SunshineMeadow_Eventscript_JumpOverFence{
    checkplayergender
    if (var(VAR_RESULT) == FEMALE){
        if (var(VAR_MITSURUS_INTRO_STATE) > 100){
            msgbox(format("{SPEAKER NAME_MITSURU}{CREATE_MUGSHOT MUGSHOT_MITSURU EMOTE_NORMAL}(There's no reason to go over there right now.)\p
            (The lab was already exploded and everything was burned away.)"))
            closemessage
            end
        }
        else{
            lock
            msgbox(format("{SPEAKER NAME_MITSURU}{CREATE_MUGSHOT MUGSHOT_MITSURU EMOTE_SERIOUS}(Here we go.)"))
            closemessage
            hidefollower
            hidefollowernpc
            specialvar(VAR_RESULT, GetPlayerFacingDirection)
            if (var(VAR_RESULT) == DIR_NORTH)
            {
                applymovement(LOCALID_PLAYER, moves(jump_up, jump_up))
                playse(SE_LEDGE)
                delay(16)
                playse(SE_LEDGE)
                waitmovement(0)
                clearflag(FLAG_TEMP_HIDE_FOLLOWER)
                updatefollowingmon
                release
            }
            elif (var(VAR_RESULT) == DIR_SOUTH)
            {
                applymovement(LOCALID_PLAYER, moves(jump_down, jump_down))
                playse(SE_LEDGE)
                delay(16)
                playse(SE_LEDGE)
                waitmovement(0)
                clearflag(FLAG_TEMP_HIDE_FOLLOWER)
                updatefollowingmon
                release
            }
        }
    }
    else{
        if (var(VAR_AKIHIKOS_INTRO_STATE) < 100){
            msgbox(format("{SPEAKER NAME_AKIHIKO}{CREATE_MUGSHOT MUGSHOT_AKIHIKO EMOTE_NORMAL}(Huh, there's a footprint here. Looks like someone climbed over the fence.)\p
            (I'd like to explore further but right now I have other more important matters to attend to.)"))
            closemessage
        }
    }
    
}

script SunshineMeadow_Eventscript_MitsuruAproachTheFence {
    lock
    applymovement(LOCALID_PLAYER, moves(player_run_up*4, player_run_left*3, player_run_up, face_up))
    waitmovement(0)
    delay(20)
    msgbox(format("{SPEAKER NAME_MITSURU}{CREATE_MUGSHOT MUGSHOT_MITSURU EMOTE_STRUGGLED}-Huff- -Huff-\p
    {SPEAKER NAME_MITSURU}{CREATE_MUGSHOT MUGSHOT_MITSURU EMOTE_SERIOUS}(Eventhough I ran with all my might, it's still too late.)"))
    special(SpawnCameraObject)
    applymovement(OBJ_EVENT_ID_CAMERA, moves(walk_up*3, delay_16, delay_16, delay_16, walk_down*3))
    waitmovement(0)
    special(RemoveCameraObject)
    msgbox(format("{SPEAKER NAME_MITSURU}{CREATE_MUGSHOT MUGSHOT_MITSURU EMOTE_SERIOUS}It's already 0:43 AM... not very ideal to tackle a mountain cave.\p
    Not to mention, my Pokémon are all tired from the unexpected battles."))
    closemessage
    applymovement(LOCALID_PLAYER, facedown)
    waitmovement
    delay(20)
    applymovement(LOCALID_PLAYER, emotepensive)
    waitmovement
    msgbox(format("{SPEAKER NAME_MITSURU}{CREATE_MUGSHOT MUGSHOT_MITSURU EMOTE_SERIOUS}(Yuuma-san said his grandson owned a house nearby in Route 2.)\p
    (He's travelling the region at the moment and I'm free to use it in the mean time.)\p
    (Alright then.)"))
    closemessage
    applymovement(LOCALID_PLAYER, moves(player_run_down *2, player_run_right *3))
    delay(20)
    warpsilent(MAP_ROUTE2, 16, 13)
    waitmovement
    waitstate
}

script SunshineMeadow_Eventscript_MitsuruPlansToJumpOverTheFence {
    lock
    msgbox(format("{SPEAKER NAME_MITSURU}{CREATE_MUGSHOT MUGSHOT_MITSURU EMOTE_NORMAL}(The cave entrance is just on the other side of the fences.)\p
    It's not too tall so I believe I can jump over them.)"))
    closemessage
    setvar(VAR_MITSURUS_INTRO_STATE, 12)
    release
}

script SunshineMeadow_Eventscript_ThundershardsCliffSign {
    lock
    msgbox(format("Thundershards Cave\n
    High risk of cave-in. DO NOT ENTER."))
    closemessage
    release
}

script SunshineMeadow_Eventscript_MitsuruLookingOverCollapsedCave {
    applymovement(LOCALID_PLAYER, moves(set_invisible, walk_slow_down*7, face_up))
    waitmovement
    delay(20)
    applymovement(LOCALID_PLAYER, visible)
    applymovement(1, invisible)
    waitmoncry
    msgbox(format("{SPEAKER NAME_MITSURU}{CREATE_MUGSHOT MUGSHOT_MITSURU EMOTE_STRUGGLED}.......\p
    I failed. They got away with all the evidences."))
    closemessage
    applymovement(LOCALID_PLAYER, facedown)
    applymovement(3, moves(walk_down, face_up))
    applymovement(2, moves(walk_down, walk_left, face_up))
    waitmovement
    delay(20)
    msgbox(format("{SPEAKER NAME_MITSURU}{CREATE_MUGSHOT MUGSHOT_MITSURU EMOTE_STRUGGLED}Let's regroup and plan our next move.\p
    We also need to visit the Pokémon Center in Sunshine Town to heal the team."))
    closemessage
    playmoncry(SPECIES_VULPIX, CRY_MODE_NORMAL)
    playmoncry(SPECIES_INTELEON, CRY_MODE_NORMAL)
    waitmoncry
    applymovement(3, moves(walk_up, walk_right, set_invisible))
    applymovement(2, moves(enter_pokeball))
    waitmovement
    delay(30)
    removeobject(1)
    removeobject(2)
    removeobject(3)
    setflag(FLAG_TEMP_1)
    call(Eventscript_CutsceneEnds)
    clearflag(FLAG_DONT_TRANSITION_MUSIC)
    fadedefaultbgm
    setvar(VAR_MITSURUS_INTRO_STATE, 18)
    special(ShowMapNamePopup)
    release
    end
}