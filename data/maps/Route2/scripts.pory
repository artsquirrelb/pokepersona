mapscripts Route2_MapScripts {
    MAP_SCRIPT_ON_LOAD{
        if (var(VAR_MITSURUS_INTRO_STATE) < 10){
            setmetatile(10, 11, 0x829, TRUE)
        }
    }
    MAP_SCRIPT_ON_WARP_INTO_MAP_TABLE[
        VAR_AKIHIKOS_INTRO_STATE, 25: Route2_Eventscript_AkihikoOnWarp25
        VAR_AKIHIKOS_INTRO_STATE, 33: Route2_Eventscript_AkihikoOnWarp33
        VAR_AKIHIKOS_INTRO_STATE, 40: Route2_Eventscript_AkihikoOnWarp40
        ////////////////
        VAR_MITSURUS_INTRO_STATE, 8: Route2_Eventscript_MitsuruOnWarp8
        VAR_MITSURUS_INTRO_STATE, 27: Route2_Eventscript_MitsuruOnWarp27
    ]
    MAP_SCRIPT_ON_TRANSITION{
        if (var(VAR_AKIHIKOS_INTRO_STATE) < 40){
            setflag(FLAG_TEMP_3)
        }
        if ((var(VAR_AKIHIKOS_INTRO_STATE) > 25 || var(VAR_MITSURUS_INTRO_STATE) < 20) &&  var(VAR_MITSURUS_INTRO_STATE) !=27){
            setflag(FLAG_TEMP_1)
            setflag(FLAG_TEMP_6)
        }
        if (var(VAR_AKIHIKOS_INTRO_STATE) > 25 && var(VAR_AKIHIKOS_INTRO_STATE) < 33){
            setflag(FLAG_TEMP_2)
        }
        if (var(VAR_AKIHIKOS_INTRO_STATE) == 26){
            clearflag(FLAG_HIDE_HELP_BUTTON)
        }
        if (var(VAR_AKIHIKOS_INTRO_STATE) == 33){
            setflag(FLAG_HIDE_HELP_BUTTON)
            setobjectxyperm(6, 10, 12)
        }
        if (var(VAR_AKIHIKOS_INTRO_STATE) == 34){
            setobjectxyperm(6, 31, 21)
            setobjectmovementtype(6, MOVEMENT_TYPE_WANDER_LEFT_AND_RIGHT)
        }
        if (var(VAR_AKIHIKOS_INTRO_STATE) >= 37 && var(VAR_AKIHIKOS_INTRO_STATE) < 40){
            setflag(FLAG_TEMP_2)
        }
        if (var(VAR_AKIHIKOS_INTRO_STATE) == 40){
            setobjectxyperm(6, 13, 13)
        }
        if (var(VAR_AKIHIKOS_INTRO_STATE) >= 42){
            setflag(FLAG_TEMP_2)
            setflag(FLAG_TEMP_3)
        }
        if((var(VAR_AKIHIKOS_INTRO_STATE) == 100) || (var(VAR_MITSURUS_INTRO_STATE) == 100)){
            setflag(FLAG_TEMP_4)
            setobjectxyperm(TAROU, 9, 19)
        }
        if (var(VAR_MITSURUS_INTRO_STATE) != 27){
            setflag(FLAG_TEMP_5)
        }
        if (var(VAR_MITSURUS_INTRO_STATE) == 27){
            setflag(FLAG_TEMP_6)
            setobjectxyperm(13, 19, 10)
            setobjectmovementtype(13, MOVEMENT_TYPE_FACE_LEFT_AND_RIGHT)
            setobjectxyperm(5, 12, 11)
            setobjectxyperm(12, 24, 14)
        }
        
    }
    MAP_SCRIPT_ON_FRAME_TABLE[
        VAR_AKIHIKOS_INTRO_STATE, 25: Route2_Eventscript_AkihikoOnFrame25
        VAR_AKIHIKOS_INTRO_STATE, 33: Route2_Eventscript_AkihikoOnFrame33
        VAR_AKIHIKOS_INTRO_STATE, 40: Route2_Eventscript_AkihikoOnFrame40
        ////////////
        VAR_MITSURUS_INTRO_STATE, 8: Route2_Eventscript_MitsuruOnFrame8
        VAR_MITSURUS_INTRO_STATE, 29: Route2_Eventscript_MitsuruOnFrame29
    ]
}

script Route2_Eventscript_AkihikoOnWarp25 {
    applymovement(LOCALID_PLAYER, faceleft)
    applymovement(7, invisible)
    applymovement(8, invisible)
    applymovement(9, invisible)
    applymovement(10, invisible)
    applymovement(11, invisible)
    applymovement(12, invisible)
}

script Route2_Eventscript_AkihikoOnWarp33 {
    applymovement(6, invisible)
}

script Route2_Eventscript_AkihikoOnWarp40 {
    applymovement(6, faceright)
}

script Route2_Eventscript_MitsuruOnWarp8 {
    applymovement(LOCALID_PLAYER, faceleft)    
}

script Route2_Eventscript_MitsuruOnWarp27 {
    applymovement(5, invisible)
}

script Route2_Eventscript_AkihikoOnFrame25 {
    goto(Route2_Eventscript_AkishinjiGettingBlockedBySusMen)
}

script Route2_Eventscript_MitsuruOnFrame8 {
    goto(Route2_Eventscript_MitsuruApproachingRoute2House)
}

script Route2_Eventscript_MitsuruOnFrame29 {
    goto(Route2_Eventscript_MitsuruPostSenpaiTrioFirstMeeting)
}

script Route2_Eventscript_AkishinjiGettingBlockedBySusMen {
    lock
    applymovement(LOCALID_PLAYER, AkishinjiGettingBlockedBySusMen)
    delay(8)
    applymovement(6, AkishinjiGettingBlockedBySusMen)
    applymovement(4, SusMenBlockingAkiShinji)
    delay(8)
    applymovement(3, SusMenBlockingAkiShinji)
    waitmovement(3)
    call(Eventscript_BlockShakeCamera)
    delay(20)
    applymovement(LOCALID_PLAYER, AkiShinjiStepBack)
    applymovement (6, AkiShinjiStepBack)
    waitmovement(0)
    msgbox(format("{SPEAKER NAME_A}{CREATE_MUGSHOT MUGSHOT_A EMOTE_STRUGGLED}Ouch!\p
    {SPEAKER NAME_S}{CREATE_MUGSHOT MUGSHOT_S EMOTE_STRUGGLED}What the hell??"))
    closemessage
    delay(30)
    playbgm(MUS_P3R_FEARFUL_EXPERIENCE, TRUE)
    setspeaker("Man In Black 1")
    applymovement(4, walkinplaceright)
    waitmovement(0)
    msgbox(format("That stupid cop is finally out of picture."))
    closemessage
    setspeaker("Man In Black 2")
    applymovement(3, walkinplaceright)
    waitmovement(0)
    msgbox(format("You two are coming with us. Like it or not.\p
    {SPEAKER NAME_A}{CREATE_MUGSHOT MUGSHOT_A EMOTE_STRUGGLED}What?"))
    closemessage
    applymovement(4, SusMan1ApproachingAkihiko)
    waitmovement(0)
    applymovement(LOCALID_PLAYER, shakehorizontal)
    call(Eventscript_BlockShakeCamera)
    applymovement(4, SusMan1StepBack)
    waitmovement(0)
    delay(20)
    msgbox(format("{SPEAKER NAME_A}{CREATE_MUGSHOT MUGSHOT_A EMOTE_MAD}No way! Who even are you!?"))
    closemessage
    goto(Route2_Eventscript_StartBattleWithSusMen)
    release
}

movement walkinplaceright {
    walk_in_place_right    
}

movement AkishinjiGettingBlockedBySusMen {
    walk_fast_left *6
    delay_8
    lock_facing_direction
    jump_right
    unlock_facing_direction
}

movement SusMenBlockingAkiShinji {
    walk_fast_up *3
    walk_fast_right
}

movement AkiShinjiStepBack {
    lock_facing_direction
    walk_right
    unlock_facing_direction
}
movement SusMan1ApproachingAkihiko {
    walk_fast_right
}

movement SusMan1StepBack {
    lock_facing_direction
    walk_left
    unlock_facing_direction
}

movement shakevertical {
    shake_vertical
}

script Route2_Eventscript_StartBattleWithSusMen {
    lock
    setspeaker("Man In Black 1")
    applymovement(4, walkinplaceright)
    waitmovement(0)
    msgbox(format("Tch.\p
    I hate disobedient kids."))
    closemessage
    setflag(FLAG_NO_WHITEOUT)
    msgbox(SusMenFightIntro)
    closemessage
    trainerbattle_no_intro(TRAINER_KG_STAFF_ROUTE2, SusMenFightDefeated)
    goto(Route2_Eventscript_PostBattleWithSusMen)
    release
}

movement lockfacing {
    lock_facing_direction
}
text SusMenFightIntro{
    format("Go, Venipede.")
}

text SusMenFightDefeated {
    format("You aren't half bad. However...")
}

script Route2_Eventscript_PostBattleWithSusMen{
    lock
    playbgm(MUS_NONE, TRUE)
    clearflag(FLAG_NO_WHITEOUT)
    special(HealPlayerParty)
    delay(30)
    setspeaker("Man In Black 1")
    msgbox(format("Resistance is futile."))
    closemessage
    delay(8)
    playse(SE_EXIT)
    applymovement(7, DrowzeeAppears)
    waitmovement(0)
    applymovement(LOCALID_PLAYER, facedown)
    applymovement(6, facedown)
    waitmovement(0)
    msgbox(format("{SPEAKER NAME_A}{CREATE_MUGSHOT MUGSHOT_A EMOTE_SURPRISED}Huh?"))
    closemessage
    setspeaker("Man In Black 2")
    applymovement(3, walkinplaceright)
    waitmovement(0)
    msgbox(format("Drowzee, Hypnosis."))
    closemessage
    fadescreenswapbuffers(FADE_TO_BLACK)
    playmoncry(SPECIES_DROWZEE, CRY_MODE_NORMAL)
    waitmoncry
    delay(30)
    applymovement(LOCALID_PLAYER, invisible)
    applymovement(6, invisible)
    waitmovement(0)
    goto (Route2_Eventscript_AkishinjiAsleep)
    release
}

movement DrowzeeAppears {
    set_visible
    jump_left
    face_up
}

movement walkinplaceup {
    walk_in_place_up
}


script Route2_Eventscript_AkishinjiAsleep {
    lock
    applymovement(8, visible)
    applymovement(9, visible)
    waitmovement(0)
    fadescreenswapbuffers(FADE_FROM_BLACK)
    delay(20)
    playse(SE_M_SNORE)
    applymovement(8, emoteasleep)
    delay(8)
    playse(SE_M_SNORE)
    applymovement(9, emoteasleep)
    waitmovement(0)
    delay(20)
    setspeaker("Man In Black 2")
    applymovement(3, walkinplaceright)
    waitmovement(0)
    msgbox("That's more like it.")
    closemessage
    applymovement(3, walkright)
    waitmovement(0)
    goto(Route2_Eventscript_MitsuruDisruptsTheMen)
    release
}

movement emoteasleep {
    emote_asleep
}
movement visible {
    set_visible
}

script Route2_Eventscript_MitsuruDisruptsTheMen {
    lock
    delay(20)
    setspeaker("???????")
    msgbox(format("{CREATE_MUGSHOT MUGSHOT_M EMOTE_SILHOUETTE}Now! Inteleon, Vulpix!"))
    closemessage
    fadescreenswapbuffers(FADE_TO_BLACK)
    delay(20)
    playmoncry(SPECIES_INTELEON, CRY_MODE_NORMAL)
    waitmoncry
    playse(SE_M_BUBBLE_BEAM)
    waitse
    playmoncry(SPECIES_VULPIX, CRY_MODE_NORMAL)
    waitmoncry
    playse(SE_M_ICY_WIND)
    waitse
    applymovement(3, invisible)
    applymovement(4, invisible)
    removeobject(3)
    removeobject(4)
    clearflag(FLAG_TEMP_6)
    applymovement(10, visible)
    applymovement(11, visible)
    waitmovement(0)
    fadescreenswapbuffers(FADE_FROM_BLACK)
    delay(20)
    applymovement(10, shakehorizontal)
    playse(SE_M_HARDEN)
    delay(4)
    applymovement(11, shakehorizontal)
    playse(SE_M_HARDEN)
    waitse
    applymovement(7, emoteexclamation)
    waitmovement(0)
    goto(Route2_Eventscript_MitsuruAppears)
    release
    
}

movement DrowzeeRunsAway {
    delay_16
    face_left
    delay_16
    face_up
    delay_16
    emote_x
    delay_16
    jump_right
    set_invisible

}

script Route2_Eventscript_MitsuruAppears {
    lock
    delay(40)
    addobject(5)
    applymovement(LOCALID_PLAYER, CameraToMitsuru)
    applymovement(5, MitsuruAppears)
    waitmovement(0)
    applymovement(7, DrowzeeRunsAway)
    delay (52)
    playse(SE_BOO)
    waitmovement(0)
    waitse
    playse(SE_EXIT)
    waitse
    delay(30)
    setspeaker("???????")
    msgbox(format("{CREATE_MUGSHOT MUGSHOT_M EMOTE_NORMAL}...\p
    Inteleon, let's get these two out of here first.\p
    The ice can only hold them off for so long."))
    closemessage
    playse(SE_EXIT)
    applymovement(12, InteleonWalksOut)
    applymovement(LOCALID_PLAYER, CameraToInteleon)
    waitmovement(0)
    delay(20)
    playmoncry(SPECIES_INTELEON, CRY_MODE_NORMAL)
    applymovement(12, walkinplacedown)
    waitmovement(0)
    delay(30)
    warpsilent(MAP_BLANK_MAP, 0, 0)
    setvar(VAR_AKIHIKOS_INTRO_STATE, 26)
    release

}

movement walkinplacedown {
    walk_in_place_down
}
movement CameraToMitsuru {
    walk_left *3
    walk_down
}

movement MitsuruAppears {
    walk_up *2
    walk_right *5  
}

movement InteleonWalksOut {
    set_visible
    walk_down
}

movement CameraToInteleon {
    walk_up *2
}
script Route2_Eventscript_RouteSign {
    msgbox("Route 2.")
    closemessage
    end

}

script Route2_Eventscript_DirectionSign {
    msgbox(format("Sunshine Meadow {UP_ARROW}\n
    Sunshine Town {RIGHT_ARROW}\p
    Route 1 {DOWN_ARROW}\n
    Coralport {LEFT_ARROW}"))
    closemessage
    end
}

script Route2_Eventscript_Yuuka {
    checkplayergender
    if (var(VAR_RESULT) == MALE){
        trainerbattle_single (TRAINER_YUUKA, Route2_YuukaIntro, Route2_YuukaDefeated)
    }
    else{
        trainerbattle_single (TRAINER_YUUKA, Route2_YuukaIntro_M, Route2_YuukaDefeated)    }
    setspeaker("Yuuka")
    msgbox(Route2_YuukaPostBattle)
    closemessage
    end
}

text Route2_YuukaIntro {
    format("Hey, I'm bored. Want a quick battle?")
}

text Route2_YuukaIntro_M {
    format("Hey, I'm bored. Let's have a quick battle!\p
    {SPEAKER NAME_M}{CREATE_MUGSHOT MUGSHOT_M EMOTE_STRUGGLED}(It's so late, you should be at home!)")
}

text Route2_YuukaDefeated {
    format("Thank you. That was fun!")
}

text Route2_YuukaPostBattle {
    format("Actually I went here to catch an Abra but they kept teleporting away...")
}

script Route2_Eventscript_Kyousuke {
    lock
    setspeaker("Tarou")
    applymovement(TAROU, walkinplacedown)
    waitmovement(0)
    msgbox(format("So you're saying this Pokémon is really strong?"))
    closemessage
    applymovement(KYOUSUKE, walkinplaceup)
    waitmovement(0)
    setspeaker("Kyousuke")
    msgbox(format("That's right, young man. Hehehehe."))
    closemessage
    release
    end
}
script Route2_Eventscript_Tarou {
    if(((var(VAR_AKIHIKOS_INTRO_STATE) > 43 && var(VAR_AKIHIKOS_INTRO_STATE) < 50)) || var(VAR_MITSURUS_INTRO_STATE) < 20){
        goto(Route2_Eventscript_Kyousuke)
    }
    else{
    trainerbattle_single (TRAINER_TAROU, Route2_TarouIntro, Route2_TarouDefeated)
    setspeaker("Tarou")
    msgbox(Route2_TarouPostBattle)
    closemessage
    end
    }
}

text Route2_TarouIntro {
    format("What are you looking at, low-life?")
}

text Route2_TarouDefeated {
    format("Tch. These Pokémon suck. I shouldn't have bought them.")
}

text Route2_TarouPostBattle {
    format("I already gave you the money. Screw off.")
}

script Route2_Eventscript_AkihikoOnFrame33 {
    goto(Route2_Eventscript_ShinjiroSplitsOutToCatchPokemon)
}

script Route2_Eventscript_ShinjiroSplitsOutToCatchPokemon {
    applymovement(6, ShinjiroSplitsOut)
    waitmovement(0)
    msgbox(format("{SPEAKER NAME_S}{CREATE_MUGSHOT MUGSHOT_S EMOTE_NORMAL}Aight, I'm gonna go ahead and catch some Pokémon.\p
    {SPEAKER NAME_A}{CREATE_MUGSHOT MUGSHOT_A EMOTE_NORMAL}Sure. Be careful, the men in black could still be around here.\p
    {SPEAKER NAME_S}{CREATE_MUGSHOT MUGSHOT_S EMOTE_SMILE}Hah. The same to you, Aki.\p
    {SPEAKER NAME_S}{CREATE_MUGSHOT MUGSHOT_S EMOTE_CONFIDENT}See you later at the Meadow."))
    closemessage
    applymovement(6, ShinjiroGoesOffCam)
    applymovement(LOCALID_PLAYER, faceright)
    waitmovement(6)
    lock
    removeobject(6)
    setobjectxyperm(6, 31, 21)
    setobjectnewmovementtype(6, MOVEMENT_TYPE_WANDER_LEFT_AND_RIGHT)
    setobjectxy(6, 31, 21)
    fwdtime(13, 45)
    release
    setvar(VAR_AKIHIKOS_INTRO_STATE, 34)
    clearflag(FLAG_HIDE_HELP_BUTTON)
    destroyfollowernpc
    makecharaUnavailable(CHARACTER_SHINJIRO)
    end
}

movement ShinjiroSplitsOut {
    set_visible
    walk_down *2
    face_up
}

movement ShinjiroGoesOffCam {
    walk_right *8
    set_invisible
}

script Route2_Eventscript_ShinjiroFindingPokemon {
    lock
    faceplayer
    msgbox(format("{SPEAKER NAME_A}{CREATE_MUGSHOT MUGSHOT_A EMOTE_NORMAL}How's it going?\p
    {SPEAKER NAME_S}{CREATE_MUGSHOT MUGSHOT_S EMOTE_SMILE}Got myself two.\p
    {SPEAKER NAME_A}{CREATE_MUGSHOT MUGSHOT_A EMOTE_HAPPY}Hahaha, I've never seen you're this worked up.\p
    {SPEAKER NAME_S}{CREATE_MUGSHOT MUGSHOT_S EMOTE_SMILE}Shut up and go to the Meadow if you're done preparing.\p
    {SPEAKER NAME_A}{CREATE_MUGSHOT MUGSHOT_A EMOTE_HAPPY}Alright, alright ahaha."))
    closemessage
    release
    end
}

script Route2_Eventscript_AkihikoOnFrame40 {
    goto (Route2_Eventscript_AkihikoNoticingShinjiroOutside)
}

script Route2_Eventscript_AkihikoNoticingShinjiroOutside {
    applymovement(LOCALID_PLAYER, AkihikoNoticingShinjiroOutside)
    delay(8)
    playse(SE_PIN)
    waitse
    waitmovement(0)
    special(SpawnCameraObject)
    applymovement(OBJ_EVENT_ID_CAMERA, walkright)
    waitmovement(0)
    delay(20)
    msgbox(format("{SPEAKER NAME_A}{CREATE_MUGSHOT MUGSHOT_A EMOTE_NORMAL}(Oh, there he is.)\p
    {SPEAKER NAME_A}{CREATE_MUGSHOT MUGSHOT_A EMOTE_NORMAL}Hey Shinji, you couldn't sleep too?"))
    closemessage
    playse(SE_PIN)
    applymovement(6, ShinjiroHeardAkihikoCalling)
    waitmovement(0)
    msgbox(format("{SPEAKER NAME_S}{CREATE_MUGSHOT MUGSHOT_S EMOTE_NORMAL}Aki.\p
    Yep, I keep thinking of what Kirijo said.\p
    {SPEAKER NAME_S}{CREATE_MUGSHOT MUGSHOT_S EMOTE_PENSIVE}Humans who can use Pokémon's ability, huh...\p
    {SPEAKER NAME_A}{CREATE_MUGSHOT MUGSHOT_A EMOTE_SMILE}Heh, guess we were thinking the same thing.\p
    {SPEAKER NAME_S}{CREATE_MUGSHOT MUGSHOT_S EMOTE_NORMAL}Well, there's not much we can do until we see for ourselves if what she said was true.\p
    {SPEAKER NAME_S}{CREATE_MUGSHOT MUGSHOT_S EMOTE_CONFIDENT}Since we're both not feeling sleepy, how about a battle?\p
    {SPEAKER NAME_A}{CREATE_MUGSHOT MUGSHOT_A EMOTE_CONFIDENT}I was just about to suggest that.\p
    The Ferroseed you used earlier against Kirijo really caught my curiosity.\p
    {SPEAKER NAME_S}{CREATE_MUGSHOT MUGSHOT_S EMOTE_CONFIDENT}I'll show what it can do. {COLOR RED}Let's do a 3-vs-3 without bag item use again.\p
    {COLOR DARK_GRAY}Step up if you're ready."))
    closemessage
    applymovement(SHINJIRO, moves(walk_right*4, face_left))
    applymovement(OBJ_EVENT_ID_CAMERA, moves(walk_right*4))
    waitmovement(0)
    delay(30)
    applymovement(OBJ_EVENT_ID_CAMERA, moves(walk_left*5))
    waitmovement(0)
    special(RemoveCameraObject)
    setvar(VAR_AKIHIKOS_INTRO_STATE, 41)
    end
}

movement AkihikoNoticingShinjiroOutside {
    walk_down
    face_right
    emote_exclamation_mark
}

movement ShinjiroHeardAkihikoCalling {
    emote_exclamation_mark
    face_left
}

script Route2_Eventscript_MustBattleShinjiro {
    lock
    applymovement(LOCALID_PLAYER, walkdown)
    waitmovement(0)
    msgbox(format("{SPEAKER NAME_A}{CREATE_MUGSHOT MUGSHOT_A EMOTE_NORMAL}(There's no point going back inside, it's not like I can sleep right now.)"))
    closemessage
    release
    end
}

script Route2_Eventscript_InitShinjiroFirstBattle {
    lock
    msgbox(format("{SPEAKER NAME_S}{CREATE_MUGSHOT MUGSHOT_S EMOTE_CONFIDENT}Ready for our first battle?"), MSGBOX_YESNO)
    if (var(VAR_RESULT) == FALSE){
        msgbox(format("{SPEAKER NAME_S}{CREATE_MUGSHOT MUGSHOT_S EMOTE_SMILE}Heh, take your sweet time. We still have all night, Aki."))
        closemessage
        applymovement(LOCALID_PLAYER, walkleft)
        waitmovement(0)
        release
        end
    }
    else{
        msgbox(format("{SPEAKER NAME_S}{CREATE_MUGSHOT MUGSHOT_S EMOTE_CONFIDENT}Before we start, do you want me to heal your Pokémon?"), MSGBOX_YESNO)
        if (var(VAR_RESULT) == TRUE){
            closemessage
            playse(MUS_HEAL)
            waitse
            special(HealPlayerParty)
        }
        goto(Route2_Eventscript_CheckingPartyNumberForShinjiroFirstBattle)
        release
    }
}
script Route2_Eventscript_CheckingPartyNumberForShinjiroFirstBattle {
    lock
    specialvar(VAR_RESULT, CountPartyNonEggMons)
    if (var(VAR_RESULT) < 3){
        msgbox(format("{SPEAKER NAME_S}{CREATE_MUGSHOT MUGSHOT_S EMOTE_SERIOUS}Wait, you don't have at least 3 Pokémon to battle. I want a fair fight.\p
        Are you sure you still want to fight handicapped?"), MSGBOX_YESNO)
        if (var(VAR_RESULT) == FALSE){
            msgbox(format("{SPEAKER NAME_S}{CREATE_MUGSHOT MUGSHOT_S EMOTE_CONFIDENT}Good thinking. I won't go down easily."))
            closemessage
            applymovement(LOCALID_PLAYER, walkleft)
            waitmovement(0)
            release
            end
        }
        else {
            playse(SE_FAILURE)
            applymovement(6, emoteangry)
            waitmovement(0)
            msgbox(format("{SPEAKER NAME_S}{CREATE_MUGSHOT MUGSHOT_S EMOTE_SERIOUS}Don't underestimate me."))
            closemessage
            subvar(VAR_SHINJIRO_RELATIONSHIP_POINTS, 3) //current point: 97
            trainerbattle_no_intro(TRAINER_SHINJIRO_ROUTE_2, ShinjiroFirstBattleDefeated)
            specialvar(VAR_RESULT, GetBattleOutcome)
            goto(Route2__Eventscript_ShinjiroFirstBattleResult)
            release
        }
    }
    elif (var(VAR_RESULT) >= 3){
        playse(SE_SHINY)
        applymovement(6, pointup2)
        waitmovement(0)
        addvar(VAR_SHINJIRO_RELATIONSHIP_POINTS, 3) //current point: 103
        msgbox(format("{SPEAKER NAME_S}{CREATE_MUGSHOT MUGSHOT_S EMOTE_CONFIDENT}Let's go.\p
        {SPEAKER NAME_A}{CREATE_MUGSHOT MUGSHOT_A EMOTE_CONFIDENT}No holding back, alright?"))
        closemessage
        goto(Route2_Eventscript_SelectingPartyShinjiroFirstBattle)
        release
    }
}

movement emoteangry {
    emote_angry
}

script Route2_Eventscript_InitShinjiroFirstBattle2 {
    lock
    applymovement(LOCALID_PLAYER, walkup)
    waitmovement(0)
    applymovement(LOCALID_PLAYER, faceright)
    waitmovement(0)
    goto(Route2_Eventscript_InitShinjiroFirstBattle)
    release
}

script Route2_Eventscript_InitShinjiroFirstBattle3 {
    lock
    applymovement(LOCALID_PLAYER, moves(walk_up*2))
    waitmovement(0)
    applymovement(LOCALID_PLAYER, faceright)
    waitmovement(0)
    goto(Route2_Eventscript_InitShinjiroFirstBattle)
    release
}

script Route2_Eventscript_SelectingPartyShinjiroFirstBattle {
    lock
    setflag(FLAG_NO_WHITEOUT)
    setflag(FLAG_NO_BAG_USE)
    clearflag(FLAG_DONT_TRANSITION_MUSIC)
    trainerbattle_selectmons(TRAINER_SHINJIRO_ROUTE_2, ShinjiroFirstBattleDefeated, Route2_Eventscript_SelectingPartyShinjiroFirstBattle_Cancel, Route2__Eventscript_ShinjiroFirstBattleResult, 3)
}
script Route2_Eventscript_SelectingPartyShinjiroFirstBattle_Cancel{
    msgbox(format("{SPEAKER NAME_A}{CREATE_MUGSHOT MUGSHOT_A EMOTE_NORMAL}(Let's pick 3 Pokémon to fight. Shinji is waiting.)"))
    closemessage
    goto (Route2_Eventscript_SelectingPartyShinjiroFirstBattle)
}

text ShinjiroFirstBattleDefeated {
    format("Whew, that was quite a work-out.")
}

script Route2__Eventscript_ShinjiroFirstBattleResult {
    fwdtime(23, 20)
    playbgm(MUS_OCEANIC_MUSEUM, TRUE)
    lock
    if (var(VAR_RESULT) == B_OUTCOME_WON && var(VAR_SHINJIRO_RELATIONSHIP_POINTS) == 97){
        msgbox (Route2_Eventscript_WonInShinjiroFirstBattlePointDown)
        closemessage
    }
    elif (var(VAR_RESULT) == B_OUTCOME_WON && var(VAR_SHINJIRO_RELATIONSHIP_POINTS) == 103){
        msgbox (Route2_Eventscript_WonInShinjiroFirstBattlePointUp)
        closemessage
    }
    elif (var(VAR_RESULT) != B_OUTCOME_WON && var(VAR_SHINJIRO_RELATIONSHIP_POINTS) == 97){
        bufferleadmonspeciesname(STR_VAR_1)
        msgbox (Route2_Eventscript_LostInShinjiroFirstBattlePointDown)
        closemessage
    }
    else{
        bufferleadmonspeciesname(STR_VAR_1)
        msgbox (Route2_Eventscript_LostInShinjiroFirstBattlePointUp)
        closemessage
    }
    goto (Route2_Eventscript_PostShinjiroFirstBattle)
    release
}

text Route2_Eventscript_WonInShinjiroFirstBattlePointUp {
    format("{SPEAKER NAME_A}{CREATE_MUGSHOT MUGSHOT_A EMOTE_STRUGGLED}-huff- -huff- (Did I win?)\p
    {SPEAKER NAME_S}{CREATE_MUGSHOT MUGSHOT_S EMOTE_STRUGGLED}-huff- Dang it, I was so close.\p
    Looks like I lost to both of you, huh.\p
    {SPEAKER NAME_S}{CREATE_MUGSHOT MUGSHOT_S EMOTE_SMILE}Well, next time I won't lose again.\p
    {SPEAKER NAME_A}{CREATE_MUGSHOT MUGSHOT_A EMOTE_CONFIDENT}Hehe, I'm always open for another battle against you. It was fun.\p
    {SPEAKER NAME_S}{CREATE_MUGSHOT MUGSHOT_S EMOTE_SMILE}Heh, it is.")
}

text Route2_Eventscript_WonInShinjiroFirstBattlePointDown {
    format("{SPEAKER NAME_A}{CREATE_MUGSHOT MUGSHOT_A EMOTE_STRUGGLED}-huff- -huff- (Did I win?)\p
    {SPEAKER NAME_S}{CREATE_MUGSHOT MUGSHOT_S EMOTE_STRUGGLED}-huff- Dang it, I was so close.\p
    Looks like I lost to both of you, huh.\p
    {SPEAKER NAME_S}{CREATE_MUGSHOT MUGSHOT_S EMOTE_SERIOUS}Well, next time I won't lose again.\p
    {SPEAKER NAME_A}{CREATE_MUGSHOT MUGSHOT_A EMOTE_CONFIDENT}Hehe, I'm always open for another battle against you. It was fun.\p
    {SPEAKER NAME_S}{CREATE_MUGSHOT MUGSHOT_S EMOTE_SERIOUS}...\p
    To be honest, I'd have prefered if you'd fought me fairly. I asked for a 3-vs-3.\p
    If you underestimate me again, I won't let it slide, unlike this time.\p
    {SPEAKER NAME_A}{CREATE_MUGSHOT MUGSHOT_A EMOTE_SAD}... Sorry.\p
    {SPEAKER NAME_S}{CREATE_MUGSHOT MUGSHOT_S EMOTE_NORMAL}Apology accepted.")
}
text Route2_Eventscript_LostInShinjiroFirstBattlePointUp {
    format("{SPEAKER NAME_A}{CREATE_MUGSHOT MUGSHOT_A EMOTE_STRUGGLED}Arg, I was so close!\p
    {SPEAKER NAME_S}{CREATE_MUGSHOT MUGSHOT_S EMOTE_STRUGGLED}-huff- Your {STR_VAR_1} did pack a punch.\p
    {SPEAKER NAME_S}{CREATE_MUGSHOT MUGSHOT_S EMOTE_CONFIDENT}But it looks like I'm not the worst out of the three of us, heh.\p
    {SPEAKER NAME_A}{CREATE_MUGSHOT MUGSHOT_A EMOTE_SERIOUS}Next time I won't lose.\p
    {SPEAKER NAME_S}{CREATE_MUGSHOT MUGSHOT_S EMOTE_CONFIDENT}Looking forward to that.\p
    {SPEAKER NAME_A}{CREATE_MUGSHOT MUGSHOT_A EMOTE_DOUBT}... Did you just imitate Kirijo...\p
    Please don't do that again.\p
    {SPEAKER NAME_S}{CREATE_MUGSHOT MUGSHOT_S EMOTE_SMILE}Pft, the look on your face.")
}

text Route2_Eventscript_LostInShinjiroFirstBattlePointDown {
    format("{SPEAKER NAME_A}{CREATE_MUGSHOT MUGSHOT_A EMOTE_STRUGGLED}Arg, I lost...\p
    {SPEAKER NAME_S}{CREATE_MUGSHOT MUGSHOT_S EMOTE_SERIOUS}...\p
    Isn't that to be expected? Since you didn't want to fight me with 3 Pokémon?\p
    {SPEAKER NAME_A}{CREATE_MUGSHOT MUGSHOT_A EMOTE_SAD}...\p
    {SPEAKER NAME_S}{CREATE_MUGSHOT MUGSHOT_S EMOTE_SERIOUS}I hope you won't underestimate me next time. It wasn't fun even when I won.\p
    {SPEAKER NAME_A}{CREATE_MUGSHOT MUGSHOT_A EMOTE_SAD}Yeah... Sorry.\p
    {SPEAKER NAME_S}{CREATE_MUGSHOT MUGSHOT_S EMOTE_PENSIVE}Sigh.\p
    {SPEAKER NAME_S}{CREATE_MUGSHOT MUGSHOT_S EMOTE_NORMAL}I'll let it slide this time. Apology accepted.")
}
script Route2_Eventscript_PostShinjiroFirstBattle {
    lock
    setvar(VAR_AKIHIKOS_INTRO_STATE, 42)
    clearflag(FLAG_NO_WHITEOUT)
    clearflag(FLAG_NO_BAG_USE)
    playse(SE_M_YAWN)
    applymovement(6, shakevertical)
    waitmovement(0)
    msgbox(format("{SPEAKER NAME_S}{CREATE_MUGSHOT MUGSHOT_S EMOTE_GEEZ}-yawn- That battle took all the energy out of me.\p
    Let's go to bed. I don't want to hear Kirijo complaining tomorrow if we sleep in.\p
    Wanna head inside?\p
    {SPEAKER NAME_A}{CREATE_MUGSHOT MUGSHOT_A EMOTE_NORMAL}Yeah, I'm tired too."))
    closemessage
    special(HealPlayerParty)
    warpsilent(MAP_BLANK_MAP, 0, 0)
}

movement Route2ShinjiroGoesInsideHouse {
    walk_down
    walk_left*3
    walk_up*3
}

script Route2_Eventscript_Himari {
    lock
    faceplayer
    setspeaker("Himari")
    if (var(VAR_TEMP_0) == 1){
        msgbox(format("Can I pet your Rockruff?"), MSGBOX_YESNO)
        closemessage
        if (var(VAR_RESULT) == TRUE){
            playse(SE_SHINY)
            applymovement(15, emotemusic)
            msgbox(format("Thank you!\p
            {SPEAKER NAME_NONE}Himari petted the Rockruff."))
            closemessage
            playmoncry(SPECIES_ROCKRUFF, CRY_MODE_NORMAL)
            waitmoncry
            setvar(VAR_TEMP_0, 2)
            release
            end
        }
        else{
            playse(SE_BOO)
            applymovement(15, emotecry)
            waitmovement(0)
            msgbox(format("Aw... Okay..."))
            closemessage
            release
            end}
    }
    elif (var(VAR_TEMP_0) == 2){
        msgbox(format("Thank you for letting me pet your Rockruff!\p
        Its fur is surprisingly soft!"))
        closemessage
        release
        end
    }
    else{
    msgbox(format("Rockruff in Kitoha region can evolve into Midday, Midnight or Dusk form Lycanroc regardless of its Ability.\p
    Also, I heard all forms of Lycanroc has Tough Claws! The only difference is their base stats spread."))
    closemessage
    checkspecies(SPECIES_ROCKRUFF, NOP_PARTY_SCREEN)
    if (var(VAR_RESULT) == TRUE){
        playse(SE_PIN)
        applymovement(15, emoteexclamation)
        waitmovement(0)
        msgbox(format("Whoa! You have a Rockruff! It's so cute!"))
        closemessage
        setvar(VAR_TEMP_0, 1)
        release
        end
    }
}}

movement emotecry {
    emote_cry
}

script Route2_Eventscript_ShinjiroFirstBattleWarning {
    lock
    msgbox(format("{COLOR RED}Important battle ahead. Do you want to proceed?"), MSGBOX_YESNO)
    closemessage
    if (var(VAR_RESULT) == FALSE){
        applymovement(LOCALID_PLAYER, walkdown)
        waitmovement(0)
    }
    release
    end
}

script Route2_Eventscript_MitsuruApproachingRoute2House {
    lock
    applymovement(LOCALID_PLAYER, moves(player_run_left*3, walk_left*2, walk_slow_left, face_up))
    waitmovement
    delay(40)
    applymovement(LOCALID_PLAYER, moves(walk_up, face_right))
    waitmovement
    msgbox(format("{SPEAKER NAME_M}{CREATE_MUGSHOT MUGSHOT_M EMOTE_NORMAL}(Yuuma-san said the key is under the pot of plant outside of the house.)\p
    (Let's see..........)\p
    (Found it.)"))
    closemessage
    applymovement(LOCALID_PLAYER, faceup)
    waitmovement
    delay(40)
    playse(SE_CLICK)
    waitse
    msgbox(format("{SPEAKER NAME_M}{CREATE_MUGSHOT MUGSHOT_M EMOTE_NORMAL}Please excuse my intrusion."))
    closemessage
    delay(20)
    applymovement(LOCALID_PLAYER, walkup)
    waitmovement
    playse(SE_DOOR)
    warpsilent(MAP_ROUTE2_HOUSE, 7, 12)
    waitstate
}

script Route2_Eventscript_HouseLocked{
    if (var(VAR_MITSURUS_INTRO_STATE) <10){
        lock
        msgbox(format("It's locked."))
        closemessage
        release
        end
    }
}

script Route2_Eventscript_MitsuruPreAkiShinjiGettingAmbushed_TriggerUp {
    lock
    call(Route2_Eventscript_MitsuruOnTriggerHideNPCs)
    applymovement(LOCALID_PLAYER, moves(player_run_right*5)) //pos 28, 13
    waitmovement
    goto(Route2_Eventscript_MitsuruWitnessingAkiShinjiGettingAmbushed)
}

script Route2_Eventscript_MitsuruPreAkiShinjiGettingAmbushed_TriggerDown {
    lock
    call(Route2_Eventscript_MitsuruOnTriggerHideNPCs)
    applymovement(LOCALID_PLAYER, moves(player_run_up, player_run_right*4)) //pos 28, 13
    waitmovement
    goto(Route2_Eventscript_MitsuruWitnessingAkiShinjiGettingAmbushed)
}

script Route2_Eventscript_MitsuruOnTriggerHideNPCs {
    if (var(VAR_RESULT) == TRUE){
        hidefollowernpc
    }
    addobject(7)
    addobject(8)
    addobject(9) 
    hideobjectat(7, MAP_ROUTE2)
    hideobjectat(8, MAP_ROUTE2)
    hideobjectat(9, MAP_ROUTE2)

}

script Route2_Eventscript_MitsuruWitnessingAkiShinjiGettingAmbushed {
    playse(SE_PIN)
    applymovement(LOCALID_PLAYER, moves(emote_exclamation_mark))
    waitmovement
    fadenewbgm(MUS_P3R_FEARFUL_EXPERIENCE)
    delay(8)
    applymovement(LOCALID_PLAYER, moves(player_run_left*4, player_run_down, face_right))
    waitmovement
    applymovement(OBJ_EVENT_ID_FOLLOWER, moves(walk_left, walk_down, face_right))
    waitmovement
    clearflag(FLAG_TEMP_6)
    addobject(12)
    addobject(11)
    addobject(10)
    applymovement(10, invisible)
    applymovement(11, invisible)
    waitmovement
    delay(20)
    applymovement(12, moves(walk_up, face_down))
    waitmovement
    applymovement(LOCALID_PLAYER, faceup)
    waitmovement
    msgbox(format("{SPEAKER NAME_M}{CREATE_MUGSHOT MUGSHOT_M EMOTE_NORMAL}It's those men from the Genesis Group. I have to know what they're plotting.\p
    Can you cover the north, Inteleon?"))
    closemessage
    playmoncry(SPECIES_INTELEON, CRY_MODE_NORMAL)
    applymovement(12, walkinplacedown)
    waitmovement
    delay(20)
    special(SpawnCameraObject)
    applymovement(OBJ_EVENT_ID_CAMERA, moves(walk_fast_up*6))
    applymovement(12, moves(walk_fast_up*6, set_invisible))
    waitmovement
    playse(SE_EXIT)
    delay(20)
    applymovement(12, moves(walk_fast_right*7))
    applymovement(OBJ_EVENT_ID_CAMERA, moves(walk_diag_southeast*5))
    waitmovement(OBJ_EVENT_ID_CAMERA)
    goto (Route2_Eventscript_AkishinjiGettingBlockedMitsuruPOV)
}

script Route2_Eventscript_AkishinjiGettingBlockedMitsuruPOV {
    delay(30)
    addobject(6)
    addobject(19)
    applymovement(OBJ_EVENT_ID_CAMERA, moves(walk_diag_northeast*2))
    applymovement(19, AkishinjiGettingBlockedBySusMen)
    delay(8)
    applymovement(6, AkishinjiGettingBlockedBySusMen)
    applymovement(4, SusMenBlockingAkiShinji)
    delay(8)
    applymovement(3, SusMenBlockingAkiShinji)
    waitmovement(3)
    call(Eventscript_BlockShakeCamera)
    delay(20)
    applymovement(19, AkiShinjiStepBack)
    applymovement (6, AkiShinjiStepBack)
    waitmovement(0)
    msgbox(format("{SPEAKER NAME_A}{CREATE_MUGSHOT MUGSHOT_A EMOTE_STRUGGLED}Ouch!\p
    {SPEAKER NAME_S}{CREATE_MUGSHOT MUGSHOT_S EMOTE_STRUGGLED}What the hell??"))
    closemessage
    delay(30)
    setspeaker("Man In Black 1")
    applymovement(4, walkinplaceright)
    waitmovement(0)
    msgbox(format("That stupid cop is finally out of picture."))
    closemessage
    setspeaker("Man In Black 2")
    applymovement(3, walkinplaceright)
    waitmovement(0)
    msgbox(format("You two are coming with us. Like it or not.\p
    {SPEAKER NAME_A}{CREATE_MUGSHOT MUGSHOT_A EMOTE_STRUGGLED}What?"))
    closemessage
    applymovement(4, SusMan1ApproachingAkihiko)
    waitmovement(0)
    applymovement(19, shakehorizontal)
    call(Eventscript_BlockShakeCamera)
    applymovement(4, SusMan1StepBack)
    waitmovement(0)
    delay(20)
    msgbox(format("{SPEAKER NAME_A}{CREATE_MUGSHOT MUGSHOT_A EMOTE_MAD}No way! Who even are you!?"))
    closemessage
    setspeaker("Man In Black 1")
    applymovement(4, walkinplaceright)
    waitmovement(0)
    msgbox(format("Tch.\p
    I hate disobedient kids."))
    msgbox(SusMenFightIntro)
    closemessage
    fadescreenswapbuffers(FADE_TO_BLACK)
    playse(SE_EFFECTIVE)
    waitse
    playmoncry(SPECIES_PAWMI, CRY_MODE_NORMAL)
    waitmoncry
    playse(SE_M_THUNDER_WAVE)
    waitse
    playse(SE_EFFECTIVE)
    waitse
    applymovement(LOCALID_PLAYER, faceright)
    waitmovement
    fadescreenswapbuffers(FADE_FROM_BLACK)
    msgbox(format("{SPEAKER NAME_M}{CREATE_MUGSHOT MUGSHOT_M EMOTE_NORMAL}(!)\n
    (Sanada won.)\p
    (His battling skill is quite good.)"))
    closemessage
    applymovement(4, walkinplaceright)
    waitmovement
    setspeaker("Man In Black 1")
    msgbox(format("You aren't half bad. However...\p
    Resistance is futile."))
    closemessage
    delay(8)
    playse(SE_EXIT)
    showobjectat(7, MAP_ROUTE2)
    applymovement(7, DrowzeeAppears)
    waitmovement(0)
    applymovement(19, facedown)
    applymovement(6, facedown)
    waitmovement(0)
    msgbox(format("{SPEAKER NAME_A}{CREATE_MUGSHOT MUGSHOT_A EMOTE_SURPRISED}Huh?"))
    closemessage
    setspeaker("Man In Black 2")
    applymovement(3, walkinplaceright)
    waitmovement(0)
    msgbox(format("Drowzee, Hypnosis."))
    closemessage
    fadescreenswapbuffers(FADE_TO_BLACK)
    playmoncry(SPECIES_DROWZEE, CRY_MODE_NORMAL)
    waitmoncry
    delay(30)
    applymovement(19, invisible)
    applymovement(6, invisible)
    waitmovement(0)
    goto (Route2_Eventscript_AkishinjiAsleepMitsuruPOV)
}

script Route2_Eventscript_AkishinjiAsleepMitsuruPOV {
    showobjectat(8, MAP_ROUTE2)
    showobjectat(9, MAP_ROUTE2)
    waitmovement(0)
    fadescreenswapbuffers(FADE_FROM_BLACK)
    delay(20)
    playse(SE_M_SNORE)
    applymovement(8, emoteasleep)
    delay(8)
    playse(SE_M_SNORE)
    applymovement(9, emoteasleep)
    waitmovement(0)
    delay(20)
    setspeaker("Man In Black 2")
    applymovement(3, walkinplaceright)
    waitmovement(0)
    msgbox("That's more like it.")
    closemessage
    applymovement(3, walkright)
    waitmovement(0)
    delay(20)
    msgbox(format("{SPEAKER NAME_M}{CREATE_MUGSHOT MUGSHOT_M EMOTE_SERIOUS}Now! Inteleon, Vulpix!"))
    closemessage
    fadescreenswapbuffers(FADE_TO_BLACK)
    delay(20)
    playmoncry(SPECIES_INTELEON, CRY_MODE_NORMAL)
    waitmoncry
    playse(SE_M_BUBBLE_BEAM)
    waitse
    playmoncry(SPECIES_VULPIX, CRY_MODE_NORMAL)
    waitmoncry
    playse(SE_M_ICY_WIND)
    waitse
    applymovement(3, invisible)
    applymovement(4, invisible)
    removeobject(3)
    removeobject(4)
    clearflag(FLAG_TEMP_6)
    applymovement(10, visible)
    applymovement(11, visible)
    fadescreenswapbuffers(FADE_FROM_BLACK)
    delay(20)
    applymovement(10, shakehorizontal)
    playse(SE_M_HARDEN)
    delay(4)
    applymovement(11, shakehorizontal)
    playse(SE_M_HARDEN)
    waitse
    applymovement(7, emoteexclamation)
    waitmovement(0)
    goto(Route2_Eventscript_MitsuruAppearsMitsuruPOV)
}

script Route2_Eventscript_MitsuruAppearsMitsuruPOV {
    delay(40)
    addobject(5)
    applymovement(OBJ_EVENT_ID_CAMERA, moves(walk_left, walk_down))
    applymovement(LOCALID_PLAYER, MitsuruAppears)
    applymovement(OBJ_EVENT_ID_FOLLOWER, MitsuruAppears)
    waitmovement(0)
    applymovement(7, DrowzeeRunsAway)
    delay (52)
    playse(SE_BOO)
    waitmovement(0)
    waitse
    playse(SE_EXIT)
    waitse
    delay(30)
    msgbox(format("{SPEAKER NAME_M}{CREATE_MUGSHOT MUGSHOT_M EMOTE_NORMAL}...\p
    Inteleon, let's get these two out of here first.\p
    The ice can only hold them off for so long."))
    closemessage
    playse(SE_EXIT)
    applymovement(12, InteleonWalksOut)
    applymovement(OBJ_EVENT_ID_CAMERA, CameraToInteleon)
    waitmovement(0)
    delay(20)
    playmoncry(SPECIES_INTELEON, CRY_MODE_NORMAL)
    applymovement(12, walkinplacedown)
    waitmovement(0)
    fadenewbgm(MUS_DUMMY)
    setvar(VAR_MITSURUS_INTRO_STATE, 28)
    setflag(FLAG_HIDE_MAP_NAME_POPUP)
    setflag(FLAG_DONT_TRANSITION_MUSIC)
    delay(30)
    warpsilent(MAP_ROUTE2_HOUSE, 5, 3)
}

script Route2_Eventscript_MitsuruPostSenpaiTrioFirstMeeting {
    lock
    msgbox(format("{SPEAKER NAME_M}{CREATE_MUGSHOT MUGSHOT_M EMOTE_NORMAL}(There's still time before the promised hour.)\p
    (Maybe I can wander around the town to gather more information.)"))
    closemessage
    subquestmenu(QUEST_MENU_CHECK_COMPLETE, STORY_QUESTS, AKIHIKOS_PROLOGUE)
    if (!var(VAR_RESULT))
    {
        call(EventScript_ShowHelpQuest)
        setflag(FLAG_SYS_QUEST_MENU_GET)
        questmenu(QUEST_MENU_SET_ACTIVE, STORY_QUESTS)
        setvar(VAR_STORY_QUESTS_STATE, 0)
        subquestmenu(QUEST_MENU_SET_ACTIVE, STORY_QUESTS, MITSURUS_PROLOGUE)
    }
    clearflag(FLAG_HIDE_QUEST_ICON)
    setvar(VAR_MITSURUS_INTRO_STATE, 30)
    release
    end
}

script Route2_Eventscript_PicnickerBlockingMitsuruTriggerRight {
    lock
    playse(SE_PIN)
    applymovement(13, moves(emote_exclamation_mark, walk_fast_left, face_down))
    waitmovement
    setspeaker("Yuuka")
    msgbox(format("I'm sorry but I dropped an important note around here. Could you please come back after I find it?"))
    closemessage
    applymovement(13, walkdown)
    applymovement(LOCALID_PLAYER, moves(lock_facing_direction, walk_down, unlock_facing_direction))
    applymovement(OBJ_EVENT_ID_FOLLOWER, moves(lock_facing_direction, walk_down, unlock_facing_direction))
    waitmovement
    applymovement(13, moves(walk_fast_up, walk_fast_right, face_left))
    waitmovement
    lock
    setobjectnewmovementtype(13, MOVEMENT_TYPE_FACE_LEFT_AND_RIGHT)
    release
    end
}

script Route2_Eventscript_PicnickerBlockingMitsuruTriggerLeft {
    lock
    playse(SE_PIN)
    applymovement(13, moves(emote_exclamation_mark, walk_fast_left*2, face_down))
    waitmovement
    setspeaker("Yuuka")
    msgbox(format("I'm sorry but I dropped an important note around here. Could you please come back after I find it?"))
    closemessage
    applymovement(13, walkdown)
    applymovement(LOCALID_PLAYER, moves(lock_facing_direction, walk_down, unlock_facing_direction))
    applymovement(OBJ_EVENT_ID_FOLLOWER, moves(lock_facing_direction, walk_down, unlock_facing_direction))
    waitmovement
    applymovement(13, moves(walk_fast_up, walk_fast_right*2, face_left))
    waitmovement
    lock
    setobjectnewmovementtype(13, MOVEMENT_TYPE_FACE_LEFT_AND_RIGHT)
    release
    end
}