mapscripts Mansion_MitsurusRoom2F_MapScripts {
    MAP_SCRIPT_ON_TRANSITION{
        setrespawn(HEAL_LOCATION_MANSION_MITSURUS_ROOM2F)
        if (var(VAR_MITSURUS_INTRO_STATE) >= 2){
            setflag(FLAG_TEMP_1)
        }
    }
    MAP_SCRIPT_ON_WARP_INTO_MAP_TABLE[
        VAR_MITSURUS_INTRO_STATE, 0 : Mansion_MitsurusRoom2F_Eventscript_OnWarp0
    ]
    MAP_SCRIPT_ON_FRAME_TABLE[
        VAR_MITSURUS_INTRO_STATE, 0: Mansion_MitsurusRoom2F_Eventscript_OnFrame0
    ]
}

script Mansion_MitsurusRoom2F_Eventscript_OnWarp0 {
    applymovement(LOCALID_PLAYER, faceleft)
}

script Mansion_MitsurusRoom2F_Eventscript_OnFrame0 {
    setvar(VAR_MITSURUS_INTRO_STATE, 1)
    lock
    call(EventScript_TellTheTimeDayHourMinute)
    goto(Mansion_MitsurusRoom2F_Eventscript_MitsuruPacking)
}

script Mansion_MitsurusRoom2F_Eventscript_MitsuruPacking {
    delay(30)
    applymovement(LOCALID_PLAYER, moves(emote_pensive))
    waitmovement(0)

    msgbox(format("{SPEAKER NAME_M}{CREATE_MUGSHOT MUGSHOT_M EMOTE_NORMAL}I'll need this... and a spare pair of boots..."))
    closemessage
    setflag(FLAG_SYS_B_DASH)
    delay(30)
    applymovement(LOCALID_PLAYER, moves(walk_up *6, face_left))
    delay(40)
    applymovement(LOCALID_VULPIX, moves(face_left, face_up))
    waitmovement(LOCALID_PLAYER)
    createfieldmugshot(MUGSHOT_M, EMOTE_NORMAL)
    msgbox(format("{SPEAKER NAME_M}My Pokédex, wallet... Repels...\p
    And the map to Thundershards Cliff."))
    closemessage
    setcaughtmon(SPECIES_VULPIX_DELTA)
    if (!flag(FLAG_SYS_POKEDEX_GET)){
        call(EventScript_ShowHelpPokedex)
        setflag(FLAG_SYS_POKEDEX_GET)
        special(SetUnlockedPokedexFlags)
        setflag(FLAG_SYS_NATIONAL_DEX)
        special(EnableNationalPokedex)
    }
    setflag(FLAG_HELP_WINDOW_GET)
    setflag(FLAG_SYS_POKEMON_GET)
    applymovement(LOCALID_PLAYER, emotepensive)
    waitmovement(0)
    msgbox(format("{SPEAKER NAME_M}{CREATE_MUGSHOT MUGSHOT_M EMOTE_NORMAL}What else..."))
    closemessage
    playse(SE_PIN)
    applymovement(LOCALID_PLAYER, emoteexclamation)
    waitmovement(0)
    msgbox(format("{SPEAKER NAME_M}{CREATE_MUGSHOT MUGSHOT_M EMOTE_NORMAL}Oh right."))
    closemessage
    applymovement(LOCALID_PLAYER, moves(walk_down *2, walk_left *3, walk_up))
    delay(8)
    applymovement(LOCALID_VULPIX, faceleft)
    waitmovement(LOCALID_PLAYER)
    delay(40)
    subquestmenu(QUEST_MENU_CHECK_COMPLETE, STORY_QUESTS, AKIHIKOS_PROLOGUE)
    if (var(VAR_RESULT) == FALSE)
    {
        additem(ITEM_MACH_BIKE) // will give to 1st MC akihiko later
    }
    giveitem(ITEM_CAMP_GEARS)
    additem(ITEM_ESCAPE_ROPE)
    additem(ITEM_EXP_SHARE)
    setflag(FLAG_EXP_SHARE)
    additem(ITEM_POKE_BALL, 20)
    additem(ITEM_POTION, 2)
    additem(ITEM_REPEL, 5)
    addmoney(15000)
    msgbox(format("{SPEAKER NAME_M}{CREATE_MUGSHOT MUGSHOT_M EMOTE_SMILE}I'll definitely need this while outdoors.\p
    {SPEAKER NAME_M}{CREATE_MUGSHOT MUGSHOT_M EMOTE_NORMAL}Hmm, that should be everything."))
    closemessage
    delay(20)
    applymovement(LOCALID_VULPIX, moves(walk_fast_left*4, jump_in_place_up))
    waitmovement(0)
    playmoncry(SPECIES_VULPIX, CRY_MODE_NORMAL)
    waitmoncry
    applymovement(LOCALID_PLAYER, facedown)
    waitmovement(0)
    delay(30)
    msgbox(format("{SPEAKER NAME_M}{CREATE_MUGSHOT MUGSHOT_M EMOTE_SMILE}I know, I know. I'll never leave you behind, Vulpix.\p
    Let's get you inside the Pokéball now, shall we?"))
    closemessage
    playmoncry(SPECIES_VULPIX, CRY_MODE_NORMAL)
    applymovement(LOCALID_VULPIX, emotemusic)
    waitmovement(0)
    waitmoncry
    lock
    setobjectnewmovementtype(LOCALID_VULPIX, MOVEMENT_TYPE_WALK_IN_PLACE_UP)
    subquestmenu(QUEST_MENU_CHECK_COMPLETE, STORY_QUESTS, AKIHIKOS_PROLOGUE)
    if (!var(VAR_RESULT)){
        call(EventScript_ShowHelpShinyChance)
        call(EventScript_ShowHelpStarter)
    }
    release
    end
}

script Mansion_MitsurusRoom2F_Eventscript_PuttingVulpixInPokeball {
    lock
    msgbox ("Hold out the Pokéball to Vulpix?", MSGBOX_YESNO)
    closemessage
    if(var(VAR_RESULT) == YES){
        setvar(VAR_MITSURUS_INTRO_STATE, 2)
        givemon(SPECIES_VULPIX_DELTA, 7, ITEM_ABILITY_CHANGER, ITEM_CHERISH_BALL, NATURE_MODEST, 1, MON_FEMALE, 0, 0, 0, 0, 0, 0, 31, 31, 31, 31, 31, 31, MOVE_ECHOED_VOICE, isStoryStarter = TRUE)
        if(var(VAR_RESULT) == MON_GIVEN_TO_PARTY){
            playfanfare(MUS_OBTAIN_ITEM)
            waitfanfare
            msgbox(format("Vulpix's pushed the Pokéball button with its nose and went inside."))
            removeobject(LOCALID_VULPIX)
            setflag(FLAG_TEMP_1)
            msgbox(format("{COLOR GREEN}Tips: You can rename a Pokémon in its summary screen."))
            closemessage
            
        }
        elif(var(VAR_RESULT) == MON_GIVEN_TO_PC){
            playfanfare(MUS_OBTAIN_ITEM)
            waitfanfare
            msgbox(format("Vulpix's pushed the Pokéball button with its nose and went inside."))
            removeobject(LOCALID_VULPIX)
            setflag(FLAG_TEMP_1)
            msgbox(format("{COLOR GREEN}Tips: You can rename a Pokémon in its summary screen."))
            closemessage
        }
        else{
            msgbox("Party full.")}
    msgbox(format("{SPEAKER NAME_M}{CREATE_MUGSHOT MUGSHOT_M EMOTE_SERIOUS}It's already 10:32PM.\p
    If I don't go now, I won't be arriving at the cliff's foot before midnight."))
    closemessage
    fwdtime(22, 32)
    msgbox(format("{COLOR GREEN}{SPEAKER NAME_NONE}{DESTROY_MUGSHOT}Tips: Check your bag for various QoL items."))
    closemessage
    setfriendship(0, 200)
    updatefollowingmon
    }
    else{
        playse(SE_PIN)
        applymovement (LOCALID_VULPIX, moves(emote_question_mark))
        waitmovement
        msgbox(format("Vulpix is wondering why you're hesitating."))
        closemessage
    }
    closemessage
    release
    end
}

script Eventscript_CutsceneStarts {
    setflag(FLAG_HIDE_HELP_BUTTON)
    setflag(FLAG_HIDE_MAP_NAME_POPUP)
}

script Eventscript_CutsceneEnds {
    clearflag(FLAG_HIDE_HELP_BUTTON)
    clearflag(FLAG_HIDE_MAP_NAME_POPUP)
}

script Mansion_MitsurusRoom2F_Eventscript_MustPickUpVulpix {
    lock
    applymovement(LOCALID_PLAYER, moves(walk_right, face_down))
    waitmovement
    msgbox(format("{SPEAKER NAME_M}{CREATE_MUGSHOT MUGSHOT_M EMOTE_NORMAL}(Vulpix is waiting for me.)"))
    closemessage
    release
    end
}

script Mansion_MitsurusRoom2F_Eventscript_DollAlolanVulpix {
    lock
    msgbox(format("It's an Alolan Vulpix doll.\p
    {SPEAKER NAME_M}{CREATE_MUGSHOT MUGSHOT_M EMOTE_SAD}...\p
    (This reminds me of how my Vulpix used to look...)"))
    closemessage
    release
    end
}

script Mansion_MitsurusRoom2F_Eventscript_PC {
    lock
    checkplayergender
    if (var(VAR_RESULT) == FEMALE){
        call (Mansion_MitsurusRoom2F_Eventscript_PlayerPC)
    }
    else{
        call (Mansion_MitsurusRoom2F_Eventscript_RivalPC)
    }
    release
    end
}

script Mansion_MitsurusRoom2F_Eventscript_PlayerPC {
    playse(SE_PC_ON)
	msgbox(gText_PlayerHouseBootPC)
	special(BedroomPC)
	waitstate
}

script Mansion_MitsurusRoom2F_Eventscript_RivalPC {
    msgbox(format("It's Mitsuru's PC. It's the latest model from Kirijo Electronics."))
    closemessage
}

script Mansion_MitsurusRoom2F_Eventscript_Piano {
    lock
    if(var(VAR_TEMP_0) == 0)
    {
        msgbox(format("Play a song? {COLOR GREEN}(It will take roughly 30 seconds.)"), MSGBOX_YESNO)
        closemessage
        if (var(VAR_RESULT) == TRUE){
            setflag(FLAG_HIDE_HELP_BUTTON)
            playbgm(MUS_XC2_GARFONT_MERCERNARIES_NIGHT, FALSE)
            delay(1800)
            fadenewbgm(MUS_XC2_TORIGOTH_NIGHT)
            savebgm(MUS_XC2_TORIGOTH_NIGHT)
            msgbox(format("{SPEAKER NAME_M}{CREATE_MUGSHOT MUGSHOT_M EMOTE_NORMAL}(It's time to go.)"))
            closemessage
            addtime(0, 0, 10)
            setvar(VAR_TEMP_0, 1)
        }}
    else
    {
        msgbox(format("{SPEAKER NAME_M}{CREATE_MUGSHOT MUGSHOT_M EMOTE_NORMAL}(I really need to get going.)"))
    }
    release
    end
}

script Mansion_MitsurusRoom2F_Eventscript_BookshelfLeft {
    lock
    msgbox(format("Documents about various big companies in Kitoha region.\p
    Eleki Express's is circled in red. Beside said document are several photos of delivery trucks.\p
    They seem to be in a mountain area."))
    closemessage
    release
    end
}

script Mansion_MitsurusRoom2F_Eventscript_BookshelfRight {
    lock
    msgbox(format("Various books about human's and Pokémon's biology, geology and genealogy."))
    closemessage
    release
    end
}

script Mansion_MitsurusRoom2F_Eventscript_Notebooks {
    lock
    msgbox(format("College materials from the previous term.\p
    {SPEAKER NAME_M}{CREATE_MUGSHOT MUGSHOT_M EMOTE_NORMAL}(Luckily, it's summer break right now.\p
    Otherwise it'd be hard for me to travel.\p
    Well, I figure I can always request a leave of absence until this matter is sorted out.)"))
    closemessage
    release
    end
}

script Eventscript_OptionsTips {
    call(EventScript_ShowHelpSpeedUp)
    setflag(FLAG_HELP_SPEEDUP)
    removeobject(VAR_LAST_TALKED)
    release
    end
}