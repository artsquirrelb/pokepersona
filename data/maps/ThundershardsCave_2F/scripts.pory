mapscripts ThundershardsCave_2F_MapScripts {
    MAP_SCRIPT_ON_WARP_INTO_MAP_TABLE[
        VAR_MITSURUS_INTRO_STATE, 16: ThundershardsCave_2F_Eventscript_OnWarp16
    ]
    MAP_SCRIPT_ON_TRANSITION{
        if (var(VAR_MITSURUS_INTRO_STATE) == M_KG_STAFF_DOUBLE || var(VAR_MITSURUS_INTRO_STATE) == M_MUST_FIGHT_KG_STAFF){
            setflag(FLAG_TEMP_3)
            setflag(FLAG_TEMP_4)
            addobject(12)
        }
        if (var(VAR_MITSURUS_INTRO_STATE) > M_KG_STAFF_DOUBLE){
            setflag(FLAG_TEMP_2)
        }
        if(var(VAR_MITSURUS_INTRO_STATE) >M_MUST_FIGHT_KG_STAFF){
            setflag(FLAG_TEMP_1)
            setflag(FLAG_TEMP_4)
        }
        if (var(VAR_MITSURUS_INTRO_STATE) == M_VS_NORBERT){
            setobjectxyperm(19, 34, 8)
            setobjectxyperm(20, 33, 8)
        }
        if(var(VAR_MITSURUS_INTRO_STATE) > M_VS_NORBERT){
            setflag(FLAG_TEMP_3)
        }
        if(var(VAR_MITSURUS_INTRO_STATE) == M_EXPLOSION){
            setweather(WEATHER_DROUGHT)
            special(StartDroughtWeatherBlend)
        }
        if(var(VAR_MITSURUS_INTRO_STATE) <= M_EXPLOSION){
            setflag(FLAG_TEMP_5)
        }
    }
    MAP_SCRIPT_ON_FRAME_TABLE[
        VAR_MITSURUS_INTRO_STATE, M_EXPLOSION: ThundershardsCave_2F_Eventscript_OnFrame16
    ]
}

script ThundershardsCave_2F_Eventscript_OnWarp16 {
    applymovement(LOCALID_PLAYER, faceleft)
}

script ThundershardsCave_2F_Eventscript_OnFrame16 {
    goto(ThundershardsCave_2F_Eventscript_MitsuruEscapingTheExplodingCave)    
}

script ThundershardsCave_2F_Eventscript_MitsuruNoticingKGStaff1 {
    lock
    setvar(VAR_MITSURUS_INTRO_STATE, M_MUST_FIGHT_KG_STAFF)
    playse(SE_PIN)
    applymovement(LOCALID_PLAYER, moves(emote_exclamation_mark, delay_16, delay_16, player_run_right*2, face_left))
    waitmovement
    delay(30)
    fadenewbgm(MUS_P3R_FEARFUL_EXPERIENCE)
    setflag(FLAG_DONT_TRANSITION_MUSIC)
    goto(ThundershardsCave_2F_Eventscript_MitsuruEavesdroppingKGStaff)
    release
}

script ThundershardsCave_2F_Eventscript_MitsuruNoticingKGStaff2 {
    lock
    setvar(VAR_MITSURUS_INTRO_STATE, M_MUST_FIGHT_KG_STAFF)
    playse(SE_PIN)
    applymovement(LOCALID_PLAYER, moves(emote_exclamation_mark, delay_16, delay_16, player_run_right, face_left))
    waitmovement
    delay(30)
    fadenewbgm(MUS_P3R_FEARFUL_EXPERIENCE)
    setflag(FLAG_DONT_TRANSITION_MUSIC)
    goto(ThundershardsCave_2F_Eventscript_MitsuruEavesdroppingKGStaff)
    release
}

movement walkinplaceleft {
    walk_in_place_left
}

script ThundershardsCave_2F_Eventscript_MitsuruEavesdroppingKGStaff {
    lock
    special(SpawnCameraObject)
    applymovement(OBJ_EVENT_ID_CAMERA, moves(walk_right*2))
    waitmovement(0)
    applymovement(14, walkinplaceleft)
    waitmovement
    setspeaker("Scientist")
    msgbox(format("The subjects were successfully transported to the new lab.\p
    There's only a few leftover reports I need to fetch.\p
    The cops already caught onto this lab so we need to move fast.\p
    Make sure no curious cats get in before I finish.\p
    There's already a weird man mining the rocks on the floor below."))
    closemessage
    applymovement(12, walkinplaceright)
    waitmovement(0)
    msgbox(format("{SPEAKER NAME_MIB}Don't worry sir. That's our job."))
    closemessage
    applymovement(14, moves(walk_right*5, set_invisible))
    delay(50)
    applymovement(OBJ_EVENT_ID_CAMERA, moves(walk_left*2))
    waitmovement(0)
    removeobject(14)
    setflag(FLAG_TEMP_2)
    special(RemoveCameraObject)
    bufferleadmonspeciesname(STR_VAR_1)
    msgbox(format("{SPEAKER NAME_M}{CREATE MUGSHOT_M EMOTE_SERIOUS}.........\p
    ({STR_VAR_1}, are you ready?)"))
    closemessage
    playfirstmoncry
    applymovement(OBJ_EVENT_ID_FOLLOWER, moves(jump_in_place_right))
    waitmovement
    release
    end
}

script ThundershardsCave_2F_Eventscript_LetsJumpKGStaff {
    lock
    msgbox(format("{SPEAKER NAME_M}{CREATE MUGSHOT_M EMOTE_SERIOUS}(They haven't noticed me yet. Let's surprise those people.)"))
    closemessage
    applymovement(LOCALID_PLAYER, walkup)
    waitmovement(0)
    release
    end
}

script ThundershardsCave_2F_Eventscript_KirijoGenesisStaff {
    clearflag(FLAG_DONT_TRANSITION_MUSIC)
    lock
    playse(SE_PIN)
    applymovement(12, moves(emote_exclamation_mark, face_left))
    waitmovement(0)
    delay(4)
    playse(SE_PIN)
    applymovement(13, moves(emote_exclamation_mark, face_left))
    waitmovement(0)
    bufferpartymonnick(STR_VAR_1, 0)
    bufferpartymonnick(STR_VAR_2, 1)
    msgbox(format("{SPEAKER NAME_WIB}What the- Why are you here-\p
    {SPEAKER NAME_M}{CREATE MUGSHOT_M EMOTE_SERIOUS}{STR_VAR_1}, {STR_VAR_2}."))
    closemessage
    trainerbattle_two_trainers(TRAINER_KG_STAFF_M_THUNDERSHARDS_CAVE_1F, KG_Staff_M_defeated, TRAINER_KG_STAFF_F_THUNDERSHARDS_CAVE_1F, KG_Staff_F_defeated)
    goto(ThundershardsCave_2F_Eventscript_Post_KirijoGenesisStaffM_Battle)
    release
}

script ThundershardsCave_2F_Eventscript_Post_KirijoGenesisStaffM_Battle {
    lock
    delay(20)
    applymovement(13, faceup)
    waitmovement(0)
    delay(4)
    applymovement(12, facedown)
    waitmovement
    msgbox(format("{SPEAKER NAME_MIB}(Should we report to the manager?)\p
    {SPEAKER NAME_WIB}(She beat both of us out of nowhere, she must be here for trouble.)\p
    {SPEAKER NAME_MIB}(Exactly, let's go.)"))
    closemessage
    applymovement(13, moves(walk_fast_right * 3))
    delay(8)
    applymovement(12, moves(walk_fast_right, walk_fast_down, walk_fast_right))
    waitmovement(0)
    getplayerxy(VAR_TEMP_0, VAR_TEMP_1)
    if (var(VAR_TEMP_0) == 27 && var(VAR_TEMP_1) == 7){
        applymovement(LOCALID_PLAYER, moves(walk_fast_down, face_right))
        waitmovement
    }
    msgbox(format("{SPEAKER NAME_M}{CREATE MUGSHOT_M EMOTE_SERIOUS}Not so fast."))
    closemessage
    goto(ThundershardsCave_2F_Eventscript_FreezingKGStaff)
    release
}

script ThundershardsCave_2F_Eventscript_FreezingKGStaff {
    clearflag(FLAG_TEMP_4)
    addobject(21, MAP_THUNDERSHARDS_CAVE_2F)
    showobjectat(21, MAP_THUNDERSHARDS_CAVE_2F)
    applymovement(21, moves(set_visible, jump_right))
    delay(8)
    playse(SE_LEDGE)
    waitmovement(21)
    special(SpawnCameraObject)
    applymovement(OBJ_EVENT_ID_CAMERA, moves(walk_fast_right*2))
    waitmovement(0)
    fadescreenswapbuffers(FADE_TO_WHITE, 1)
    delay(20)
    playmoncry(SPECIES_INTELEON, CRY_MODE_NORMAL)
    waitmoncry
    playse(SE_M_BUBBLE_BEAM)
    waitse
    playmoncry(SPECIES_VULPIX, CRY_MODE_NORMAL)
    waitmoncry
    playse(SE_M_ICY_WIND)
    waitse
    applymovement(12, invisible)
    applymovement(13, invisible)
    waitmovement(0)
    removeobject(12)
    removeobject(13)
    setflag(FLAG_TEMP_1)
    clearflag(FLAG_TEMP_3)
    addobject(19)
    addobject(20)
    waitmovement(0)
    fadescreenswapbuffers(FADE_FROM_WHITE)
    delay(20)
    applymovement(19, shakehorizontal)
    playse(SE_M_HARDEN)
    delay(4)
    applymovement(20, shakehorizontal)
    playse(SE_M_HARDEN)
    waitse
    delay(8)
    applymovement(OBJ_EVENT_ID_CAMERA, moves(walk_left*2))
    waitmovement(0)
    special(RemoveCameraObject)
    delay(20)
    bufferleadmonspeciesname(STR_VAR_1)
    msgbox(format("{SPEAKER NAME_M}{CREATE MUGSHOT_M EMOTE_SERIOUS}Don't worry, you'll thaw out in about 20 minutes.\p
    Let's go, {STR_VAR_1}, Inteleon."))
    closemessage
    applymovement(21, moves(walk_left, set_invisible))
    waitmovement
    removeobject(21)
    setflag(FLAG_TEMP_4)
    fwdtime(8, 40)
    release
    end
}

text KG_Staff_M_defeated {
    format("Dammit. I knew she graduated the Ranger School early but still-")
}

text KG_Staff_F_defeated {
    format("Why is the Kirijo young lady here?")
}

script ThundershardsCave_2F_Eventscript_FrozenStaff {
    if (var(VAR_MITSURUS_INTRO_STATE) == M_MUST_FIGHT_KG_STAFF){
    lock
    msgbox(format("{SPEAKER NAME_M}{CREATE MUGSHOT_M EMOTE_SERIOUS}(I should be able to push them.)"))
    closemessage
    playse(SE_SELECT)
    applymovement(19, moves(lock_facing_direction, walk_fast_right*3))
    applymovement(20, moves(lock_facing_direction, walk_fast_right*3))
    waitmovement(0)
    playse(SE_CLICK)
    waitse
    msgbox(format("{SPEAKER NAME_M}{CREATE MUGSHOT_M EMOTE_CONFIDENT}(Magnifique.)"))
    setvar(VAR_MITSURUS_INTRO_STATE, M_VS_NORBERT)
    release
    end
    }
}

script ShakeScreenNoSound {
    delay(20)
    setvar(VAR_0x8004, 2) //vertical pan
    setvar(VAR_0x8005, 4) //horizontal pan
    setvar(VAR_0x8006, 6) //number of shakes
    setvar(VAR_0x8007, 4) //shake delay
    special (ShakeCamera)
    waitstate
    delay(20)
}

script ThundershardsCave_2F_Eventscript_MitsuruEscapingTheExplodingCave {
    playse(SE_INTRO_BLAST)
    setvar(VAR_0x8004, 2) //vertical pan
    setvar(VAR_0x8005, 4) //horizontal pan
    setvar(VAR_0x8006, 6) //number of shakes
    setvar(VAR_0x8007, 4) //shake delay
    special (ShakeCamera)
    setfieldeffectargument (0, 37)
    setfieldeffectargument (1, 7)
    dofieldeffect(FLDEFF_EXPLOSION)
    applymovement(LOCALID_PLAYER, moves(player_run_left*6, face_up))
    waitfieldeffect(FLDEFF_EXPLOSION)
    waitmovement
    delay(10)
    special(SpawnCameraObject)    
    applymovement(OBJ_EVENT_ID_CAMERA, walkup)
    waitmovement
    call(ShakeScreenNoSound)
    msgbox(format("{SPEAKER NAME_M}{CREATE MUGSHOT_M EMOTE_SERIOUS}(Looks like those two managed to escape.\p
    That's a relief.)"))
    closemessage
    call(ShakeScreenNoSound)
    applymovement(OBJ_EVENT_ID_CAMERA, walkdown)
    waitmovement
    special(RemoveCameraObject)
    delay(30)
    playse(SE_INTRO_BLAST)
    setvar(VAR_0x8004, 2) //vertical pan
    setvar(VAR_0x8005, 4) //horizontal pan
    setvar(VAR_0x8006, 6) //number of shakes
    setvar(VAR_0x8007, 4) //shake delay
    special (ShakeCamera)
    setfieldeffectargument (0, 29)
    setfieldeffectargument (1, 10)
    dofieldeffect(FLDEFF_EXPLOSION)
    waitfieldeffect(FLDEFF_EXPLOSION)
    playse(SE_PIN)
    applymovement(LOCALID_PLAYER, moves(emote_exclamation_mark, face_down))
    waitmovement
    delay(30)
    applymovement(LOCALID_PLAYER, moves(player_run_left, player_run_up*2, player_run_left*7, player_run_down*8))
    delay(20)
    call(ShakeScreenNoSound)
    delay(10)
    fadescreenswapbuffers(FADE_TO_WHITE)
    playse(SE_INTRO_BLAST)
    setvar(VAR_0x8004, 2) //vertical pan
    setvar(VAR_0x8005, 4) //horizontal pan
    setvar(VAR_0x8006, 6) //number of shakes
    setvar(VAR_0x8007, 4) //shake delay
    special (ShakeCamera)
    clearflag(FLAG_DONT_TRANSITION_MUSIC)
    fadenewbgm(MUS_DUMMY)
    setflag(FLAG_DONT_TRANSITION_MUSIC)
    setflag(FLAG_HIDE_MAP_NAME_POPUP)
    fwdtime(9, 35)
    warp(MAP_SUNSHINE_MEADOW, 11, 3)
    waitstate
}
