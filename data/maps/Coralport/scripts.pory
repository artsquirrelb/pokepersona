mapscripts Coralport_MapScripts {
    MAP_SCRIPT_ON_TRANSITION{
        setflag(FLAG_VISITED_CORALPORT)
        setflag(FLAG_TEMP_2)
        setflag(FLAG_TEMP_3)
        setflag(FLAG_TEMP_4)
        setrespawn(HEAL_LOCATION_CORALPORT)
        if(var(VAR_CHAPTER_1_STATE) == C1_WAITING_SHIP){
            setobjectxyperm(M_CORAL, 13, 10)
            setobjectmovementtype(M_CORAL, MOVEMENT_TYPE_FACE_DOWN)

            setobjectxyperm(S_CORAL, 11, 11)
            setobjectmovementtype(S_CORAL, MOVEMENT_TYPE_FACE_LEFT)

            setobjectxyperm(A_CORAL, 13, 13)
            setobjectmovementtype(A_CORAL, MOVEMENT_TYPE_FACE_DOWN)
            clearflag(FLAG_TEMP_2)
            setflag(FLAG_SPAWN_INVISIBLE)
        }
        if (var(VAR_QUEST_FIND_FISHERMANS_DAUGHTER_STATE) == 1){
            clearflag(FLAG_TEMP_3)
            setobjectxyperm(27, 15, 29)
            setobjectxyperm(28, 16, 29)
            setobjectmovementtype(27, MOVEMENT_TYPE_FACE_RIGHT)
            setobjectmovementtype(28, MOVEMENT_TYPE_FACE_RIGHT)
        }
        if (var(VAR_QUEST_FIND_FISHERMANS_DAUGHTER_STATE) == 2){
            clearflag(FLAG_TEMP_3)
            clearflag(FLAG_HIDE_HELP_BUTTON)
        }
        if (var(VAR_QUEST_FIND_FISHERMANS_DAUGHTER_STATE) == 3){ //lapras by the beach
            clearflag(FLAG_TEMP_3)
            clearflag(FLAG_TEMP_4)
            setobjectxyperm(28, 36, 27)
            setobjectxyperm(27, 36, 29)
            setobjectxyperm(26, 36, 28)
            setobjectmovementtype(28, MOVEMENT_TYPE_WALK_IN_PLACE_DOWN)
            setobjectmovementtype(27, MOVEMENT_TYPE_FACE_LEFT)
        }
        if (var(VAR_QUEST_FIND_FISHERMANS_DAUGHTER_STATE) == 4){ //lapras by the cave entrace
            clearflag(FLAG_TEMP_3)
            clearflag(FLAG_TEMP_4)
            setobjectxyperm(28, 36, 27) //growlithe stays on the beach
            setobjectxyperm(27, 42, 26)
            setobjectxyperm(26, 41, 26)
            setobjectmovementtype(28, MOVEMENT_TYPE_WALK_IN_PLACE_DOWN)
            setobjectmovementtype(27, MOVEMENT_TYPE_FACE_UP)
            setobjectmovementtype(26, MOVEMENT_TYPE_WALK_IN_PLACE_UP)
        }
        if(var(VAR_QUEST_FIND_FISHERMANS_DAUGHTER_STATE) == 6){
            clearflag(FLAG_TEMP_3)
            clearflag(FLAG_TEMP_2)
            setflag(FLAG_SPAWN_INVISIBLE)
            setflag(FLAG_HIDE_QUEST_ICON)

            setobjectxyperm(28, 20, 11)
            setobjectmovementtype(28, MOVEMENT_TYPE_WALK_IN_PLACE_DOWN)

            setobjectxyperm(27, 21, 11)
            setobjectmovementtype(27, MOVEMENT_TYPE_FACE_DOWN)

            setobjectxyperm(S_CORAL, 22, 13)
            setobjectxyperm(M_CORAL, 21, 13)
            setobjectxyperm(A_CORAL, 20, 13)
        }
    }
    MAP_SCRIPT_ON_FRAME_TABLE[
        //VAR_TEMP_1, 0: Coralport_MapScripts_UpdatingShipState
        VAR_CHAPTER_1_STATE, C1_WAITING_SHIP: Coralport_MapScripts_OnFrame3
        VAR_QUEST_FIND_FISHERMANS_DAUGHTER_STATE, 1: Coralport_MapScripts_QuestOldRodOnFrame1
        VAR_QUEST_FIND_FISHERMANS_DAUGHTER_STATE, 6: Coralport_MapScripts_QuestOldRodOnFrame6
    ]
    MAP_SCRIPT_ON_WARP_INTO_MAP_TABLE[
        VAR_QUEST_FIND_FISHERMANS_DAUGHTER_STATE, 3: Coralport_MapScripts_QuestOldRodOnWarp3
        VAR_QUEST_FIND_FISHERMANS_DAUGHTER_STATE, 4: Coralport_MapScripts_QuestOldRodOnWarp4
    ]
}
        
script Coralport_MapScripts_OnFrame3 {
    goto(Coralport_Eventscript_AkihikoQuestioningWhyMitsuruDoesntAskPoliceForHelp)
}

script Coralport_MapScripts_QuestOldRodOnFrame1 {
    goto(Coralport_Eventscript_QuestOldRodSeeingFishermanAndGrowlitheRunToTheEast)
}

script Coralport_MapScripts_QuestOldRodOnFrame6 {
    goto(Coralport_Eventscript_QuestOldRodFishermanReturningTheGrowlithe)
}

script Coralport_MapScripts_QuestOldRodOnWarp3 {
    getplayerxy(VAR_0x8000, VAR_0x8001)
    if (var(VAR_0x8000) == 36 && var(VAR_0x8001) == 28){
        applymovement(LOCALID_PLAYER, faceleft)
    }
}

script Coralport_MapScripts_QuestOldRodOnWarp4 {
    getplayerxy(VAR_0x8000, VAR_0x8001)
    if (var(VAR_0x8000) == 41 && var(VAR_0x8001) == 26){
        applymovement(LOCALID_PLAYER, faceup)
    }
}

script Coralport_Eventscript_DirectionSign {
    lock
    msgbox(format("Route 3 {LEFT_ARROW}\n
    Route 2 {RIGHT_ARROW}"))
    closemessage
    release
    end
}

script Coralport_Eventscript_TownSign {
    lock
    msgbox(format("{CLEAR_TO 45}Welcome to Coralport!\n
    {CLEAR_TO 30}“Home of the sea breeze.”"))
    closemessage
    release
    end
}

script Coralport_Eventscript_PoliceStationSign {
    lock
    msgbox(format("{CLEAR_TO 45}Coralport Police Station\n
    {CLEAR_TO 60}“For the people.”"))
    closemessage
    release
    end
}

script Coralport_Eventscript_CandyShopSign {
    lock
    msgbox(format("{CLEAR_TO 60}AmeAme Candy Shop\n
    {CLEAR_TO 24}“Welcome all the sweet lovers!”"))
    closemessage
    release
    end
}

script Coralport_Eventscript_Policeman {
    lock
    faceplayer
    setspeaker("Takagi")
    msgbox(format("Welcome to Coralport!\p
    If you have any problems, please let us know."))
    closemessage
    release
    end
}

script Coralport_Eventscript_OldMan {
    lock
    faceplayer
    setspeaker("Tomoki")
    msgbox("I and my darling have been travelling\n
    around the world.\p
    Coralport has this special charm that\n
    made us want to spend the rest of\l
    our days here.")
    closemessage
    release
    end
}

script Coralport_Eventscript_OldWoman {
    lock
    faceplayer
    setspeaker("Yukine")
    msgbox(format("This guy said to me one day,\n
    “Let's travel the world!”\p
    We were still so young and I agreed.\p
    Then we went to all kinds of\n
    beautiful places.\p
    It was only until last year that\n
    he proposed, on the beach here.\p
    That was quite a while ago now.\p
    I was also having so much fun\n
    being around him."))
    closemessage
    release
    end
}

script Coralport_Eventscript_SailorOnBreak {
    lock
    faceplayer
    setspeaker("Etsuo")
    msgbox(format("Please don't talk to me.\n
    I'm on my break.\p
    If you need help, talk to the sailors on the harbor."))
    closemessage
    applymovement(VAR_LAST_TALKED, facedown)
    waitmovement
    release
    end
}

script Coralport_Eventscript_Machamp {
    moncryscript(SPECIES_MACHAMP)
    release
    end
}

script Coralport_Eventscript_Vigoroth {
    moncryscript(SPECIES_VIGOROTH)
    release
    end
}

script Coralport_Eventscript_SailorOnHarbor {
    lock
    faceplayer
    setspeaker("Chikatake")
    msgbox(format("We have to move these barrels on board before departure.\p
    Please excuse us, we're very busy."))
    closemessage
    release
    end
}

script Coralport_Eventscript_SailorOnGuard {
    lock
    faceplayer
    setspeaker("Umito")
    subquestmenu(QUEST_MENU_CHECK_COMPLETE, STORY_QUESTS, MITSURUS_PROLOGUE)
    if(!var(VAR_RESULT)){
        showhelpwindow(HELP_PROTAGONISTS)
        waitbuttonpress
        hidehelpwindow
        release
        end
    }
    subquestmenu(QUEST_MENU_CHECK_COMPLETE, STORY_QUESTS, AKIHIKOS_PROLOGUE)
    if(!var(VAR_RESULT)){
        showhelpwindow(HELP_PROTAGONISTS)
        waitbuttonpress
        hidehelpwindow
        release
        end
    }
    if (var(VAR_CHAPTER_1_STATE) < C1_BOARDING_SHIP)
    {
        msgbox("We're extremely sorry but there's\n
        a problem with the shipment.\p
        The departure to Windward Port has\n
        been delayed until further notice.")
        closemessage
        if (var(VAR_CHAPTER_1_STATE) == C1_START){
            goto(Coralport_Eventscript_NotYetBoardingTime)
        }
    }
    else
    {
        showmoneybox(0, 0)
        msgbox(format("This ship is departing for Windward Port.\p
        The trip will estimatedly take {COLOR RED}3 hours{COLOR 2}.\p
        It costs {COLOR GREEN}¥100 {COLOR 2}per ticket. Would you like to board the ship?"), MSGBOX_YESNO)
        closemessage
        if (var(VAR_RESULT)){
            checkmoney(300)
            if (var(VAR_RESULT)){
                playse(SE_SHOP)
                removemoney(300)
                updatemoneybox
                setspeaker("Umito")
                msgbox(format("This way, please."))
                closemessage
                hidemoneybox
                applymove(10, walkinplaceup)
                delay(30)
                addtime(0, 3, 0)
                warp(MAP_WINDWARD_PORT_HARBOR, 23, 20)
                waitstate
            }
            else{
                setspeaker("Umito")
                msgbox(format("I'm sorry but you don't have enough money for 3 tickets."))
                hidemoneybox
                release
                end
            } 
        }
        else{
            hidemoneybox
        }
    }
    release
    end
}

script Coralport_Eventscript_ShipSchedule {
    msgbox(format("{CLEAR_TO 45}Coralport -> Windward Port\n
    {CLEAR_TO 30}Boarding time: 6AM - 5:59PM."))
    closemessage
    release
    end
}

script Coralport_Eventscript_NotYetBoardingTime {
    release
    addtime(0, 2, 0)
    lock
    hidefollower
    hidefollowernpc
    delay(20)
    clearflag(FLAG_TEMP_2)
    addobject(S_CORAL)
    addobject(A_CORAL)
    addobject(M_CORAL)
    applymovement(LOCALID_PLAYER, invisible)
    waitmovement
    applymovement(M_CORAL, facedown)
    applymovement(S_CORAL, moves(walk_right, walk_down, face_up))
    applymovement(A_CORAL, moves(walk_down, face_up))
    waitmovement
    delay(30)
    msgbox(format("{SPEAKER NAME_S}{CREATE MUGSHOT_S EMOTE_SERIOUS}What now?\p
    {SPEAKER NAME_M}{CREATE MUGSHOT_M EMOTE_SERIOUS}It appears that we have no other choice but to wait until boarding time.\p
    {SPEAKER NAME_A}{CREATE MUGSHOT_A EMOTE_NORMAL}Maybe we can pay the police station a visit?\p
    I'd like to check if they've found anything about Miki.\p
    {SPEAKER NAME_M}{CREATE MUGSHOT_M EMOTE_NORMAL}Yes, let's do that."))
    closemessage
    applymovement(M_CORAL, walkdown)
    delay(4)
    applymovement(S_CORAL, walkleft)
    applymovement(A_CORAL, facedown)
    applymovement(LOCALID_PLAYER, moves(walk_down, set_visible))
    waitmovement
    removeobject(M_CORAL)
    delay(1)
    removeobject(A_CORAL)
    delay(1)
    removeobject(S_CORAL)
    delay(1)
    setvar(VAR_CHAPTER_1_STATE, C1_TO_POLICE_STATION)
    clearflag(FLAG_TEMP_HIDE_FOLLOWER)
    updatefollowingmon
    release
    end
}

script Coralport_Eventscript_TuberM {
    lock
    faceplayer
    setspeaker("Ujou")
    msgbox(format("Sometimes when I poke the sand castles, they move!"))
    closemessage
    release
    end
}

script Coralport_Eventscript_Sandygast {
    lock
    playmoncry(SPECIES_SANDYGAST, CRY_MODE_NORMAL)
    setwildbattle(SPECIES_SANDYGAST, 11)
    dowildbattle
    fadescreenswapbuffers(FADE_TO_BLACK)
    setmetatile(11, 29, 0x246, FALSE)
    special(DrawWholeMapView)
    fadescreenswapbuffers(FADE_FROM_BLACK)
    release
    end
}

script Coralport_Eventscript_Beauty {
    lock
    faceplayer
    setspeaker("Harehina")
    msgbox(format("I bought two tickets for this vacation, but my damned ex cheated on me so I brought my younger brother instead.\p
    I'm enjoying this a lot more than I thought.\p
    {SPEAKER NAME_S}{CREATE MUGSHOT_S EMOTE_SERIOUS}Good for you."))
    setspeaker("Harehina")
    msgbox(format("{DESTROY_MUGSHOT}Oh? Wait, you're just my type.\n
    How old are you?\p
    {SPEAKER NAME_S}{CREATE MUGSHOT_S EMOTE_SURPRISED}W-what?\p
    {SPEAKER NAME_S}{CREATE MUGSHOT_S EMOTE_SERIOUS}(Hey! Let's just bail!)"))
    closemessage
    hidefollowernpc
    moveobjecttocoords(LOCALID_PLAYER, 22, 29, DIR_NORTH, 2)
    waitmovement
    release
    end
}

script Coralport_Eventscript_Fisherman {
    lock
    faceplayer
    setspeaker("Jumaro")
    msgbox(format("I'm waiting for a friend of mine.\p
    We promised to go fishing together but he's taking so long.\p
    I wonder what's wrong."))
    closemessage
    release
    end
}

script Coralport_Eventscript_Krabby {
    lockall
    startoverworldencounter(12)
    setflag(FLAG_TEMP_1)
    removeobject(VAR_LAST_TALKED)
    releaseall
    end
}

script Coralport_Eventscript_MustTalkToSailorOnGuard {
    lock
    applymovement(LOCALID_PLAYER, walkdown)
    waitmovement
    checkplayergender
    if(var(VAR_RESULT) == MALE){
        msgbox(format("{SPEAKER NAME_A}{CREATE MUGSHOT_A EMOTE_SERIOUS}(It's not the time to wander around.\p
        We need to board the ship to Windward Port.)"))
    }
    else{
        msgbox(format("{SPEAKER NAME_M}{CREATE MUGSHOT_M EMOTE_SERIOUS}(It's not the time to wander around.\p
        We need to board the ship to Windward Port.)"))
    }
    closemessage
    release
    end
}

script setameamequestasreward {
    lock
    msgbox("set ameame quest state reward")
    closemessage
    setvar(VAR_AMEAME_CANDY_SHOP_QUEST_STATE, 10)
    questmenu(QUEST_MENU_SET_REWARD, QUEST_AMEAME_PARTTIME)
    release
    end
}

script Coralport_Eventscript_AkihikoQuestioningWhyMitsuruDoesntAskPoliceForHelp {
    delay(20)
    applymovement(LOCALID_PLAYER, moves(walk_down*6))//13,12
    waitmovement
    msgbox(format("{SPEAKER NAME_A}{CREATE MUGSHOT_A EMOTE_SAD}......"))
    closemessage
    applymovement(A_CORAL, walkup) //13,12
    applymovement(LOCALID_PLAYER, walkup)//13,11
    waitmovement
    delay(20)
    msgbox(format("{SPEAKER NAME_A}{CREATE MUGSHOT_A EMOTE_SERIOUS}Kirijo.\p
    {SPEAKER NAME_M}{CREATE MUGSHOT_M EMOTE_NORMAL}Yes, Sanada?"))
    applymovement(S_CORAL, faceright)
    msgbox(format("{SPEAKER NAME_A}{CREATE MUGSHOT_A EMOTE_SERIOUS}If you know who's behind the explosion, why didn't you say anything to the police?\p
    {SPEAKER NAME_M}{CREATE MUGSHOT_M EMOTE_SERIOUS}I'd prefer not to get them involved.\p
    {SPEAKER NAME_A}{CREATE MUGSHOT_A EMOTE_SERIOUS}What?\p
    {SPEAKER NAME_M}{CREATE MUGSHOT_M EMOTE_SERIOUS}Besides, I doubt they'd be any help, even if they knew.\p
    Otherwise I wouldn't be here, asking you two to accompany me on this journey.\p
    {SPEAKER NAME_A}{CREATE MUGSHOT_A EMOTE_SERIOUS}But- why?\p
    {SPEAKER NAME_S}{CREATE MUGSHOT_S EMOTE_SERIOUS}Corruption or something like that?"))
    closemessage
    applymovement(M_CORAL, faceleft)
    waitmovement
    msgbox(format("{SPEAKER NAME_M}{CREATE MUGSHOT_M EMOTE_SMILE}Exactly."))
    delay(4)
    applymovement(M_CORAL, facedown)
    delay(30)
    msgbox(format("{SPEAKER NAME_M}{CREATE MUGSHOT_M EMOTE_SERIOUS}The illegal experiments have already
    proceeded to the state of producing successful prototypes.\p
    Why do you think they haven't already been caught nor stopped?\p
    {SPEAKER NAME_A}{CREATE MUGSHOT_A EMOTE_SERIOUS}......"))
    closemessage
    delay(30)
    msgbox(format("{SPEAKER NAME_A}{CREATE MUGSHOT_A EMOTE_PENSIVE}... But Kurosawa-san has always been really kind and willingly helped us."))
    delay(20)
    msgbox(format("{SPEAKER NAME_S}{CREATE MUGSHOT_S EMOTE_NORMAL}Not all cops are the same.\p
    I guess he also has a battle he's fighting on his own."))
    applymovement(A_CORAL, moves(emote_pensive))
    msgbox(format("{SPEAKER NAME_A}{CREATE MUGSHOT_A EMOTE_PENSIVE}......"))
    closemessage
    delay(30)
    playse(SE_RG_SS_ANNE_HORN)
    delay(30)
    playse(SE_PIN)
    applymovement(M_CORAL, emoteexclamation)
    delay(4)
    applymovement(A_CORAL, moves(emote_exclamation_mark, face_down))
    delay(4)
    applymovement(S_CORAL, moves(emote_exclamation_mark, face_down))
    delay(60)
    applymovement(A_CORAL, faceup)
    delay(4)
    applymovement(S_CORAL, faceright)
    waitmovement
    delay (30)
    msgbox(format("{SPEAKER NAME_M}{CREATE MUGSHOT_M EMOTE_NORMAL}I suppose the ship is ready for boarding.\l
    Let's move out."))
    closemessage
    applymovement(M_CORAL, walkdown)
    applymovement(S_CORAL, moves(walk_right*2))
    applymovement(A_CORAL, walkup)
    waitmovement(S_CORAL)
    waitmovement
    applymovement(LOCALID_PLAYER, moves(face_down, set_visible))
    removeobject(M_CORAL)
    delay(1)
    removeobject(S_CORAL)
    delay(1)
    removeobject(A_CORAL)
    delay(1)
    setflag(FLAG_TEMP_2)
    setvar(VAR_CHAPTER_1_STATE, C1_BOARDING_SHIP)
    addminutes(10)
    release
    end
}

script Coralport_Eventscript_QuestOldRodSeeingFishermanAndGrowlitheRunToTheEast {
    lock
    delay(20)
    special(SpawnCameraObject)
    applymovement(LOCALID_CAMERA, moves(walk_diag_southeast*2))
    waitmovement
    playmoncry(SPECIES_GROWLITHE, CRY_MODE_NORMAL)
    applymovement(28, jumpinplaceleft)
    waitmovement
    applymovement(28, moves(walk_fast_right*8))
    delay(4)
    playse(SE_PIN)
    applymovement(27, moves(emote_exclamation_mark, delay_16, delay_16, walk_fast_right*8))
    delay(60)
    applymovement(LOCALID_CAMERA, moves(walk_diag_northwest*2))
    waitmovement
    special(RemoveCameraObject)
    delay(30)
    setvar(VAR_QUEST_FIND_FISHERMANS_DAUGHTER_STATE, 2)
    clearflag(FLAG_HIDE_MAP_NAME_POPUP)
    clearflag(FLAG_HIDE_QUEST_ICON)
    refreshquesticon
    warpsilent(MAP_CORALPORT, 13, 27)
    waitstate
}

script Coralport_Eventscript_Fisherman_QUEST_FIND_FISHERMANS_DAUGHTER {
    lock
    setflag(FLAG_HIDE_QUEST_ICON)
    setspeaker("Ikumi")
    if (var(VAR_QUEST_FIND_FISHERMANS_DAUGHTER_STATE) == 2){
        msgbox(format("{COLOR RED}Continuing this quest will fast forward the clock at least 4 hours.\p
        Do you want to proceed?"), MSGBOX_YESNO)
        if (var(VAR_RESULT)){
            goto(Coralport_Eventscript_Fisherman_QUEST_FIND_FISHERMANS_DAUGHTER_Start)
        }
    }
    if (var(VAR_QUEST_FIND_FISHERMANS_DAUGHTER_STATE) == 3){
        faceplayer
        msgbox(format("You're back! Let's hop on my Lapras to go back to Coralport Cave entrance!"))
        closemessage
    }
    if (var(VAR_QUEST_FIND_FISHERMANS_DAUGHTER_STATE) == 4){
        faceplayer
        msgbox(format("You need to stock up on healing items? Feel free to hop on my Lapras to go back to the main land.\p
        Please come back soon, though.\l
        My daughter is still in there."))
    }
    clearflag(FLAG_HIDE_QUEST_ICON)
    refreshquesticon
    release
    end
}

script Coralport_Eventscript_Fisherman_QUEST_FIND_FISHERMANS_DAUGHTER_Start {
    applymovement(27, walkinplaceright)
    msgbox(format("How is it now, Growlithe?"))
    closemessage
    playmoncry(SPECIES_GROWLITHE, CRY_MODE_NORMAL)
    applymovement(28, jumpinplaceleft)
    waitmovement
    delay(30)
    applymovement(28, faceright)
    special(SpawnCameraObject)
    applymovement(LOCALID_CAMERA, moves(walk_diag_northeast*5))
    waitmovement
    delay(40)
    applymovement(LOCALID_CAMERA, moves(walk_diag_southwest*5))
    waitmovement
    delay(20)
    setspeaker("Ikumi")
    msgbox(format("You mean Misa is in that cave over there?"))
    playmoncry(SPECIES_GROWLITHE, CRY_MODE_NORMAL)
    applymovement(28, jumpinplaceleft)
    waitmovement
    setspeaker("Ikumi")
    msgbox(format("Alright!"))
    closemessage
    applymovement(27, walkright)
    applymovement(28, moves(face_down, lock_facing_direction, walk_up, unlock_facing_direction))
    waitmovement
    delay(20)
    clearflag(FLAG_TEMP_4)
    playse(SE_BALL_OPEN)
    addobject(26)
    applymovement(26, moves(set_invisible, exit_pokeball, set_visible, face_left))
    waitmovement
    playmoncry(SPECIES_LAPRAS, CRY_MODE_NORMAL)
    waitmoncry
    lock
    setobjectnewmovementtype(26, MOVEMENT_TYPE_WALK_IN_PLACE_LEFT)
    release
    delay(30)
    applymovement(27, moves(walk_left, face_player))
    waitmovement
    setspeaker("Ikumi")
    msgbox(format("Lapras can carry all of us. Let us proceed then!"))
    closemessage
    applymovement(28, shakehorizontal)
    waitmovement
    playmoncry(SPECIES_GROWLITHE, CRY_MODE_WEAK)
    waitmoncry
    applymovement(27, faceright)
    waitmovement
    setspeaker("Ikumi")
    msgbox(format("What's wrong, Growlithe?\p
    Oh, right. You're scared of water, aren't you?\p
    Well... I don't have your Pokéball and returning to your owner to get it would be time-consuming...\p
    Can you wait for us here?"))
    closemessage
    playmoncry(SPECIES_GROWLITHE, CRY_MODE_NORMAL)
    applymovement(28, walkinplacedown)
    waitmovement
    setspeaker("Ikumi")
    msgbox(format("Thank you, you're such a good boy."))
    applymovement(27, moves(face_player))
    msgbox(format("Let's go, my daughter is waiting!"))
    closemessage
    setvar(VAR_QUEST_FIND_FISHERMANS_DAUGHTER_STATE, 4) //3 = lapras by the beach, 4 = lapras by the cave entrance
    clearflag(FLAG_HIDE_QUEST_ICON)
    warp(MAP_CORALPORT_CAVE, 0)
    waitstate
}

movement jumpinplacedown {
    jump_in_place_down
}

script Coralport_Eventscript_Growlithe {
    lock
    if (var(VAR_QUEST_FIND_FISHERMANS_DAUGHTER_STATE) == 2){
        setflag(FLAG_HIDE_QUEST_ICON)
        msgbox(format("{COLOR RED}Continuing this quest will fast forward the clock at least 4 hours.\p
        Do you want to proceed?"), MSGBOX_YESNO)
        if (var(VAR_RESULT)){
            applymovement(LOCALID_PLAYER, moves(walk_left, face_up))
            waitmovement
            goto(Coralport_Eventscript_Fisherman_QUEST_FIND_FISHERMANS_DAUGHTER_Start)
        }
        else{
            closemessage
            clearflag(FLAG_HIDE_QUEST_ICON)
            release
            refreshquesticon
            end
        }
    }
    else{
        playmoncry(SPECIES_GROWLITHE, CRY_MODE_NORMAL)
        waitmoncry
        msgbox(format("Growlithe looks worried. It seems like it won't go back to its owner until you rescue Misa.\p
        What a good boy!"))
        closemessage
    }
    release
    end
}

script Coralport_Eventscript_Lapras {
    if(var(VAR_QUEST_FIND_FISHERMANS_DAUGHTER_STATE) == 3){ //by the beach
        msgbox(format("Hop on Lapras and go to Coralport Cave entrance?"), MSGBOX_YESNO)
        if(var(VAR_RESULT)){
            playmoncry(SPECIES_LAPRAS, CRY_MODE_NORMAL)
            waitmoncry
            applymovement(26, walkright)
            waitmovement
            delay(4)
            applymovement(LOCALID_PLAYER, walkright)
            waitmovement
            setvar(VAR_QUEST_FIND_FISHERMANS_DAUGHTER_STATE, 4)
            warpsilent(MAP_CORALPORT, 41, 25)
            waitstate
        }
    }
    if(var(VAR_QUEST_FIND_FISHERMANS_DAUGHTER_STATE) >= 4){ //by the cave entrance
        msgbox(format("Hop on Lapras and go back to the main land?"), MSGBOX_YESNO)
        if(var(VAR_RESULT)){
            playmoncry(SPECIES_LAPRAS, CRY_MODE_NORMAL)
            waitmoncry
            applymovement(26, walkdown)
            waitmovement
            delay(4)
            applymovement(LOCALID_PLAYER, walkdown)
            waitmovement
            setvar(VAR_QUEST_FIND_FISHERMANS_DAUGHTER_STATE, 3)
            warpsilent(MAP_CORALPORT, 36, 28)
            waitstate
        }
    }
    end
}

script Coralport_Eventscript_ConfirmEnterCave {
    lock
    msgbox(format("{COLOR GREEN}Once you enter this cave, leaving it will fast forward the clock by {COLOR RED}4 hours{COLOR GREEN}.\p
    Do you want to proceed?"), MSGBOX_YESNO)
    if (!var(VAR_RESULT)){
        applymovement(LOCALID_PLAYER, walkright)
        waitmovement
        release
        end
    }
    else{
        release
        end
    }
}

script Coralport_Eventscript_CaveSign {
    lock
    msgbox(format("{CLEAR_TO 60}Coralport Cave\n
    {CLEAR_TO 45}{COLOR GREEN}Time forwarding: {COLOR RED}4 hours{COLOR GREEN}."))
    closemessage
    release
    end
}

script Coralport_Eventscript_QuestOldRodFishermanReturningTheGrowlithe {
    applymovement(LOCALID_PLAYER, moves(walk_slow_down*4))
    waitmovement
    delay(30)
    applymovement(27, walkinplacedown)
    waitmovement
    setspeaker("Ikumi")
    msgbox(format("Thank you so much for saving my family back there.\p
    {SPEAKER NAME_M}{CREATE MUGSHOT_M EMOTE_SMILE}You're welcome, Ikumi-san.\n
    We're glad that Misa was found safely."))
    setspeaker("Ikumi")
    msgbox(format("{DESTROY_MUGSHOT}Indeed, indeed."))
    closemessage
    applymovement(27, faceleft)
    applymovement(28, faceright)
    setspeaker("Ikumi")
    msgbox(format("Thanks to you too, Growlithe! Without you, I wouldn't even know where she went to."))
    closemessage
    playmoncry(SPECIES_GROWLITHE, CRY_MODE_NORMAL)
    applymovement(28, moves(emote_music, walk_in_place_right))
    waitmovement
    applymovement(28, facedown)
    lock
    setobjectnewmovementtype(28, MOVEMENT_TYPE_WALK_IN_PLACE_DOWN)
    release
    applymovement(27, facedown)
    waitmovement
    setspeaker("Ikumi")
    msgbox(format("Now I have to return the Growlithe to the policeman.\p
    Please make sure to come by my house later. I want to give you all something.\p
    Misa and Wooper would also be really happy to see you.\p
    {SPEAKER NAME_A}{CREATE MUGSHOT_A EMOTE_SMILE}We will!"))
    closemessage
    applymovement(28, walkinplacedown)
    waitmovement
    delay(30)
    applymovement(27, moves(walk_in_place_left, delay_16, delay_16, walk_up, set_invisible))
    applymovement(28, moves(walk_in_place_right, delay_16, delay_16, walk_right, walk_up, set_invisible))
    waitmovement(27)
    playse(SE_EXIT)
    waitmovement(28)
    playse(SE_EXIT)
    removeobject(27)
    delay(1)
    removeobject(28)
    delay(30)
    applymovement(LOCALID_PLAYER, moves(walk_down, set_visible))
    applymovement(S_CORAL, moves(walk_left, set_invisible))
    applymovement(A_CORAL, moves(walk_right, face_down, set_invisible))
    applymovement(M_CORAL, moves(face_down, set_invisible))
    waitmovement(LOCALID_PLAYER)
    removeobject(S_CORAL)
    delay(1)
    removeobject(A_CORAL)
    delay(1)
    removeobject(M_CORAL)
    delay(1)
    setvar(VAR_QUEST_FIND_FISHERMANS_DAUGHTER_STATE, 7)
    questmenu(QUEST_MENU_SET_REWARD, QUEST_FIND_FISHERMANS_DAUGHTER)
    clearflag(FLAG_DONT_TRANSITION_MUSIC)
    clearflag(FLAG_HIDE_QUEST_ICON)
    fadedefaultbgm
    delay(30)
    setseenmon(SPECIES_GROWLITHE)
    setseenmon(SPECIES_WOOPER_PALDEA)
    setseenmon(SPECIES_CLODSIRE)
    special(ShowMapNamePopup)
    release
    end
}

script Coralport_Eventscript_Maniac {
    lock
    faceplayer
    setspeaker("Chuuei")
    msgbox(format("Whenever I'm here I'm reminded of a proposal scene of an old couple.\p
    I witnessed it, you know. It was so cute.\p
    Man... I want a love like that too...\p
    Boy or girl, I don't really mind, as long as they're breathing.\p
    Ah... I'm so lonely."))
    doemote(VAR_LAST_TALKED, E_SAD)
    release
    end
}

script Coralport_Eventscript_Magnemite {
    moncryscript(SPECIES_MAGNEMITE)
    turnobject(VAR_LAST_TALKED, DIR_EAST)
    delay(30)
    faceplayer
    msgbox(format("Magnemite seems to be worried about its trainer's love life."))
    release
    end
}

script Coralport_Eventscript_CoupleF {
    lock
    setspeaker("Miku")
    stepinplace(MIKU_CORAL, DIR_EAST)
    msgbox(format("How's the work going, sweetie?"))
    setspeaker("Emi")
    stepinplace(EMI_CORAL, DIR_WEST)
    msgbox(format("The Riolus are still young so it's hard to teach them new moves.\p
    The owner is a kind old lady, though."))
    setspeaker("Miku")
    stepinplace(MIKU_CORAL, DIR_EAST)
    msgbox(format("I'm glad it's going well, Emi."))
    doemote(MIKU_CORAL, 0)
    setspeaker("Miku")
    msgbox(format("Wait, you got bruises on your arms!"))
    setspeaker("Emi")
    stepinplace(EMI_CORAL, DIR_WEST)
    msgbox(format("Oh, don't worry. They were just from the baby Riolus-"))
    setspeaker("Miku")
    doemote(MIKU_CORAL, E_ANGRY)
    msgbox(format("Shut up and let me treat them."))
    setspeaker("Emi")
    doemote(EMI_CORAL, E_SWEAT)
    msgbox(format("Y-yes, ma'am."))
    release
    end
}