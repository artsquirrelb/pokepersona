mapscripts Route2_2_MapScripts {
    MAP_SCRIPT_ON_TRANSITION{
        if (flag(TRAINER_FLAGS_START + TRAINER_WAKAMU)){
            setobjectmovementtype(WAKAMU, MOVEMENT_TYPE_FACE_LEFT)
        }
    }
}

script Route2_2_Eventscript_BerrySign {
    lock
    msgbox(format("If you pick the berries, please plant back some!\p
    Let's spread the greens!\n
    {CLEAR_TO 150}-BRG"))
    closemessage
    release
    end
}

script Route2_2_Eventscript_DirectionSign {
    lock
    msgbox(format("Coralport {LEFT_ARROW}\n
    Sunshine Town {RIGHT_ARROW}"))
    closemessage
    release
    end
}

script Route2_2_Eventscript_Pansage {
    lockall
    startoverworldencounter(12)
    setflag(FLAG_TEMP_1)
    removeobject(VAR_LAST_TALKED)
    releaseall
    end
}

script Route2_2_Eventscript_Man {
    lock
    faceplayer
    setspeaker("Fusahiro")
    msgbox(format("If you're wondering, no. I'm not BGR.\p
    I'm simply here to enjoy the peaceful evening.\p
    Who might this BRG character be, though?\p
    They seem to be taking care of this little garden."))
    release
    end
}

script Route2_2_Eventscript_Maniac {
    setspeaker("Wakamu")
    if (!flag(TRAINER_FLAGS_START + TRAINER_WAKAMU))
    {
        if (flag(FLAG_TEMP_1))
        {
            msgbox(Route2_Wakamu_Intro)
            msgbox(format("{CREATE MUGSHOT_S EMOTE_SERIOUS}... Damned scalper."))
            closemessage
            faceplayer
            doemote(VAR_LAST_TALKED, 0)
            trainerbattle_no_intro(TRAINER_WAKAMU, Route2_Wakamu_Defeated)
            msgbox(Route2_Wakamu_Outro)
        }
        else{
            msgbox(Route2_Wakamu_Intro)
        }
    }
    else
    {
        doemote(WAKAMU, E_ANGRY)
        msgbox(Route2_Wakamu_Outro)
    }
    release
    end
}

text Route2_Wakamu_Intro{
    format("Hehehe, I lined up early and bought all the Pokémon TCG packs in store.\p
    These are gonna sell so well.")
}

text Route2_Wakamu_Defeated{
    format("Hey! Don't touch my cards!\nNooooo!")
}

text Route2_Wakamu_Outro{
    format("What do you mean scalper?\n
    I'm a legit fan! Give back my cards!!")
}

script Route2_2_Eventscript_NinjaBoy {
    turnobject(BENKICHI, DIR_SOUTH)
    turnobject(ICHIME, DIR_SOUTH)
    setspeaker("Benkichi")
    if(!flag(TRAINER_FLAGS_START + TRAINER_WAKAMU))
    {
        setflag(FLAG_TEMP_1)
        stepinplace(BENKICHI, DIR_SOUTH)
        msgbox(Route2_Benkichi_Intro)
        setspeaker("Ichime")
        stepinplace(ICHIME, DIR_SOUTH)
        msgbox(Route2_Ichime_Intro)
        release
        end
    }
    elif (flag(FLAG_TEMP_1) && flag(TRAINER_FLAGS_START + TRAINER_WAKAMU) && !flag(TRAINER_FLAGS_START + TRAINER_BENKICHI))
    {
        stepinplace(BENKICHI, DIR_SOUTH)
        msgbox(format("Woah! You defeated that guy?"))
        stepinplace(ICHIME, DIR_SOUTH)
        setspeaker("Ichime")
        msgbox(format("Please battle us too!"))
        trainerbattle_two_trainers(TRAINER_BENKICHI, Route2_Benkichi_Defeated, TRAINER_ICHIME, Route2_Ichime_Defeated)
        stepinplace(BENKICHI, DIR_SOUTH)
        setspeaker("Benkichi")
        msgbox(Route2_Benkichi_Outro)
        stepinplace(ICHIME, DIR_SOUTH)
        setspeaker("Ichime")
        msgbox(Route2_Ichime_Outro)
    }
    else{
        stepinplace(BENKICHI, DIR_SOUTH)
        msgbox(Route2_Benkichi_Outro)
        stepinplace(ICHIME, DIR_SOUTH)
        setspeaker("Ichime")
        msgbox(Route2_Ichime_Outro)
    }   
    release
    end
}

text Route2_Benkichi_Intro{
    format("That man over there bought all of the cards in the store...")
}

text Route2_Benkichi_Defeated{
    format("Woah! Those are Pokémon cards!")
}

text Route2_Benkichi_Outro{
    format("Thank you for the cards!")
}

text Route2_Ichime_Intro{
    format("We're so sad...")
}

text Route2_Ichime_Defeated {
    format("You're giving them to us?")
}

text Route2_Ichime_Outro {
    format("Thank you!!\n
        The cards are so cute!")
}