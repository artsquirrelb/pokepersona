mapscripts ThundershardsCliff_2F_Exterior_MapScripts {
    MAP_SCRIPT_ON_WARP_INTO_MAP_TABLE[
    ]
    MAP_SCRIPT_ON_TRANSITION{
        if (var(VAR_MITSURUS_INTRO_STATE) >= M_VS_NORBERT ){
            setflag(FLAG_TEMP_1)
        }
    }
}


script ThundershardsCave_2F_Eventscript_Warning {
    lock
    msgbox(format("{COLOR RED}Important scene ahead. Do you want to proceed?"), MSGBOX_YESNO)
    if (var(VAR_RESULT) == FALSE){
        closemessage
        applymovement(LOCALID_PLAYER, walkleft)
        waitmovement
    }
    release
    end
}


script ThundershardsCave_2F_Eventscript_DailyChansey_Heal {
    lock
    setflag(FLAG_HIDE_HELP_BUTTON)
    faceplayer
    msgbox(format("Ask Chansey to heal your party?"), MSGBOX_YESNO)
    closemessage
    if (var(VAR_RESULT) == TRUE){
        dotimebasedevents
        playse(MUS_HEAL)
        waitse
        special(HealPlayerParty)
        playmoncry(SPECIES_CHANSEY, CRY_MODE_NORMAL)
        waitmovement
        release
        specialvar(VAR_RESULT, GetPlayerFacingDirection)
        if (var(VAR_RESULT) == DIR_NORTH){
            applymovement(9, walkinplacedown)
        }
        else{
            applymovement(9, walkinplaceleft)
        }
        waitmovement
        lock
        fadescreenswapbuffers(FADE_TO_BLACK)
        removeobject(9)
        playse(SE_FLEE)
        waitse
        setflag(FLAG_DAILY_CHANSEY_HEAL_THUNDERSHARD_CAVE_2F_EXTERIOR)
        fadescreenswapbuffers(FADE_FROM_BLACK)
        msgbox(format("Chansey ran off somewhere. It may be back tomorrow."))
        closemessage
    }
    release
    clearflag(FLAG_HIDE_HELP_BUTTON)
    end
}
script ThundershardsCliff_2F_Exterior_Eventscript_MitsuruApproaching_CaveEntrance {
    lock
    applymovement(LOCALID_PLAYER, moves(walk_down, face_right))
    waitmovement
    goto(ThundershardsCliff_2F_Exterior_Eventscript_ScientistExitsCave)
}

script ThundershardsCliff_2F_Exterior_Eventscript_ScientistExitsCave {
    lock
    fadenewbgm(MUS_P3R_FEARFUL_EXPERIENCE)
    delay(20)
    clearflag(FLAG_TEMP_1)
    addobject(8)
    playse(SE_EXIT)
    waitse
    playse(SE_PIN)
    applymovement(LOCALID_PLAYER, moves(emote_exclamation_mark, face_right))
    delay(4)
    playse(SE_PIN)
    applymovement(8, emoteexclamation)
    waitmovement
    delay(20)
    applymovement(8, walkinplaceleft)
    waitmovement
    setspeaker("Scientist")
    bufferleadmonspeciesname(STR_VAR_1)
    msgbox(format("Tch. I told those two to not let anyone through.\p
    And here you are, Young Lady.\p
    {SPEAKER NAME_M}{CREATE MUGSHOT_M EMOTE_SERIOUS}They were no match for me.\p
    And you're not going anywhere either.\p
    Let's go, {STR_VAR_1}."))
    closemessage
    playbgm(MUS_P5_MENTAL_BREAKDOWN, FALSE)
    setflag(FLAG_DONT_TRANSITION_MUSIC)
    trainerbattle_no_intro(TRAINER_NORBERT_THUNDERSHARDS_EXTERIOR, KGScientist_Defeated)
    goto(ThundershardsCliff_2F_Exterior_Eventscript_ScientistRetreat)
}

text KGScientist_Defeated {
    format("You really live up to your name, Young Lady.")
}

script ThundershardsCliff_2F_Exterior_Eventscript_ScientistRetreat {
    lock
    setspeaker("Scientist Norbert")
    msgbox(format("That was pretty informative.\p
    Looks like your synergy level has increased again, Young Lady.\p
    As expected of our premium prototype.\p
    {SPEAKER NAME_M}{CREATE MUGSHOT_M EMOTE_MAD}Norbert, you...!"))
    setspeaker("Scientist Norbert")
    msgbox(format("{DESTROY_MUGSHOT}As the prize for winning, I'll spare you a bit of information.\p
    You aren't the only premium prototype anymore.\p
    {SPEAKER NAME_M}{CREATE MUGSHOT_M EMOTE_SURPRISED}...!"))
    setspeaker("Scientist Norbert")
    msgbox(format("{DESTROY_MUGSHOT}Well then, as much as I enjoyed our little chat, there's somewhere else I need to be.\p
    I hope we don't meet again, for your own good, little miss.\p
    Oh, and be careful of the fire. Hehehehehe.\p
    {SPEAKER NAME_M}{CREATE MUGSHOT_M EMOTE_MAD}What- WAIT-!"))
    closemessage
    applymovement(LOCALID_PLAYER, moves(player_run_right*2, face_left))
    dofieldeffectnpcflyout(24, 25, 1)
    applymovement(8, invisible)
    waitfieldeffect(FLDEFF_NPCFLY_OUT)
    waitmovement(LOCALID_PLAYER)
    removeobject(8)
    setflag(FLAG_TEMP_1)
    msgbox(format("{SPEAKER NAME_M}{CREATE MUGSHOT_M EMOTE_MAD}Damn it!!"))
    closemessage
    delay(8)
    msgbox(format("{SPEAKER NAME_M}{CREATE MUGSHOT_M EMOTE_SERIOUS}Wait... He said “fire”?"))
    closemessage
    clearflag(FLAG_DONT_TRANSITION_MUSIC)
    setvar(VAR_MITSURUS_INTRO_STATE, M_EXPLOSION)
    goto(ThundershardsCliff_2F_Exterior_Eventscript_LabExploding)
    release
}

script ThundershardsCliff_2F_Exterior_Eventscript_LabExploding {
    lock
    special(StopMapMusic)
    playse(SE_INTRO_BLAST)
    setvar(VAR_0x8004, 2) //vertical pan
    setvar(VAR_0x8005, 4) //horizontal pan
    setvar(VAR_0x8006, 6) //number of shakes
    setvar(VAR_0x8007, 4) //shake delay
    special (ShakeCamera)
    setfieldeffectargument (0, 25)
    setfieldeffectargument (1, 25)
    dofieldeffect(FLDEFF_EXPLOSION)
    waitfieldeffect(FLDEFF_EXPLOSION)
    playse(SE_PIN)
    applymovement(LOCALID_PLAYER, moves(emote_exclamation_mark, face_right, lock_facing_direction, jump_left, unlock_facing_direction))
    waitmovement
    playbgm(MUS_P3_CALAMITY, FALSE)
    setflag(FLAG_DONT_TRANSITION_MUSIC)
    msgbox(format("{SPEAKER NAME_M}{CREATE MUGSHOT_M EMOTE_SURPRISED}What the-"))
    closemessage
    applymovement(LOCALID_PLAYER, moves(player_run_left*5, player_run_down, player_run_left,
    player_run_down, player_run_left*7, player_run_down*3, player_run_left, player_run_down*3))
    playse(SE_INTRO_BLAST)
    setvar(VAR_0x8004, 2) //vertical pan
    setvar(VAR_0x8005, 4) //horizontal pan
    setvar(VAR_0x8006, 6) //number of shakes
    setvar(VAR_0x8007, 4) //shake delay
    special (ShakeCamera)
    setfieldeffectargument (0, 18)
    setfieldeffectargument (1, 23)
    dofieldeffect(FLDEFF_EXPLOSION)
    waitfieldeffect(FLDEFF_EXPLOSION)
    delay(20)
    playse(SE_INTRO_BLAST)
    setvar(VAR_0x8004, 2) //vertical pan
    setvar(VAR_0x8005, 4) //horizontal pan
    setvar(VAR_0x8006, 6) //number of shakes
    setvar(VAR_0x8007, 4) //shake delay
    special (ShakeCamera)
    setfieldeffectargument (0, 8)
    setfieldeffectargument (1, 28)
    dofieldeffect(FLDEFF_EXPLOSION)
    waitfieldeffect(FLDEFF_EXPLOSION)
    applymovement(9, moves(jump_right, set_invisible))
    waitmovement
    fadescreenswapbuffers(FADE_TO_WHITE)
    warp(MAP_THUNDERSHARDS_CAVE_2F, 2)
    release
}