
script EventScript_OverworldHelpButton
{
    if (!flag(FLAG_PARTTIME_GET) && !flag(FLAG_HELP_WINDOW_GET))
    {
        goto(EventScript_OverworldStoryTalk)
    }
    else{
        playse(SE_WIN_OPEN)
        waitse
        msgbox(format("What would you like to get help with?"))
        dynmultipush(StoryHints, 0)
        dynmultipush(TrainersTips, 1)
        if (flag(FLAG_PARTTIME_GET))
        {
            dynmultipush(Work, 2)
        }
        dynmultistack(0, 0, FALSE, 6, FALSE, 0, NULL)
        switch(var(VAR_RESULT)){
            case 0: goto(EventScript_OverworldStoryTalk)
            case 1: goto(EventScript_OverworldTrainerTips)
            case 2: goto(EventScript_OverworldTryStartWorkParttime)
        }
    }
    closemessage
    release
    end
}

text StoryHints{"Story Hints"}
text TrainersTips {"Trainer's Tips"}
text Work {"Work Part-time"}

script EventScript_OverworldStoryTalk {
    checkplayergender
    if (var(VAR_RESULT) == MALE && var(VAR_MITSURUS_INTRO_STATE) == 50){
        goto (EventScript_OverworldStoryTalk_AkihikosPrologue)
    }
    elif (var(VAR_RESULT) == FEMALE && var(VAR_AKIHIKOS_INTRO_STATE) == 50){
        goto (EventScript_OverworldStoryTalk_MitsurusPrologue)
    }
    else {
        msgbox("No script yet.")
    }
    closemessage
    end
}

////////////////////////
///AKIHIKO'S PROLOGUE///
////////////////////////

script EventScript_OverworldStoryTalk_AkihikosPrologue {
    if (var(VAR_AKIHIKOS_INTRO_STATE) == 2){
        call(EventScript_OverworldStoryTalk_MustSetClock)
    }
    elif (var(VAR_AKIHIKOS_INTRO_STATE) == 3 || var(VAR_AKIHIKOS_INTRO_STATE) == 4){
       call(EventScript_OverworldStoryTalk_ClockIsSet)
    }
    elif (var(VAR_AKIHIKOS_INTRO_STATE) == 5){
        iscurrentmap(MAP_SUNSHINE_ORPHANAGE)
        if (var(VAR_RESULT) == 1){
            call(EventScript_OverworldStoryTalk_ShinjiroAndMikiJoinedGoingToWoods)
            }
        iscurrentmap(MAP_SUNSHINE_WOODS)
        if (var(VAR_RESULT) == 1){
            call(EventScript_OverworldStoryTalk_LookingAroundInTheWoods)
            }
    }
    elif (var(VAR_AKIHIKOS_INTRO_STATE) == 8){
        call(EventScript_OverworldStoryTalk_PrePickPawmi)
    }
    elif (var(VAR_AKIHIKOS_INTRO_STATE) == 9){
        call(EventScript_OverworldStoryTalk_PostPickPawmi)
    }
    elif (var(VAR_AKIHIKOS_INTRO_STATE) == 10){
        call(EventScript_OverworldStoryTalk_HaveTalkedToShinjiro)
    }
    elif (var(VAR_AKIHIKOS_INTRO_STATE) == 11){
        iscurrentmap(MAP_SUNSHINE_ORPHANAGE_AKIHIKOS_ROOM2F)
        if (var(VAR_RESULT) == 1){
            call(EventScript_OverworldStoryTalk_ShinjiroJoinedAkihikosRoom)
        }
        else{
            call(EventScript_OverworldStoryTalk_PreTalkKobayashi)
        }
    }
    elif (var(VAR_AKIHIKOS_INTRO_STATE) == 12){
        iscurrentmap(MAP_SUNSHINE_WOODS)
        if (var(VAR_RESULT) == 1){
            call(EventScript_OverworldStoryTalk_PostTalkKobayashi_InWoods)
            }
        
        else {iscurrentmap(MAP_SUNSHINE_TOWN_POKEMON_CENTER)
                if(var(VAR_RESULT) == 1){
                call(EventScript_OverworldStoryTalk_PreTalkMartLady)
                }
                else{
                call (EventScript_OverworldStoryTalk_PostTalkKobayashi)
                }
        }
    }
    elif (var(VAR_AKIHIKOS_INTRO_STATE) == 15){
        call(EventScript_OverworldStoryTalk_PostTalkMartLady)
    }
    elif (var(VAR_AKIHIKOS_INTRO_STATE) == 17){
        call(EventScript_OverworldStoryTalk_TalkToTheTownPeople)
    }
    elif (var(VAR_AKIHIKOS_INTRO_STATE) == 18){
        call(EventScript_OverworldStoryTalk_GoBackToTheOrphanage)
    }
    elif (var(VAR_AKIHIKOS_INTRO_STATE) == 24){
        iscurrentmap(MAP_SUNSHINE_TOWN_RIVERBANK)
        if (var(VAR_RESULT) == 1)
        {
            call(EventScript_OverworldStoryTalk_PosttalkKurosawa_Riverbank)
        }
        else 
        {iscurrentmap(MAP_SUNSHINE_WOODS)
            if (var(VAR_RESULT) == 1)
            {
            call(EventScript_OverworldStoryTalk_PosttalkKurosawa_Woods)
            }
            else
            {
            call(EventScript_OverworldStoryTalk_PosttalkKurosawa)
            }
        }
    }
    elif (var(VAR_AKIHIKOS_INTRO_STATE) == 29){
        call(EventScript_OverworldStoryTalk_PostFirstMeetingMitsuru)
    }
    elif (var(VAR_AKIHIKOS_INTRO_STATE) == 30){
        call (EventScript_OverworldStoryTalk_PostFirstMeetingShinjiro)
    }
    elif (var(VAR_AKIHIKOS_INTRO_STATE) == 33){
        call (EventScript_OverworldStoryTalk_PreFirstBattleMitsuruRoute2House)
    }
    elif (var(VAR_AKIHIKOS_INTRO_STATE) == 34){
        iscurrentmap(MAP_SUNSHINE_MEADOW)
        if (var(VAR_RESULT) == 1){
            call (EventScript_OverworldStoryTalk_PreFirstBattleMitsuru_Meadow)
        }
        else{
            call (EventScript_OverworldStoryTalk_PreFirstBattleMitsuruShinjiroLeft)
        }
    }
    elif (var(VAR_AKIHIKOS_INTRO_STATE) == 38){
        call (EventScript_OverworldStoryTalk_PostFirstBattleMitsuru)
    }
    elif (var(VAR_AKIHIKOS_INTRO_STATE) == 44){
        call (EventScript_OverworldStoryTalk_PostPrologue)
    }
    else
    {
        msgbox("No script yet.")
    }
    closemessage
	end
}

//////////////////////
//////////////////////

script EventScript_OverworldStoryTalk_MustSetClock {
    msgbox(format("{SPEAKER NAME_A}{CREATE_MUGSHOT MUGSHOT_A EMOTE_NORMAL}(Let's fix the clock and join Miki outside.)"))
}

script EventScript_OverworldStoryTalk_ClockIsSet {
    msgbox(format("{SPEAKER NAME_A}{CREATE_MUGSHOT MUGSHOT_A EMOTE_NORMAL}(Miki's waiting for me outside. Better hurry)."))
}

script EventScript_OverworldStoryTalk_ShinjiroAndMikiJoinedGoingToWoods {
    msgbox(format("{SPEAKER NAME_A}{CREATE_MUGSHOT MUGSHOT_A EMOTE_SMILE}What are you going to do in the woods?\p
        {SPEAKER NAME_MK}{DESTROY_MUGSHOT}You'll know soon! I'm sure you'll love it!\p
        {SPEAKER NAME_S}{CREATE_MUGSHOT MUGSHOT_S EMOTE_SMILE}Gonna be funny if you fall into the pond again, Aki.\p
        {SPEAKER NAME_A}{CREATE_MUGSHOT MUGSHOT_A EMOTE_DOUBT}I was just surprised!\p
        {SPEAKER NAME_S}{CREATE_MUGSHOT MUGSHOT_S EMOTE_SMILE}Yeah, by a Caterpie.\p
        {SPEAKER NAME_A}{CREATE_MUGSHOT MUGSHOT_A EMOTE_DOUBT}Ugh...\p
        {SPEAKER NAME_MK}{DESTROY_MUGSHOT}Hehe."))
    }

script EventScript_OverworldStoryTalk_LookingAroundInTheWoods {
    applymovement(2, faceup)
    applymovement(3, faceup)
    waitmovement(0)
    msgbox(format("
    {SPEAKER NAME_S}{CREATE_MUGSHOT MUGSHOT_S EMOTE_NORMAL}What are you doing?\p
    {SPEAKER NAME_A}{CREATE_MUGSHOT MUGSHOT_A EMOTE_NORMAL}Just looking around.\p
    {SPEAKER NAME_A}{CREATE_MUGSHOT MUGSHOT_S EMOTE_SMILE}Making sure there's no Caterpie?\p
    {SPEAKER NAME_A}{CREATE_MUGSHOT MUGSHOT_A EMOTE_DOUBT}Screw you.\p
    {SPEAKER NAME_MK}{DESTROY_MUGSHOT}Ehehe."))
}
script EventScript_OverworldStoryTalk_PrePickPawmi {
    msgbox(format("{SPEAKER NAME_A}{CREATE_MUGSHOT MUGSHOT_A EMOTE_NORMAL}(Come to think of it, this Pawmi's fur color is kinda... weird.)"))
}

script EventScript_OverworldStoryTalk_PostPickPawmi {
    msgbox(format("{SPEAKER NAME_A}{CREATE_MUGSHOT MUGSHOT_A EMOTE_SMILE}(I slept so well.)"))
    if (flag(FLAG_FOLLOWERS_DISABLED) == FALSE){
        lock
        msgbox(format("... You make a good hug pillow, huh?"))
        playmoncry(SPECIES_PAWMI, CRY_MODE_NORMAL)
        applymovement(OBJ_EVENT_ID_FOLLOWER, PawmiHappy)
        waitmovement(0)
        release
    }
}

movement PawmiHappy {
    emote_heart
    face_player
    lock_facing_direction
    jump_in_place_down
    unlock_facing_direction
}

script EventScript_OverworldStoryTalk_HaveTalkedToShinjiro {
    msgbox(format("{SPEAKER NAME_A}{CREATE_MUGSHOT MUGSHOT_A EMOTE_NORMAL}I wonder what Ms. Kobayashi needs us for..."))
    closemessage
    applymovement(2,facedown)
    msgbox(format("{SPEAKER NAME_S}{CREATE_MUGSHOT MUGSHOT_S EMOTE_NORMAL}No idea, she hurried off somewhere before I could ask."))
}

script EventScript_OverworldStoryTalk_ShinjiroJoinedAkihikosRoom {
    msgbox(format("{SPEAKER NAME_S}{CREATE_MUGSHOT MUGSHOT_S EMOTE_NORMAL}Did you forget something? Hurry up, Ms. Kobayashi is waiting."))
}

script EventScript_OverworldStoryTalk_PreTalkKobayashi {
    msgbox(format("{SPEAKER NAME_S}{CREATE_MUGSHOT MUGSHOT_S EMOTE_NORMAL}Ms. Kobayashi said she'd be at the main hall."))
}

script EventScript_OverworldStoryTalk_PostTalkKobayashi {
    msgbox(format("{SPEAKER NAME_S}{CREATE_MUGSHOT MUGSHOT_S EMOTE_NORMAL}The package is in the Pokémon Center in Sunshine Town.\p
    Let's get this over with. The show's on air soon.\p
    {SPEAKER NAME_A}{CREATE_MUGSHOT MUGSHOT_A EMOTE_SMILE}Oh right, your favorite cooking show."))
}

script EventScript_OverworldStoryTalk_PostTalkKobayashi_InWoods {
    msgbox(format("{SPEAKER NAME_S}{CREATE_MUGSHOT MUGSHOT_S EMOTE_SMILE}Never knew you would get your first Pokémon here, huh?\p
    {SPEAKER NAME_A}{CREATE_MUGSHOT MUGSHOT_A EMOTE_SMILE}Yeah."))
    if (flag(FLAG_FOLLOWERS_DISABLED) == FALSE){
        lock
        msgbox(format("I'm glad I met you, Pawmi."))
        playmoncry(SPECIES_PAWMI, CRY_MODE_NORMAL)
        applymovement(OBJ_EVENT_ID_FOLLOWER, PawmiHappy)
        waitmovement(0)
        release
    }
}

script EventScript_OverworldStoryTalk_PreTalkMartLady {
    msgbox(format("
    {SPEAKER NAME_S}{CREATE_MUGSHOT MUGSHOT_S EMOTE_NORMAL}What are you doing? Just ask the Mart Lady for the package."))
}

script EventScript_OverworldStoryTalk_PostTalkMartLady {
    msgbox(format("{SPEAKER NAME_A}{CREATE_MUGSHOT MUGSHOT_A EMOTE_SERIOUS}Let's hurry up outside. I have a bad feeling.\p
    {SPEAKER NAME_S}{CREATE_MUGSHOT MUGSHOT_S EMOTE_SERIOUS}Damn it, you too?"))
}

script EventScript_OverworldStoryTalk_TalkToTheTownPeople {
    msgbox(format("{SPEAKER NAME_A}{CREATE_MUGSHOT MUGSHOT_A EMOTE_SERIOUS}(Someone must've known what happened. Let's ask around.)"))
}

script EventScript_OverworldStoryTalk_GoBackToTheOrphanage {
    msgbox(format("{SPEAKER NAME_S}{CREATE_MUGSHOT MUGSHOT_S EMOTE_MAD}Let's hurry back to the orphanage!!"))
}

script EventScript_OverworldStoryTalk_PosttalkKurosawa {
    msgbox(format("{SPEAKER NAME_S}{CREATE_MUGSHOT MUGSHOT_S EMOTE_NORMAL}You know where to go?\p
    {SPEAKER NAME_A}{CREATE_MUGSHOT MUGSHOT_A EMOTE_NORMAL}No. But there's no point in returning to the orphanage right now. Let's start with Route 2."))
}
script EventScript_OverworldStoryTalk_PosttalkKurosawa_Riverbank 
{
    msgbox(format("{SPEAKER NAME_A}{CREATE_MUGSHOT MUGSHOT_A EMOTE_SAD}Hey Shinji...\p
    Why do you think Ms. Kobayashi would do that to us?\p
    {SPEAKER NAME_S}{CREATE_MUGSHOT MUGSHOT_S EMOTE_PENSIVE}Pushing us into the river...\p
    {SPEAKER NAME_A}{CREATE_MUGSHOT MUGSHOT_A EMOTE_SAD}..."))
}

script EventScript_OverworldStoryTalk_PosttalkKurosawa_Woods {
    msgbox(format("
    {SPEAKER NAME_A}{CREATE_MUGSHOT MUGSHOT_A EMOTE_SAD}Miki gifted me a flower crown here.\p
    I left it in our room so it was probably burned to ash.\p
    {SPEAKER NAME_S}{CREATE_MUGSHOT MUGSHOT_S EMOTE_PENSIVE}..."))

}

script EventScript_OverworldStoryTalk_PostFirstMeetingMitsuru {
    msgbox(format("{SPEAKER NAME_A}{CREATE_MUGSHOT MUGSHOT_A EMOTE_NORMAL}(I wonder what Shinji wanted to say.)"))
}

script EventScript_OverworldStoryTalk_PostFirstMeetingShinjiro {
    msgbox(format("{SPEAKER NAME_A}{CREATE_MUGSHOT MUGSHOT_A EMOTE_NORMAL}You think she'll agree?\p
    {SPEAKER NAME_S}{CREATE_MUGSHOT MUGSHOT_S EMOTE_NORMAL}We have a good chance. She must've also wanted our help."))
}

script EventScript_OverworldStoryTalk_PreFirstBattleMitsuruRoute2House {
    msgbox(format("{SPEAKER NAME_A}{CREATE_MUGSHOT MUGSHOT_A EMOTE_NORMAL}Kirijo's waiting for us in Sunshine Meadow.\p
    {SPEAKER NAME_S}{CREATE_MUGSHOT MUGSHOT_S EMOTE_NORMAL}Let's go meet up with her after we've sorted out our party.\p
    We'll be having 3-vs-3 battles."))
}

script EventScript_OverworldStoryTalk_PreFirstBattleMitsuruShinjiroLeft {
    msgbox(format("{SPEAKER NAME_A}{CREATE_MUGSHOT MUGSHOT_A EMOTE_NORMAL}(Shinji went ahead to prepare his team.\p
    I should do the same and go to Sunshine Meadow to have my first battle with Kirijo.)"))
}

script EventScript_OverworldStoryTalk_PreFirstBattleMitsuru_Meadow{
    msgbox(format("{SPEAKER NAME_A}{CREATE_MUGSHOT MUGSHOT_A EMOTE_NORMAL}(There she is. Her Inteleon is gonna be hard to take down.\p
    There's also that weird-colored Vulpix.)"))
}

script EventScript_OverworldStoryTalk_PostFirstBattleMitsuru {
    msgbox(format("{SPEAKER NAME_A}{CREATE_MUGSHOT MUGSHOT_A EMOTE_NORMAL}By the way, Shinji. The Ferroseed you used earlier...\p
    {SPEAKER NAME_S}{CREATE_MUGSHOT MUGSHOT_S EMOTE_NORMAL}Looks a bit different, right?\p
    It just started following me when I was looking for my first Pokémon in Route 1.\p
    {SPEAKER NAME_A}{CREATE_MUGSHOT MUGSHOT_A EMOTE_NORMAL}Huh...\p
    {SPEAKER NAME_A}{CREATE_MUGSHOT MUGSHOT_A EMOTE_SURPRISED}Wait, all three of us have Pokémons that look slightly different from usual.\p
    {SPEAKER NAME_A}{CREATE_MUGSHOT MUGSHOT_A EMOTE_SMILE}A special coincident, huh?\p
    {SPEAKER NAME_M}{CREATE_MUGSHOT MUGSHOT_M EMOTE_SAD}..."))
}

script EventScript_OverworldStoryTalk_PostPrologue {
    msgbox(format("{SPEAKER NAME_M}{CREATE_MUGSHOT MUGSHOT_M EMOTE_NORMAL}First, let's go through Route 2 to Coralport.\p
    {SPEAKER NAME_M}{CREATE_MUGSHOT MUGSHOT_M EMOTE_CONFIDENT}Alright! There are a few trainers on that route. Time to train!\p
    {SPEAKER NAME_S}{CREATE_MUGSHOT MUGSHOT_S EMOTE_SMILE}Someone sure is fired up."))
}


////////////////////////
///MITSURU'S PROLOGUE///
////////////////////////

script EventScript_OverworldStoryTalk_MitsurusPrologue {
    if (var(VAR_MITSURUS_INTRO_STATE) == 1){
        call(EventScript_OverworldStoryTalk_MustPickUpVulpix)
    }
    elif( var(VAR_MITSURUS_INTRO_STATE) == 2){
        call(EventScript_OverworldStoryTalk_PostPickUpVulpix)
    }
    elif (var(VAR_MITSURUS_INTRO_STATE) == 3){
        call(EventScript_OverworldStoryTalk_PostInteleonJoining)
    }
    elif (var(VAR_MITSURUS_INTRO_STATE) == 4){
        call(EventScript_OverworldStoryTalk_PrePickUpFennekin)
    }
    elif (var(VAR_MITSURUS_INTRO_STATE) == 6){
        call(EventScript_OverworldStoryTalk_PostGoodByeKikuno)
    }
    else
    {
        call(EventScript_OverworldStoryTalk_NoScript)
    }
    closemessage
    end
}

script EventScript_OverworldStoryTalk_NoScript {
    msgbox("No script yet.")
    closemessage
}
script EventScript_OverworldStoryTalk_MustPickUpVulpix {
    msgbox(format("{SPEAKER NAME_M}{CREATE_MUGSHOT MUGSHOT_M EMOTE_NORMAL}
        (I should return Vulpix to its Pokéball and hurry to Thundershards Cliff.)"))
}

script EventScript_OverworldStoryTalk_PostPickUpVulpix {
    lock
    applymovement(LOCALID_PLAYER, Common_Movement_FacePlayer)
    applymovement(OBJ_EVENT_ID_FOLLOWER, Common_Movement_FacePlayer)
    waitmovement(0)
    msgbox(format("{SPEAKER NAME_M}{CREATE_MUGSHOT MUGSHOT_M EMOTE_SMILE}How do you feel getting back on the field, Vulpix?"))
    closemessage
    playmoncry(SPECIES_VULPIX, CRY_MODE_NORMAL)
    waitmoncry
    if (flag(FLAG_FOLLOWERS_DISABLED) == FALSE){
        applymovement(OBJ_EVENT_ID_FOLLOWER, moves(emote_music, face_up, delay_16, face_right, delay_16, face_down, delay_16, face_left, delay_16, face_player))
        waitmovement(0)
    }
    msgbox(format("(Vulpix's dancing excitedly.)\p
    {SPEAKER NAME_M}{CREATE_MUGSHOT MUGSHOT_M EMOTE_SMILE}Haha, honestly, me too."))
    closemessage
    release

}

script EventScript_OverworldStoryTalk_PostInteleonJoining {
    msgbox(format("{SPEAKER NAME_M}{CREATE_MUGSHOT MUGSHOT_M EMOTE_NORMAL}Well then, I'll be counting on you to watch my back, Inteleon."))
    closemessage
    playmoncry(SPECIES_INTELEON, CRY_MODE_NORMAL)
    msgbox(format("{SPEAKER NAME_ITLN}{CREATE_MUGSHOT MUGSHOT_INTELEON EMOTE_SERIOUS}(Inteleon looks determinated, nodding at you.)"))
}

script EventScript_OverworldStoryTalk_PrePickUpFennekin {
    msgbox(format("{SPEAKER NAME_M}{CREATE_MUGSHOT MUGSHOT_M EMOTE_SMILE}A fire type would be great for my journey."))
}

script EventScript_OverworldStoryTalk_PostGoodByeKikuno {
    msgbox(format("{SPEAKER NAME_M}{CREATE_MUGSHOT MUGSHOT_M EMOTE_SERIOUS}(Now I should make my way to Thundershards Scree.\p
    According to the map, it should be on the north of Sunshine Meadow.)"))
}

///////////////////////
////TRAINER'S TIPS/////
///////////////////////

script EventScript_OverworldTrainerTips {
    if (flag(FLAG_SYS_POKEDEX_GET)){
        dynmultipush(HelpPokedex, 0)
    }
    if (var(VAR_MITSURUS_INTRO_STATE) >= 2){
        dynmultipush(HelpShinyChance, 1)
        dynmultipush(HelpStarter, 2)
    }
    if (flag(FLAG_COMPANION_MENU_GET)){
        dynmultipush(HelpCompanion, 3)
    }
    if (flag(FLAG_SYS_QUEST_MENU_GET)){
        dynmultipush(HelpQuest, 4)
    }
    if (var(VAR_AKIHIKOS_INTRO_STATE) >= 100 || var(VAR_MITSURUS_INTRO_STATE) >= 100){
        dynmultipush(HelpDeadline, 5)
    }
    if (flag(FLAG_HELP_MAKE_CHOICE)){
        dynmultipush(HelpChoice, 6)
    }
    if (flag(FLAG_PARTTIME_GET)){
        dynmultipush(HelpParttime, 7)
    }
    if (flag(FLAG_HELP_EXIT_DUNGEON)){
        dynmultipush(HelpExitDungeon, 8)
    }
    if (flag(FLAG_HELP_DUNGEON_ENCOUNTER)){
        dynmultipush(HelpDungeonEncounter, 9)
    }
    if (flag(FLAG_HELP_SPEEDUP)){
        dynmultipush(HelpSpeedUp, 10)
    }
    if (flag(FLAG_DEXNAV_GET)){
        dynmultipush(HelpDexNav, 11)
        dynmultipush(HelpBoxLink, 12)
    }
    msgbox(format("Which Trainer's Tips would you like to check?"))
    dynmultistack(0, 0, FALSE, 6, FALSE, 0, NULL)
    switch(var(VAR_RESULT)){
        case 0: call(EventScript_ShowHelpPokedex)
        case 1: call(EventScript_ShowHelpShinyChance)
        case 2: call(EventScript_ShowHelpStarter)
        case 3: call(EventScript_ShowHelpCompanion)
        case 4: call(EventScript_ShowHelpQuest)
        case 5: call(EventScript_ShowHelpDeadline)
        case 6: call(EventScript_ShowHelpChoice)
        case 7: call(EventScript_ShowHelpPartTime)
        case 8: call(EventScript_ShowHelpExitDungeon)
        case 9: call(EventScript_ShowHelpDungeonEncounter)
        case 10: call(EventScript_ShowHelpSpeedUp)
        case 11: call(EventScript_ShowHelpDexNav)
        case 12: call(EventScript_ShowHelpBoxLink)
    }
    release
    end
}

text NextBackB{"Next/({B_BUTTON}: Back)"}
text QuitBack{"Quit/({B_BUTTON}: Back)"}
script EventScript_ShowHelpPokedex {
    closemessage
    showhelpwindow(HELP_POKEDEX)
    waitbuttonpress
    hidehelpwindow
}

script EventScript_ShowHelpShinyChance {
    closemessage
    showhelpwindow(HELP_SHINY_CHANCE)
    waitbuttonpress
    hidehelpwindow
}

script EventScript_ShowHelpStarter {
    closemessage
    showhelpwindow(HELP_STORY_STARTER)
    waitbuttonpress
    hidehelpwindow
}

script EventScript_ShowHelpQuest {
    closemessage
    showhelpwindow(HELP_QUEST_MENU)
    waitbuttonpress
    hidehelpwindow
}

script EventScript_ShowHelpCompanion {
    closemessage
    showhelpwindow(HELP_COMPANION_MENU)
    waitbuttonpress
    hidehelpwindow
}

script EventScript_ShowHelpChoice {
    closemessage
    showhelpwindow(HELP_MAKE_CHOICES)
    waitbuttonpress
    hidehelpwindow
}

script EventScript_ShowHelpDeadline {
    closemessage
    showhelpwindow(HELP_DEADLINE)
    waitbuttonpress
    hidehelpwindow
}

script EventScript_ShowHelpPartTime {
    closemessage
    showhelpwindow(HELP_PART_TIME)
    waitbuttonpress
    hidehelpwindow
}

script EventScript_ShowHelpExitDungeon {
    closemessage
    showhelpwindow(HELP_EXIT_DUNGEON)
    waitbuttonpress
    hidehelpwindow
}

script EventScript_ShowHelpDungeonEncounter {
    closemessage
    showhelpwindow(HELP_DUNGEON_ENCOUNTER)
    waitbuttonpress
    hidehelpwindow
}

script EventScript_ShowHelpSpeedUp {
    closemessage
    showhelpwindow(HELP_SPEED_UP)
    waitbuttonpress
    hidehelpwindow
}

script EventScript_ShowHelpDexNav {
    closemessage
    showhelpwindow(HELP_DEXNAV)
    waitbuttonpress
    hidehelpwindow  
}

script EventScript_ShowHelpBoxLink {
    closemessage
    showhelpwindow(HELP_BOX_LINK)
    waitbuttonpress
    hidehelpwindow 
}

text HelpPokedex{"Pokédex"}
text HelpShinyChance{"Shiny Chance"}
text HelpStarter{"Story Starter"}
text HelpCompanion{"Companions"}
text HelpQuest{"Quest Menu"}
text HelpChoice{"Your actions"}
text HelpDeadline{"Deadline"}
text HelpParttime{"Part-time Jobs"}
text HelpDungeonEncounter{"Dungeon Encounter"}
text HelpExitDungeon{"Exiting Dungeons"}
text HelpSpeedUp{"Game Speedup"}
text HelpDexNav{"DexNav"}
text HelpBoxLink{"Box Link"}

///////////////////////
////WORK PART-TIME/////
///////////////////////

script EventScript_OverworldTryStartWorkParttime {
    specialvar(VAR_RESULT, DoesExitingMapForwardTime)
    if (var(VAR_RESULT)){
        msgbox(format("Can't start a part-time job in this dungeon."))
        release
        end
    }
    else{
        goto(EventScript_OverworldWorkParttime)
    }
}

script EventScript_OverworldWorkParttime {
    msgbox(format("Which part-time job would you like to work at?"))
    if(flag(FLAG_AMEAME_PARTTIME_UNLOCKED))
    {
        dynmultipush(AmeAme, 0)
    }
    dynmultistack(0, 0, FALSE, 6, FALSE, 0, NULL)
    switch(var(VAR_RESULT)){
        case 0: goto(EventScript_TryStartAmeAmeParttime)
    }
    release
    end
}

text AmeAme{"AmeAme Candy Shop (7AM - 3PM)"}

script EventScript_TryStartAmeAmeParttime {
    msgbox(format("Work at AmeAme Candy Shop for {COLOR RED}6 hours{COLOR 2} and get paid with{COLOR RED}¥2000{COLOR 2}?\n"), MSGBOX_YESNO)
    if (var(VAR_RESULT)){
        gettime
        if (var(VAR_0x8000) < 7 || var(VAR_0x8000) > 15){
            msgbox(format("You can only work there between 7AM and 3PM."))
            closemessage
            release
            end
        }
        else{
            goto(CoralportCandyShop_Eventscript_RandomizeCutscenes)
        }
    }
    else{
        goto(EventScript_OverworldWorkParttime)
    }
    release
    end
}