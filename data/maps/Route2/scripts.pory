mapscripts Route2_MapScripts {
    MAP_SCRIPT_ON_WARP_INTO_MAP_TABLE[
        VAR_AKIHIKOS_INTRO_STATE, 25: Route2_Eventscript_AkihikoOnWarp25
        VAR_AKIHIKOS_INTRO_STATE, 33: Route2_Eventscript_AkihikoOnWarp33
        VAR_AKIHIKOS_INTRO_STATE, 40: Route2_Eventscript_AkihikoOnWarp40
    ]
    MAP_SCRIPT_ON_TRANSITION{
        if (var(VAR_AKIHIKOS_INTRO_STATE) < 40){
            setflag(FLAG_TEMP_3)
        }
        if (var(VAR_AKIHIKOS_INTRO_STATE) > 25){
            setflag(FLAG_TEMP_1)
        }
        if (var(VAR_AKIHIKOS_INTRO_STATE) > 25 && var(VAR_AKIHIKOS_INTRO_STATE) < 33){
            setflag(FLAG_TEMP_2)
        }
        if (var(VAR_AKIHIKOS_INTRO_STATE) == 26){
            clearflag(FLAG_HIDE_TALK_BUTTON)
        }
        if (var(VAR_AKIHIKOS_INTRO_STATE) == 33){
            setflag(FLAG_HIDE_TALK_BUTTON)
            setobjectxyperm(6, 10, 12)
        }
        if (var(VAR_AKIHIKOS_INTRO_STATE) == 34){
            setobjectxyperm(6, 31, 21)
            setobjectmovementtype(6, MOVEMENT_TYPE_WANDER_LEFT_AND_RIGHT)
        }
        if (var(VAR_AKIHIKOS_INTRO_STATE) >= 37 && var(VAR_AKIHIKOS_INTRO_STATE) < 40){
            setflag(FLAG_TEMP_2)
        }
        if (var(VAR_AKIHIKOS_INTRO_STATE) == 40){
            setobjectxyperm(6, 12, 13)
            //playbgm(MUS_RG_SS_ANNE, TRUE)
        }
        if (var(VAR_AKIHIKOS_INTRO_STATE) >= 42){
            setflag(FLAG_TEMP_2)
            setflag(FLAG_TEMP_3)
        }
    }
    MAP_SCRIPT_ON_FRAME_TABLE[
        VAR_AKIHIKOS_INTRO_STATE, 25: Route2_Eventscript_AkihikoOnFrame25
        VAR_AKIHIKOS_INTRO_STATE, 33: Route2_Eventscript_AkihikoOnFrame33
        VAR_AKIHIKOS_INTRO_STATE, 40: Route2_Eventscript_AkihikoOnFrame40
    ]
}

script Route2_Eventscript_AkihikoOnWarp25 {
    applymovement(LOCALID_PLAYER, faceleft)
    applymovement(7, invisible)
    applymovement(8, invisible)
    applymovement(9, invisible)
    applymovement(10, invisible)
    applymovement(11, invisible)
    applymovement(12, invisible)
}

script Route2_Eventscript_AkihikoOnWarp33 {
    applymovement(6, invisible)
}

script Route2_Eventscript_AkihikoOnWarp40 {
    applymovement(6, faceright)
}
script Route2_Eventscript_AkihikoOnFrame25 {
    goto(Route2_Eventscript_AkishinjiGettingBlockedBySusMen)
}

script Route2_Eventscript_AkishinjiGettingBlockedBySusMen {
    lock
    applymovement(LOCALID_PLAYER, AkishinjiGettingBlockedBySusMen)
    delay(8)
    applymovement(6, AkishinjiGettingBlockedBySusMen)
    applymovement(4, SusMenBlockingAkiShinji)
    delay(8)
    applymovement(3, SusMenBlockingAkiShinji)
    waitmovement(3)
    call(SunshineOrphanage_Burning_Eventscript_BlockShakeCamera)
    delay(20)
    applymovement(LOCALID_PLAYER, AkiShinjiStepBack)
    applymovement (6, AkiShinjiStepBack)
    waitmovement(0)
    msgbox(format("{SPEAKER NAME_AKIHIKO}{CREATE_MUGSHOT MUGSHOT_AKIHIKO EMOTE_STRUGGLED}Ouch!\p
    {SPEAKER NAME_SHINJIRO}{CREATE_MUGSHOT MUGSHOT_SHINJIRO EMOTE_STRUGGLED}What the hell??"))
    closemessage
    delay(30)
    playbgm(MUS_ABNORMAL_WEATHER, TRUE)
    setspeaker("Man In Black 1")
    applymovement(4, walkinplaceright)
    waitmovement(0)
    msgbox(format("That stupid cop is finally out of picture."))
    closemessage
    setspeaker("Man In Black 2")
    applymovement(3, walkinplaceright)
    waitmovement(0)
    msgbox(format("You two are coming with us. Like it or not.\p
    {SPEAKER NAME_AKIHIKO}{CREATE_MUGSHOT MUGSHOT_AKIHIKO EMOTE_STRUGGLED}What?"))
    closemessage
    applymovement(4, SusMan1ApproachingAkihiko)
    waitmovement(0)
    applymovement(LOCALID_PLAYER, shakehorizontal)
    call(SunshineOrphanage_Burning_Eventscript_BlockShakeCamera)
    applymovement(4, SusMan1StepBack)
    waitmovement(0)
    delay(20)
    msgbox(format("{SPEAKER NAME_AKIHIKO}{CREATE_MUGSHOT MUGSHOT_AKIHIKO EMOTE_MAD}No way! Who even are you!?"))
    closemessage
    goto(Route2_Eventscript_StartBattleWithSusMen)
    release
    end
}

movement walkinplaceright {
    walk_in_place_right    
}

movement AkishinjiGettingBlockedBySusMen {
    walk_fast_left *6
    delay_8
    lock_facing_direction
    jump_right
    unlock_facing_direction
}

movement SusMenBlockingAkiShinji {
    walk_fast_up *3
    walk_fast_right
}

movement AkiShinjiStepBack {
    lock_facing_direction
    walk_right
    unlock_facing_direction
}
movement SusMan1ApproachingAkihiko {
    walk_fast_right *2
}

movement SusMan1StepBack {
    lock_facing_direction
    walk_left *2
    unlock_facing_direction
}

movement shakevertical {
    shake_vertical
}

script Route2_Eventscript_StartBattleWithSusMen {
    lock
    setspeaker("Man In Black 1")
    applymovement(4, walkinplaceright)
    waitmovement(0)
    msgbox(format("Tch.\p
    I hate disobedient kids."))
    closemessage
    setflag(FLAG_NO_WHITEOUT)
    msgbox(SusMenFightIntro)
    closemessage
    trainerbattle_no_intro(TRAINER_GRUNT_ROUTE2, SusMenFightDefeated)
    goto(Route2_Eventscript_PostBattleWithSusMen)
    release
    end
}

movement lockfacing {
    lock_facing_direction
}
text SusMenFightIntro{
    format("Go, Venipede.")
}

text SusMenFightDefeated {
    format("You aren't half bad. However...")
}

script Route2_Eventscript_PostBattleWithSusMen{
    lock
    playbgm(MUS_NONE, TRUE)
    clearflag(FLAG_NO_WHITEOUT)
    special(HealPlayerParty)
    delay(30)
    setspeaker("Man In Black 1")
    msgbox(format("Resistance is futile."))
    closemessage
    delay(8)
    playse(SE_EXIT)
    applymovement(7, DrowzeeAppears)
    waitmovement(0)
    applymovement(LOCALID_PLAYER, facedown)
    applymovement(6, facedown)
    waitmovement(0)
    msgbox(format("{SPEAKER NAME_AKIHIKO}{CREATE_MUGSHOT MUGSHOT_AKIHIKO EMOTE_SURPRISED}Huh?"))
    closemessage
    setspeaker("Man In Black 2")
    applymovement(3, walkinplaceright)
    waitmovement(0)
    msgbox(format("Drowzee, Hypnosis."))
    closemessage
    fadescreenswapbuffers(FADE_TO_BLACK)
    playmoncry(SPECIES_DROWZEE, CRY_MODE_NORMAL)
    waitmoncry
    delay(30)
    applymovement(LOCALID_PLAYER, invisible)
    applymovement(6, invisible)
    waitmovement(0)
    goto (Route2_Eventscript_AkishinjiAsleep)
    release
    end
}

movement DrowzeeAppears {
    set_visible
    jump_left
    face_up
}

movement walkinplaceup {
    walk_in_place_up
}


script Route2_Eventscript_AkishinjiAsleep {
    lock
    applymovement(8, visible)
    applymovement(9, visible)
    waitmovement(0)
    fadescreenswapbuffers(FADE_FROM_BLACK)
    delay(20)
    playse(SE_M_SNORE)
    applymovement(8, emoteasleep)
    delay(8)
    playse(SE_M_SNORE)
    applymovement(9, emoteasleep)
    waitmovement(0)
    delay(20)
    setspeaker("Man In Black 2")
    applymovement(3, walkinplaceright)
    waitmovement(0)
    msgbox("That's more like it.")
    closemessage
    applymovement(3, walkright)
    waitmovement(0)
    goto(Route2_Eventscript_MitsuruDisruptsTheMen)
    release
    end
}

movement emoteasleep {
    emote_asleep
}
movement visible {
    set_visible
}

script Route2_Eventscript_MitsuruDisruptsTheMen {
    lock
    delay(20)
    setspeaker("???????")
    msgbox(format("{CREATE_MUGSHOT MUGSHOT_MITSURU EMOTE_SILHOUETTE}Now! Inteleon, Vulpix!"))
    closemessage
    fadescreenswapbuffers(FADE_TO_BLACK)
    delay(20)
    playmoncry(SPECIES_INTELEON, CRY_MODE_NORMAL)
    waitmoncry
    playse(SE_M_BUBBLE_BEAM)
    waitse
    playmoncry(SPECIES_VULPIX, CRY_MODE_NORMAL)
    waitmoncry
    playse(SE_M_ICY_WIND)
    waitse
    applymovement(3, invisible)
    applymovement(4, invisible)
    removeobject(3)
    removeobject(4)
    applymovement(10, visible)
    applymovement(11, visible)
    waitmovement(0)
    fadescreenswapbuffers(FADE_FROM_BLACK)
    delay(20)
    applymovement(10, shakehorizontal)
    playse(SE_M_HARDEN)
    delay(4)
    applymovement(11, shakehorizontal)
    playse(SE_M_HARDEN)
    waitse
    applymovement(7, emoteexclamation)
    waitmovement(0)
    goto(Route2_Eventscript_MitsuruAppears)
    release
    end
}

movement DrowzeeRunsAway {
    delay_16
    face_left
    delay_16
    face_up
    delay_16
    emote_x
    delay_16
    jump_right
    set_invisible

}

script Route2_Eventscript_MitsuruAppears {
    lock
    delay(40)
    addobject(5)
    applymovement(LOCALID_PLAYER, CameraToMitsuru)
    applymovement(5, MitsuruAppears)
    waitmovement(0)
    applymovement(7, DrowzeeRunsAway)
    waitmovement(0)
    playse(SE_EXIT)
    waitse
    delay(30)
    setspeaker("???????")
    msgbox(format("{CREATE_MUGSHOT MUGSHOT_MITSURU EMOTE_NORMAL}...\p
    Inteleon, let's get these two out of here first.\p
    The ice can only hold them off for so long."))
    closemessage
    playse(SE_EXIT)
    applymovement(12, InteleonWalksOut)
    applymovement(LOCALID_PLAYER, CameraToInteleon)
    waitmovement(0)
    delay(20)
    playmoncry(SPECIES_INTELEON, CRY_MODE_NORMAL)
    applymovement(12, walkinplacedown)
    waitmovement(0)
    delay(30)
    warpsilent(MAP_BLANK_MAP, 0, 0)
    setvar(VAR_AKIHIKOS_INTRO_STATE, 26)
    release
    end
}

movement walkinplacedown {
    walk_in_place_down
}
movement CameraToMitsuru {
    walk_left *3
    walk_down
}

movement MitsuruAppears {
    walk_up *2
    walk_right *5  
}

movement InteleonWalksOut {
    set_visible
    walk_down
}

movement CameraToInteleon {
    walk_up *2
}
script Route2_Eventscript_RouteSign {
    msgbox("Route 2.")
    closemessage
    end

}

script Route2_Eventscript_DirectionSign {
    msgbox(format("Sunshine Meadow {UP_ARROW}\n
    Sunshine Town {RIGHT_ARROW}\p
    Route 1 {DOWN_ARROW}\n
    Coralport {LEFT_ARROW}"))
    closemessage
    end
}

script Route2_Eventscript_Yuuka {
    trainerbattle_single (TRAINER_YUUKA, Route2_YuukaIntro, Route2_YuukaDefeated)
    setspeaker("Yuuka")
    msgbox(Route2_YuukaPostBattle)
    closemessage
    end
}

text Route2_YuukaIntro {
    format("Hey, I'm bored. Want a quick battle?")
}

text Route2_YuukaDefeated {
    format("Thank you. That was fun!")
}

text Route2_YuukaPostBattle {
    format("Actually I went here to catch an Abra but they kept teleporting away...")
}

script Route2_Eventscript_Tarou {
    trainerbattle_single (TRAINER_TAROU, Route2_TarouIntro, Route2_TarouDefeated)
    setspeaker("Tarou")
    msgbox(Route2_TarouPostBattle)
    closemessage
    end
}

text Route2_TarouIntro {
    format("What are you looking at, low-life?")
}

text Route2_TarouDefeated {
    format("Tch. This Pokémon sucks. I shouldn't have bought it.")
}

text Route2_TarouPostBattle {
    format("I already gave you the money. Screw off.")
}

script Route2_Eventscript_AkihikoOnFrame33 {
    goto(Route2_Eventscript_ShinjiroSplitsOutToCatchPokemon)
}

script Route2_Eventscript_ShinjiroSplitsOutToCatchPokemon {
    applymovement(6, ShinjiroSplitsOut)
    waitmovement(0)
    msgbox(format("{SPEAKER NAME_SHINJIRO}{CREATE_MUGSHOT MUGSHOT_SHINJIRO EMOTE_NORMAL}Aight, I'm gonna go ahead and catch some Pokémon.\p
    {SPEAKER NAME_AKIHIKO}{CREATE_MUGSHOT MUGSHOT_AKIHIKO EMOTE_NORMAL}Sure. Be careful, the men in black could still be around here.\p
    {SPEAKER NAME_SHINJIRO}{CREATE_MUGSHOT MUGSHOT_SHINJIRO EMOTE_SMILE}Hah. The same to you, Aki.\p
    {SPEAKER NAME_SHINJIRO}{CREATE_MUGSHOT MUGSHOT_SHINJIRO EMOTE_CONFIDENT}See you later at the Meadow."))
    closemessage
    applymovement(6, ShinjiroGoesOffCam)
    applymovement(LOCALID_PLAYER, faceright)
    waitmovement(6)
    lock
    removeobject(6)
    setobjectxyperm(6, 29, 21)
    setobjectnewmovementtype(6, MOVEMENT_TYPE_WANDER_LEFT_AND_RIGHT)
    addobject(6)
    fwdtime(13, 45)
    release
    setvar(VAR_AKIHIKOS_INTRO_STATE, 34)
    clearflag(FLAG_HIDE_TALK_BUTTON)
    end
}

movement ShinjiroSplitsOut {
    set_visible
    walk_down *2
    face_up
}

movement ShinjiroGoesOffCam {
    walk_right *8
    set_invisible
}

script Route2_Eventscript_ShinjiroFindingPokemon {
    lock
    faceplayer
    msgbox(format("{SPEAKER NAME_AKIHIKO}{CREATE_MUGSHOT MUGSHOT_AKIHIKO EMOTE_NORMAL}How's it going?\p
    {SPEAKER NAME_SHINJIRO}{CREATE_MUGSHOT MUGSHOT_SHINJIRO EMOTE_SMILE}Got myself two.\p
    {SPEAKER NAME_AKIHIKO}{CREATE_MUGSHOT MUGSHOT_AKIHIKO EMOTE_HAPPY}Hahaha, I've never seen you're this worked up.\p
    {SPEAKER NAME_SHINJIRO}{CREATE_MUGSHOT MUGSHOT_SHINJIRO EMOTE_SMILE}Shut up and go to the Meadow if you're done preparing.\p
    {SPEAKER NAME_AKIHIKO}{CREATE_MUGSHOT MUGSHOT_AKIHIKO EMOTE_HAPPY}Alright, alright ahaha."))
    closemessage
    release
    end
}

script Route2_Eventscript_AkihikoOnFrame40 {
    goto (Route2_Eventscript_AkihikoNoticingShinjiroOutside)
}

script Route2_Eventscript_AkihikoNoticingShinjiroOutside {
    applymovement(LOCALID_PLAYER, AkihikoNoticingShinjiroOutside)
    delay(8)
    playse(SE_PIN)
    waitse
    waitmovement(0)
    special(SpawnCameraObject)
    applymovement(OBJ_EVENT_ID_CAMERA, walkright)
    waitmovement(0)
    delay(20)
    msgbox(format("{SPEAKER NAME_AKIHIKO}{CREATE_MUGSHOT MUGSHOT_AKIHIKO EMOTE_NORMAL}(Oh, there he is.)\p
    {SPEAKER NAME_AKIHIKO}{CREATE_MUGSHOT MUGSHOT_AKIHIKO EMOTE_NORMAL}Hey Shinji, you coudln't sleep too?"))
    closemessage
    playse(SE_PIN)
    applymovement(6, ShinjiroHeardAkihikoCalling)
    waitmovement(0)
    msgbox(format("{SPEAKER NAME_SHINJIRO}{CREATE_MUGSHOT MUGSHOT_SHINJIRO EMOTE_NORMAL}Aki.\p
    Yep, I keep thinking of what Kirijo said.\p
    {SPEAKER NAME_SHINJIRO}{CREATE_MUGSHOT MUGSHOT_SHINJIRO EMOTE_DOUBT}Humans who can use Pokémon's ability, huh...\p
    {SPEAKER NAME_AKIHIKO}{CREATE_MUGSHOT MUGSHOT_AKIHIKO EMOTE_SMILE}Heh, guess we were thinking the same thing.\p
    {SPEAKER NAME_SHINJIRO}{CREATE_MUGSHOT MUGSHOT_SHINJIRO EMOTE_NORMAL}Well, there's not much we can do until we see for ourselves if what she said was true.\p
    {SPEAKER NAME_SHINJIRO}{CREATE_MUGSHOT MUGSHOT_SHINJIRO EMOTE_CONFIDENT}Since we're both not feeling sleepy, how about a battle?\p
    {SPEAKER NAME_AKIHIKO}{CREATE_MUGSHOT MUGSHOT_AKIHIKO EMOTE_CONFIDENT}I was just about to suggest that.\p
    The Ferroseed you used earlier against Kirijo really caught my curiosity.\p
    {SPEAKER NAME_SHINJIRO}{CREATE_MUGSHOT MUGSHOT_SHINJIRO EMOTE_CONFIDENT}I'll show what it can do. Let's do a 3-vs-3 again.\p
    Step up if you're ready."))
    closemessage
    applymovement(OBJ_EVENT_ID_CAMERA, walkleft)
    waitmovement
    special(RemoveCameraObject)
    setvar(VAR_AKIHIKOS_INTRO_STATE, 41)
    clearflag(FLAG_HIDE_TALK_BUTTON)
}

movement AkihikoNoticingShinjiroOutside {
    walk_down
    face_right
    emote_exclamation_mark
}

movement ShinjiroHeardAkihikoCalling {
    emote_exclamation_mark
    face_left
}

script Route2_Eventscript_MustBattleShinjiro {
    lock
    applymovement(LOCALID_PLAYER, walkdown)
    waitmovement(0)
    msgbox(format("{SPEAKER NAME_AKIHIKO}{CREATE_MUGSHOT MUGSHOT_AKIHIKO EMOTE_NORMAL}(There's no point going back inside, it's not like I can sleep right now.)"))
    closemessage
    release
    end
}

script Route2_Eventscript_InitShinjiroFirstBattle {
    lock
    msgbox(format("{SPEAKER NAME_SHINJIRO}{CREATE_MUGSHOT MUGSHOT_SHINJIRO EMOTE_CONFIDENT}Ready for our first battle?"), MSGBOX_YESNO)
    if (var(VAR_RESULT) == FALSE){
        msgbox(format("{SPEAKER NAME_SHINJIRO}{CREATE_MUGSHOT MUGSHOT_SHINJIRO EMOTE_SMILE}Heh, take your sweet time. We still have all night, Aki."))
        closemessage
        applymovement(LOCALID_PLAYER, walkleft)
        waitmovement(0)
        release
        end
    }
    else{
        msgbox(format("{SPEAKER NAME_SHINJIRO}{CREATE_MUGSHOT MUGSHOT_SHINJIRO EMOTE_CONFIDENT}Before we start, do you want me to heal your Pokémon?"), MSGBOX_YESNO)
        if (var(VAR_RESULT) == TRUE){
            closemessage
            playse(MUS_HEAL)
            waitse
            special(HealPlayerParty)
        }
        goto(Route2_Eventscript_CheckingPartyNumberForShinjiroFirstBattle)
        release
        end
    }
}
script Route2_Eventscript_CheckingPartyNumberForShinjiroFirstBattle {
    lock
    specialvar(VAR_RESULT, CountPartyNonEggMons)
    if (var(VAR_RESULT) < 3){
        msgbox(format("{SPEAKER NAME_SHINJIRO}{CREATE_MUGSHOT MUGSHOT_SHINJIRO EMOTE_SERIOUS}Wait, you don't have at least 3 Pokémons to battle. I want a fair fight.\p
        Are you sure you still want to fight handicapped?"), MSGBOX_YESNO)
        if (var(VAR_RESULT) == FALSE){
            msgbox(format("{SPEAKER NAME_SHINJIRO}{CREATE_MUGSHOT MUGSHOT_SHINJIRO EMOTE_CONFIDENT}Good thinking. I won't go down easily."))
            closemessage
            applymovement(LOCALID_PLAYER, walkleft)
            waitmovement(0)
            release
            end
        }
        else {
            playse(SE_FAILURE)
            applymovement(6, emoteangry)
            waitmovement(0)
            msgbox(format("{SPEAKER NAME_SHINJIRO}{CREATE_MUGSHOT MUGSHOT_SHINJIRO EMOTE_SERIOUS}Don't underestimate me."))
            closemessage
            subvar(VAR_SHINJIRO_RELATIONSHIP_POINTS, 3) //current point: 97
            goto(Route2_Eventscript_SelectingPartyShinjiroFirstBattle)
            release
        }
        closemessage
        end
    }
    elif (var(VAR_RESULT) >= 3){
        playse(SE_SHINY)
        applymovement(6, pointup2)
        waitmovement(0)
        addvar(VAR_SHINJIRO_RELATIONSHIP_POINTS, 3) //current point: 103
        msgbox(format("{SPEAKER NAME_SHINJIRO}{CREATE_MUGSHOT MUGSHOT_SHINJIRO EMOTE_CONFIDENT}Let's go.\p
        {SPEAKER NAME_AKIHIKO}{CREATE_MUGSHOT MUGSHOT_AKIHIKO EMOTE_CONFIDENT}No holding back, alright?"))
        closemessage
        goto(Route2_Eventscript_SelectingPartyShinjiroFirstBattle)
        release
    }
}

movement emoteangry {
    emote_angry
}

script Route2_Eventscript_InitShinjiroFirstBattle2 {
    lock
    applymovement(LOCALID_PLAYER, walkup)
    waitmovement(0)
    applymovement(LOCALID_PLAYER, faceright)
    waitmovement(0)
    goto(Route2_Eventscript_InitShinjiroFirstBattle)
    release
    end
}

script Route2_Eventscript_SelectingPartyShinjiroFirstBattle {
    lock
    setflag(FLAG_NO_WHITEOUT)
    special(ChooseHalfPartyForBattle)
    waitstate()
    if (var(VAR_RESULT) == FALSE){
        msgbox(format("{SPEAKER NAME_AKIHIKO}{CREATE_MUGSHOT MUGSHOT_AKIHIKO EMOTE_NORMAL}(Let's pick 3 Pokémon to fight. Shinji is waiting.)"))
        closemessage
        goto (Route2_Eventscript_SelectingPartyShinjiroFirstBattle)
    }
    else {
        special(ReducePlayerPartyToSelectedMons)
        setvar (VAR_0x8004, FRONTIER_UTIL_FUNC_SET_DATA)
	    setvar (VAR_0x8005, FRONTIER_DATA_SELECTED_MON_ORDER)
        special (CallFrontierUtilFunc)
        setvar (VAR_0x8004, SPECIAL_BATTLE_MULTI)
        setvar (VAR_0x8005, MULTI_BATTLE_CHOOSE_MONS)
        trainerbattle_no_intro(TRAINER_SHINJIRO_ROUTE_2, ShinjiroFirstBattleDefeated)
        fwdtime(23, 20)
        setvar (VAR_0x8004, FRONTIER_UTIL_FUNC_SAVE_PARTY)
        special (CallFrontierUtilFunc)
	    special (LoadPlayerParty)
        goto (Route2__Eventscript_ShinjiroFirstBattleResult)       
    }
    release
}

text ShinjiroFirstBattleDefeated {
    format("Whew, that was quite a work-out.")
}

script Route2__Eventscript_ShinjiroFirstBattleResult {
    lock
    specialvar(VAR_RESULT, GetBattleOutcome)
        if (var(VAR_RESULT) == B_OUTCOME_WON && var(VAR_SHINJIRO_RELATIONSHIP_POINTS) == 97){
            msgbox (Route2_Eventscript_WonInShinjiroFirstBattlePointDown)
            closemessage
        }
        elif (var(VAR_RESULT) == B_OUTCOME_WON && var(VAR_SHINJIRO_RELATIONSHIP_POINTS) == 103){
            msgbox (Route2_Eventscript_WonInShinjiroFirstBattlePointUp)
            closemessage
        }
        elif (var(VAR_RESULT) != B_OUTCOME_WON && var(VAR_SHINJIRO_RELATIONSHIP_POINTS) == 97){
            bufferleadmonspeciesname(STR_VAR_1)
            msgbox (Route2_Eventscript_LostInShinjiroFirstBattlePointDown)
            closemessage
        }
        else{
            bufferleadmonspeciesname(STR_VAR_1)
            msgbox (Route2_Eventscript_LostInShinjiroFirstBattlePointUp)
            closemessage
        }
    goto (Route2_Eventscript_PostShinjiroFirstBattle)
    release
}

text Route2_Eventscript_WonInShinjiroFirstBattlePointUp {
    format("{SPEAKER NAME_AKIHIKO}{CREATE_MUGSHOT MUGSHOT_AKIHIKO EMOTE_STRUGGLED}-huff- -huff- (Did I win?)\p
    {SPEAKER NAME_SHINJIRO}{CREATE_MUGSHOT MUGSHOT_SHINJIRO EMOTE_STRUGGLED}-huff- Dang it, I was so close.\p
    Looks like I lost to both of you, huh.\p
    {SPEAKER NAME_SHINJIRO}{CREATE_MUGSHOT MUGSHOT_SHINJIRO EMOTE_SMILE}Well, next time I won't lose again.\p
    {SPEAKER NAME_AKIHIKO}{CREATE_MUGSHOT MUGSHOT_AKIHIKO EMOTE_CONFIDENT}Hehe, I'm always open for another battle against you. It was fun.\p
    {SPEAKER NAME_SHINJIRO}{CREATE_MUGSHOT MUGSHOT_SHINJIRO EMOTE_SMILE}Heh, it is.")
}

text Route2_Eventscript_WonInShinjiroFirstBattlePointDown {
    format("{SPEAKER NAME_AKIHIKO}{CREATE_MUGSHOT MUGSHOT_AKIHIKO EMOTE_STRUGGLED}-huff- -huff- (Did I win?)\p
    {SPEAKER NAME_SHINJIRO}{CREATE_MUGSHOT MUGSHOT_SHINJIRO EMOTE_STRUGGLED}-huff- Dang it, I was so close.\p
    Looks like I lost to both of you, huh.\p
    {SPEAKER NAME_SHINJIRO}{CREATE_MUGSHOT MUGSHOT_SHINJIRO EMOTE_SERIOUS}Well, next time I won't lose again.\p
    {SPEAKER NAME_AKIHIKO}{CREATE_MUGSHOT MUGSHOT_AKIHIKO EMOTE_CONFIDENT}Hehe, I'm always open for another battle against you. It was fun.\p
    {SPEAKER NAME_SHINJIRO}{CREATE_MUGSHOT MUGSHOT_SHINJIRO EMOTE_SERIOUS}...\p
    To be honest, I'd have prefered if you'd fought me fairly. I asked for a 3-vs-3.\p
    If you underestimate me again, I won't let it slide, unlike this time.\p
    {SPEAKER NAME_AKIHIKO}{CREATE_MUGSHOT MUGSHOT_AKIHIKO EMOTE_SAD}... Sorry.\p
    {SPEAKER NAME_SHINJIRO}{CREATE_MUGSHOT MUGSHOT_SHINJIRO EMOTE_NORMAL}Apology accepted.")
}
text Route2_Eventscript_LostInShinjiroFirstBattlePointUp {
    format("{SPEAKER NAME_AKIHIKO}{CREATE_MUGSHOT MUGSHOT_AKIHIKO EMOTE_STRUGGLED}Arg, I was so close!\p
    {SPEAKER NAME_SHINJIRO}{CREATE_MUGSHOT MUGSHOT_SHINJIRO EMOTE_STRUGGLED}-huff- Your {STR_VAR_1} did pack a punch.\p
    {SPEAKER NAME_SHINJIRO}{CREATE_MUGSHOT MUGSHOT_SHINJIRO EMOTE_CONFIDENT}But it looks like I'm not the worst out of the three of us, heh.\p
    {SPEAKER NAME_AKIHIKO}{CREATE_MUGSHOT MUGSHOT_AKIHIKO EMOTE_SERIOUS}Next time I won't lose.\p
    {SPEAKER NAME_SHINJIRO}{CREATE_MUGSHOT MUGSHOT_SHINJIRO EMOTE_CONFIDENT}Looking forward to that.\p
    {SPEAKER NAME_AKIHIKO}{CREATE_MUGSHOT MUGSHOT_AKIHIKO EMOTE_DOUBT}... Did you just imitate Kirijo...\p
    Please don't do that again.\p
    {SPEAKER NAME_SHINJIRO}{CREATE_MUGSHOT MUGSHOT_SHINJIRO EMOTE_SMILE}Pft, the look on your face.")
}

text Route2_Eventscript_LostInShinjiroFirstBattlePointDown {
    format("{SPEAKER NAME_AKIHIKO}{CREATE_MUGSHOT MUGSHOT_AKIHIKO EMOTE_STRUGGLED}Arg, I lost...\p
    {SPEAKER NAME_SHINJIRO}{CREATE_MUGSHOT MUGSHOT_SHINJIRO EMOTE_SERIOUS}...\p
    Isn't that to be expected? Since you didn't want to fight me with 3 Pokémons?\p
    {SPEAKER NAME_AKIHIKO}{CREATE_MUGSHOT MUGSHOT_AKIHIKO EMOTE_SAD}...\p
    {SPEAKER NAME_SHINJIRO}{CREATE_MUGSHOT MUGSHOT_SHINJIRO EMOTE_SERIOUS}I hope you won't underestimate me next time. It wasn't fun even when I won.\p
    {SPEAKER NAME_AKIHIKO}{CREATE_MUGSHOT MUGSHOT_AKIHIKO EMOTE_SAD}Yeah... Sorry.\p
    {SPEAKER NAME_SHINJIRO}{CREATE_MUGSHOT MUGSHOT_SHINJIRO EMOTE_DOUBT}Sigh.\p
    {SPEAKER NAME_SHINJIRO}{CREATE_MUGSHOT MUGSHOT_SHINJIRO EMOTE_NORMAL}I'll let it slide this time. Apology accepted.")
}
script Route2_Eventscript_PostShinjiroFirstBattle {
    lock
    setvar(VAR_AKIHIKOS_INTRO_STATE, 42)
    clearflag(FLAG_NO_WHITEOUT)
    playse(SE_M_YAWN)
    applymovement(6, shakevertical)
    waitmovement(0)
    msgbox(format("{SPEAKER NAME_SHINJIRO}{CREATE_MUGSHOT MUGSHOT_SHINJIRO EMOTE_GEEZ}-yawn- That battle took all the energy out of me.\p
    Let's go to bed. I don't want to hear Kirijo complaining tomorrow if we sleep in.\p
    Wanna head inside?\p
    {SPEAKER NAME_AKIHIKO}{CREATE_MUGSHOT MUGSHOT_AKIHIKO EMOTE_NORMAL}Yeah, I'm tired too."))
    closemessage
    special(HealPlayerParty)
    clearflag(FLAG_DONT_TRANSITION_MUSIC)
    warpsilent(MAP_BLANK_MAP, 0, 0)
        
}

movement Route2ShinjiroGoesInsideHouse {
    walk_down
    walk_left*3
    walk_up*3
}

script Route2_Eventscript_Himari {
    lock
    faceplayer
    setspeaker("Himari")
    if (var(VAR_TEMP_0) == 1){
        msgbox(format("Can I pet your Rockruff?"), MSGBOX_YESNO)
        closemessage
        if (var(VAR_RESULT) == TRUE){
            playse(SE_SHINY)
            applymovement(15, emotemusic)
            msgbox(format("Thank you!\p
            {SPEAKER NAME_NONE}Himari petted the Rockruff."))
            closemessage
            playmoncry(SPECIES_ROCKRUFF, CRY_MODE_NORMAL)
            waitmoncry
            setvar(VAR_TEMP_0, 2)
            release
            end
        }
        else{
            playse(SE_BOO)
            applymovement(15, emotecry)
            waitmovement(0)
            msgbox(format("Aw... Okay..."))
            closemessage
            release
            end}
    }
    elif (var(VAR_TEMP_0) == 2){
        msgbox(format("Thank you for letting me pet your Rockruff!\p
        Its fur is surprisingly soft!"))
    }
    else{
    msgbox(format("Rockruff in Kitoha region can evolve into Midday, Midnight or Dusk form Lycanroc regardless of its Ability.\p
    Also, I heard all forms of Lycanroc has Tough Claws! The only difference is their base stats spread."))
    closemessage
    checkspecies(SPECIES_ROCKRUFF)
    if (var(VAR_RESULT) == TRUE){
        playse(SE_PIN)
        applymovement(15, emoteexclamation)
        waitmovement(0)
        msgbox(format("Whoa! You have a Rockruff! It's so cute!"))
        closemessage
        setvar(VAR_TEMP_0, 1)
    }
}}

movement emotecry {
    emote_cry
}

script GiveOutfitbox {
    msgbox(format("Got Outfit Box"))
    closemessage
    additem(ITEM_OUTFIT_BOX)
    end
}