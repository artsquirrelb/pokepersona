mapscripts SunshineOrphanage_MapScripts {
    MAP_SCRIPT_ON_TRANSITION {
        if (var(VAR_AKIHIKOS_INTRO_STATE) == 0) {
            setflag(FLAG_SPAWN_INVISIBLE)
            setflag(FLAG_HIDE_HELP_BUTTON)
            setflag(FLAG_TEMP_1)
        }
        if (var(VAR_AKIHIKOS_INTRO_STATE) == 4) {
            setflag(FLAG_HIDE_HELP_BUTTON)
        }
        if (var(VAR_AKIHIKOS_INTRO_STATE) >= 5) {
            setflag(FLAG_TEMP_1)
        }
    }
    MAP_SCRIPT_ON_FRAME_TABLE [
        VAR_AKIHIKOS_INTRO_STATE, 0: SunshineOrphanageOnFrame0
        VAR_AKIHIKOS_INTRO_STATE, 4: SunshineOrphanageOnFrame4
    ]
}

script SunshineOrphanageOnFrame0 {
    manuallysettime(WEEKDAY_WED, 16, 30, 0)
    setflag(FLAG_SET_WALL_CLOCK)
    setflag(FLAG_SYS_CLOCK_SET)
    goto (SunshineOrphanage_Eventscript_ShowTheMap)}

script SunshineOrphanage_Eventscript_ShowTheMap {
    applymovement(LOCALID_PLAYER, SlowlyShowTheMap)
    waitmovement(0)
    msgbox(format("Akihiko's Prologue: Sunshine Memories\n
    {COLOR GREEN}{CLEAR_TO 64}Level cap: 10"))
    warpsilent(MAP_SUNSHINE_ORPHANAGE_AKIHIKOS_ROOM2F, 5, 4)
    setvar(VAR_AKIHIKOS_INTRO_STATE, 1)
    setvar(VAR_CURRENT_LEVEL_CAP, 10)
    setvar(VAR_CURRENT_EV_CAP, 52)
}

movement SlowlyShowTheMap {
    walk_up *8
}

script SunshineOrphanageOnFrame4{
    goto (Miki_and_Shinjiro_Notice_Akihiko)
}

script Miki_and_Shinjiro_Notice_Akihiko {
    lockall
    playse (SE_PIN)
    applymovement (3, Miki_Runs_To_Akihiko)
    waitmovement(0)
    msgbox("{SPEAKER NAME_MK}There you are!")
    releaseall
    goto(Miki_and_Akihiko_walk_to_Shinjiro)
}

movement Miki_Runs_To_Akihiko {
    emote_exclamation_mark
    delay_16
    walk_fast_right *2
    walk_fast_up
}

script Miki_and_Akihiko_walk_to_Shinjiro {
    lock
    applymovement (3, Miki_leading_Akihiko_to_Shinjiro)
    applymovement (LOCALID_PLAYER, Miki_leading_Akihiko_to_Shinjiro)
    waitmovement(0)
    msgbox(format("{SPEAKER NAME_S}{CREATE_MUGSHOT MUGSHOT_S EMOTE_NORMAL}Miki wants to go to the woods but there are wild Pokémon there.\p
    {SPEAKER NAME_A}{CREATE_MUGSHOT MUGSHOT_A EMOTE_SMILE}Shinji, it's fineee. As long as we stay out of the tall grass.\p
    {SPEAKER NAME_MK}{DESTROY_MUGSHOT}To the woods! To the woods!\p
    {SPEAKER NAME_S}{CREATE_MUGSHOT MUGSHOT_S EMOTE_PENSIVE}Sigh..."))
    closemessage
    release
    goto(Shinjiro_and_Miki_Join_Party)
    }

movement Miki_leading_Akihiko_to_Shinjiro {
    walk_down
    walk_left
}

script Shinjiro_and_Miki_Join_Party {
    lock
    applymovement (4, Shinjiro_joins)
    applymovement (3, Miki_joins)
    waitmovement(0)
    removeobject (4)
    removeobject(3)
    setflag(FLAG_TEMP_1)
    setvar(VAR_AKIHIKOS_INTRO_STATE, 5)
    fwdtime(16, 40)
    release
    end
}
movement Shinjiro_joins {
    walk_right
}

movement Miki_joins {
    walk_up
}

script Boy_1_OrphanageEntrance {
    lock
    faceplayer
    setspeaker("Yuta")
    msgbox(format("Can I grow tall like these sunflowers?\p
    {SPEAKER NAME_A}{CREATE_MUGSHOT MUGSHOT_A EMOTE_HAPPY}Just drink lots of milk!"))
    release
    end
}

script Girl_OrphanageEntrance {
    lock
    faceplayer
    setspeaker("Yuko")
    msgbox(format("Sometimes I see people wearing black accompanying a man in a white coat speaking with our teachers.\p 
    I wonder who they are...\p
    {SPEAKER NAME_A}{CREATE_MUGSHOT MUGSHOT_A EMOTE_NORMAL}I asked Ms. Kobayashi once and she said they donated a lot to the orphanage."))
    release
    end
}

script Sunshine_Orphanage_Direction_Sign {
    lock
    msgbox("Sunshine Woods {DOWN_ARROW}\n
    Beware of wild Pokémon!")
    release
    end
}

script Welcome_To_Sunshine_Orphanage {
    lock
    msgbox (format("Welcome to Sunshine Orphanage!\p
    If you have any questions, feel free to ask the staff at the main hall counter."))
    release
    end
}

script Donors_List {
    lock
    msgbox (format("List of top donors (in alphabet order):
    --- Kai --- Kazuhisa ---\p
    --- Kirijo --- --- ---
    --- Ryota --- --- Takuya"))
    release
    end
}

script Sunflora_NPC {
    lock
    faceplayer
    playmoncry(SPECIES_SUNFLORA, CRY_MODE_NORMAL)
    release
    end
}

script Budew_NPC {
    lock
    faceplayer
    playmoncry(SPECIES_BUDEW, CRY_MODE_NORMAL)
    release
    end
}

script SunshineOrphanage_Eventscript_MetalClawTutor {
    lock
    faceplayer
    setspeaker("Materu")
    msgbox(format("I'm interested in Steel-type Pokémon and recently
    learned how to teach the move {COLOR BLUE}Metal Claw {COLOR 2}to a Pokémon.\p
    Do you want me to?"), MSGBOX_YESNO)
    if (var(VAR_RESULT) == TRUE){
        specialvar(VAR_RESULT, CountPartyNonEggMons)
        if (var(VAR_RESULT) == 0){
            setspeaker("Materu")
            msgbox(format("But you don't have any Pokémon..."))
            closemessage
            release
            end
        }
        setspeaker("Materu")
        msgbox(format("Which Pokémon should I teach {COLOR BLUE}Metal Claw {COLOR 2}to?"))
        closemessage
	    setvar (VAR_0x8005, MOVE_METAL_CLAW)
	    buffermovename (STR_VAR_2, MOVE_METAL_CLAW)
	    call (MoveTutor_EventScript_OpenPartyMenu)
	    if (var(VAR_RESULT) == 4) {
            goto(SunshineOrphanage_Eventscript_MetalClawDeclined)
        }
        else{
            setspeaker("Materu")
            msgbox(format("Just speak to me if you want me to teach {COLOR BLUE}Metal Claw {COLOR 2}to a Pokémon!"))
            closemessage
        }
    }
    else{
        goto(SunshineOrphanage_Eventscript_MetalClawDeclined)
    }
    release
	end
}

script SunshineOrphanage_Eventscript_MetalClawDeclined {
    setspeaker("Materu")
    msgbox(format("Aw... Okay."))
    closemessage
    release
    end
}