mapscripts SunshineOrphanage_AkihikosRoom2F_MapScripts {
    MAP_SCRIPT_ON_WARP_INTO_MAP_TABLE [
        VAR_AKIHIKOS_INTRO_STATE, 1: SunshineOrphanage_AkihikosRoom2F_OnWarp1]
    MAP_SCRIPT_ON_FRAME_TABLE [ 
        VAR_AKIHIKOS_INTRO_STATE, 1: AkihikosRoom2F_ONFRAME1]
    MAP_SCRIPT_ON_TRANSITION {
        if(var(VAR_AKIHIKOS_INTRO_STATE) >= 2){
            setflag(FLAG_TEMP_1)
        }
        if ((var(VAR_AKIHIKOS_INTRO_STATE) > 0 && var(VAR_AKIHIKOS_INTRO_STATE) < 9) || var(VAR_AKIHIKOS_INTRO_STATE) >= 11){
            setflag(FLAG_TEMP_2)
        }
        if (var(VAR_AKIHIKOS_INTRO_STATE) == 9) {
            clearflag(FLAG_HIDE_HELP_BUTTON)
            clearflag(FLAG_DONT_TRANSITION_MUSIC)
        }
    }
}

script SunshineOrphanage_AkihikosRoom2F_OnWarp1{
    applymovement (LOCALID_PLAYER, faceup)
}

movement walkdown {walk_down}
movement faceup {face_up}

script AkihikosRoom2F_ONFRAME1 {
    lockall
    goto (AkihikosRoom2F_Cutscene1)
    releaseall
}


movement emotepensive {
    emote_pensive
}


script AkihikosRoom2F_Cutscene1 {
    lockall
    applymovement (LOCALID_PLAYER, emotepensive)
    waitmovement(0)
    addmoney(3000)
    delay(60)
    playse(SE_EXIT)
    releaseall
    goto (AkihikosRoom2F_Eventscript_MikiAppears)}

movement Miki_Appears {
    walk_fast_up *4
    jump_in_place_up *2
    
}
script AkihikosRoom2F_Eventscript_MikiAppears {
    lockall
    special(SpawnCameraObject)
    applymovement(OBJ_EVENT_ID_CAMERA, Camera_On_Miki)
    waitmovement(0)
    msgbox(format("
    {SPEAKER NAME_MK}BIG BRO!"))
    closemessage
    playse(SE_PIN)
    applymovement(LOCALID_PLAYER, Akihiko_Surprised)
    applymovement(1, Miki_Appears)
    waitmovement(0)
    delay(8)
    msgbox(Lets_Go_Play)
    releaseall
    goto(Miki_Inviting_Akihiko_To_Play)}

script Miki_Inviting_Akihiko_To_Play{
    lock
    //applymovement(OBJ_EVENT_ID_CAMERA, Camera_Back_to_Player)
    applymovement(1, Miki_Leaves)
    waitmovement(0)
    playse(SE_EXIT)
    setflag(FLAG_TEMP_1)
    setvar(VAR_AKIHIKOS_INTRO_STATE , 2)
    clearflag(FLAG_HIDE_HELP_BUTTON)
    removeobject(1)
    special(RemoveCameraObject)
    release
    end
}

text Lets_Go_Play {
    format( 
        "{SPEAKER NAME_MK}It's time!\p You promised to take me out today!\p"
        "{CREATE MUGSHOT_A EMOTE_SMILE}{SPEAKER NAME_A}
        Let me fix this clock real quick.\p Otherwise Shinji's gonna grill me.\p"
        "{DESTROY_MUGSHOT}{SPEAKER NAME_MK}Okay! I'll be waiting outside with Ms. Kobayashi!"
    )
}

movement Camera_On_Miki {
    walk_down *2
    
}


movement Akihiko_Surprised {
    emote_exclamation_mark
    face_down
    delay_16
    delay_16
    walk_down *2
}


movement Miki_Leaves {
    walk_fast_down *4
    set_invisible
}

movement Camera_Back_to_Player {
    walk_fast_up *2
    
}

script GoSetTheClock{
    lockall
    msgbox(format("{CREATE MUGSHOT_A EMOTE_NORMAL}{SPEAKER NAME_A}I should fix the clock."))
    applymovement (LOCALID_PLAYER, StepBack)
    waitmovement(0)
    releaseall
    end
}
movement StepBack {
    walk_up  
}

script SetWallclock {
    lock
    manuallysettime(WEEKDAY_WED, 16, 32, 0)
	msgbox(format("The clock isn't working.\p
    {SPEAKER NAME_A}{CREATE MUGSHOT_A EMOTE_NORMAL}Let's see... The current time is... 4:32PM."))
    closemessage
	delay(30)
    createfieldmugshot(MUGSHOT_A EMOTE_NORMAL)
    setspeaker ("Akihiko")
    msgbox ("All done.")
	setvar(VAR_AKIHIKOS_INTRO_STATE, 3)
    release
	end
}

script GiveRunningShoesAtStart {
    lockall
    createfieldmugshot(MUGSHOT_A EMOTE_NORMAL)
    setspeaker("Akihiko")
    msgbox("Now where are my shoes...")
    createfieldmugshot(MUGSHOT_A EMOTE_HAPPY)
    setspeaker ("Akihiko")
    msgbox("There they are!")
    closemessage
    setspeaker(NULL)
    playfanfare(MUS_OBTAIN_ITEM)
    waitfanfare
    msgbox(format("{PLAYER} put on the {COLOR GREEN}Running Shoes{COLOR DARK_GRAY}."))
    setflag(FLAG_SYS_B_DASH)
    setvar(VAR_AKIHIKOS_INTRO_STATE, 4)
    releaseall
    end
}

script RunningShoesToggleManual {
    lock
    msgbox(format("{COLOR GREEN}Tips: You can toggle various settings in Options Menu."))
    release
    end
}

script AkihikosPC {
    lockall
    checkplayergender
    goto_if_eq(VAR_RESULT MALE SunshineOrphanage_AkihikosRoom2F_PlayersPC)
    goto_if_eq(VAR_RESULT FEMALE SunshineOrphanage_AkihikosRoom2F_RivalsPC)
}

script SunshineOrphanage_AkihikosRoom2F_PlayersPC {
    //setvar VAR_0x8004, PC_LOCATION_BRENDANS_HOUSE
	//special DoPCTurnOnEffect
	playse(SE_PC_ON)
	msgbox(gText_PlayerHouseBootPC)
	special(BedroomPC)
	waitstate
	releaseall
	end
}
script SunshineOrphanage_AkihikosRoom2F_RivalsPC {
    lockall
    msgbox(format("It's Akihiko's PC. It's an older model from Kirijo Electronics."))
    releaseall
    end
}

script ShinjirosPC {
    lockall
    msgbox("{SPEAKER NAME_A}{CREATE MUGSHOT_A EMOTE_NORMAL}It's Shinji's PC. It was a donation.")
    releaseall
    end
}

script BookshelfLeft {
    lockall
    msgbox(format("A book of how to draw anime characters.\p
    {SPEAKER NAME_A}{CREATE MUGSHOT_A EMOTE_SMILE}
    Miki often asks me to draw her favorite character from the game Persona 3."))
    releaseall
    end
}

script BookshelfRight {
    lockall
    msgbox(format("A book about Pokémon species in their natural habitats.\p"
    "{CREATE MUGSHOT_A EMOTE_HAPPY}{SPEAKER NAME_A} I hope I can get my own Pokémon soon."))
    releaseall
    end
}

script ChoresList {
    lock
    msgbox("{SPEAKER NAME_A}{CREATE MUGSHOT_A EMOTE_NORMAL}Shinji and my chores list.")
    release
    end
}

script Calendar {
    lock
    msgbox(format("{SPEAKER NAME_A}{CREATE MUGSHOT_A EMOTE_NORMAL}
    My sis and I are going to get adopted in 3 days, Shinji on the following day.\p 
    The Sanadas and the Aragakis look like they are good people.\p 
    {CREATE MUGSHOT_A EMOTE_SAD}I'll miss everyone here..."))
    release
    end
}

script TrashcanEmptyAkihikoRoom {
    lock
    msgbox(format ("It's empty.\p
    {SPEAKER NAME_A}{CREATE MUGSHOT_A EMOTE_NORMAL} Shinji just took out the trash."))
    release
    end
}

script SunshineOrphanage_AkihikosRoom2F_Eventscript_ShinjiroInformingAboutMsKobayashi {
    lock
    faceplayer
    if (var(VAR_TEMP_1) == 1) {
        msgbox(format("
    {SPEAKER NAME_S}{CREATE MUGSHOT_S EMOTE_NORMAL}Ms. Kobayashi asked for both of us to meet her at the main hall."))
    }
    else{
    msgbox("
    {SPEAKER NAME_S}{CREATE MUGSHOT_S EMOTE_NORMAL}Morning, Akihiko.")
    if (flag(FLAG_FOLLOWERS_DISABLED) == FALSE){
        msgbox("{SPEAKER NAME_S}{CREATE MUGSHOT_S EMOTE_SMILE}Morning to you too, Pawmi.")
        closemessage
        playmoncry(SPECIES_PAWMI, CRY_MODE_NORMAL)
        waitmoncry
        applymovement(OBJ_EVENT_ID_FOLLOWER, SunshineOrphanage_AkihikosRoom2F_Eventscript_PawmiGreetsBack)
        waitmovement(0)}
    msgbox(format("
    {SPEAKER NAME_S}{CREATE MUGSHOT_S EMOTE_NORMAL}Ms. Kobayashi asked for both of us to meet her at the main hall."))
    
    closemessage
    setvar(VAR_AKIHIKOS_INTRO_STATE, 10)
    setvar(VAR_TEMP_1, 1)}
    release
    end
}

movement SunshineOrphanage_AkihikosRoom2F_Eventscript_PawmiGreetsBack {
    emote_music
    lock_facing_direction
    jump_in_place_down
    unlock_facing_direction
}

script SunshineOrphanage_AkihikosRoom2F_Eventscript_HaventTalkedToShinji1 {
    lock
    applymovement(2,SunshineOrphanage_AkihikosRoom2F_Eventscript_ShinjiAsksToJoin1)
    waitmovement(0)
    msgbox(format("
    {SPEAKER NAME_S}{CREATE MUGSHOT_S EMOTE_NORMAL}Hey, Akihiko.\n Ms. Kobayashi said she wanted to meet us at the main hall.\p
    Let's go.\p
    {SPEAKER NAME_A}{CREATE MUGSHOT_A EMOTE_NORMAL}Oh, sure."))
    call(SunshineOrphanage_AkihikosRoom2F_Eventscript_Shinjijoinexit)
    removeobject(2)
    setflag(FLAG_TEMP_2)
    setvar(VAR_AKIHIKOS_INTRO_STATE, 11)
    release
    end
}

script SunshineOrphanage_AkihikosRoom2F_Eventscript_HaventTalkedToShinji2 {
    lock
    applymovement(2,SunshineOrphanage_AkihikosRoom2F_Eventscript_ShinjiAsksToJoin2)
    waitmovement(0)
    msgbox(format("
    {SPEAKER NAME_S}{CREATE MUGSHOT_S EMOTE_NORMAL}Hey, Akihiko.\n Ms. Kobayashi said she wanted to meet us at the main hall.\p
    Let's go.\p
    {SPEAKER NAME_A}{CREATE MUGSHOT_A EMOTE_NORMAL}Oh, sure."))
    call(SunshineOrphanage_AkihikosRoom2F_Eventscript_Shinjijoinexit)
    removeobject(2)
    setflag(FLAG_TEMP_2)
    setvar(VAR_AKIHIKOS_INTRO_STATE, 11)
    release
    end
}

movement SunshineOrphanage_AkihikosRoom2F_Eventscript_ShinjiAsksToJoin1 {
    walk_left*3
    walk_down*3
}
movement SunshineOrphanage_AkihikosRoom2F_Eventscript_ShinjiAsksToJoin2 {
    walk_left*2
    walk_down*3
}

script SunshineOrphanage_AkihikosRoom2F_Eventscript_Shinjijoinexit {
    applymovement(2, walkdown)
    waitmovement(0)
    fwdtime(8,35)
}

script SunshineOrphanage_AkihikosRoom2F_Eventscript_HaveTalkedToShinji1{
    lock
    msgbox(format("
    {SPEAKER NAME_S}{CREATE MUGSHOT_S EMOTE_NORMAL}Oh, you're leaving?"))
    closemessage
    applymovement(2,SunshineOrphanage_AkihikosRoom2F_Eventscript_ShinjiAsksToJoin1)
    waitmovement(0)
    msgbox(format("
    {SPEAKER NAME_S}{CREATE MUGSHOT_S EMOTE_NORMAL}Let's go."))
    call(SunshineOrphanage_AkihikosRoom2F_Eventscript_Shinjijoinexit)
    removeobject(2)
    setflag(FLAG_TEMP_2)
    setvar(VAR_AKIHIKOS_INTRO_STATE, 11)
    release
    end
}

script SunshineOrphanage_AkihikosRoom2F_Eventscript_HaveTalkedToShinji2 {
    lock
    msgbox(format("
    {SPEAKER NAME_S}{CREATE MUGSHOT_S EMOTE_NORMAL}Oh, you're leaving?"))
    closemessage
    applymovement(2,SunshineOrphanage_AkihikosRoom2F_Eventscript_ShinjiAsksToJoin2)
    waitmovement(0)
    msgbox(format("
    {SPEAKER NAME_S}{CREATE MUGSHOT_S EMOTE_NORMAL}Let's go."))
    call(SunshineOrphanage_AkihikosRoom2F_Eventscript_Shinjijoinexit)
    removeobject(2)
    setflag(FLAG_TEMP_2)
    setvar(VAR_AKIHIKOS_INTRO_STATE, 11)
    release
    end
}

script Eventscript_PostGivemon{
    if(var(VAR_RESULT) == MON_GIVEN_TO_PARTY){
        call(Eventscript_MonGivenToParty)
    }
    elif(var(VAR_RESULT) == MON_GIVEN_TO_PC){
        call(Eventscript_MonSendToBox)
    }
    else{
        call(Eventscript_CantGiveMon)
    }
}

script Eventscript_MonGivenToParty {
    msgbox(format("{PLAYER} put {STR_VAR_1} in the party."))
    closemessage
}

script Eventscript_MonSendToBox {
    msgbox(format("{STR_VAR_1} was sent to the Pokémon Storage."))
    closemessage
}

script Eventscript_CantGiveMon {
    msgbox(format("There's no room for {STR_VAR_1}.\n
    {PLAYER} released it to the wild."))
    closemessage
}