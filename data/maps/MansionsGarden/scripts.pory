mapscripts MansionsGarden_MapScripts {
    MAP_SCRIPT_ON_LOAD {
        setflag(FLAG_TEMP_4)
        if (var(VAR_AKIHIKOS_INTRO_STATE) == 100 || var(VAR_MITSURUS_INTRO_STATE) == 100){
            setmetatile(12, 15, 0x911, FALSE)
            setmetatile(13, 15, 0x912, FALSE)
            setmetatile(14, 15, 0x913, FALSE)
            setmetatile(15, 15, 0x914, FALSE)

            setmetatile(12, 16, 0x919, TRUE)
            setmetatile(13, 16, 0x91A, TRUE)
            setmetatile(14, 16, 0x91B, TRUE)
            setmetatile(15, 16, 0x91C, TRUE)

            setmetatile(12, 17, 0x921, TRUE)
            setmetatile(13, 17, 0x922, TRUE)
            setmetatile(14, 17, 0x923, TRUE)
            setmetatile(15, 17, 0x924, TRUE)
            }
    }
    MAP_SCRIPT_ON_TRANSITION {
        if (var(VAR_MITSURUS_INTRO_STATE) == 0) {
            setflag(FLAG_SPAWN_INVISIBLE)
            setflag(FLAG_TEMP_2)
            setobjectxyperm(KIKUNO, 42, 20)
            setobjectmovementtype(KIKUNO, MOVEMENT_TYPE_WANDER_LEFT_AND_RIGHT)
            setobjectxyperm(INDEEDEE_F, 46, 22)
        }
        if (var(VAR_MITSURUS_INTRO_STATE) == 3) {
            setflag(FLAG_HIDE_HELP_BUTTON)
            setobjectxyperm(KIKUNO, 37, 23)
            setobjectmovementtype(KIKUNO, MOVEMENT_TYPE_FACE_RIGHT)
            setobjectxyperm(INDEEDEE_F, 38, 23)
            setobjectmovementtype(INDEEDEE_F, MOVEMENT_TYPE_WALK_IN_PLACE_RIGHT)
        }
        if (var(VAR_MITSURUS_INTRO_STATE) == 4){
            setobjectxyperm(KIKUNO, 38, 20)
            setobjectnewmovementtype(KIKUNO, MOVEMENT_TYPE_FACE_RIGHT)
            setobjectxyperm(INDEEDEE_F, 38, 21)
            setobjectnewmovementtype(INDEEDEE_F, MOVEMENT_TYPE_WALK_IN_PLACE_UP)
            setobjectxyperm(FENNEKIN, 39, 21)
            setobjectnewmovementtype(FENNEKIN, MOVEMENT_TYPE_WALK_IN_PLACE_UP)
        }
        if (var(VAR_MITSURUS_INTRO_STATE) == 5) {
            setflag(FLAG_TEMP_2)
            setobjectxyperm(KIKUNO, 44, 18)
            setobjectxyperm(INDEEDEE_F, 48, 20)
        }
        if (var(VAR_MITSURUS_INTRO_STATE) >5){
            setflag(FLAG_TEMP_2)
        }
    }
    MAP_SCRIPT_ON_FRAME_TABLE [
        VAR_MITSURUS_INTRO_STATE, 0: MansionsGarden_Eventscript_OnFrame0
        VAR_MITSURUS_INTRO_STATE, 3: MansionsGarden_Eventscript_OnFrame3
        VAR_MITSURUS_INTRO_STATE, 5: MansionsGarden_Eventscript_OnFrame5

        VAR_TEMP_8, 0: MansionsGarden_CheckIfMitsuruPrologueIsDoneForAkihiko
    ]
}

script EventScript_NewGameSetUp {
    setvar (VAR_SHINJIRO_RELATIONSHIP_POINTS, 100)
	setvar (VAR_MITSURU_RELATIONSHIP_POINTS, 100)
	setvar (VAR_AKIHIKO_RELATIONSHIP_POINTS, 100)
    setvar (VAR_TIME_RESET_ATTEMPTS, 5)
    setflag(FLAG_HIDE_HELP_BUTTON)
    setflag(FLAG_SAFE_FOLLOWER_MOVEMENT)

    checkplayergender
    if (var(VAR_RESULT) == MALE){
        
        setvar(VAR_MITSURUS_INTRO_STATE, 50) // will be reset to 0 if player choose to play mitsuru's prologue after doing akihiko's
    }
    else{
        
        setvar(VAR_AKIHIKOS_INTRO_STATE, 50) // will be reset to 0 if player choose to play akihiko's prologue after doing mitsuru's
    }
    callnative(ScrCmd_InitTimeBasedEvent)
	pausefakertc // pauses the time so that we can do our scripting part
}

script MansionsGarden_CheckIfMitsuruPrologueIsDoneForAkihiko {
    subquestmenu(QUEST_MENU_CHECK_COMPLETE, STORY_QUESTS, MITSURUS_PROLOGUE)
    if (var(VAR_RESULT) == FALSE && var(VAR_AKIHIKOS_INTRO_STATE) == 100){
        clearflag(FLAG_TEMP_4)
        clearflag(FLAG_HIDE_QUEST_ICON)
    }
    setvar(VAR_TEMP_8, 1)
}

script MansionsGarden_Eventscript_OnFrame0 {
    manuallysettime(WEEKDAY_WED, 22, 30, 0)
    setflag(FLAG_SET_WALL_CLOCK)
    setflag(FLAG_SYS_CLOCK_SET)
    goto(MansionsGarden_Eventscript_SlowlyShowTheMap)
}

script MansionsGarden_Eventscript_OnFrame3 {
    setvar(VAR_MITSURUS_INTRO_STATE, 4)
    goto(MansionsGarden_Eventscript_KikunoVSFennekin)
}

script MansionsGarden_Eventscript_OnFrame5 {
    goto(MansionsGarden_Eventscript_SayingGoodbyeToKikuno)
}

script MansionsGarden_Eventscript_SlowlyShowTheMap {
    call(Eventscript_CutsceneStarts)
    delay(30)
    applymovement(LOCALID_PLAYER, moves(walk_up * 10))
    waitmovement
    msgbox(format("{CLEAR_TO 10}Mitsuru's Prologue: Responsibilities\n
    {CLEAR_TO 64}{COLOR GREEN}Level cap: 10"))
    closemessage
    setvar(VAR_CURRENT_LEVEL_CAP, 10)
    setvar(VAR_CURRENT_EV_CAP, 52)
    warpsilent(MAP_MITSURUS_ROOM2F, 15, 10)
}



script MansionsGarden_Eventscript_KikunoVSFennekin {
    setflag(FLAG_HIDE_HELP_BUTTON)
    delay(20)
    special(SpawnCameraObject)
    applymovement(OBJ_EVENT_ID_CAMERA, moves(walk_down*2))
    waitmovement(0)
    playmoncry(SPECIES_FENNEKIN, CRY_MODE_NORMAL)
    applymovement(FENNEKIN, moves(jump_in_place_left, delay_16, shake_vertical, delay_16, delay_16, walk_fast_left))
    waitmovement(0)
    call(Eventscript_BlockShakeCamera)
    delay(30)
    applymovement(FENNEKIN, moves(lock_facing_direction, jump_right, unlock_facing_direction))
    applymovement(KIKUNO, moves(walk_in_place_right))
    waitmovement(0)
    msgbox(format("{SPEAKER NAME_KKN}Indeedee, Round!"))
    closemessage
    playmoncry(SPECIES_INDEEDEE_F, CRY_MODE_NORMAL)
    applymovement(INDEEDEE_F, moves(jump_in_place_right, delay_16, shake_horizontal))
    waitmovement(0)
    playse(SE_LEDGE)
    applymovement(FENNEKIN, moves(lock_facing_direction, jump_up, unlock_facing_direction))
    waitmovement(0)
    playse(SE_SHINY)
    applymovement(FENNEKIN, emotemusic)
    waitmovement(0)
    lock
    setobjectnewmovementtype(FENNEKIN, MOVEMENT_TYPE_WALK_IN_PLACE_LEFT)
    release
    delay(30)
    playse(SE_PIN)
    applymovement(INDEEDEE_F, moves(emote_sad))
    waitmovement(0)
    msgbox(format("{SPEAKER NAME_KKN}Damn it. This one is so slippery."))
    closemessage
    delay(30)
    playse(SE_PIN)
    applymovement(FENNEKIN, moves(emote_exclamation_mark, face_up))
    waitmovement(0)
    delay(40)
    playse(SE_SHINY)
    applymovement(FENNEKIN, moves(emote_happy))
    waitmovement(0)
    applymovement(INDEEDEE_F, faceup)
    applymovement(KIKUNO, moves(emote_exclamation_mark, face_up))
    waitmovement(0)
    delay(30)
    msgbox(format("{SPEAKER NAME_KKN}Young Lady! Be careful!"))
    closemessage
    playse(SE_PIN)
    applymovement(LOCALID_PLAYER, emoteexclamation)
    applymovement(OBJ_EVENT_ID_CAMERA, moves(walk_up *2))
    applymovement(FENNEKIN, moves(walk_fast_left, walk_fast_up))
    waitmovement(0)
    setflag(FLAG_NO_WHITEOUT)
    setflag(FLAG_NO_CATCHING)
    delay(30)
    setwildbattle(SPECIES_FENNEKIN, 5)
    dowildbattle
    goto (MansionsGarden_Eventscript_VSFennekin)
}

script MansionsGarden_Eventscript_VSFennekin {
    delay(30)
    clearflag(FLAG_NO_WHITEOUT)
    clearflag(FLAG_NO_CATCHING)
    clearflag(FLAG_HIDE_MAP_NAME_POPUP)
    playse(SE_SHINY)
    applymovement(FENNEKIN, emotemusic)
    waitmovement
    msgbox(format("Fennekin looks satisfied after spending all of its energy in battle."))
    closemessage
    applymovement(KIKUNO, moves(walk_up*3, walk_right))
    delay(8)
    applymovement(INDEEDEE_F, moves(walk_up*2))
    waitmovement(KIKUNO)
    applymovement(LOCALID_PLAYER, faceleft)
    waitmovement(0)
    playse(MUS_HEAL)
    waitse
    special(HealPlayerParty)
    msgbox(format("{SPEAKER NAME_KKN}Phew. Thanks to you, this Fennekin finally calmed down.\p
    {SPEAKER NAME_M}{CREATE_MUGSHOT MUGSHOT_M EMOTE_NORMAL}Where did it come from? We don't normally see Fennekin around here.\p
    {SPEAKER NAME_KKN}{DESTROY_MUGSHOT}It managed to sneak up into the east hall and made a mess of the place."))
    closemessage
    delay(20)
    playse(SE_SHINY)
    applymovement(FENNEKIN, moves(emote_happy, delay_16, face_left, delay_16, face_down, delay_16, face_right, delay_16, face_up, delay_16, jump_in_place_up*2))
    applymovement(KIKUNO, facedown)
    applymovement(LOCALID_PLAYER, facedown)
    waitmovement(FENNEKIN)
    lock
    setobjectnewmovementtype(FENNEKIN, MOVEMENT_TYPE_WALK_IN_PLACE_UP)
    release
    delay(30)
    msgbox(format("{SPEAKER NAME_KKN}Looks like it wants to go with you?\p
    {SPEAKER NAME_M}{CREATE_MUGSHOT MUGSHOT_M EMOTE_SMILE}Good timing. I do want a fire type in my team."))
    closemessage
    setflag(FLAG_DONT_TRANSITION_MUSIC)
    fwdtime(23, 0)
    end
}

script MansionsGarden_Eventscript_Kikuno {
    lock
    faceplayer
    if (var(VAR_MITSURUS_INTRO_STATE) == 4){
        msgbox(format("{SPEAKER NAME_KKN}Go on, Young Lady. It'll be a great teammate on your journey."))
        closemessage
    }
    else{
        msgbox(format("{SPEAKER NAME_KKN}Please stay safe, Young Lady."))
        closemessage
    }
    release
    end
}

script MansionsGarden_Eventscript_Fennekin {
    lock
    call(Eventscript_CutsceneStarts)
    faceplayer
    msgbox(format("Touch Fennekin with the Pokéball?"), MSGBOX_YESNO)
    closemessage
    if(var(VAR_RESULT) == TRUE){
        msgbox(format("{SPEAKER NAME_M}{CREATE_MUGSHOT MUGSHOT_M EMOTE_NORMAL}(Now that I get a closer look at it...\p
        It seems like there are sparkles around this Fennekin?)"), MSGBOX_YESNO)
        closemessage
        if (var(VAR_RESULT) == TRUE){
            //giveshiny
            call (MansionsGarden_Eventscript_GetShinyFennekin)
        }
        else{
            msgbox(format("{SPEAKER NAME_M}{CREATE_MUGSHOT MUGSHOT_M EMOTE_NORMAL}(Oh, it's just dust.)"))
            closemessage
            call (MansionsGarden_Eventscript_GetNormalFennekin)
        }
        warpsilent(MAP_BLANK_MAP, 0, 0)
        release
    }
    else{
        playse(SE_PIN)
        applymovement(FENNEKIN, moves(emote_question_mark))
        waitmovement(0)
        msgbox(format("Fennekin looks confused, but it's still waiting for you."))
        closemessage
        release
        end
    }
}

script MansionsGarden_Eventscript_GetShinyFennekin {
    givemon(SPECIES_FENNEKIN, 5, 0, ITEM_POKE_BALL, NATURE_MODEST, 0, MON_FEMALE, 0, 0, 0, 0, 0, 0, 31, 31, 31, 31, 31, 31, MOVE_CALM_MIND, shinyMode = SHINY_MODE_ALWAYS)
        if(var(VAR_RESULT) == MON_GIVEN_TO_PARTY){
            playfanfare(MUS_OBTAIN_ITEM)
            waitfanfare
            msgbox(format("Fennekin happily went into the Pokéball."))
            closemessage
            applymovement(FENNEKIN, moves(enter_pokeball, set_invisible))
            waitmovement(0)
        }
        elif(var(VAR_RESULT) == MON_GIVEN_TO_PC){
            playfanfare(MUS_OBTAIN_ITEM)
            waitfanfare
            msgbox(format("Fennekin happily went into the Pokéball."))
            closemessage
            applymovement(FENNEKIN, moves(enter_pokeball, set_invisible))
            waitmovement(0)
        }
        else{
            msgbox("Party full.")}
}
script MansionsGarden_Eventscript_GetNormalFennekin {
    givemon(SPECIES_FENNEKIN, 5, 0, ITEM_POKE_BALL, NATURE_MODEST, 0, MON_FEMALE, 0, 0, 0, 0, 0, 0, 31, 31, 31, 31, 31, 31, MOVE_CALM_MIND, shinyMode = SHINY_MODE_NEVER)
         if(var(VAR_RESULT) == MON_GIVEN_TO_PARTY){
             playfanfare(MUS_OBTAIN_ITEM)
        waitfanfare
        msgbox(format("Fennekin happily went into the Pokéball."))
        closemessage
        applymovement(FENNEKIN, moves(enter_pokeball, set_invisible))
        waitmovement(0)
        removeobject(FENNEKIN)
        setflag(FLAG_TEMP_2)
        }
        elif(var(VAR_RESULT) == MON_GIVEN_TO_PC){
             playfanfare(MUS_OBTAIN_ITEM)
        waitfanfare
        msgbox(format("Fennekin happily went into the Pokéball."))
        closemessage
        applymovement(FENNEKIN, moves(enter_pokeball, set_invisible))
        waitmovement(0)
        removeobject(FENNEKIN)
        setflag(FLAG_TEMP_2)
        }
        else{
            msgbox("Party full.")}
}

script MustGetFennekin {
    lock
    applymovement(LOCALID_PLAYER, moves(walk_left, face_down))
    waitmovement(0)
    msgbox(format("{SPEAKER NAME_M}{CREATE_MUGSHOT MUGSHOT_M EMOTE_NORMAL}(I should let Fennekin join the team. It'll be a great help for my mission.)"))
    closemessage
    release
    end
}

script MansionsGarden_Eventscript_SayingGoodbyeToKikuno {
    setvar(VAR_MITSURUS_INTRO_STATE, 6)
    delay(30)
    applymovement(KIKUNO, emotepensive)
    waitmovement(0)
    delay(30)
    msgbox(format("{SPEAKER NAME_KKN}I guess no one managed to stop you at all.\p
    {SPEAKER NAME_M}{CREATE_MUGSHOT MUGSHOT_M EMOTE_NORMAL}Kikuno...\p
    {SPEAKER NAME_KKN}{DESTROY_MUGSHOT}Young Lady, how about I join you on your mission?\p
    {SPEAKER NAME_M}{CREATE_MUGSHOT MUGSHOT_M EMOTE_NORMAL}You know I won't agree to that.\p
    It's too dangerous and I can't guarantee your safety.\p
    {SPEAKER NAME_KKN}{DESTROY_MUGSHOT}Hahaha, it's funny how you say the exact same thing as everyone who tried to stop you."))
    closemessage
    delay(40)
    special(SpawnCameraObject)
    applymovement(OBJ_EVENT_ID_CAMERA, moves(walk_down*2))
    applymovement(KIKUNO, moves(walk_down*2, face_up))
    waitmovement(0)
    msgbox(format("{SPEAKER NAME_KKN}I guess this is goodbye then.\p
    {SPEAKER NAME_M}{CREATE_MUGSHOT MUGSHOT_M EMOTE_NORMAL}Yes... Please take care, Kikuno.\p
    {SPEAKER NAME_KKN}{DESTROY_MUGSHOT}You too, Young Lady."))
    closemessage
    delay(30)
    applymovement(OBJ_EVENT_ID_CAMERA, moves(walk_right*2))
    applymovement(KIKUNO, moves(walk_right*2, delay_16, face_left))
    waitmovement(0)
    msgbox(format("{SPEAKER NAME_KKN}Oh right.\p
    I'm not sure if you have heard, but in case you need some healing, Nurse Sawa has returned from her maternity leave.\p
    She should be at the Training House.\p
    {SPEAKER NAME_M}{CREATE_MUGSHOT MUGSHOT_M EMOTE_NORMAL}I see. Thank you for informing me.\p
    {SPEAKER NAME_KKN}{DESTROY_MUGSHOT}My pleasure."))
    closemessage
    delay(10)
    applymovement(KIKUNO, moves(walk_right *2, face_down))
    delay(16)
    applymovement(INDEEDEE_F, moves(walk_right, face_down))
    waitmovement(0)
    applymovement(OBJ_EVENT_ID_CAMERA, moves(walk_diag_northwest *2))
    waitmovement(0)
    special(RemoveCameraObject)
    delay(30)
    msgbox(format("{SPEAKER NAME_M}{CREATE_MUGSHOT MUGSHOT_M EMOTE_NORMAL}(I should get going now.)"))
    closemessage
    setrespawn(HEAL_LOCATION_MANSIONS_GARDEN)
    clearflag(FLAG_DONT_TRANSITION_MUSIC)
    clearflag(FLAG_HIDE_HELP_BUTTON)
    clearflag(FLAG_HIDE_MAP_NAME_POPUP)
    fwdtime(23, 15)
    release
    end
}

script MansionsGarden_Eventscript_Indeedee {
    lock
    faceplayer
    playmoncry(SPECIES_INDEEDEE_F, CRY_MODE_NORMAL)
    waitmoncry
    release
    end
}

script MansionsGarden_Eventscript_TrainingHouse_Sign {
    lock
    msgbox("Training House")
    closemessage
    release
    end
}

script MansionsGarden_Eventscript_GuestHouse_Sign {
    lock
    msgbox("Guest & Staff House")
    closemessage
    release
    end
}

script MansionsGarden_Eventscript_WaiterKouhei {
    lock
    faceplayer
    setspeaker("Kouhei")
    if (var(VAR_MITSURUS_INTRO_STATE) < 10){
    msgbox(format("It's my shift to stand guard tonight, Young Lady.\p
    I will make sure no wild Pokémon disrupt our staff's goodnight sleep."))
    closemessage
    }
    release
    end
}

script MansionsGarden_Eventscript_Furfrou {
    lock
    faceplayer
    playmoncry(SPECIES_FURFROU, CRY_MODE_NORMAL)
    waitmoncry
    startoverworldencounter(5)
    setflag(FLAG_TEMP_3)
    removeobject(FURFROU)
    release
    end
}

script MansionsGarden_Eventscript_MitsuruPrologueStart {
    hidefollower
    hidefollowernpc
    setflag(FLAG_HIDE_QUEST_ICON)
    lock
    faceplayer
    if (var(VAR_TEMP_0) == 1){
        msgbox(format("{SPEAKER NAME_M}{CREATE_MUGSHOT MUGSHOT_M EMOTE_SAD}.......{PAUSE 60}
        Could we talk later? I still want to stay here alone for a while."))
        closemessage
        release
        end
    }
    else
    {
        msgbox(format("{SPEAKER NAME_A}{CREATE_MUGSHOT MUGSHOT_A EMOTE_SURPRISED}So... This is your uh... house? Mansion? Land??\p
        {SPEAKER NAME_M}{CREATE_MUGSHOT MUGSHOT_M EMOTE_NORMAL}To be more exact, it's my family's mansion.\p
        {SPEAKER NAME_A}{CREATE_MUGSHOT MUGSHOT_A EMOTE_SURPRISED}Woah.............\p
        Why aren't you going in, by the way? The gate's locked and no one's home?\p
        {SPEAKER NAME_M}{CREATE_MUGSHOT MUGSHOT_M EMOTE_NORMAL}There's always somebody home. Although my Father may have been at the company in Paulownia City at the moment.\p
        I'm not going in because...\p
        {CREATE_MUGSHOT MUGSHOT_M EMOTE_SMILE}Well, you may call it “resolve”, I suppose?\p
        {SPEAKER NAME_M}{CREATE_MUGSHOT MUGSHOT_M EMOTE_NORMAL}I've made a promise to myself to never waver until my mission is completed.\p
        {SPEAKER NAME_A}{CREATE_MUGSHOT MUGSHOT_A EMOTE_SMILE}Oh, I get it. It's like visiting home would make you reluctant to travel far from home again.\p
        {CREATE_MUGSHOT MUGSHOT_A EMOTE_SERIOUS}Although in my case, it'd strenghten my resolve further.\p
        'Cause you know, I want my life before the explosion back.\p
        {SPEAKER NAME_A}{CREATE_MUGSHOT MUGSHOT_A EMOTE_SMILE}But enough about me, I actually want to ask you something.\p
        {SPEAKER NAME_M}{CREATE_MUGSHOT MUGSHOT_M EMOTE_SMILE}I'm all ears.\p
        {SPEAKER NAME_A}{CREATE_MUGSHOT MUGSHOT_A EMOTE_SMILE}How did you know about me and Shinji?{PAUSE_UNTIL_PRESS}\n
        {SPEAKER NAME_A}{CREATE_MUGSHOT MUGSHOT_A EMOTE_NORMAL}I mean, we're just 2 random orphan kids.\p
        {SPEAKER NAME_M}{CREATE_MUGSHOT MUGSHOT_M EMOTE_NORMAL}.........."))
        msgbox(format("{SPEAKER NAME_M}{CREATE_MUGSHOT MUGSHOT_M EMOTE_NORMAL}It's a long story. Do you still want to listen? {COLOR GREEN}(Begin Mitsuru's prologue quest?)"), MSGBOX_YESNO)
        if (var(VAR_RESULT) == FALSE)
        {
            msgbox(format("{SPEAKER NAME_M}{CREATE_MUGSHOT MUGSHOT_M EMOTE_SAD}......... {PAUSE 60}Very well."))
            closemessage
            playse(SE_M_STAT_DECREASE)
            applymovement(27, pointdown3)
            waitmovement
            subvar(VAR_MITSURU_RELATIONSHIP_POINTS, 3)
            setvar(VAR_TEMP_0, 1)
            release
            end
        }
        else
        {
            closemessage
            playse(SE_SHINY)
            applymovement(27, pointup3)
            waitmovement
            msgbox(format("{SPEAKER NAME_M}{CREATE_MUGSHOT MUGSHOT_M EMOTE_SMILE}Very well."))
            closemessage
            fadescreenswapbuffers(FADE_TO_BLACK)
            switchmaincharacter
            setvar(VAR_MITSURUS_INTRO_STATE, 0)
            setvar(VAR_AKIHIKOS_INTRO_STATE, 50)
            unlockcharacter(CHARACTER_MITSURU)
            makecharaAvailable(CHARACTER_MITSURU)
            makecharaUnavailable(CHARACTER_AKIHIKO)
            makecharaUnavailable(CHARACTER_INTELEON_T)
            makecharaUnavailable(CHARACTER_SHINJIRO)
            destroyfollowernpc
            clearflag(FLAG_SYS_B_DASH)
            clearflag(FLAG_SYS_POKEDEX_GET)
            clearflag(FLAG_SYS_POKEMON_GET)
            removeitem(ITEM_EXP_SHARE)
            removeitem(ITEM_CAMP_GEARS)
            removeitem(ITEM_ESCAPE_ROPE)
            removeitem(ITEM_MACH_BIKE)
            manuallysettime(WEEKDAY_WED, 22, 30, 0)
            subquestmenu(QUEST_MENU_SET_ACTIVE, STORY_QUESTS, MITSURUS_PROLOGUE)
            setvar(VAR_STORY_QUESTS_STATE, 0)
            warpsilent(MAP_MANSIONS_GARDEN, 43, 27)
            special(DrawWholeMapView)
            waitstate
        }
    }
}