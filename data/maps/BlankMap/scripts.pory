mapscripts BlankMap_MapScripts {
    MAP_SCRIPT_ON_TRANSITION {
        setflag(FLAG_SPAWN_INVISIBLE)
        setflag(FLAG_HIDE_HELP_BUTTON)
    }
    MAP_SCRIPT_ON_FRAME_TABLE [
        VAR_AKIHIKOS_INTRO_STATE, 0: Eventscript_NewGameMode
        VAR_AKIHIKOS_INTRO_STATE, A_INTRO_GAMEOVER: SunshineMeadow_Eventscript_GameOver
        ////////////////////////
        VAR_MITSURUS_INTRO_STATE, 7: SunshineMeadow_Eventscript_MitsuruOnframe7
        VAR_MITSURUS_INTRO_STATE, 9: Route2_House_Eventscript_MitsuruOnFrame9
        VAR_MITSURUS_INTRO_STATE, 26: SunshineTown_Eventscript_MitsuruOnFrame26
        VAR_MITSURUS_INTRO_STATE, 32: Route2_House_Eventscript_MitsuruOnFrame32Warp
    ]
}

script SunshineMeadow_Eventscript_GameOver {
    lock
    msgbox(format("Mitsuru Kirijo walked away without turning back.\p
    Her Vulpix trotted along her lonesome yet determined silhouette.\p
    The Inteleon gave you one last disappointed look before trailing behind its two companions."))
    closemessage
    delay(30)
    msgbox(format("You and Shinjiro continued your search for Miki to no avail.\p
    Not long after, both of you ended up getting captured by the men in black and suffered endless nightmares.\p
    Only darkness awaits..."))
    closemessage
    setflag(FLAG_DONT_TRANSITION_MUSIC)
    warpsilent(MAP_GAME_OVER, 0, 5)
    waitstate
}

script SunshineMeadow_Eventscript_MitsuruOnframe7 {
    setvar(VAR_MITSURUS_INTRO_STATE, 8)
    warpsilent(MAP_SUNSHINE_MEADOW, 15, 15)
}

script Route2_House_Eventscript_MitsuruOnFrame9 {
    fwdtime(7, 50)
    setvar(VAR_MITSURUS_INTRO_STATE, 10)
    playse(MUS_HEAL)
    special(HealPlayerParty)
    delay(50)
    warpsilent(MAP_ROUTE2_HOUSE, 4, 4)
}

script SunshineTown_Eventscript_MitsuruOnFrame26 {
    msgbox(format("A few days later."))
    closemessage
    warpsilent(MAP_ROUTE2_HOUSE, 5, 3)
    waitstate
}

script Route2_House_Eventscript_MitsuruOnFrame32Warp {
    setvar(VAR_MITSURUS_INTRO_STATE, 33)
    fwdtime(6,20)
    fwdweekday(WEEKDAY_MON)
    delay(60)
    msgbox(format("The next morning..."))
    warpsilent(MAP_ROUTE2_HOUSE, 5,5)
    waitstate
}

script Eventscript_NewGameMode {
    msgbox(gText_NewGame_WhatDifficulty)
    dynmultipush("Casual", 0)
    dynmultipush("Story", 1)
    dynmultipush("Merciless", 2)
    dynmultipush("Infos", 3)
    dynmultistack(18, 4, TRUE, 4, FALSE, 0, NULL)
    switch(var(VAR_RESULT))
    {
        case 0: goto(Eventscript_ChoosingCasual)
        case 1: goto(Eventscript_ChoosingStory)
        case 2: goto(Eventscript_ChoosingMerciless)
        case 3: goto(Eventscript_ShowInfos)
    }
}

script Eventscript_ChoosingCasual {
    msgbox(gText_NewGame_ChoosingCasual, MSGBOX_YESNO)
    if (var(VAR_RESULT))
    {
        closemessage
        setvar(VAR_DIFFICULTY, 0)
        goto(Eventscript_WarpToMap)
    }
    else{
        goto(Eventscript_NewGameMode)
    }
}

script Eventscript_ChoosingStory {
    msgbox(gText_NewGame_ChoosingStory, MSGBOX_YESNO)
    if (var(VAR_RESULT))
    {
        closemessage
        setvar(VAR_DIFFICULTY, 1)
        goto(Eventscript_WarpToMap)
    }
    else{
        goto(Eventscript_NewGameMode)
    }
}

script Eventscript_ChoosingMerciless {
    msgbox(gText_NewGame_ChoosingMerciless, MSGBOX_YESNO)
    if (var(VAR_RESULT))
    {
        closemessage
        setvar(VAR_DIFFICULTY, 2)
        goto(Eventscript_WarpToMap)
    }
    else{
        goto(Eventscript_NewGameMode)
    }
}

script Eventscript_ShowInfos {
    msgbox(gText_NewGame_DifficultyInfos)
    goto(Eventscript_NewGameMode)
}

script Eventscript_WarpToMap {
    msgbox(gText_NewGame_Outro)
    closemessage
	call (EventScript_NewGameSetUp)
	call (EventScript_ResetAllBerries)
    checkplayergender
    if (var(VAR_RESULT) == MALE)
    {
        warpwhitefade(MAP_SUNSHINE_ORPHANAGE, 13, 18)
    }
    else
    {
        warpwhitefade(MAP_MANSIONS_GARDEN, 43, 27)
    }
    waitstate
}
